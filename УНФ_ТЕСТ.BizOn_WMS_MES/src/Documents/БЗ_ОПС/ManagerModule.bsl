Функция ПечатьОПС_ТСД(АктивныйДокумент,Паллета=Неопределено) Экспорт
	Таб=Новый ТабличныйДокумент();
	Таб.ОриентацияСтраницы=ОриентацияСтраницы.Портрет;
	Таб.ДвусторонняяПечать=ТипДвустороннейПечати.Нет;
	Таб.АвтоМасштаб=Истина;
	Таб.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОПС_ТСД";
	Таб.ПолеСлева = 10;
	Таб.ПолеСправа = 10;
	Таб.ПолеСверху = 10;
	Таб.ПолеСнизу = 10;
	Таб.РазмерКолонтитулаСверху = 0;
	Таб.РазмерКолонтитулаСнизу = 0;
	Таб.ЭкземпляровНаСтранице = 1;
	Макет = ПолучитьМакет("ОтчетТСД");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьРазрыв = Макет.ПолучитьОбласть("Разрыв");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");	
	ОбластьПодвалДляСклада = Макет.ПолучитьОбласть("ПодвалДляСклада");	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Данные.НомерПаллеты КАК НомерПаллеты,
	|	Данные.Номенклатура КАК Номенклатура,
	|	Данные.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(Данные.Количество) КАК Количество,
	|	Данные.ЗНП КАК ЗНП,
	|	ВЫБОР
	|		КОГДА Данные.ЗНП = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СверхЗаказа,
	|	ВЫБОР
	|		КОГДА Данные.ЗНП = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|			ТОГДА ""---""
	|		ИНАЧЕ Данные.ЗНП.НомерЗаказаПроизводстваМебели
	|	КОНЕЦ КАК НомерПартии
	|ПОМЕСТИТЬ ОПС
	|ИЗ
	|	Документ.БЗ_ОПС.ТСД_ВводДанных КАК Данные
	|ГДЕ
	|	Данные.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.НомерПаллеты,
	|	Данные.ХарактеристикаНоменклатуры,
	|	Данные.ЗНП,
	|	ВЫБОР
	|		КОГДА Данные.ЗНП = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	Данные.Номенклатура,
	|	ВЫБОР
	|		КОГДА Данные.ЗНП = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|			ТОГДА ""---""
	|		ИНАЧЕ Данные.ЗНП.НомерЗаказаПроизводстваМебели
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОПС.НомерПаллеты КАК НомерПаллеты,
	|	ОПС.Номенклатура КАК Номенклатура,
	|	ОПС.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ОПС.Количество КАК Количество,
	|	ОПС.НомерПартии КАК НомерПартии,
	|	ОПС.ЗНП КАК ЗНП
	|ИЗ
	|	ОПС КАК ОПС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОПС.НомерПаллеты,
	|	ОПС.СверхЗаказа,
	|	ОПС.НомерПартии,
	|	ОПС.ЗНП.Номер,
	|	ОПС.Номенклатура.Наименование,
	|	ОПС.ХарактеристикаНоменклатуры.Наименование
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	НомерПаллеты";	
	Запрос.УстановитьПараметр("Ссылка", АктивныйДокумент);
	ВыборкаПаллеты = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПаллеты.Следующий() Цикл
		Если ЗначениеЗаполнено(Паллета) Тогда
			Если Паллета<>ВыборкаПаллеты.НомерПаллеты Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ОбластьЗаголовок.Параметры.ШтрихкодCODE128 = БЗ_Штрихкодирование.CODE128Строка(БЗ_Штрихкодирование.ПолучитьШтрихкодДокумента(АктивныйДокумент)+Формат(ВыборкаПаллеты.НомерПаллеты,"ЧЦ=2; ЧВН="));
		ОбластьЗаголовок.Параметры.Паллета = Формат(ВыборкаПаллеты.НомерПаллеты,"ГП=")+"/"+Формат(ВыборкаПаллеты.Количество(),"ГП=");
		ОбластьЗаголовок.Параметры.НомерКратко=Прав(АктивныйДокумент.Номер,3);
		ОбластьЗаголовок.Параметры.НомерОПС=АктивныйДокумент.Номер;
		ОбластьЗаголовок.Параметры.ДатаДок=АктивныйДокумент.Дата;
		ОбластьЗаголовок.Параметры.ПользовательТСД=АктивныйДокумент.Исполнитель;
		ОбластьЗаголовок.Параметры.СкладПриемки=АктивныйДокумент.СкладПриемки;
		Таб.Вывести(ОбластьЗаголовок);
		Таб.Вывести(ОбластьШапкаТаблицы);
		ВыборкаТовары=ВыборкаПаллеты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Ном=0;
		ПредЗНП=Неопределено;
		Пока ВыборкаТовары.Следующий() Цикл
			Ном = Ном + 1;
			Если ПредЗНП<>ВыборкаТовары.ЗНП Тогда
				Если Ном<>1 Тогда
					Таб.Вывести(ОбластьРазрыв);
				КонецЕсли;
				ПредЗНП=ВыборкаТовары.ЗНП;
			КонецЕсли;
			ОбластьСтрока.Параметры.Ном=Формат(Ном,"ЧГ=");
			ОбластьСтрока.Параметры.Номенклатура=ВыборкаТовары.Номенклатура.Наименование;
			//ОбластьСтрока.Параметры.ХарактеристикаНоменклатуры=ВыборкаТовары.ХарактеристикаНоменклатуры.Наименование;
			Если ЗначениеЗаполнено(ВыборкаТовары.ЗНП) Тогда
				Попытка
					ОбластьСтрока.Параметры.ЗНП="ЗНП "+Формат(Число(ВыборкаТовары.ЗНП.Номер),"ЧГ=");
				Исключение
					ОбластьСтрока.Параметры.ЗНП="ЗНП "+ВыборкаТовары.ЗНП.Номер;
				КонецПопытки;
				ОбластьСтрока.Параметры.НомерПартии=ВыборкаТовары.НомерПартии;
			Иначе
				ОбластьСтрока.Параметры.ЗНП="";
				ОбластьСтрока.Параметры.НомерПартии="СВЕРХ ПАРТИИ";
			КонецЕсли;
			ОбластьСтрока.Параметры.ЕдиницаИзмерения=ВыборкаТовары.Номенклатура.ЕдиницаХраненияОстатков.Наименование;
			ОбластьСтрока.Параметры.Количество=Формат(ВыборкаТовары.Количество,"ЧГ=");
			Таб.Вывести(ОбластьСтрока);
		КонецЦикла;
		ОбластьПодвал.Параметры.КоличествоВсего=Формат(ВыборкаПаллеты.Количество,"ЧГ=");
		Таб.ТолькоПросмотр=Истина;
		Таб.ОтображатьСетку=Ложь;
		Таб.ОтображатьЗаголовки=Ложь;
		Таб.Вывести(ОбластьПодвал);
		Таб.Вывести(ОбластьПодвалДляСклада);
		Таб.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЦикла;	
	Возврат Таб;
КонецФункции