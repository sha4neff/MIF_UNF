
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Для Каждого Версия Из Параметры.Версии Цикл
		НоваяСтрока = ВерсииЗапроса.Добавить();
		НоваяСтрока.Время = Версия.ДатаВерсии;
		
		РазностьВДнях = Цел((НачалоДня(ТекущаяДата()) - НачалоДня(Версия.ДатаВерсии))/(24*60*60));
		
		Если РазностьВДнях = 0 Тогда
			НоваяСтрока.ДатаСтрокой = "сегодня";
		ИначеЕсли РазностьВДнях = 1 Тогда
			НоваяСтрока.ДатаСтрокой = "вчера";
		Иначе	
			НоваяСтрока.ДатаСтрокой = Формат(Версия.ДатаВерсии,"ДФ=dd.MM.yyyy");
		КонецЕсли; 		
		
		НоваяСтрока.ТекстЗапроса = Версия.ТекстЗапроса;
		Для Каждого СтрокаПараметров Из Версия.Параметры Цикл
			НовПарам = НоваяСтрока.Параметры.Добавить();
			НовПарам.ИмяПараметра = СтрокаПараметров.ИмяПараметра;
			НовПарам.ЗначениеПараметра = СтрокаПараметров.ЗначениеПараметра;
		КонецЦикла;
		Если НоваяСтрока.Время = Параметры.ДатаТекВерсии Тогда
			НоваяСтрока.ЭтоАктуальный = Истина;
		КонецЕсли;
	КонецЦикла;
	ВерсииЗапроса.Сортировать("Время УБЫВ");
КонецПроцедуры

&НаКлиенте
Процедура ВерсииЗапросаПриАктивизацииСтроки(Элемент)
	ТекДанные = Элементы.ВерсииЗапроса.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = ТекДанные.ТекстЗапроса;
	ПараметрыЗапроса.Очистить();
	Для Каждого Пар Из ТекДанные.Параметры Цикл
		НовыйПараметр = ПараметрыЗапроса.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйПараметр,Пар);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВерсииЗапросаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Закрыть(ВерсииЗапроса.НайтиПоИдентификатору(ВыбраннаяСтрока).Время);
	СтандартнаяОбработка = Ложь;

КонецПроцедуры
