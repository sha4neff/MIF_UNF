
&НаКлиенте
Процедура ИзЗапроса(Команда)

	ТекстЗапроса = ВладелецФормы.ТекстЗапроса.ПолучитьТекст();
	Если ТекстЗапроса = "" Тогда
		Возврат;
	КонецЕсли;
	ТекстВопроса = "Перед заполнением будет очищена текущая настройка выборок.
	|Увы :(
	|Продолжить?";
	Если ТаблицаВыборок.Количество()>0 Тогда
		Если ВладелецФормы.Это82() Тогда
			Ответ = Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНетОтмена);
			ВыполнениеПослеВопроса(Ответ,Неопределено);
		Иначе
			Выполнить("
			|	Если ТаблицаВыборок.Количество() > 0 Тогда
			|		ОписаниеОповещения = Новый ОписаниеОповещения(""ВыполнениеПослеВопроса"",ЭтаФорма);
			|		ПоказатьВопрос(ОписаниеОповещения,ТекстВопроса,РежимДиалогаВопрос.ДаНетОтмена);
			|	КонецЕсли; ");
		КонецЕсли;
	Иначе
		ВыполнениеПослеВопроса(КодВозвратаДиалога.Да,Неопределено);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеПослеВопроса(Результат,ДополнительныеПараметры)    Экспорт
	Если Результат <> КодВозвратаДиалога.Да или Результат = Неопределено Тогда Возврат КонецЕсли; 

	ТаблицаВыборок.Очистить();
	ТекстЗапроса = ВладелецФормы.ТекстЗапроса.ПолучитьТекст();
	Если Найти(Врег(ТекстЗапроса),"ОБЩИЕ") Тогда
		НоваяСтрока = ТаблицаВыборок.Добавить();
		НоваяСтрока.Включена = Истина;
		НоваяСтрока.Группировки = "ОБЩИЕ";
		НоваяСтрока.Имя = "ОБЩИЕ";
		НоваяСтрока.ТипОбхода = "По группировкам";
	КонецЕсли; 
	ДозаполнитьНаСервере(ТекстЗапроса);	

КонецПроцедуры // Выполнение()

&НаСервере
Процедура ДозаполнитьНаСервере(ТекстЗапроса)

	Построитель = Новый ПостроительЗапроса;
	Построитель.Текст = ТекстЗапроса;
	Построитель.ЗаполнитьНастройки();
	СписокГруппировок = Новый СписокЗначений;
	Для каждого Измерение Из Построитель.Измерения Цикл
		НоваяСтрока = ТаблицаВыборок.Добавить();
		НоваяСтрока.Включена = Истина;
		НоваяСтрока.Группировки = Измерение.Имя;
		НоваяСтрока.Имя = Измерение.Имя;
		Если Измерение.ТипИзмерения = ТипИзмеренияПостроителяЗапроса.Элементы Тогда
			НоваяСтрока.ТипОбхода = ОбходРезультатаЗапроса.ПоГруппировкам;
		Иначе	
			НоваяСтрока.ТипОбхода = ОбходРезультатаЗапроса.ПоГруппировкамСИерархией;
		КонецЕсли; 
	КонецЦикла; 
	
	НоваяСтрока = ТаблицаВыборок.Добавить();
	НоваяСтрока.Включена = Истина;
	НоваяСтрока.Группировки = "";
	НоваяСтрока.Имя = "ДетальныеЗаписи";
	НоваяСтрока.ТипОбхода = ОбходРезультатаЗапроса.ПоГруппировкам;

КонецПроцедуры // ДозаполнитьНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ТЗВладельца = ВладелецФормы.ТаблицаВыборок;
	Для Каждого СтрокаТЗ Из ТЗВладельца Цикл
		НоваяСтрока = ТаблицаВыборок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТЗ);
	КонецЦикла;
	ПоказыватьСлужебныеКолонкиРезультата = ВладелецФормы.ПоказыватьСлужебныеКолонкиРезультата;
	СпособВыборки = ВладелецФормы.СпособВыборки;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	ТЗВладельца = ВладелецФормы.ТаблицаВыборок;
	ТЗВладельца.Очистить();
	Для Каждого СтрокаТЗ Из ТаблицаВыборок Цикл
		НоваяСтрока = ТЗВладельца.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТЗ);
	КонецЦикла;
	ВладелецФормы.ПоказыватьСлужебныеКолонкиРезультата = ПоказыватьСлужебныеКолонкиРезультата;
	ВладелецФормы.СпособВыборки = СпособВыборки;
КонецПроцедуры

&НаКлиенте
Процедура СпособВыборкиПриИзменении(Элемент)
	Если СпособВыборки = 1 Тогда
		ИзЗапроса(Неопределено);
	КонецЕсли;
КонецПроцедуры
