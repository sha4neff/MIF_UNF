Процедура ОбработчикОперации() Экспорт
	Если БЗ_TSDRU.Сессия().ПараметрыСессии.Режим="" Тогда
		Инициализация();
	Иначе
		Если БЗ_TSDRU.Запрос().action="" Тогда
			Выполнить(БЗ_TSDRU.Сессия().ПараметрыСессии.Режим+"_Активация()");
		Иначе
			Выполнить(БЗ_TSDRU.Сессия().ПараметрыСессии.Режим+"_ОбработчикСобытий()");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура АктивироватьРежим(Режим)
	БЗ_TSDRU.Сессия().ПараметрыСессии.Режим=Режим;
	Выполнить(Режим+"_Активация()");	
КонецПроцедуры

Процедура Инициализация()
	АктивироватьРежим("ВыборОперации");
КонецПроцедуры

//---- Выбор операции ----
Процедура ВыборОперации_Активация()
	Перем Шапка,Данные,Подвал,РядШапка,РядПодвал,Колонки,ТП;
	БЗ_TSDAPI.ИнициализацияПакетаОтвета(БЗ_TSDRU.ТипыФреймов().list);
	БЗ_TSDAPI.ИнициализацияПараметров(Шапка,Данные,Подвал,РядШапка,РядПодвал,Колонки,ТП);
	БЗ_TSDAPI.ПерехватСобытий("0066,0007,0008,0009,0010,0011,0012,0013,0014,0015,0016");
	
	ПараметрыДанных=БЗ_TSDRU.ПараметрыДанныхФреймаСписка();
	
	Данные=Новый Структура();
	Шапка=Новый Структура();
	Шапка.Вставить(РядШапка.hr1,БЗ_TSDAPI.РядАвторизацииПользователя());
	Данные.Вставить(ПараметрыДанных.hd,Шапка);
	ВыборОперации_Заполнить();
	Данные.Вставить(ПараметрыДанных.ls,ВыборОперации_ПолучитьСписок());
	
	// Автовход в единственную доступную операцию
	Если БЗ_TSDRU.ВременныеПеременные().КоличествоОпераций=1 И БЗ_TSDRU.Ответ().Свойство("play_sound") Тогда
		Выбрано=БЗ_TSDRU.ВременныеПеременные().ТБМенюОпераций[0];
		ИмяПеречисления = Выбрано.Операция.Метаданные().Имя;
		Индекс = Перечисления[ИмяПеречисления].Индекс(Выбрано.Операция);
		БЗ_TSDAPI.ВыполнитьОперацию(Метаданные.Перечисления[ИмяПеречисления].ЗначенияПеречисления[Индекс].Имя);
		Возврат;
	КонецЕсли; 
	
	БЗ_TSDRU.Ответ().payload=Данные;
КонецПроцедуры

Процедура ВыборОперации_Заполнить()
	ТБМенюОпераций=Новый ТаблицаЗначений();
	ТБМенюОпераций.Колонки.Добавить("Название",Новый ОписаниеТипов("Строка"));
	ТБМенюОпераций.Колонки.Добавить("Операция",Новый ОписаниеТипов("ПеречислениеСсылка.БЗ_ТСД_Операции"));
	ТБМенюОпераций.Колонки.Добавить("Системная",Новый ОписаниеТипов("Строка"));	
	ТБМенюОпераций.Колонки.Добавить("БыстрыйВход",Новый ОписаниеТипов("Строка"));
	
	ОперацииСчетчик=0;
	Для Каждого Операция Из БЗ_TSDRU.Сессия().Сотрудник.ДоступныеОперации Цикл		
		Если ЗначениеЗаполнено(Операция.Операция) Тогда
			ОперацииСчетчик=ОперацииСчетчик+1;
			Строка=ТБМенюОпераций.Добавить();	
			Строка.Название=ВРег(СокрЛП(Операция.Операция));
			Строка.Операция=Операция.Операция;
			Строка.БыстрыйВход=?(ОперацииСчетчик>8,"",Строка(ОперацииСчетчик));
		КонецЕсли;
	КонецЦикла;
	БЗ_TSDRU.ВременныеПеременные().Вставить("КоличествоОпераций",ОперацииСчетчик);
	
	//--
	ОперацииСчетчик=ОперацииСчетчик+1;
	Строка=ТБМенюОпераций.Добавить();	
	Строка.Название="СМЕНИТЬ СОТРУДНИКА";
	Строка.Системная="СменитьСотрудника";
	Строка.БыстрыйВход="9";
	//--
	ОперацииСчетчик=ОперацииСчетчик+1;
	Строка=ТБМенюОпераций.Добавить();	
	Строка.Название="ВЫХОД";
	Строка.Системная="Выход";
	Строка.БыстрыйВход="0";
	
	БЗ_TSDRU.ВременныеПеременные().Вставить("Позиция",0);
	Если БЗ_TSDRU.ВременныеПеременные().Свойство("ВыбраннаяОперация") Тогда
		Строка=ТБМенюОпераций.Найти(БЗ_TSDRU.ВременныеПеременные().ВыбраннаяОперация,"Операция");
		Если ЗначениеЗаполнено(Строка) Тогда
			БЗ_TSDRU.ВременныеПеременные().Вставить("Позиция",ТБМенюОпераций.Индекс(Строка));
		КонецЕсли;
	КонецЕсли;
	БЗ_TSDRU.ВременныеПеременные().Вставить("ТБМенюОпераций",ТБМенюОпераций);	
КонецПроцедуры

Функция ВыборОперации_ПолучитьСписок(Обновление=Ложь)
	Список=БЗ_TSDAPI.ШаблонСписокМеню(Обновление);
	ТБСтроки=Новый Массив();
	Поз=0;
	Для Каждого Выборка Из БЗ_TSDRU.ВременныеПеременные().ТБМенюОпераций Цикл		
		Если ЗначениеЗаполнено(Выборка.Системная) Тогда
			ТБСтроки.Добавить(Новый Структура("id,c1,c2,tp",Поз,Выборка.БыстрыйВход,Выборка.Название,1));
		Иначе
			ТБСтроки.Добавить(Новый Структура("id,c1,c2",Поз,Выборка.БыстрыйВход,Выборка.Название));
		КонецЕсли;		
		Поз=Поз+1;
	КонецЦикла;
	Список.Вставить("ct",ТБСтроки);
	Если БЗ_TSDRU.ВременныеПеременные().Свойство("Позиция") Тогда
		Список.Вставить("sl",БЗ_TSDRU.ВременныеПеременные().Позиция);
	Иначе
		Список.Вставить("sl",0);
	КонецЕсли;
	Список.Вставить("sl_tp",1); // Один тач = выбор
	Возврат Список;
КонецФункции

Процедура ВыборОперации_Выбор(Выбрано)
	Если ЗначениеЗаполнено(Выбрано.Операция) Тогда		
		ИмяПеречисления = Выбрано.Операция.Метаданные().Имя;
		Индекс = Перечисления[ИмяПеречисления].Индекс(Выбрано.Операция);
		БЗ_TSDAPI.ВыполнитьОперацию(Метаданные.Перечисления[ИмяПеречисления].ЗначенияПеречисления[Индекс].Имя);
	Иначе
		Если Выбрано.Системная="СменитьСотрудника" Тогда	
			БЗ_TSDRU.Сессия().Сотрудник=Справочники.БЗ_Сотрудники_ТСД.ПустаяСсылка();
			БЗ_TSDAPI.ВыполнитьОперацию("Авторизация");
			Возврат;
		ИначеЕсли Выбрано.Системная="Выход" Тогда
			БЗ_TSDAPI.ИнициализацияПакетаОтвета(БЗ_TSDRU.ТипыФреймов().disconnect);
			БЗ_TSDRU.Ответ().result=0;
			Возврат;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

Процедура ВыборОперации_ОбработчикСобытий()
	СТЗапрос=БЗ_TSDRU.Запрос();
	Если СТЗапрос.action="select" Тогда
		Выбрано=БЗ_TSDAPI.ПолучитьВыбраннуюСтроку(БЗ_TSDRU.ВременныеПеременные().ТБМенюОпераций);
		Если Выбрано=Неопределено Тогда
			БЗ_TSDAPI.ПустойОтветНаЗапрос();
			Возврат                                                                                  
		КонецЕсли;
		ВыборОперации_Выбор(Выбрано);
	ИначеЕсли СТЗапрос.action="key" Тогда
		БП=Число(Лев(СТЗапрос.data,4))-7;
		Выбрано=БЗ_TSDRU.ВременныеПеременные().ТБМенюОпераций.Найти(Строка(БП),"БыстрыйВход");
		Если Выбрано=Неопределено Тогда
			БЗ_TSDAPI.ПустойОтветНаЗапрос();
			Возврат;
		КонецЕсли;
		ВыборОперации_Выбор(Выбрано);
	Иначе
		БЗ_TSDAPI.ПустойОтветНаЗапрос();
	КонецЕсли;	
КонецПроцедуры   