&НаСервере
Функция ПечатьМаркировкиНаСервере()
	Записать();
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Маркировка_Принтера";
	ТабДокумент.ПолеСверху = 0;
	ТабДокумент.ПолеСлева = 0;
	ТабДокумент.ПолеСнизу = 0;
	ТабДокумент.ПолеСправа = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДокумент.ТолькоПросмотр = Истина;
	ТабДокумент.ОтображатьСетку = Ложь;	
	ТабДокумент.ОтображатьЗаголовки=Ложь;	
	ТабДокумент.АвтоМасштаб=Ложь;
	Макет = Справочники.БЗ_Принтеры.ПолучитьМакет("Маркировка");
	ОбластьМакета = Макет.ПолучитьОбласть("Маркировка");
	ОбластьМакета.Параметры.Наименование=Объект.Наименование;
	ОбластьМакета.Параметры.ШКод=БЗ_Штрихкодирование.CODE128Строка(БЗ_Штрихкодирование.ПолучитьШК_Принтера(Объект.Ссылка));
	ТабДокумент.Вывести(ОбластьМакета);	
	
	ЭлементКоллекцииДляПечати = Новый Структура;
	Для Каждого ИмяКолонки Из УправлениеПечатьюКлиентСервер.ИменаПолейКоллекцииПечатныхФорм() Цикл
		ЭлементКоллекцииДляПечати.Вставить(ИмяКолонки);
	КонецЦикла;
	
	ЭлементКоллекцииДляПечати.ТабличныйДокумент = ТабДокумент;
	ЭлементКоллекцииДляПечати.СинонимМакета = "Печать маркировки принтера";
	ЭлементКоллекцииДляПечати.ИмяМакета = "ПечатьМаркировкиПринтера";
	ЭлементКоллекцииДляПечати.ИмяВРЕГ = ЭлементКоллекцииДляПечати.ИмяМакета;
	ЭлементКоллекцииДляПечати.Экземпляров = 1;
	
	СтруктураВозврат = Новый Структура;
	СтруктураВозврат.Вставить("ЭлементКоллекцииДляПечати", ЭлементКоллекцииДляПечати);
	МассивОбъектов = Новый Массив;
	СтруктураВозврат.Вставить("ИсточникДанных", МассивОбъектов);	
	Возврат СтруктураВозврат;	
КонецФункции

&НаКлиенте
Процедура ПечатьМаркировки(Команда)
	СтруктураВозврат = ПечатьМаркировкиНаСервере();
	КоллекцияПечатныхФорм = Новый Массив;
	КоллекцияПечатныхФорм.Добавить(СтруктураВозврат.ЭлементКоллекцииДляПечати);
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("ПереопределитьПользовательскиеНастройкиКоличества", Истина);
	
	ПараметрыОткрытия = Новый Структура("КоллекцияПечатныхФорм,ИсточникДанных,ПараметрыПечати");
	ПараметрыОткрытия.КоллекцияПечатныхФорм = КоллекцияПечатныхФорм;
	ПараметрыОткрытия.ИсточникДанных = КоллекцияПечатныхФорм;
	ПараметрыОткрытия.ПараметрыПечати	 = ПараметрыПечати;
	
	ОткрытьФорму("ОбщаяФорма.ПечатьДокументов", ПараметрыОткрытия, ЭтаФорма, Строка(Новый УникальныйИдентификатор));	
КонецПроцедуры
