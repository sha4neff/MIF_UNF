
#Область СлужебныйПрограммныйИнтерфейс

Процедура ПолучитьПредставлениеПолейДокумента(Источник, Поля, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Ссылка");
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	Поля.Добавить("Проведен");
	Поля.Добавить("ПометкаУдаления");
	
КонецПроцедуры

Процедура ПолучитьПредставлениеПолейДокументаТолькоЗапись(Источник, Поля, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Ссылка");
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	Поля.Добавить("ПометкаУдаления");
	
КонецПроцедуры

Процедура ПолучитьПредставлениеДокумента(Источник, Данные, Представление, СтандартнаяОбработка) Экспорт
	
	Если Данные.Номер = Null ИЛИ Не ЗначениеЗаполнено(Данные.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ШаблонПредставления = НСтр("ru='%1 %2 от %3 %4'");
	
	ПредставлениеОбъекта = Данные.Ссылка.Метаданные().Представление();
	
	Если Данные.Свойство("Номер") Тогда
		ПредставлениеНомера = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Данные.Номер, Истина, Истина);
	Иначе
		ПредставлениеНомера = "";
	КонецЕсли;
	
	ПредставлениеДаты = Формат(Данные.Дата, "ДЛФ=D");
	
	Если Данные.ПометкаУдаления Тогда
		ПредставлениеСостояния = НСтр("ru='(удален)'");
	ИначеЕсли Данные.Свойство("Проведен") И НЕ Данные.Проведен Тогда
		ПредставлениеСостояния = НСтр("ru='(не проведен)'");
	Иначе
		ПредставлениеСостояния = "";
	КонецЕсли;
	
	Представление = СтрШаблон(ШаблонПредставления, ПредставлениеОбъекта, ПредставлениеНомера, ПредставлениеДаты,
		ПредставлениеСостояния);
	
КонецПроцедуры

Процедура ПередЗаписьюДокумента(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Хозяйственные операции
	ДокументМетаданные = Источник.Метаданные();
	Если ДокументМетаданные.Реквизиты.Найти("ХозяйственнаяОперация") <> Неопределено Тогда
		Если ДокументМетаданные.Реквизиты.Найти("ВидОперации") <> Неопределено Тогда
			Источник.ХозяйственнаяОперация = ХозяйственнаяОперация(Источник.ВидОперации, Источник.Ссылка);
		ИначеЕсли ДокументМетаданные.Реквизиты.Найти("ВидДоговора") <> Неопределено Тогда
			Источник.ХозяйственнаяОперация = ХозяйственнаяОперация(Источник.ВидДоговора, Источник.Ссылка);
		Иначе
			Источник.ХозяйственнаяОперация = ХозяйственнаяОперация(ДокументМетаданные.Имя, Источник.Ссылка);
		КонецЕсли;
	КонецЕсли;
	// Конец Хозяйственные операции
	
	УправлениеНебольшойФирмойСобытияПереопределяемый.ПередЗаписьюДокумента(Источник, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ХозяйственнаяОперация(ВидОперации, СсылкаНаДокумент)
	
	Возврат ХозяйственныеОперацииСервер.ХозяйственнаяОперацияПоВидуОперацииДокумента(ВидОперации, СсылкаНаДокумент);
	
КонецФункции

#КонецОбласти