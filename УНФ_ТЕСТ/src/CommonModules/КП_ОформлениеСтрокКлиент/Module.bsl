// Общий модуль (выполняется на стороне клиента) продукта "Конструктор процессов для 1С:УНФ"
// Разработчик Компания "Аналитика. Проекты и решения" +7 495 005-1653, https://kp-unf.ru

Процедура УстановитьОформлениеНаименования(ЭлементФормы, МассивНастройкиЦветов) Экспорт
	
	Если МассивНастройкиЦветов=Неопределено ИЛИ МассивНастройкиЦветов.Количество()<>4 Тогда
		//настройки не было
		ЦветТекста=Новый Цвет;
		ЦветФона=Новый Цвет;
		ШрифтЖирный=Ложь;
		ШрифтНаклонный=Ложь;
		
		ЭлементФормы.ЦветФона=ЦветФона;
		ЭлементФормы.ЦветТекста=ЦветТекста;
		
	Иначе	
		СтруктураЦветаТекста=КП_ОформлениеСтрокСервер.ПолучитьСтруктуруЦвета(МассивНастройкиЦветов[0]);
		Если СтруктураЦветаТекста<>Неопределено Тогда
			ЦветТекста=Новый Цвет(СтруктураЦветаТекста.Red, СтруктураЦветаТекста.Green, СтруктураЦветаТекста.Blue);	
			ЭлементФормы.ЦветТекста=ЦветТекста;
		КонецЕсли;

		СтруктураЦветаФона=КП_ОформлениеСтрокСервер.ПолучитьСтруктуруЦвета(МассивНастройкиЦветов[1]);
		Если СтруктураЦветаФона<>Неопределено Тогда
			ЦветФона=Новый Цвет(СтруктураЦветаФона.Red, СтруктураЦветаФона.Green, СтруктураЦветаФона.Blue);
			ЭлементФормы.ЦветФона=ЦветФона;
		КонецЕсли;
		
		ШрифтЖирный=МассивНастройкиЦветов[2];
		ШрифтНаклонный=МассивНастройкиЦветов[3];
		
	КонецЕсли;
				
	ШрифтЗаголовка=Новый Шрифт(,,ШрифтЖирный, ШрифтНаклонный);
	ЭлементФормы.Шрифт=ШрифтЗаголовка;
	
КонецПроцедуры

Процедура Оповещение_ЦветВыбран(СтруктураРезультат, ДопПараметры) Экспорт
	
	Если СтруктураРезультат=Неопределено ИЛИ ТипЗнч(СтруктураРезультат)<>Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементОформления=ДопПараметры.ОформляемыйЭлемент;
	ЭтаФорма=ДопПараметры.ЭтаФорма;	
	
	АдресХраненияНастройкиЦветов=ДопПараметры.АдресХраненияНастройкиЦветов;
	
	МассивНастройкиЦветов=Новый Массив;
	
	Если НЕ (СтруктураРезультат.Свойство("РежимПоУмолчанию") И СтруктураРезультат.РежимПоУмолчанию) Тогда
		МассивНастройкиЦветов.Добавить(СтруктураРезультат.ЦветТекста);
		МассивНастройкиЦветов.Добавить(СтруктураРезультат.ЦветФона);
		МассивНастройкиЦветов.Добавить(СтруктураРезультат.ШрифтЖирный);
		МассивНастройкиЦветов.Добавить(СтруктураРезультат.ШрифтНаклонный);		
	Иначе
		МассивНастройкиЦветов=Неопределено;
		
	КонецЕсли;
		
	КП_ОформлениеСтрокКлиент.УстановитьОформлениеНаименования(ЭлементОформления, МассивНастройкиЦветов);
	
	ПоместитьВоВременноеХранилище(МассивНастройкиЦветов, АдресХраненияНастройкиЦветов);	
	ЭтаФорма.Модифицированность=Истина;	
	
КонецПроцедуры

Процедура Оповещение_ЦветДляСпискаВыбран(СтруктураРезультат, ДопПараметры) Экспорт
	
	Если СтруктураРезультат=Неопределено ИЛИ ТипЗнч(СтруктураРезультат)<>Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	МассивОбъектов=ДопПараметры.МассивОбъектов;
	ТекПользователь=ДопПараметры.ТекущийПользователь;
	
	Если СтруктураРезультат.Свойство("РежимПоУмолчанию") И СтруктураРезультат.РежимПоУмолчанию Тогда
		СписокУдаляемыхНастроек=Новый СписокЗначений;
		Для Каждого ЭлементМассива Из МассивОбъектов Цикл
			Если СписокУдаляемыхНастроек.НайтиПоЗначению(ЭлементМассива)=Неопределено Тогда
				СписокУдаляемыхНастроек.Добавить(ЭлементМассива);
			КонецЕсли;
		КонецЦикла;
		
		////КП_ОформлениеСтрокСервер.УдалитьОформлениеСпискаОбъектов(СписокУдаляемыхНастроек, ТекПользователь);
		
	Иначе	
		СтруктураЦвета=Новый Структура;
		СтруктураЦвета.Вставить("ЦветТекста", СтруктураРезультат.ЦветТекста);
		СтруктураЦвета.Вставить("ЦветФона", СтруктураРезультат.ЦветФона);
		СтруктураЦвета.Вставить("ШрифтЖирный", СтруктураРезультат.ШрифтЖирный);
		СтруктураЦвета.Вставить("ШрифтНаклонный", СтруктураРезультат.ШрифтНаклонный);
		
		Для Каждого ЭлементМассива Из МассивОбъектов Цикл
			КП_ОформлениеСтрокСервер.ЗаписатьСтруктуруЦветаОбъекта(ЭлементМассива, СтруктураЦвета, ТекПользователь);
		КонецЦикла;
		
	КонецЕсли;
	
	ЭлементСписок=ДопПараметры.ЭлементСписок;
	ЭлементСписок.Обновить();
	
	Если ДопПараметры.Свойство("ЭлементСписок2") Тогда
		ЭлементСписок2=ДопПараметры.ЭлементСписок2;
		ЭлементСписок2.Обновить();
	КонецЕсли;
			
КонецПроцедуры
