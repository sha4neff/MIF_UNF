
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	ЗагрузкаДанныхИзВнешнегоИсточника.ПриСозданииНаСервере(Метаданные.Справочники.Номенклатура, НастройкиЗагрузкиДанных, ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, РезультатЗагрузки, Источник)
	
	Если ИмяСобытия = "ОбработатьПодготовленныеДанные" 
		И ТипЗнч(РезультатЗагрузки) = Тип("Структура") 
		И РезультатЗагрузки.НастройкиЗагрузкиДанных.Свойство("ЧерезФормуБыстрыйСтартЗагрузкаИзExcel")
		Тогда
		
		ДлительнаяОперация = ОбработатьПодготовленныеДанные(РезультатЗагрузки);
		ПроверитьНеобходимостьПодключенияОбработчикаОжидания(ДлительнаяОперация, РезультатЗагрузки.НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения);
	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Декорация2Нажатие(Элемент)
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура Декорация9Нажатие(Элемент)

	ОткрытьФорму("Справочник.Контрагенты.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура Декорация5Нажатие(Элемент)

	ОткрытьФорму("Справочник.ВидыЦен.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура Декорация14Нажатие(Элемент)

	ОткрытьФорму("Документ.ВводНачальныхОстатков.Форма.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрайсЛист(Элемент)
	
	КонтекстОткрытия = КонтекстОткрытияПрайсЛиста();
	
	ИмяОткрываемойФормы = "Справочник.ПрайсЛисты.ФормаСписка";
	Если КонтекстОткрытия.ЕстьПравоДобавленияНового
		И (КонтекстОткрытия.КоличествоПрайсЛистов < 2
			ИЛИ КонтекстОткрытия.ОткрыватьПрайсЛистОрганизацииКакСписок = Ложь) Тогда
		
		ИмяОткрываемойФормы = "Обработка.ПрайсЛистыОрганизации.Форма";
		
	КонецЕсли;
	
	ОткрытьФорму(ИмяОткрываемойФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗакрытьНажатие(Элемент)
	
	ДекорацияЗакрытьНаСервере();
	ОбновитьИнтерфейс();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	
	НастройкиЗагрузкиДанных.Вставить("ИмяМакетаСШаблоном", "ЗагрузкаИзФайла");
	НастройкиЗагрузкиДанных.Вставить("ЗагрузкаБыстрыйСтарт", Истина);
	НастройкиЗагрузкиДанных.Вставить("ЧерезФормуБыстрыйСтартЗагрузкаИзExcel", Истина);
	НастройкиЗагрузкиДанных.Вставить("ОписаниеСтрокиВыбора", Новый Структура("ПолноеИмяОбъектаМетаданных, Тип", "Номенклатура", "ПрикладнаяЗагрузка"));
	
	ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных,, ЭтотОбъект);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОбработатьПодготовленныеДанные(РезультатЗагрузки)
	
	ПараметрыВызоваСервера = Новый Структура;
	ПараметрыВызоваСервера.Вставить("НастройкиЗагрузкиДанных", РезультатЗагрузки.НастройкиЗагрузкиДанных);
	ПараметрыВызоваСервера.Вставить("ТаблицаСопоставленияДанных", ДанныеФормыВЗначение(РезультатЗагрузки.ТаблицаСопоставленияДанных, Тип("ТаблицаЗначений")));
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Подсистема ЗагрузкаДанныхИзВнешнегоИсточника: Выполнение серверного метода загрузки результата'");
	ПараметрыВыполнения.ЗапуститьНеВФоне    = Ложь;
	ПараметрыВыполнения.ЗапуститьВФоне      = Истина;	
	
	Если РезультатЗагрузки.НастройкиЗагрузкиДанных.ЭтоЗагрузкаСправочника Тогда
		ИмяМетода = СтрЗаменить(РезультатЗагрузки.НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения, "Справочник.", "Справочники.")
			+ ".ОбработатьПодготовленныеДанные";
	ИначеЕсли РезультатЗагрузки.НастройкиЗагрузкиДанных.ЭтоЗагрузкаРегистраСведений Тогда
		ИмяМетода = СтрЗаменить(РезультатЗагрузки.НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения, "РегистрСведений.", "РегистрыСведений.")
			+ ".ОбработатьПодготовленныеДанные";
	КонецЕсли;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыВызоваСервера, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПроверитьНеобходимостьПодключенияОбработчикаОжидания(ДлительнаяОперация, ПолноеИмяОбъектаЗаполнения)
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ПослеВыполненияФоновогоЗадания(ДлительнаяОперация, ПолноеИмяОбъектаЗаполнения);
		
	Иначе
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеВыполненияФоновогоЗадания", ЭтотОбъект, ПолноеИмяОбъектаЗаполнения);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Идет загрузка данных.'");
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияФоновогоЗадания(Прогресс, ПолноеИмяОбъектаЗаполнения) Экспорт
	
	Если ЗначениеЗаполнено(ПолноеИмяОбъектаЗаполнения) Тогда
		ОткрытьФорму(ПолноеИмяОбъектаЗаполнения + ".ФормаСписка");
	КонецЕсли;
	ПоказатьПредупреждение(,Нстр("ru ='Загрузка данных завершена.'"));
	
КонецПроцедуры

&НаСервере
Процедура ДекорацияЗакрытьНаСервере()

	КлючОбъекта         = "Общее/НастройкиНачальнойСтраницы";
	КлючОбъектаХранения = "Общее/НастройкиНачальнойСтраницыПередОчисткой";
	СохраненныеНастройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъектаХранения, "");
	НастройкиСохранены   = ТипЗнч(СохраненныеНастройки) = Тип("ХранилищеЗначения");
	
	Если НастройкиСохранены Тогда
		ТекущиеНастройки = СохраненныеНастройки.Получить();
	Иначе
		ТекущиеНастройки = ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта);
	КонецЕсли;
	Если ТипЗнч(ТекущиеНастройки) = Тип("НастройкиНачальнойСтраницы") Тогда
		СоставФорм = ТекущиеНастройки.ПолучитьСоставФорм();
	Иначе
		СоставФорм = Новый СоставФормНачальнойСтраницы;
	КонецЕсли;
	
	БыстрыйСтартЗагрузкаИзExcel = СоставФорм.ЛеваяКолонка.Найти("Обработка.БыстрыйСтартЗагрузкаИзExcel.Форма.Форма");
	Если БыстрыйСтартЗагрузкаИзExcel <> Неопределено Тогда
		СоставФорм.ЛеваяКолонка.Удалить(БыстрыйСтартЗагрузкаИзExcel);
	КонецЕсли; 
	
	НовыеНастройки = Новый НастройкиНачальнойСтраницы;
	СоставФормВМетаданных = НовыеНастройки.ПолучитьСоставФорм();
	
	ТекущиеНастройки = Новый НастройкиНачальнойСтраницы;
	ТекущиеНастройки.УстановитьСоставФорм(СоставФорм);
	
	Если НастройкиСохранены Тогда
		СохраняемыеНастройки = Новый ХранилищеЗначения(ТекущиеНастройки);
		ХранилищеСистемныхНастроек.Сохранить(КлючОбъектаХранения, "", СохраняемыеНастройки);
	Иначе
		ХранилищеСистемныхНастроек.Сохранить(КлючОбъекта, "", ТекущиеНастройки);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция КонтекстОткрытияПрайсЛиста()
	
	КонтекстОткрытия = Новый Структура;
	КонтекстОткрытия.Вставить("КоличествоПрайсЛистов", Справочники.ПрайсЛисты.КоличествоПрайсЛистовПользователя());
	КонтекстОткрытия.Вставить("ОткрыватьПрайсЛистОрганизацииКакСписок", УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОткрыватьПрайсЛистОрганизацииКакСписок"));
	КонтекстОткрытия.Вставить("ЕстьПравоДобавленияНового", ПравоДоступа("Добавление", Метаданные.Справочники.ПрайсЛисты));
	
	Возврат КонтекстОткрытия;
	
КонецФункции

#КонецОбласти

