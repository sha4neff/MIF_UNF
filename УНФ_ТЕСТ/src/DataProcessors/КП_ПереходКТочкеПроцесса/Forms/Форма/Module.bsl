&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("БизнесПроцесс", Объект.БизнесПроцесс);
	
	ФлагОткрытоИзПроцесса=ЗначениеЗаполнено(Объект.БизнесПроцесс);
	
	Если ФлагОткрытоИзПроцесса Тогда
		ПроцессОписание=СокрЛП(Объект.БизнесПроцесс);
	КонецЕсли;
	
	Элементы.БизнесПроцесс.Видимость=НЕ ФлагОткрытоИзПроцесса;
	
	Объект.ТипТочки=Перечисления.КП_ВидыТочекБизнесПроцесса.Действие;
	
	УстановитьОформлениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура БизнесПроцессПриИзменении(Элемент)
	Объект.ТочкаПроцесса=Неопределено;	
	УстановитьОформлениеЭлементов();
	
КонецПроцедуры

Процедура УстановитьОформлениеЭлементов()
	Если ЗначениеЗаполнено(Объект.БизнесПроцесс) Тогда
		Элементы.ТипТочки.Доступность=Истина;
	Иначе
		Элементы.ТипТочки.Доступность=Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ТипТочки) И ЗначениеЗаполнено(Объект.БизнесПроцесс) Тогда
		Элементы.ТочкаПроцесса.Доступность=Истина;
	Иначе
		 Элементы.ТочкаПроцесса.Доступность=Ложь;
	 КонецЕсли;
	 
 КонецПроцедуры

&НаКлиенте
Процедура ТипТочкиПриИзменении(Элемент)
	Объект.ТочкаПроцесса=Неопределено;
	УстановитьОформлениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ТочкаПроцессаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;
	
	ПараметрыФормы=Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	ПараметрыОтбора=Новый Структура("ВладелецТочки", Объект.БизнесПроцесс);
	ПараметрыОтбора.Вставить("ТипТочки", Объект.ТипТочки);
	
	ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
	ОткрытьФорму("Справочник.КП_ТочкиПроцессов.ФормаВыбора", ПараметрыФормы,,,,,Новый ОписаниеОповещения("Подключаемый_ОбработатьОткрытиеТочки", ЭтотОбъект, Новый Структура) ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
 КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ОбработатьОткрытиеТочки(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ТочкаПроцесса=Результат;
	
	Если ЗначениеЗаполнено(Объект.ТочкаПроцесса) Тогда
		ВладелецТочки=КП_ОбщееСервер.ПолучитьЗначениеРеквизитаОбъекта(Объект.ТочкаПроцесса, "ВладелецТочки");
		ПроцессОписание=СокрЛП(ВладелецТочки);
		Объект.БизнесПроцесс=ВладелецТочки;
	Иначе
		Объект.БизнесПроцесс="";
	КонецЕсли;
	
	УстановитьОформлениеЭлементов();
	
	Элементы.ФормаПерейтиКТочке.КнопкаПоУмолчанию=ЗначениеЗаполнено(Объект.ТочкаПроцесса);

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКТочке(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.БизнесПроцесс) Тогда
		ПоказатьПредупреждение(Неопределено, "Сначала укажите бизнес-процесс",,НСтр("ru='Аналитика: Service Desk';en='Analitica: Service Desk';"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ТочкаПроцесса) Тогда
		ПоказатьПредупреждение(Неопределено, "Укажите точку перехода",,НСтр("ru='Аналитика: Service Desk';en='Analitica: Service Desk';"));
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("Подключаемый_ВыполнитьПереходКТочке", ЭтотОбъект, Новый Структура), "Выполнить переход процесса к точке """+СокрЛП(Объект.ТочкаПроцесса)+""" и продолжить выполнение процесса?", РежимДиалогаВопрос.ДаНет, 60,, НСтр("ru='Аналитика: Service Desk';en='Analitica: Service Desk';"));
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереходКТочке(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса=КодВозвратаДиалога.Нет Тогда
        Возврат;
        
	КонецЕсли;
	
	Если ВыполнитьПереходКТочкеСервером() Тогда
		//запишем сообщение в журнал процесса
		КП_Процессы.ЗаписатьВЖурналПроцесса("Выполнен принудительный переход к точке """+СокрЛП(Объект.ТочкаПроцесса)+""" из обработки перехода к точке", Объект.БизнесПроцесс, Объект.ТочкаПроцесса, , 2);
		
		Если ФлагОткрытоИзПроцесса Тогда
			Оповестить("БизнесПроцесс", "Обновить", Объект.БизнесПроцесс);
			ПоказатьПредупреждение(Неопределено, "Переход процесса к точке """+СокрЛП(Объект.ТочкаПроцесса)+""" выполнен.");
		Иначе
			ПоказатьВопрос(Новый ОписаниеОповещения("Подключаемый_ОткрытьПроцессПослеВопроса", ЭтотОбъект, Новый Структура), "Переход процесса к точке """+СокрЛП(Объект.ТочкаПроцесса)+""" выполнен."+Символы.ПС+"Открыть бизнес-процесс?", РежимДиалогаВопрос.ДаНет, 60,, НСтр("ru='Аналитика: Service Desk';en='Analitica: Service Desk';"));
		КонецЕсли;
		
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию=Истина;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьПроцессПослеВопроса(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса=КодВозвратаДиалога.Нет Тогда
        Возврат;
        
	КонецЕсли;
	
	ПараметрыФормы=Новый Структура("Ключ", Объект.БизнесПроцесс);
	ОткрытьФорму("БизнесПроцесс.КП_БизнесПроцесс.ФормаОбъекта",ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПереходКТочкеСервером()
	
	УстановитьПривилегированныйРежим(Истина);
	
	//обработаем регистр состояний точек
	РегСведений=РегистрыСведений.КП_ОбработкаТочек.СоздатьНаборЗаписей();
	РегСведений.Отбор.БизнесПроцесс.Установить(Объект.БизнесПроцесс);
	РегСведений.Прочитать();
	
	Для н=0 По РегСведений.Количество()-1 Цикл
		Запись=РегСведений[н];
		Если Запись.ТочкаКБП=Объект.ТочкаПроцесса Тогда
			Запись.Состояние=Перечисления.КП_СостояниеТочек.НеВыполняется;
		Иначе
			Если Запись.Состояние=Перечисления.КП_СостояниеТочек.Выполняется Тогда
				Запись.Состояние=Перечисления.КП_СостояниеТочек.Выполнена;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		РегСведений.Записать(Истина);
	Исключение
		КП_ОбщееСервер.СообщитьОбОшибке(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
			
	//удалим все не выполненные задачи
	Запрос=Новый Запрос("ВЫБРАТЬ
	                    |	КП_ЗадачиТочек.Задача КАК Задача
	                    |ИЗ
	                    |	РегистрСведений.КП_ЗадачиТочек КАК КП_ЗадачиТочек
	                    |ГДЕ
	                    |	КП_ЗадачиТочек.ТочкаКБП.ВладелецТочки = &ВладелецТочки
	                    |	И КП_ЗадачиТочек.ТочкаКБП.ПометкаУдаления = ЛОЖЬ");
						
	Запрос.УстановитьПараметр("ВладелецТочки", Объект.БизнесПроцесс);
	Выборка=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.Прямой);
	
	Пока Выборка.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(Выборка.Задача) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗадачаОбъект=Выборка.Задача.ПолучитьОбъект();

		Попытка
			ЗадачаОбъект.УстановитьПометкуУдаления(Истина);
		Исключение
			КП_ОбщееСервер.СообщитьОбОшибке(ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
		
	//обработаем маршрут процесса для перехода к точке
	КП_Процессы.ОбработатьМаршрутКБП(Объект.БизнесПроцесс, Объект.ТочкаПроцесса);
	
	Возврат Истина;
	
КонецФункции
