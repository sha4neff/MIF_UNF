

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.ЗаголовокФормы;
	Элементы.Строка.МногострочныйРежим = Параметры.Многострочность;
	Обязательность = Параметры.Обязательность;
	КомментарийОбязательностиВвода = Параметры.КомментарийОбязательностиВвода;
	Элементы.ОК.Заголовок = Параметры.НазваниеКнопкиПоУмолчанию;
	
	ПредставлениеДанных = Параметры.ПредставлениеДанных;
	Если Не ЗначениеЗаполнено(ПредставлениеДанных) Тогда
		ПредставлениеДанных = НСтр("ru = 'Обрабатываемые данные'");
	КонецЕсли;
	Если Параметры.Данные <> Неопределено Тогда
		Для каждого ЭлементДанных Из Параметры.Данные Цикл
			Данные.Добавить(ЭлементДанных, ЭлементДанных);
		КонецЦикла;
		Элементы.ПредставлениеДанных.Видимость = Истина;
		Если Параметры.ОбработчикПолученияПредставлений <> "" И Данные.Количество() = 1 Тогда
			ПараметрыВычисления = Новый Структура;
			ПараметрыВычисления.Вставить("Данные", Параметры.Данные);
			ПредставленияДанных = ОбщегоНазначения.ВычислитьВБезопасномРежиме(
				Параметры.ОбработчикПолученияПредставлений + "(Параметры.Данные)",
				ПараметрыВычисления);
			ПредставлениеДанных = ПредставленияДанных[Параметры.Данные[0]];
		Иначе 
			ПредставлениеДанных = ПредставлениеДанных + СтрШаблон(" (%1)", Данные.Количество());
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Элементы.ГруппаМК.Видимость = Истина;
		ОбменСКонтрагентамиСлужебный.ПереместитьЭлемент(Элементы, "ОК", "ГруппаМК");
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОК", "Шрифт", ШрифтыСтиля.КрупныйШрифтТекста);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОК", "ГоризонтальноеПоложениеВГруппе", ГоризонтальноеПоложениеЭлемента.Центр);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОК", "РастягиватьПоГоризонтали", Истина);
		ОбменСКонтрагентамиСлужебный.ПереместитьЭлемент(Элементы, "Отмена", "ГруппаМК");
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отмена", "ГоризонтальноеПоложениеВГруппе", ГоризонтальноеПоложениеЭлемента.Центр);
		Элементы.Строка.ЦветФона = ЦветаСтиля.ЦветФонаПоляВводаМКБЭД;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставлениеДанныхНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСКонтрагентамиДиагностикаКлиент.ПоказатьДанныеВСписке(Данные.ВыгрузитьЗначения(),
		"ОбменСКонтрагентамиВнутренний.ДанныеПриглашений", ПредставлениеДанных);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Обязательность И Не ЗначениеЗаполнено(Строка) Тогда
		ТекстСообщения = ?(ЗначениеЗаполнено(КомментарийОбязательностиВвода), КомментарийОбязательностиВвода, НСтр("ru = 'Необходимо ввести текст'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат; 
	КонецЕсли;
	
	Закрыть(Строка);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти
