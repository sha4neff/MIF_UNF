
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ПараметрыЗаполнения") Тогда
		Объект.ИсходнаяЗадача=Параметры.ПараметрыЗаполнения.ИсходнаяЗадача;
		ЗаполнитьПараметрыЗадания(Объект.ИсходнаяЗадача);
		
	КонецЕсли;
	
	ТекущийПользователь=Пользователи.ТекущийПользователь();
  	ШрифтПользователя=КП_ОбщееСервер.ПолучитьШрифтПользователя();
	Элементы.ТекстЗадачи.Шрифт=ШрифтПользователя;

КонецПроцедуры

&НаКлиенте
Процедура ТекстЗаданияПриИзменении(Элемент)
	ТекстЗадачиИзменен=Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьПереадресацию(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ИсходнаяЗадача) Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("en='Initial task is not specified.';ru='Внимание! Не указана исходная задача для переадресации.'"), 60, КП_ОбщееКлиент.ЗаголовокДиалога());
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Исполнитель) Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("en='New user is not specified.';ru='Внимание! Не указан новый исполнитель для переадресации.'"), 60, КП_ОбщееКлиент.ЗаголовокДиалога());
		Возврат;
	КонецЕсли;
		
	ТекстВопроса=НСтр("ru='Переадресовать задачу '; en='Do you want to forward task to ';")+КП_ОбщееСервер.Падеж(СокрЛП(Объект.Исполнитель), 3)+"?";
			
	ПоказатьВопрос(Новый ОписаниеОповещения("ВыполнитьПереадресациюЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, , КП_ОбщееКлиент.ЗаголовокДиалога());

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПереадресациюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса=КодВозвратаДиалога.Нет Тогда
        Возврат;
        
    КонецЕсли;
    
    СписокИсполнителей=Новый СписокЗначений;
    
    //передаресация единственному исполнителю
    СтруктураРезультата=ПереадресоватьЗадачу(Объект.ИсходнаяЗадача, Объект.Исполнитель);
    Если СтруктураРезультата=Неопределено Тогда
        Возврат;
    КонецЕсли;
    
    СтрокаПереадресации=КП_ОбщееСервер.Падеж(СокрЛП(Объект.Исполнитель), 3);
    Состояние(НСтр("ru='Переадресовано '; en='Forwarded to ';")+СтрокаПереадресации);
	
	Оповестить("КП_Задача");
	
    ЭтаФорма.Закрыть(СтруктураРезультата);

КонецПроцедуры

&НаСервере
Функция ПереадресоватьЗадачу(ИсходнаяЗадача, НовыйИсполнитель)
	
	СтруктураРезультата=Новый Структура;
	
	СтруктураРезультата.Вставить("НовыйИсполнитель", Объект.Исполнитель);
	СтруктураРезультата.Вставить("ТекстЗадачиИзменен", ТекстЗадачиИзменен);
	Если ТекстЗадачиИзменен Тогда
		ТекстЗаданияHTML="";
		ТекстЗадачи.ПолучитьHTML(ТекстЗаданияHTML, Новый Структура);
		СтруктураРезультата.Вставить("НовыйТекстЗадачи", ТекстЗаданияHTML);
	Иначе
		СтруктураРезультата.Вставить("НовыйТекстЗадачи", Неопределено);
	КонецЕсли;
	
	СтруктураРезультата.Вставить("НаименованиеЗадачи", Объект.НаименованиеЗадачи);
	СтруктураРезультата.Вставить("ДатаВыполненияПлан", Объект.ДатаВыполненияПлан);
	
	Возврат СтруктураРезультата;
	
КонецФункции

Процедура ЗаполнитьПараметрыЗадания(ИсходнаяЗадача)
	
	Если ЗначениеЗаполнено(ИсходнаяЗадача) Тогда
		ТекстЗадачи=ИсходнаяЗадача.ХранилищеТекстаЗадания.Получить();
		Объект.НаименованиеЗадачи=СокрЛП(ИсходнаяЗадача.Наименование);
		Объект.ДатаВыполненияПлан=СокрЛП(ИсходнаяЗадача.ДатаВыполненияПлан);
		
	Иначе
		ТекстЗадания="";
		Объект.НаименованиеЗадачи="";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
