
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ КП_ОбщееСерверПС.ЭтоРольПолныеПрава() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("en='Starting execution only possible with full rights.';ru='Запуск обработки возможен только с полными правами.'"));
		Отказ=Истина;
		Возврат;
		
	КонецЕсли;
	
	ВыполнятьНаСервере=1;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиСервером()
	КП_ОбщееСервер.СохранитьНастройкуПользователя("КП_ВыполнениеПроизвольногоКода", "ПрограммныйКод", ПрограммныйКод);	
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиСервером()
	ПрограммныйКод=КП_ОбщееСервер.ЗагрузитьНастройкуПользователя("КП_ВыполнениеПроизвольногоКода", "ПрограммныйКод");	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьКод(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("Оповещение_ВыполнитьКодЗавершение", ЭтотОбъект), НСтр("en='Do you want to run the code?';ru='Выполнить программный код?'"), РежимДиалогаВопрос.ДаНет,,,КП_ОбщееКлиент.ЗаголовокДиалога());
		
	
КонецПроцедуры

&НаКлиенте
Процедура Оповещение_ВыполнитьКодЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса=КодВозвратаДиалога.Нет Тогда
        Возврат;
        
    КонецЕсли;
	
	ЗаписатьНастройкиСервером();

    Если ВыполнятьНаСервере=1 Тогда
        РезультатВыполнения=ВыполнитьКодНаСервере();
        
    Иначе
        
        РезультатВыполнения=ВыполнитьКодНаКлиенте();
        
    КонецЕсли;
    
    Если РезультатВыполнения Тогда
        Состояние(НСтр("en='Code has been executed successfully.';ru='Код выполнен успешно.'"));
        
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ВыполнитьКодНаСервере()
	
	//установим переменную для программного кода
	СсылкаНаОбъект=Объект.СсылкаНаОбъект;
	
	Попытка
		Выполнить(ПрограммныйКод);
		
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция ВыполнитьКодНаКлиенте()
	
	//установим переменную для программного кода
	СсылкаНаОбъект=Объект.СсылкаНаОбъект;
	
	Попытка
		Выполнить(ПрограммныйКод);
		
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ДекорацияВосстановитьПредыдущийНажатие(Элемент)
	ПрочитатьНастройкиСервером();
	
КонецПроцедуры

#КонецОбласти
