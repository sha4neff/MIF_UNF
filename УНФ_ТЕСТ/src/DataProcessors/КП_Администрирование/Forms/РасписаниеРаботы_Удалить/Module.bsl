&НаКлиенте
Перем ЗакрытьБезВопроса;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ХранилищеРасписания=Константы.КП_ХранилищеРасписания.Получить();	
	Таблица=ХранилищеРасписания.Получить();
	Если ТипЗнч(Таблица)<>Тип("ТаблицаЗначений") ИЛИ Таблица.Количество()=0 Тогда
		Обработки.КП_Администрирование.ЗаполнитьРасписание(ТаблицаРасписания);
	Иначе
		ЗначениеВРеквизитФормы(Таблица, "ТаблицаРасписания");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьРасписание(Команда)
	ПоказатьВопрос(Новый ОписаниеОповещения("Оповещение_ОтветНаВопросПерезаполнение", ЭтотОбъект, Новый Структура), "Перезаполнить расписание работы?", РежимДиалогаВопрос.ДаНет, , ,КП_ОбщееКлиент.ЗаголовокДиалога());
КонецПроцедуры

&НаКлиенте
Процедура Оповещение_ОтветНаВопросПерезаполнение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса=КодВозвратаДиалога.Нет тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРасписаниеСервером();
   
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасписаниеСервером()
	Обработки.КП_Администрирование.ЗаполнитьРасписание(ТаблицаРасписания);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если НЕ ЗакрытьБезВопроса И РасписаниеИзменено Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("Оповещение_ОтветНаВопросСохранение", ЭтотОбъект, Новый Структура), "Расписание было изменено. Сохранить изменения?", РежимДиалогаВопрос.ДаНет, , ,КП_ОбщееКлиент.ЗаголовокДиалога());
		Отказ=Истина;		
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Оповещение_ОтветНаВопросСохранение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса=КодВозвратаДиалога.Нет тогда
		ЗакрытьБезВопроса=Истина;
		ЭтаФорма.Закрыть();
		Возврат;
	КонецЕсли;
	
	РезультатСохранения=СохранитьРасписаниеСервером();
	Если РезультатСохранения Тогда
		ЭтаФорма.Закрыть();
	КонецЕсли;
   
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРасписанияПриИзменении(Элемент)
	РасписаниеИзменено=Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если РасписаниеИзменено Тогда
		РезультатСохранения=СохранитьРасписаниеСервером();
		Если РезультатСохранения Тогда
			ЭтаФорма.Закрыть();
		КонецЕсли;		
	Иначе
		ЭтаФорма.Закрыть();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьРасписаниеСервером()
	
	Таблица=РеквизитФормыВЗначение("ТаблицаРасписания", Тип("ТаблицаЗначений"));
	
	//проверим таблицу на корректность
	Для Каждого СтрокаТЧ Из Таблица Цикл
		Если    (НЕ ЗначениеЗаполнено(СтрокаТЧ.ВремяНачалаОбеда) И ЗначениеЗаполнено(СтрокаТЧ.ВремяОкончанияОбеда))
			ИЛИ (НЕ ЗначениеЗаполнено(СтрокаТЧ.ВремяОкончанияОбеда) И ЗначениеЗаполнено(СтрокаТЧ.ВремяНачалаОбеда))
			ИЛИ (ЗначениеЗаполнено(СтрокаТЧ.ВремяОкончанияОбеда)<ЗначениеЗаполнено(СтрокаТЧ.ВремяНачалаОбеда))
		Тогда
			Сообщить("Время обеда для дня """+СокрЛП(СтрокаТЧ.ДеньНеделиНаименование)+""" заполнено некорректно. Запись не выполнена.");
			Возврат Ложь;
		КонецЕсли;

		Если СтрокаТЧ.ВремяНачалаРаботы>СтрокаТЧ.ВремяОкончанияРаботы Тогда
			Сообщить("Время начала работы для дня """+СокрЛП(СтрокаТЧ.ДеньНеделиНаименование)+""" больше, чем окончание. Запись не выполнена.");
			Возврат Ложь;			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.ВремяНачалаРаботы) И ЗначениеЗаполнено(СтрокаТЧ.ВремяОкончанияРаботы) И (СтрокаТЧ.ВремяНачалаРаботы=СтрокаТЧ.ВремяОкончанияРаботы) Тогда
			Сообщить("Время начала работы для дня """+СокрЛП(СтрокаТЧ.ДеньНеделиНаименование)+""" равно окончанию. Запись не выполнена.");
			Возврат Ложь;			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.ВремяНачалаОбеда) И ЗначениеЗаполнено(СтрокаТЧ.ВремяОкончанияОбеда) 
			И ((СтрокаТЧ.ВремяНачалаРаботы>=СтрокаТЧ.ВремяНачалаОбеда)
			ИЛИ (СтрокаТЧ.ВремяОкончанияРаботы<=СтрокаТЧ.ВремяОкончанияОбеда))
		Тогда
			Сообщить("Время для дня """+СокрЛП(СтрокаТЧ.ДеньНеделиНаименование)+""" заполнено некорректно. Запись не выполнена.");
			Возврат Ложь;			
		КонецЕсли;
	
	КонецЦикла;
	
	//сохраним данные
	ТаблицаДанные=Новый ХранилищеЗначения(Таблица);
	Константы.КП_ХранилищеРасписания.Установить(ТаблицаДанные);
	
	РасписаниеИзменено=Ложь;
		
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ТаблицаРасписанияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ=Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРасписанияПередУдалением(Элемент, Отказ)
	Отказ=Истина;
КонецПроцедуры

ЗакрытьБезВопроса=Ложь;
