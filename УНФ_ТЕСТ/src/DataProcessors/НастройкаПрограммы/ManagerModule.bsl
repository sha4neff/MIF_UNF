#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция НоваяТаблицаНастроек() Экспорт
	
	Строка250 = Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(250));
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка");
	ОписаниеТипаБулево = Новый ОписаниеТипов("Булево");
	
	ТаблицаНастроек = Новый ТаблицаЗначений;
	ТаблицаНастроек.Колонки.Добавить("Идентификатор",		Строка250);
	ТаблицаНастроек.Колонки.Добавить("Раздел",				Строка250);
	ТаблицаНастроек.Колонки.Добавить("Представление",		Строка250);
	ТаблицаНастроек.Колонки.Добавить("Описание",			ОписаниеТипаСтрока);
	ТаблицаНастроек.Колонки.Добавить("ПравоРедактирования",	ОписаниеТипаБулево);
	ТаблицаНастроек.Колонки.Добавить("ОбновлятьИнтерфейс",	ОписаниеТипаБулево);
	ТаблицаНастроек.Колонки.Добавить("Потенциальная",		ОписаниеТипаБулево);
	ТаблицаНастроек.Колонки.Добавить("ТипРеквизита",		Новый ОписаниеТипов("ОписаниеТипов"));
	ТаблицаНастроек.Колонки.Добавить("ТипЭлемента",			Новый ОписаниеТипов("Тип"));
	ТаблицаНастроек.Колонки.Добавить("ДобавлятьОтступ",		ОписаниеТипаБулево);
	ТаблицаНастроек.Колонки.Добавить("СвойстваЭлемента");
	ТаблицаНастроек.Колонки.Добавить("ВместеС",				Строка250);
	ТаблицаНастроек.Колонки.Добавить("Теги",				ОписаниеТипаСтрока);
	ТаблицаНастроек.Колонки.Добавить("Зависимости",			ОписаниеТипаСтрока);
	ТаблицаНастроек.Колонки.Добавить("РасширеннаяПодсказка", Новый ОписаниеТипов("ФорматированнаяСтрока"));
	
	Возврат ТаблицаНастроек;
	
КонецФункции

Функция НовоеДеревоРазделов() Экспорт
	
	ДеревоРазделов = Новый ДеревоЗначений;
	ДеревоРазделов.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	ДеревоРазделов.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	ДеревоРазделов.Колонки.Добавить("Описание",      Новый ОписаниеТипов("Строка"));
	ДеревоРазделов.Колонки.Добавить("ЭтоГруппа",     Новый ОписаниеТипов("Булево"));
	
	Возврат ДеревоРазделов;
	
КонецФункции

Процедура ДобавитьОповещение(ОповещенияНаКлиенте, знач ИмяСобытия, знач Параметр = Неопределено, знач Источник = Неопределено) Экспорт
	
	Оповещение = Новый Структура;
	Оповещение.Вставить("ИмяСобытия",	ИмяСобытия);
	Оповещение.Вставить("Параметр",		Параметр);
	Оповещение.Вставить("Источник",		Источник);
	
	ОповещенияНаКлиенте.Добавить(Оповещение);
	
КонецПроцедуры

Процедура ДобавитьДействиеНаКлиенте(ОповещенияНаКлиенте, знач Действие, знач Параметр = Неопределено, знач Источник = Неопределено) Экспорт
	
	Оповещение = Новый Структура;
	Оповещение.Вставить("Действие",		Действие);
	Оповещение.Вставить("Параметр",		Параметр);
	Оповещение.Вставить("Источник",		Источник);
	
	ОповещенияНаКлиенте.Добавить(Оповещение);
	
КонецПроцедуры

// Формирует служебную информацию для письма о новых возможностях
//
// Параметры:
//	Тег - Строка - Тег для добавляемой возможности
//
Функция СлужебнаяИнформацияДляПисьма(Тег) Экспорт
	
	Разделитель = "-------------------------------------------------------------------------------------";
	ВступительныйТекст = НСтр("ru = 'Спасибо, что помогаете развивать приложение 1С:УНФ! 
	|Мы обязательно прочитаем письмо и рассмотрим ваше пожелание, но вероятнее всего на письмо не ответим. 
	|Если у вас есть вопрос по работе приложения, требующий ответа, то зарегистрируйте обращение на линию консультации.'")
	+ Символы.ПС + Символы.ПС;

	Шаблон = НСтр("ru = 'Просьба не удалять данную информацию:
	|Конфигурация: [СинонимКонфигурации] ([ВерсияКонфигурации])
	|Платформа: 1С:Предприятие ([ВерсияПлатформы])
	|Тег: [Тег]'");
	
	СисИнфо = Новый СистемнаяИнформация;
	ВставляемыеЗначения = Новый Структура;
	ВставляемыеЗначения.Вставить("СинонимКонфигурации",	Метаданные.Синоним);
	ВставляемыеЗначения.Вставить("ВерсияКонфигурации",	Метаданные.Версия);
	ВставляемыеЗначения.Вставить("ВерсияПлатформы",		СисИнфо.ВерсияПриложения);
	ВставляемыеЗначения.Вставить("Тег",					Тег);
	ИнформацияОПрограмме = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ВставляемыеЗначения);
	
	СлужебнаяИнформацияДляПисьма = Разделитель + Символы.ПС + ВступительныйТекст + Символы.ПС + ИнформацияОПрограмме;
	Возврат СлужебнаяИнформацияДляПисьма;
	
КонецФункции

#КонецОбласти

#Область ВспомогательныйAPI

Функция ДобавитьНастройкуФлаг(ТаблицаНастроек,
								Идентификатор,
								Раздел = "",
								Представление = "",
								Описание = "") Экспорт
	
	НоваяНастройка = ТаблицаНастроек.Добавить();
	НоваяНастройка.Идентификатор	= Идентификатор;
	НоваяНастройка.Раздел			= Раздел;
	НоваяНастройка.Представление	= Представление;
	НоваяНастройка.Описание			= Описание;
	НоваяНастройка.ТипРеквизита		= Новый ОписаниеТипов("Булево");
	НоваяНастройка.ТипЭлемента		= Тип("ПолеФормы");
	НоваяНастройка.СвойстваЭлемента	= Новый Структура;
	НоваяНастройка.СвойстваЭлемента.Вставить("Вид", ВидПоляФормы.ПолеФлажка);
	НоваяНастройка.СвойстваЭлемента.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Право);
	НоваяНастройка.СвойстваЭлемента.Вставить("Заголовок", Представление);
	НоваяНастройка.СвойстваЭлемента.Вставить("Подсказка", Описание);
	НоваяНастройка.СвойстваЭлемента.Вставить("ОтображениеПодсказки", ОтображениеПодсказки.Кнопка);
	
	Возврат НоваяНастройка;
	
КонецФункции

Функция ДобавитьНастройкуГиперссылкаНавигации(ТаблицаНастроек,
												Идентификатор,
												Раздел = "",
												Представление = "",
												Описание = "",
												НавигационнаяСсылкаФорматированнойСтроки = "") Экспорт
	
	Гиперссылка = Новый ФорматированнаяСтрока(Представление,, ЦветаСтиля.ЦветГиперссылки,, НавигационнаяСсылкаФорматированнойСтроки);
	
	НоваяНастройка = ТаблицаНастроек.Добавить();
	НоваяНастройка.Идентификатор	= Идентификатор;
	НоваяНастройка.Раздел			= Раздел;
	НоваяНастройка.Представление	= Представление;
	НоваяНастройка.Описание			= Описание;
	НоваяНастройка.ТипЭлемента		= Тип("ДекорацияФормы");
	НоваяНастройка.СвойстваЭлемента	= Новый Структура;
	НоваяНастройка.СвойстваЭлемента.Вставить("Вид", ВидДекорацииФормы.Надпись);
	НоваяНастройка.СвойстваЭлемента.Вставить("Гиперссылка", Ложь);
	НоваяНастройка.СвойстваЭлемента.Вставить("Заголовок", Гиперссылка);
	НоваяНастройка.СвойстваЭлемента.Вставить("Подсказка", Описание);
	НоваяНастройка.СвойстваЭлемента.Вставить("ОтображениеПодсказки", ОтображениеПодсказки.Кнопка);
	
	Возврат НоваяНастройка;
	
КонецФункции

Функция ДобавитьНастройкуКоманда(ТаблицаНастроек,
									Идентификатор,
									Раздел = "",
									Представление = "",
									Описание = "") Экспорт
	
	НоваяНастройка = ТаблицаНастроек.Добавить();
	НоваяНастройка.Идентификатор	= Идентификатор;
	НоваяНастройка.Раздел			= Раздел;
	НоваяНастройка.Представление	= Представление;
	НоваяНастройка.Описание			= Описание;
	НоваяНастройка.ТипЭлемента		= Тип("ДекорацияФормы");
	НоваяНастройка.СвойстваЭлемента	= Новый Структура;
	НоваяНастройка.СвойстваЭлемента.Вставить("Вид", ВидДекорацииФормы.Надпись);
	НоваяНастройка.СвойстваЭлемента.Вставить("Гиперссылка", Истина);
	НоваяНастройка.СвойстваЭлемента.Вставить("Заголовок", Представление);
	НоваяНастройка.СвойстваЭлемента.Вставить("Подсказка", Описание);
	НоваяНастройка.СвойстваЭлемента.Вставить("ОтображениеПодсказки", ОтображениеПодсказки.Кнопка);
	
	Возврат НоваяНастройка;
	
КонецФункции

#КонецОбласти

#КонецЕсли