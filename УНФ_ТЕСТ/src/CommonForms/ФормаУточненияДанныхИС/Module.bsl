#Область ОписаниеПеременных

&НаКлиенте
Перем Ссылка Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьДанныеФормыПриСозданииНаСервере();
	СброситьРазмерыИПоложениеОкна();
	ВывестиПоясняющийТекст();
	СобытияФормИСКлиентСерверПереопределяемый.УстановитьПараметрыВыбораНоменклатуры(
		ЭтотОбъект, ПараметрыСканирования.ДопустимыеВидыПродукции, "Номенклатура");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(
		ЭтотОбъект, "Характеристика", "Номенклатура");
	Если ОбщегоНазначения.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("СобытияФормИСМППереопределяемый");
		Модуль.УстановитьПараметрыВыбораШаблонаЭтикетки(ЭтотОбъект, Элементы.ШаблонЭтикетки.Имя);
	КонецЕсли;
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	УстановитьЗаголовокЭлементаИдентификаторВЕТИС();
	УстановитьФорматДатыГоденДо(ЭтотОбъект);
	УстановитьВидимостьДоступностьЭлементовПриСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ЭтоМаркировкаОстатков
		И ПараметрыСканирования.РазрешенаОбработкаКодовСПустойНоменклатурой Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Номенклатура");
	КонецЕсли;
	
	Если Не УказатьИдентификаторВЕТИС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроисхожденияВЕТИС");
		МассивНепроверяемыхРеквизитов.Добавить("ГоденДо");
	КонецЕсли;
	
	Если Не РежимПечатиЭтикеток Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ШаблонЭтикетки");
		МассивНепроверяемыхРеквизитов.Добавить("ШаблонКодаМаркировки");
	ИначеЕсли Не ВидимостьШаблонаЭтикетки Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ШаблонЭтикетки");
	КонецЕсли;
	
	Если Не Элементы.Коэффициент.Видимость Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Коэффициент");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	СобытияФормИСПереопределяемый.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ИмяФормыУказанияСерии = "";
	ШтрихкодированиеИСКлиентПереопределяемый.ЗаполнитьПолноеИмяФормыУказанияСерии(ИмяФормыУказанияСерии);
	
	Если ИсточникВыбора.ИмяФормы = ИмяФормыУказанияСерии Тогда
		Серия = ВыбранноеЗначение.Значение;
	КонецЕсли;
	
	Если ТипыНоменклатуры.НайтиПоЗначению(ТипЗнч(ВыбранноеЗначение)) <> Неопределено Тогда
		
		Если ВыбранноеЗначение <> Номенклатура
			И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			
			Номенклатура = ВыбранноеЗначение;
			НоменклатураПриИзмененииСервер();
			ОтключитьОтметкуНезаполненного();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ДекорацияУдалосьПодобрать.Видимость = ЗначениеЗаполнено(Серия);
	
	ОтключитьОтметкуНезаполненного();
	ОбновитьДоступностьКомандыГотово();
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Не Элементы.ПоискПоШтрихкоду.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСоСканераСтруктура = СобытияФормИСКлиент.ВнешнееСобытиеПреобразоватьДанныеСоСканераВСтруктуру(
		ЭтотОбъект, Источник, Событие, Данные);
	
	Если ДанныеСоСканераСтруктура = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВводШтрихкодаЗавершение(ДанныеСоСканераСтруктура);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОповещениеПриОтключении = Новый ОписаниеОповещения("ОтключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(ОповещениеПриОтключении, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПоддерживаемыеТипыПодключаемогоОборудования = "СканерШтрихкода";
	
	ОповещениеПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(
		ОповещениеПриПодключении,
		ЭтотОбъект,
		ПоддерживаемыеТипыПодключаемогоОборудования);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	НоменклатураПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВидыПродукции = ПараметрыСканирования.ДопустимыеВидыПродукции;
	
	Если ШтрихкодированиеИСКлиентСервер.ЭтоШтрихкодВводаОстатков(КодМаркировки) Тогда
		ВидыПродукцииПоКодуМаркировкиОстатков(ВидыПродукции, КодМаркировки, ПараметрыСканирования.Организация);
	КонецЕсли;
	
	СобытияФормИСКлиентПереопределяемый.ПриНачалеВыбораНоменклатуры(Элемент, ВидыПродукции, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СерияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Новый Структура;
	ТекущиеДанные.Вставить("Номенклатура",               Номенклатура);
	ТекущиеДанные.Вставить("Характеристика",             Характеристика);
	ТекущиеДанные.Вставить("Серия",                      Неопределено);
	ТекущиеДанные.Вставить("Склад",                      Склад);
	ТекущиеДанные.Вставить("ИдентификаторТекущейСтроки", 1);
	ТекущиеДанные.Вставить("СтатусУказанияСерий",        СтатусУказанияСерий);
	ТекущиеДанные.Вставить("ХарактеристикиИспользуются", ЗначениеЗаполнено(Характеристика));
	ТекущиеДанные.Вставить("Количество",                 1);
	
	ИнтеграцияИСКлиент.ОткрытьПодборСерий(ЭтотОбъект,, "", Ложь, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПроисхожденияВЕТИСПриИзменении(Элемент)
	УстановитьФорматДатыГоденДо(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторПроисхожденияВЕТИСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтеграцияИСМПВЕТИСКлиент");
	Модуль.ИдентификаторПроисхожденияВЕТИСОбработкаВыбора(ЭтотОбъект, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	
	ОбновитьДоступностьКомандыГотово();
	
КонецПроцедуры

&НаКлиенте
Процедура СерияПриИзменении(Элемент)
	
	ОбновитьДоступностьКомандыГотово();
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеДокументаВыбратьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДанныеДокумента.ТекущиеДанные;
	Если ТекущиеДанные.Выбрать Тогда
		ИзменениеНоменклатуры = ТекущиеДанные.Номенклатура <> Номенклатура;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеДанные);
		ГоденДо = ТекущиеДанные.СрокГодности;
		Для Каждого СтрокаТЧ Из ДанныеДокумента Цикл
			Если СтрокаТЧ.Выбрать И СтрокаТЧ.ПолучитьИдентификатор() <> ТекущиеДанные.ПолучитьИдентификатор() Тогда
				СтрокаТЧ.Выбрать = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ИзменениеНоменклатуры Тогда
			НоменклатураПриИзмененииСервер();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ЗакрытьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктурыДанных = ШтрихкодированиеИСКлиент.ИнициализацияСтруктурыДанныхСохраненногоВыбора();
	
	СтруктурыДанных.ЗапомнитьВыбор = ЗапомнитьВыбор;
	
	СтруктурыДанных.ДанныеВыбора.Номенклатура   = Номенклатура;
	СтруктурыДанных.ДанныеВыбора.Характеристика = Характеристика;
	СтруктурыДанных.ДанныеВыбора.Серия          = Серия;
	СтруктурыДанных.ДанныеВыбора.GTIN           = GTIN;
	
	СтруктурыДанных.ДанныеВыбора.ШаблонЭтикетки   = ШаблонЭтикетки;
	СтруктурыДанных.ДанныеВыбора.СразуНаПринтер   = СразуНаПринтер;
	СтруктурыДанных.ДанныеВыбора.ШаблонМаркировки = ШаблонКодаМаркировки;
	
	СтруктурыДанных.ДанныеВыбора.СоставКодаМаркировки = СоставКодаМаркировки;
	
	Закрыть(СтруктурыДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоясняющийТекстОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СкопироватьШтрихкодБуферОбмена" Тогда
		ИнтеграцияИСКлиент.СкопироватьШтрихКодВБуферОбмена(Элементы.БуферОбмена, ШтрихкодEAN);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СкопироватьКодМаркировкиВБуферОбмена" Тогда 
		ИнтеграцияИСКлиент.СкопироватьШтрихКодВБуферОбмена(Элементы.БуферОбмена, КодМаркировки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ОчиститьСообщения();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводШтрихкодаЗавершение", ЭтотОбъект);
	ШтрихкодированиеИСКлиент.ПоказатьВводШтрихкода(ОписаниеОповещения);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НоменклатураПриИзмененииСервер()
	
	ПроверитьНеобходимостьЗаполненияСерии();
	ХарактеристикиИспользуются = ИнтеграцияИС.ПризнакИспользованияХарактеристик(Номенклатура);
	ОбновитьВидимостьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить("ОбщаяФорма.ФормаУточненияДанныхИС", "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиПоясняющийТекст()
	
	МассивСтрок = Новый Массив;
	
	Элементы.ПоясняющийТекст.Высота = 1;
	Если Не РежимПечатиЭтикеток И Не ПустаяСтрока(КодМаркировки) Тогда
		
		Элементы.ПоясняющийТекст.Высота = 3;
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Обработка кода маркировки:'"),, ЦветаСтиля.ПоясняющийТекст));
		МассивСтрок.Добавить(" ");
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(КодМаркировки, Новый Шрифт(,,,,Истина), ЦветаСтиля.ЦветГиперссылкиГосИС,, "СкопироватьКодМаркировкиВБуферОбмена"));
		МассивСтрок.Добавить(Символы.ПС);
		
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Номенклатура) = Тип("Массив") Тогда
		
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Штрихкоду'"),, ЦветаСтиля.ПоясняющийТекст));
		МассивСтрок.Добавить(" ");
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ШтрихкодEAN, Новый Шрифт(,,,,Истина), ЦветаСтиля.ЦветГиперссылкиГосИС,, "СкопироватьШтрихкодБуферОбмена"));
		МассивСтрок.Добавить(" ");
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'сопоставлено несколько номенклатурных позиций. Укажите номенклатуру.'"),, ЦветаСтиля.ПоясняющийТекст));
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Номенклатура) Тогда
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Укажите номенклатуру.'"),, ЦветаСтиля.ПоясняющийТекст));
	КонецЕсли;
	
	Если УказатьИдентификаторВЕТИС Тогда
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Укажите специфику ВетИС.'"),, ЦветаСтиля.ПоясняющийТекст));
	КонецЕсли;
	
	Если Элементы.Серия.СписокВыбора.Количество() > 1 Тогда
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Укажите серию номенклатуры.'"),, ЦветаСтиля.ПоясняющийТекст));
	КонецЕсли;
	
	Если РежимПечатиЭтикеток Тогда
		Если МассивСтрок.Количество() Тогда
			Элементы.ПоясняющийТекст.Высота = Элементы.ПоясняющийТекст.Высота + 1;
			МассивСтрок.Добавить(Символы.ПС);
		КонецЕсли;
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Укажите шаблон этикетки.'"),, ЦветаСтиля.ПоясняющийТекст));
	КонецЕсли;
	
	ПоясняющийТекст = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормыПриСозданииНаСервере()
	
	Заголовок = НСтр("ru = 'Уточнение данных'");
	
	ОбработатьРежимВыбораИзДокумента();
	
	ТипыНоменклатуры.ЗагрузитьЗначения(Метаданные.ОпределяемыеТипы.Номенклатура.Тип.Типы());
	
	ИспользоватьХарактеристикиНоменклатуры = ИнтеграцияИС.ХарактеристикиИспользуются();
	ИспользоватьСерииНоменклатуры          = ИнтеграцияИС.СерииИспользуются();
	
	ПараметрыСканирования = ОбщегоНазначения.СкопироватьРекурсивно(Параметры.ПараметрыСканирования);
	Если ТипЗнч(Параметры.Номенклатура) = Тип("Массив") Тогда
		
		Элементы.Номенклатура.РежимВыбораИзСписка = Истина;
		Элементы.Номенклатура.СписокВыбора.ЗагрузитьЗначения(Параметры.Номенклатура);
		
	Иначе
		
		Номенклатура   = Параметры.Номенклатура;
		Характеристика = Параметры.Характеристика;
		Серия          = Параметры.Серия;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Номенклатура) И Не Параметры.ПроизвольноеРедактированиеРеквизитов Тогда
		Элементы.Номенклатура.ТолькоПросмотр = Истина;
	Иначе
		Элементы.Номенклатура.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Характеристика) И Не Параметры.ПроизвольноеРедактированиеРеквизитов Тогда
		Элементы.Характеристика.ТолькоПросмотр = Истина;
	Иначе
		Элементы.Характеристика.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ПредставлениеНоменклатуры) Тогда
		Элементы.Номенклатура.ПодсказкаВвода = Параметры.ПредставлениеНоменклатуры;
	КонецЕсли;
	
	КодМаркировки             = Параметры.КодМаркировки;
	ШтрихкодEAN               = Параметры.ШтрихкодEAN;
	GTIN                      = Параметры.GTIN;
	ПараметрыУказанияСерий    = Параметры.ПараметрыСканирования.ПараметрыУказанияСерий;
	Склад                     = Параметры.ПараметрыСканирования.Склад;
	ЭтоМаркировкаОстатков     = ШтрихкодированиеИСКлиентСервер.ЭтоШтрихкодВводаОстатков(GTIN)
		Или ШтрихкодированиеИСКлиентСервер.ЭтоШтрихкодВводаОстатков(ШтрихкодEAN);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
		ВидимостьШаблонаЭтикетки  = РегистрыСведений["ПулКодовМаркировкиСУЗ"].ВидимостьШаблонаЭтикетки();
	КонецЕсли;
	
	СоставКодаМаркировки = РазборКодаМаркировкиИССлужебный.РазобратьКодМаркировки(КодМаркировки, ПараметрыСканирования.ДопустимыеВидыПродукции);
	Если Не(СоставКодаМаркировки = Неопределено) Тогда
		СоставКодаМаркировки = Новый ФиксированнаяСтруктура(СоставКодаМаркировки.СоставКодаМаркировки);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ШтрихкодEAN) И Не ЗначениеЗаполнено(GTIN) Тогда
		GTIN = ШтрихкодированиеИСКлиентСервер.GTINПоШтрихкодуEAN(ШтрихкодEAN);
	КонецЕсли;
	
	ИзменитьПараметрыУказанияСерий();
	ПроверитьНеобходимостьЗаполненияСерии();
	ЗаполнитьСерииПоДокументу();
	ЗаполнитьИдентификаторыПроисхожденияВЕТИС();
	ЗаполнитьДанныеДляПечатиЭтикеток();
	
	Если Не Параметры.УточнитьКоэффициент Тогда
		Элементы.Коэффициент.Видимость = Ложь;
	КонецЕсли;
	
	ХарактеристикиИспользуются = ИнтеграцияИС.ПризнакИспользованияХарактеристик(Номенклатура);
	
	//Заполнение значений по-умолчанию: в источнике вызова есть ровно 1 подходящая строка с незаполненными кодами маркировки
	Если ДанныеДокумента.Количество() = 1 Тогда
		ДанныеДокумента[0].Выбрать = Истина;
		ИзменениеНоменклатуры = ДанныеДокумента[0].Номенклатура<>Номенклатура;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеДокумента[0]);
		ГоденДо = ДанныеДокумента[0].СрокГодности;
		Если ИзменениеНоменклатуры Тогда
			НоменклатураПриИзмененииСервер();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРежимВыбораИзДокумента()
	
	ПредставлениеВыбираемыхДанных = Новый Массив;
	Колонки = Новый Массив;
	
	Элементы.СтраницаУказаниеДанных.Видимость = Параметры.РежимПроизвольногоВвода;
	Элементы.СтраницаВыборИзСписка.Видимость  = Параметры.РежимПодбораИзДокумента;
	Элементы.ДанныеДокумента.Видимость = Параметры.РежимПодбораИзДокумента;
	Если Параметры.РежимПодбораИзДокумента Тогда
		Если Параметры.ДанныеДокумента.Количество() Тогда
			Элементы.РежимыВыбора.ТекущаяСтраница = Элементы.СтраницаВыборИзСписка;
			ПредставлениеИзвестныхДанныхМассивом = Новый Массив;
			Элемент = Параметры.ДанныеДокумента[0];
			// Настроим колонки таблицы выбора
			Для Каждого Колонка Из ДанныеДокумента.Выгрузить().Колонки Цикл
				Если Колонка.Имя = "Выбрать"
					Или Колонка.Имя = "ПредставлениеПустого" Тогда
				ИначеЕсли Не Элемент.Свойство(Колонка.Имя) Тогда
					Элементы["ДанныеДокумента"+Колонка.Имя].Видимость = Ложь;
					Если (Колонка.Имя = "Номенклатура"
						Или Колонка.Имя = "Характеристика"
						Или Колонка.Имя = "Серия")
						И ЗначениеЗаполнено(Параметры[Колонка.Имя]) Тогда
							Если ПредставлениеИзвестныхДанныхМассивом.Количество() Тогда
								ПредставлениеИзвестныхДанныхМассивом.Добавить(" / ");
							КонецЕсли;
							ПредставлениеИзвестныхДанныхМассивом.Добавить(Новый ФорматированнаяСтрока(
								""+Параметры[Колонка.Имя], , ЦветаСтиля.ЦветГиперссылкиГосИС, , ПолучитьНавигационнуюСсылку(Параметры[Колонка.Имя])));
					КонецЕсли;
				Иначе
					ПредставлениеВыбираемыхДанных.Добавить(Элементы["ДанныеДокумента"+Колонка.Имя].Заголовок);
					Колонки.Добавить(Колонка.Имя);
				КонецЕсли;
			КонецЦикла;
			
			Элементы.ДанныеДокументаВыбрать.Заголовок = СтрСоединить(ПредставлениеВыбираемыхДанных, ",");
			
			Если ПредставлениеИзвестныхДанныхМассивом.Количество() Тогда
				ПредставлениеИзвестныхДанных = Новый ФорматированнаяСтрока(ПредставлениеИзвестныхДанныхМассивом);
			Иначе
				Элементы.ПредставлениеИзвестныхДанных.Видимость = Ложь;
			КонецЕсли;
			// Загрузим данные документа для выбора
			Для Каждого ВариантВыбора Из Параметры.ДанныеДокумента Цикл
				НоваяСтрока = ДанныеДокумента.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Параметры);
				НоваяСтрока.СрокГодности = Параметры.ГоденДо;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВариантВыбора);
			КонецЦикла;
			ТаблицаДанныеДокумента = ДанныеДокумента.Выгрузить();
			ТаблицаДанныеДокумента.Свернуть("Номенклатура,Характеристика,Серия,ИдентификаторПроисхожденияВЕТИС,СрокГодности","Количество");
			ДанныеДокумента.Загрузить(ТаблицаДанныеДокумента);
			Для Каждого СтрокаДанныеДокумента Из ДанныеДокумента Цикл
				ЭтоПустаяСтрока = Истина;
				Для Каждого ВидимаяКолонка Из Колонки Цикл
					ЭтоПустаяСтрока = ЭтоПустаяСтрока И Не ЗначениеЗаполнено(СтрокаДанныеДокумента[ВидимаяКолонка]);
				КонецЦикла;
				Если ЭтоПустаяСтрока Тогда
					СтрокаДанныеДокумента.ПредставлениеПустого = НСтр("ru = '<Без уточнения>'")
				КонецЕсли;
			КонецЦикла;
		Иначе
			Элементы.СтраницаУказаниеДанных.Видимость = Истина;
			Элементы.СтраницаВыборИзСписка.Видимость = Ложь;
			Элементы.ДанныеДокумента.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Не(Параметры.РежимПроизвольногоВвода И Параметры.РежимПодбораИзДокумента) Тогда
		Элементы.РежимыВыбора.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИдентификаторыПроисхожденияВЕТИС()
	
	УказатьИдентификаторВЕТИС = ПараметрыСканирования.Свойство("ЗаполнятьДанныеВЕТИС")
		И ПараметрыСканирования.ЗаполнятьДанныеВЕТИС;
	
	Если Не УказатьИдентификаторВЕТИС Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоКодМаркировкиСоСрокомГодности = ЗначениеЗаполнено(Параметры.ГоденДо);
	Если ЭтоКодМаркировкиСоСрокомГодности Тогда
		ГоденДо         = Параметры.ГоденДо;
		Скоропортящаяся = Параметры.Скоропортящаяся;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыСканирования.ТипИдентификатораПроисхожденияВЕТИС) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыВЕТИС = Новый Массив;
	
	Если ТипЗнч(Параметры.ИдентификаторыПроисхожденияВЕТИС) = Тип("Массив") Тогда
		ИдентификаторыВЕТИС = Параметры.ИдентификаторыПроисхожденияВЕТИС;
	ИначеЕсли ЗначениеЗаполнено(Параметры.ИдентификаторыПроисхожденияВЕТИС) Тогда
		ИдентификаторПроисхожденияВЕТИС = Параметры.ИдентификаторыПроисхожденияВЕТИС;
		ИдентификаторыВЕТИС.Добавить(ИдентификаторПроисхожденияВЕТИС);
	ИначеЕсли ЗначениеЗаполнено(Параметры.ДанныеДокумента) Тогда
		Для Каждого СтрокаДокумента Из Параметры.ДанныеДокумента Цикл
			Если СтрокаДокумента.Свойство("ИдентификаторПроисхожденияВЕТИС")
				И ИдентификаторыВЕТИС.Найти(СтрокаДокумента.ИдентификаторПроисхожденияВЕТИС) = Неопределено Тогда
				ИдентификаторыВЕТИС.Добавить(СтрокаДокумента.ИдентификаторПроисхожденияВЕТИС);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДобавитьИдентификаторыВЕТИСВСписокВыбора(ИдентификаторыВЕТИС, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьИдентификаторыВЕТИСВСписокВыбора(Идентификаторы, РежимОткрытияСпискаВыбора = Ложь)
	
	ИмяИдентификатораПроисхожденияВЕТИС = Метаданные.НайтиПоТипу(ПараметрыСканирования.ТипИдентификатораПроисхожденияВЕТИС).Имя;
	
	Если РежимОткрытияСпискаВыбора Тогда
		
		ИдентификаторПустаяСсылка = Справочники[ИмяИдентификатораПроисхожденияВЕТИС].ПустаяСсылка();
		ПредставлениеКоманды = Новый ФорматированнаяСтрока(НСтр("ru = 'Показать все'"), Новый Шрифт(,,,,Истина), ЦветаСтиля.ЦветГиперссылкиГосИС);
		Элементы.ИдентификаторПроисхожденияВЕТИС.СписокВыбора.Добавить(ИдентификаторПустаяСсылка, ПредставлениеКоманды);
		
	КонецЕсли;
	
	Если Идентификаторы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Модуль = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияИСМПВЕТИСКлиентСервер");
	ИмяИдентификатораВЕТИС = Модуль.ЗаголовокПоляИдентификаторПроисхожденияВЕТИСПоТипу(
		ПараметрыСканирования.ТипИдентификатораПроисхожденияВЕТИС);
	Модуль = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияИСМПВЕТИС");
	ДанныеИдентификаторовПроисхождения = Модуль.ДанныеИдентификаторовПроисхождения(Идентификаторы);
	
	Для Каждого КлючИЗначение Из ДанныеИдентификаторовПроисхождения Цикл
		
		ПредставлениеСсылки = СтрШаблон(НСтр("ru = '%1 от %2'"), ИмяИдентификатораВЕТИС, КлючИЗначение.Значение.Представление);
		Элементы.ИдентификаторПроисхожденияВЕТИС.СписокВыбора.Вставить(0, КлючИЗначение.Ключ, ПредставлениеСсылки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеДляПечатиЭтикеток()
	
	РежимПечатиЭтикеток       = Параметры.РежимПечатиЭтикеток;
	
	Если Не РежимПечатиЭтикеток Тогда
		Возврат;
	КонецЕсли;
	
	СразуНаПринтер            = Параметры.СразуНаПринтер;
	ШаблонЭтикетки            = Параметры.ШаблонЭтикетки;
	
	Если ЗначениеЗаполнено(Параметры.ШаблоныКодовМаркировки) Тогда
		
		Элементы.ШаблонКодаМаркировки.СписокВыбора.ЗагрузитьЗначения(Параметры.ШаблоныКодовМаркировки);
		
		Если Элементы.ШаблонКодаМаркировки.СписокВыбора.НайтиПоЗначению(Параметры.ШаблонКодаМаркировки) <> Неопределено Тогда
			ШаблонКодаМаркировки = Параметры.ШаблонКодаМаркировки;
		Иначе
			ШаблонКодаМаркировки = Элементы.ШаблонКодаМаркировки.СписокВыбора.Получить(0).Значение;
		КонецЕсли;
		
	КонецЕсли;
	
	ВидПродукции = ИнтеграцияИС.ВидПродукцииПоНоменклатуре(Номенклатура);
	
	Элементы.ШаблонКодаМаркировки.СписокВыбора.Очистить();
	Если ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукции, Истина) Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияИСМП");
		Модуль.НастроитьШаблоныФормыУточненияДанных(ЭтотОбъект, ВидПродукции);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьДоступностьЭлементов()
	
	Элементы.Характеристика.Видимость = ИспользоватьХарактеристикиНоменклатуры;
	Элементы.Серия.Видимость          = ИспользоватьСерииНоменклатуры;
	Элементы.ДекорацияУдалосьПодобрать.Видимость = Ложь;
	
	ОбновитьОтображениеПоляНоменклатура();
	ОбновитьОтображениеПоляСерия();
	ОбновитьОтображениеПоляХарактеристика();
	ОбновитьДоступностьКомандыГотово();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеПоляНоменклатура()
	
	Если ПараметрыСканирования.РазрешенаОбработкаКодовСПустойНоменклатурой
		И Не ПолученоПредставлениеGTIN Тогда
		
		ДанныеДляПолученияПредставленияGTINОстатков = Новый ТаблицаЗначений;
		ДанныеДляПолученияПредставленияGTINОстатков.Колонки.Добавить("Номенклатура",             Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
		ДанныеДляПолученияПредставленияGTINОстатков.Колонки.Добавить("GTIN",                     Метаданные.ОпределяемыеТипы.GTIN.Тип);
		ДанныеДляПолученияПредставленияGTINОстатков.Колонки.Добавить("ПредставлениеGTINОстатки", Новый ОписаниеТипов("Строка"));
		ДанныеДляПолученияПредставленияGTINОстатков.Колонки.Добавить("ВидПродукции",             Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПродукцииИС"));
		
		СтрокаТаблицы = ДанныеДляПолученияПредставленияGTINОстатков.Добавить();
		СтрокаТаблицы.GTIN = GTIN;
		
		ВидПродукции = Неопределено;
		Если ПараметрыСканирования.ДопустимыеВидыПродукции.Количество() = 1 Тогда
			ВидПродукции = ПараметрыСканирования.ДопустимыеВидыПродукции[0];
		КонецЕсли;
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
			РегистрыСведений["КэшОписанияОстатковИСМП"].ЗаполнитьТаблицуПредставленийGTINОстатки(
				ДанныеДляПолученияПредставленияGTINОстатков,,ВидПродукции);
		КонецЕсли;
			
		Если ДанныеДляПолученияПредставленияGTINОстатков.Количество() > 0 Тогда
			ПредставлениеGTIN = ДанныеДляПолученияПредставленияGTINОстатков[0].ПредставлениеGTINОстатки;
		Иначе
			ПредставлениеGTIN = GTIN;
		КонецЕсли;
		
		Элементы.Номенклатура.ПодсказкаВвода            = ПредставлениеGTIN;
		Элементы.Номенклатура.АвтоОтметкаНезаполненного = Ложь;
		
		ПолученоПредставлениеGTIN = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеПоляСерия()
	
	Если ТребуетсяУказаниеСерии Тогда
		
		Элементы.Серия.ТолькоПросмотр = Ложь;
		Если ЗначениеЗаполнено(Склад) Тогда
			Элементы.Серия.ПодсказкаВвода = "";
			Элементы.Серия.АвтоОтметкаНезаполненного = Истина;
		Иначе
			Элементы.Серия.ПодсказкаВвода = НСтр("ru = '<допустимо указание серии>'");
			Элементы.Серия.АвтоОтметкаНезаполненного = Ложь;
		КонецЕсли;
		
	Иначе
		
		Элементы.Серия.ТолькоПросмотр = Истина;
		Элементы.Серия.ПодсказкаВвода = НСтр("ru = '<серии не используются>'");
		Элементы.Серия.АвтоОтметкаНезаполненного = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеПоляХарактеристика()
	
	Если ХарактеристикиИспользуются Тогда
		Элементы.Характеристика.ПодсказкаВвода            = "";
		Элементы.Характеристика.АвтоОтметкаНезаполненного = Истина;
		Элементы.Характеристика.ТолькоПросмотр            = Ложь;
	Иначе
		Элементы.Характеристика.ПодсказкаВвода            = НСтр("ru = '<характеристики не используются>'");
		Элементы.Характеристика.АвтоОтметкаНезаполненного = Ложь;
		Элементы.Характеристика.ТолькоПросмотр            = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьПараметрыУказанияСерий()
	
	Если ПараметрыУказанияСерий = Неопределено
		Или Не ИспользоватьСерииНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияИСПереопределяемый.ЗаполнитьПараметрыУказанияСерий(
		ПараметрыУказанияСерий, Метаданные.ОбщиеФормы.ФормаУточненияДанныхИС, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНеобходимостьЗаполненияСерии()
	
	ТребуетсяУказаниеСерии = Ложь;
	Если ПараметрыУказанияСерий = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляРасчетаСерии = Новый Структура("Номенклатура, Характеристика", Номенклатура, Характеристика);
	ТребуетсяУказаниеСерии = ИнтеграцияИС.ТребуетсяВыборСерии(ДанныеДляРасчетаСерии, ПараметрыСканирования);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДоступностьКомандыГотово()
	
	Если Не ЗначениеЗаполнено(Номенклатура) И Не ПараметрыСканирования.РазрешенаОбработкаКодовСПустойНоменклатурой
		Или ТребуетсяУказаниеСерии И Не ЗначениеЗаполнено(Серия) И ЗначениеЗаполнено(Склад)
		Или ХарактеристикиИспользуются И Не ЗначениеЗаполнено(Характеристика) Тогда
		Элементы.ЗавершитьРедактирование.Доступность = Ложь;
	Иначе
		Элементы.ЗавершитьРедактирование.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементовПриСозданииНаСервере()
	
	ОбновитьВидимостьДоступностьЭлементов();
	
	Элементы.ГруппаПечатьЭтикеток.Видимость = РежимПечатиЭтикеток;
	Если РежимПечатиЭтикеток Тогда
		Элементы.ЗавершитьРедактирование.Видимость = Ложь;
		Элементы.Печать.Видимость                  = Истина;
		Элементы.Печать.КнопкаПоУмолчанию          = Истина;
	КонецЕсли;
	Элементы.ШаблонЭтикетки.Видимость = ВидимостьШаблонаЭтикетки;
	
	Элементы.ШаблонКодаМаркировки.Видимость = Элементы.ШаблонКодаМаркировки.СписокВыбора.Количество() > 1
		Или Не ЗначениеЗаполнено(ШаблонКодаМаркировки);
	
	Элементы.ГруппаДанныеВЕТИС.Видимость = УказатьИдентификаторВЕТИС;
	Если УказатьИдентификаторВЕТИС Тогда
		
		Если ЭтоКодМаркировкиСоСрокомГодности Тогда
			Элементы.ГоденДо.ТолькоПросмотр = Истина;
			Элементы.ИдентификаторПроисхожденияВЕТИС.ПараметрыВыбора = Новый ФиксированныйМассив(
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
					Новый ПараметрВыбора("Отбор.СкоропортящаясяПродукция", Скоропортящаяся)));
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции Тогда
		Элементы.ГруппаЗапомнитьВыбор.Видимость = Ложь;
	КонецЕсли;
	
	Если Элементы.Номенклатура.ТолькоПросмотр
		И Элементы.Характеристика.ТолькоПросмотр
		И Элементы.Серия.ТолькоПросмотр Тогда
		Элементы.ПоискПоШтрихкоду.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСерииПоДокументу()
	
	Если ИспользоватьСерииНоменклатуры
		И ЭтоАдресВременногоХранилища(ПараметрыСканирования.АдресДанныхДокументаОснования) Тогда
		ДанныеДокументаОснования = ПолучитьИзВременногоХранилища(ПараметрыСканирования.АдресДанныхДокументаОснования);
		ДоступныеСерии = ДоступныеСерииПоДаннымТаблицыТовары(ДанныеДокументаОснования);
		
		Если ДоступныеСерии.Количество() = 0 И Не Параметры.ПроизвольноеРедактированиеРеквизитов Тогда
			
			Элементы.Серия.ТолькоПросмотр = Истина;
			
		Иначе
			
			Элементы.Серия.КнопкаВыпадающегоСписка = Истина;
			Элементы.Серия.СписокВыбора.ЗагрузитьЗначения(ДоступныеСерии);
			Если ДоступныеСерии.Количество() = 1 Тогда
				Серия = ДоступныеСерии[0];
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИспользоватьСерииНоменклатуры
		И ЗначениеЗаполнено(ПараметрыСканирования.ДанныеТаблицыТовары) Тогда
		
		ДанныеТаблицыТовары = ПолучитьИзВременногоХранилища(ПараметрыСканирования.ДанныеТаблицыТовары);
		ДоступныеСерии = ДоступныеСерииПоДаннымТаблицыТовары(ДанныеТаблицыТовары);
		
		Если ДоступныеСерии.Количество() > 0 Тогда
			
			Элементы.Серия.КнопкаВыпадающегоСписка = Истина;
			Для Каждого Серия Из ДоступныеСерии Цикл
			
				ДоступныеСерии = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДоступныеСерии);
				Если ЗначениеЗаполнено(Серия) Тогда
					Элементы.Серия.СписокВыбора.Добавить(Серия);
				Иначе
					Элементы.Серия.СписокВыбора.Добавить(Серия, НСтр("ru='<Неопределена>'"));
				КонецЕсли;
			
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДоступныеСерииПоДаннымТаблицыТовары(ДанныеТаблицыТовары)
	
	СписокДоступныхСерий = Новый Массив;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", Номенклатура);
	Если ИнтеграцияИС.ХарактеристикиИспользуются() Тогда
		Отбор.Вставить("Характеристика", Характеристика);
	КонецЕсли;
	
	НайденныеСтроки = ДанныеТаблицыТовары.НайтиСтроки(Отбор);
	
	Для Каждого Строка Из НайденныеСтроки Цикл
		СписокДоступныхСерий.Добавить(Строка.Серия);
	КонецЦикла;
	
	СписокДоступныхСерий = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СписокДоступныхСерий);
	
	Возврат СписокДоступныхСерий;
	
КонецФункции

&НаКлиенте
Процедура ВводШтрихкодаЗавершение(ДанныеШтрихкода, ДополнительныеПараметры = Неопределено) Экспорт
	
	ТекстОшибки = "";
	ДанныеШтрихкода.Штрихкод = ШтрихкодированиеИСКлиентСервер.ШтрихкодВBase64(ДанныеШтрихкода.Штрихкод);
	ДанныеШтрихкода.Вставить("ШтрихкодBase64", Истина);
	
	ШтрихкодНайден = Ложь;
	ОбработатьШтрихкод(ДанныеШтрихкода, ШтрихкодНайден, ТекстОшибки);
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		
		ПараметрыОткрытия = ШтрихкодированиеИСКлиент.ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного();
		
		Если ТипЗнч(ТекстОшибки) = Тип("ФорматированнаяСтрока") Тогда
			ПараметрыОткрытия.ТекстОшибкиФорматированнаяСтрока = ТекстОшибки;
		Иначе
			ПараметрыОткрытия.ТекстОшибки = ТекстОшибки;
		КонецЕсли;
		
		ШтрихкодированиеИСКлиент.ОткрытьФормуНевозможностиДобавленияОтсканированного(ЭтотОбъект, ПараметрыОткрытия);
		
		Возврат;
		
	КонецЕсли;
	
	Если Не ШтрихкодНайден Тогда
		
		Штрихкоды = Новый Массив;
		Штрихкоды.Добавить(ДанныеШтрихкода);
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеСопоставленияНеизвестныхШтрихкодов", ЭтотОбъект);
		ШтрихкодированиеИСКлиентПереопределяемый.ОткрытьФормуПодбораНоменклатурыПоШтрихкодам(
			Штрихкоды, ЭтотОбъект, ОповещениеОЗакрытии);
		
		Возврат;
		
	КонецЕсли;
	
	Если ЗакрытьПослеСканированияШтрихкодаНоменклатуры() Тогда
		ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьШтрихкод(ДанныеШтрихкода, ШтрихкодНайден, ТекстОшибки = "")
	
	Если Не ДопустимыйФорматШтрихкода(ДанныеШтрихкода) Тогда
		ТекстОшибки = НСтр("ru='Недопустимый формат штрихкода'");
		Возврат;
	КонецЕсли;
	
	Штрихкоды = Новый Массив;
	Штрихкоды.Добавить(ДанныеШтрихкода.Штрихкод);
	ДанныеПоШтрихкодамEAN = ШтрихкодированиеИС.ДанныеПоШтрихкодамEAN(Штрихкоды);
	
	ДопустимыеДанныеПоШтрихкодамEAN = ДанныеПоШтрихкодамEAN.СкопироватьКолонки();
	// Номенклатура уже выбрана. Можно предупреждать о несоответствии.
	Если Элементы.Номенклатура.ТолькоПросмотр Тогда
		Для Каждого СтрокаТЧ Из ДанныеПоШтрихкодамEAN Цикл
			Если СтрокаТЧ.Номенклатура = Номенклатура
				И СтрокаТЧ.Характеристика = Характеристика Тогда
				ЗаполнитьЗначенияСвойств(ДопустимыеДанныеПоШтрихкодамEAN.Добавить(), СтрокаТЧ);
			КонецЕсли;
		КонецЦикла;
	// Проверим известный вид продукции или хотя бы признак маркируемой продукции
	Иначе
		Для Каждого СтрокаТЧ Из ДанныеПоШтрихкодамEAN Цикл
			Подходит = СтрокаТЧ.МаркируемаяПродукция;
			Если ПараметрыСканирования.ДопустимыеВидыПродукции.Количество() Тогда
				Подходит = Подходит И ПараметрыСканирования.ДопустимыеВидыПродукции.Найти(СтрокаТЧ.ВидПродукции)<>Неопределено;
			КонецЕсли;
			Если Подходит Тогда
				ЗаполнитьЗначенияСвойств(ДопустимыеДанныеПоШтрихкодамEAN.Добавить(), СтрокаТЧ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеПоШтрихкодамEAN.Количество() = 0
		Или Не ЗначениеЗаполнено(ДанныеПоШтрихкодамEAN[0].Номенклатура) Тогда
		ШтрихкодНайден = Ложь;
		Возврат;
	ИначеЕсли ДопустимыеДанныеПоШтрихкодамEAN.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru='Отсканированный штрихкод не соответствует уточняемой номенклатуре'");
		Возврат;
	КонецЕсли;
	
	ШтрихкодНайден = Истина;
	
	Если ДопустимыеДанныеПоШтрихкодамEAN.Количество() = 1 Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДопустимыеДанныеПоШтрихкодамEAN[0]);
		
	Иначе
		
		НоменклатураСписок   = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДопустимыеДанныеПоШтрихкодамEAN.ВыгрузитьКолонку("Номенклатура"));
		Элементы.Номенклатура.СписокВыбора.ЗагрузитьЗначения(НоменклатураСписок);
		Номенклатура = НоменклатураСписок[0];
		
	КонецЕсли;
	
	НоменклатураПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСопоставленияНеизвестныхШтрихкодов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ЗарегистрированныеШтрихкоды.Количество() > 0 Тогда
		ОбработатьШтрихкод(Результат.ЗарегистрированныеШтрихкоды[0], Неопределено);
		
		Если ЗакрытьПослеСканированияШтрихкодаНоменклатуры() Тогда
			ПодключитьОбработчикОжидания("ЗакрытьФорму",0.1,Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДопустимыйФорматШтрихкода(ДанныеШтрихкода)
	
	Если Не ДанныеШтрихкода.Свойство("ШтрихкодBase64") Или Не ДанныеШтрихкода.ШтрихкодBase64 Тогда
		Возврат Истина;
	КонецЕсли;
	
	ЗначениеШтрихкода = ШтрихкодированиеИСКлиентСервер.Base64ВШтрихкод(ДанныеШтрихкода.Штрихкод);
	Если НайтиНедопустимыеСимволыXML(ЗначениеШтрихкода) > 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеШтрихкода.Штрихкод = ЗначениеШтрихкода;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовокЭлементаИдентификаторВЕТИС()
	
	Если Не УказатьИдентификаторВЕТИС Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыСканирования.ТипИдентификатораПроисхожденияВЕТИС) Тогда
		ТипыОграничения = Новый Массив;
		ТипыОграничения.Добавить(ПараметрыСканирования.ТипИдентификатораПроисхожденияВЕТИС);
		Элементы.ИдентификаторПроисхожденияВЕТИС.ОграничениеТипа = Новый ОписаниеТипов(ТипыОграничения);
		Модуль = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияИСМПВЕТИСКлиентСервер");
		Элементы.ИдентификаторПроисхожденияВЕТИС.Заголовок =
			Модуль.ЗаголовокПоляИдентификаторПроисхожденияВЕТИСПоТипу(ПараметрыСканирования.ТипИдентификатораПроисхожденияВЕТИС);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВидыПродукцииПоКодуМаркировкиОстатков(ВидыПродукции, КодМаркировки, Организация)
	
	Если ВидыПродукции.Количество() = 1
		Или Не ЗначениеЗаполнено(КодМаркировки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ШтрихкодированиеИСМП");
		ВидыПродукцииКодаМаркировки = Модуль.ВидыПродукцииПоКодуМаркировкиОстатков(КодМаркировки, Организация);
		Если ВидыПродукцииКодаМаркировки <> Неопределено Тогда
			ВидыПродукции = ИнтеграцияИС.ПересечениеМассивов(ВидыПродукции, ВидыПродукцииКодаМаркировки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Область Оборудование

&НаКлиенте
Процедура ОтключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если Не РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЗакрытьФорму()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктурыДанных = ШтрихкодированиеИСКлиент.ИнициализацияСтруктурыДанныхСохраненногоВыбора();
	СтруктурыДанных.ДанныеВыбора.Номенклатура                    = Номенклатура;
	СтруктурыДанных.ДанныеВыбора.Характеристика                  = Характеристика;
	СтруктурыДанных.ДанныеВыбора.Серия                           = Серия;
	СтруктурыДанных.ДанныеВыбора.ЭтоКодВводаОстатков             = ЭтоМаркировкаОстатков;
	СтруктурыДанных.ДанныеВыбора.ПредставлениеНоменклатуры       = ПредставлениеНоменклатуры();
	СтруктурыДанных.ДанныеВыбора.ИдентификаторПроисхожденияВЕТИС = ИдентификаторПроисхожденияВЕТИС;
	СтруктурыДанных.ДанныеВыбора.ГоденДо                         = ГоденДо;
	СтруктурыДанных.ДанныеВыбора.Скоропортящаяся                 = Скоропортящаяся;
	СтруктурыДанных.ДанныеВыбора.КодМаркировки                   = КодМаркировки;
	СтруктурыДанных.ДанныеВыбора.СоставКодаМаркировки            = СоставКодаМаркировки;
	СтруктурыДанных.ДанныеВыбора.Коэффициент                     = Коэффициент;
	СтруктурыДанных.ЗапомнитьВыбор = ЗапомнитьВыбор;
	
	Если ЗначениеЗаполнено(GTIN) Тогда
		СтруктурыДанных.ДанныеВыбора.GTIN = GTIN;
	ИначеЕсли ЗначениеЗаполнено(ШтрихкодEAN) Тогда
		СтруктурыДанных.ДанныеВыбора.GTIN = ШтрихкодированиеИСКлиентСервер.GTINПоШтрихкодуEAN(ШтрихкодEAN);
	КонецЕсли;
	
	Закрыть(СтруктурыДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборИдентификатораПроисхожденияВЕТИСЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыВЕТИС = Новый Массив;
	ИдентификаторыВЕТИС.Добавить(Результат);
	ДобавитьИдентификаторыВЕТИСВСписокВыбора(ИдентификаторыВЕТИС);
	
	ИдентификаторПроисхожденияВЕТИС = Результат;
	УстановитьГоденДоИСкоропортящаясяПоИдентификаторПроисхожденияВЕТИС();
	
	УстановитьФорматДатыГоденДо(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьФорматДатыГоденДо(Форма)
	
	Если Форма.Скоропортящаяся Тогда
		ФорматДаты = "ДФ='dd.MM.yyyy HH:mm';";
	Иначе
		ФорматДаты = "ДФ=dd.MM.yyyy;";
	КонецЕсли;
	
	Форма.Элементы.ГоденДо.Формат               = ФорматДаты;
	Форма.Элементы.ГоденДо.ФорматРедактирования = ФорматДаты;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГоденДоИСкоропортящаясяПоИдентификаторПроисхожденияВЕТИС()
	
	Если Не ЗначениеЗаполнено(ИдентификаторПроисхожденияВЕТИС) Или ЭтоКодМаркировкиСоСрокомГодности Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВЕТИС = ДанныеИдентификаторовПроисхожденияВЕТИС(ИдентификаторПроисхожденияВЕТИС);
	
	ГоденДо         = ДанныеВЕТИС[ИдентификаторПроисхожденияВЕТИС].СрокГодности;
	Скоропортящаяся = ДанныеВЕТИС[ИдентификаторПроисхожденияВЕТИС].Скоропортящаяся;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеИдентификаторовПроисхожденияВЕТИС(ИдентификаторыПроисхождения)
	
	Модуль = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияИСМПВЕТИС");
	Возврат Модуль.ДанныеИдентификаторовПроисхождения(ИдентификаторыПроисхождения);
	
КонецФункции

&НаКлиенте
Функция ПредставлениеНоменклатуры()
	
	Если ПараметрыСканирования.РазрешенаОбработкаКодовСПустойНоменклатурой
		И Не ЗначениеЗаполнено(Номенклатура) Тогда
		
		Если Не ПустаяСтрока(Элементы.Номенклатура.ПодсказкаВвода) Тогда
			Возврат Элементы.Номенклатура.ПодсказкаВвода;
		КонецЕсли;
		
	Иначе
		
		Возврат ИнтеграцияИСКлиентСервер.ПредставлениеНаименования(Строка(Номенклатура), Ложь);	
		
	КонецЕсли;
	
КонецФункции

// Проверяет возможность закрытия формы уточнения данных после сканирования штрихкода номенклатуры
// 
// Возвращаемое значение:
//  Булево - форму можно закрыть
&НаКлиенте
Функция ЗакрытьПослеСканированияШтрихкодаНоменклатуры()
	
	// Специальные режимы уточнения: сканирование не закрывает форму
	Если РежимПечатиЭтикеток Тогда
		Возврат Ложь;
	ИначеЕсли УказатьИдентификаторВЕТИС
		И Не(ЗначениеЗаполнено(ИдентификаторПроисхожденияВЕТИС) И ЗначениеЗаполнено(ГоденДо))Тогда
		Возврат Ложь;
	// Штрихкод соответствует одному элементу
	ИначеЕсли Элементы.Номенклатура.СписокВыбора.Количество() Тогда
		Возврат Ложь;
	// Данные заполнены
	ИначеЕсли Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат Ложь;
	// Для ввода коэффициента групповой упаковки
	ИначеЕсли Не ЗначениеЗаполнено(Коэффициент) И Элементы.Коэффициент.Видимость Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Отказ = Ложь;
	СобытияФормИСКлиентПереопределяемый.ПроверитьЗаполнение(ЭтотОбъект, Отказ);
	Возврат Не Отказ;
	
КонецФункции

#КонецОбласти
