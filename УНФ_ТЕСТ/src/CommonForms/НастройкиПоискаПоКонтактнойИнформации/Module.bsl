
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ЗаголовокФормы") Тогда
		Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	Если Параметры.Свойство("ЗаголовокПоляПоиска") Тогда
		Элементы.НастройкиПоискаЗначение.Заголовок = Параметры.ЗаголовокПоляПоиска;
	КонецЕсли;
	Если Параметры.Свойство("ВариантПоиска") Тогда
		ВариантПоиска = Параметры.ВариантПоиска;
	Иначе
		ВариантПоиска = 0;
	КонецЕсли;
	
	Для каждого структураНастроек Из Параметры.НастройкиПоискаСоответствие.НастройкиПоиска Цикл
		
		НовСтр = НастройкиПоиска.Добавить();
		
		Для каждого строкаНастроек Из структураНастроек Цикл
			Если НовСтр.Свойство(строкаНастроек.Ключ) Тогда
				
				НовСтр[строкаНастроек.Ключ] = строкаНастроек.Значение;
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	НастройкиПоиска.Сортировать("Порядок");
	
	Для каждого стр Из НастройкиПоиска Цикл
		стр.Порядок = НастройкиПоиска.Индекс(стр)+1;
	КонецЦикла;
	
	УправлениеНебольшойФирмойСервер.НастроитьВспомогательнуюФормуМобильныйКлиент(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СортироватьПоПометке();	
	ОбновитьПорядок();
	ОбновитьВидимость();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Пример(Команда)
	
	РежимТестирования = НЕ РежимТестирования;
	Элементы.ТекстРезультатПоиска.Заголовок = "";
	
	ОбновитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Отказ = Ложь;
	
	Закрыть(ТекущиеНайстройкиВСоответствие(НастройкиПоиска, ВариантПоиска));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьВидимость()

	Элементы.НастройкиПоискаПорядок.Видимость = НЕ ВариантПоиска;
	Если ВариантПоиска = 0 Тогда
		ТекстПодсказки = НСтр("ru = 'Последовательный поиск по отмеченным галочкой полям. Если подходящий контрагент найден, дальнейший поиск прекращается.'");
	Иначе
		ТекстПодсказки = НСтр("ru = 'Поиск контрагента, у которого совпадут все выбранные поля'");
	КонецЕсли;
	
	Элементы.Пример.Заголовок = ?(РежимТестирования, 
		"Выйти из режима тестирования",
		"Попробовать в режиме тестирования"
	);
	Элементы.ГруппаПример.Видимость = РежимТестирования;
	Элементы.ГруппаТестирование.Видимость = РежимТестирования;
	Элементы.ГруппаГалочки.Видимость = НЕ РежимТестирования;
	
	Элементы.ПримерОтметка.Видимость = Ложь;
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Функция ТекущиеНайстройкиВСоответствие(НастройкиПоиска, ВариантПоиска)

	СоответствиеВозврат = Новый Соответствие;
	Для каждого стр Из НастройкиПоиска Цикл
		
		СоответствиеПолей = Новый Соответствие;
		СоответствиеПолей.Вставить("Значение", стр.Значение);
		СоответствиеПолей.Вставить("Порядок", Строка(НастройкиПоиска.Индекс(стр)));
		СоответствиеПолей.Вставить("КонтрагентыПометка", стр.КонтрагентыПометка);
		СоответствиеПолей.Вставить("КонтактныеЛицаПометка", стр.КонтактныеЛицаПометка);
		
		СоответствиеВозврат.Вставить(стр.Значение, СоответствиеПолей);
	
	КонецЦикла;
	СоответствиеВозврат.Вставить("ВариантПоиска", ВариантПоиска);

	Возврат СоответствиеВозврат;
	
КонецФункции
 
&НаКлиенте
Процедура СортироватьПоПометке()
	
	НомерПоследнейСтрокиСПометкой = -1;
	Для каждого стр Из НастройкиПоиска Цикл
		
		ТекСтрока = НастройкиПоиска.Индекс(стр);
		Если (стр.КонтрагентыПометка ИЛИ стр.КонтактныеЛицаПометка) И НомерПоследнейСтрокиСПометкой <> ТекСтрока Тогда
			НастройкиПоиска.Сдвинуть(ТекСтрока, 1 + НомерПоследнейСтрокиСПометкой - ТекСтрока);
			НомерПоследнейСтрокиСПометкой = НастройкиПоиска.Индекс(стр);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СправочникКонтактныеЛицаПометкаПриИзменении(Элемент)
	
	СортироватьПоПометке();
	
КонецПроцедуры

&НаКлиенте
Процедура СправочникКонтрагентыПометкаПриИзменении(Элемент)
	
	СортироватьПоПометке();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоискаПриИзменении(Элемент)

	ОбновитьПорядок();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПорядок()
	
	Для каждого стр Из НастройкиПоиска Цикл
		Если стр.КонтрагентыПометка ИЛИ стр.КонтактныеЛицаПометка Тогда
			стр.Порядок = НастройкиПоиска.Индекс(стр)+1;
		Иначе	
			стр.Порядок = 0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПоискаПриИзменении(Элемент)
	
	ОбновитьВидимость();

КонецПроцедуры

&НаСервере
Процедура ИскатьПокупателяНаСервере()
	
	ТекНастройкиПоиска = Новый Соответствие;
	Для каждого стр Из НастройкиПоиска Цикл
		
		СоответствиеПолей = Новый Соответствие;
		СоответствиеПолей.Вставить("Значение", стр.Значение);
		СоответствиеПолей.Вставить("ЗначениеТип", стр.ЗначениеТип);
		СоответствиеПолей.Вставить("Порядок", Строка(НастройкиПоиска.Индекс(стр)));
		СоответствиеПолей.Вставить("КонтрагентыПометка", стр.КонтрагентыПометка);
		СоответствиеПолей.Вставить("КонтактныеЛицаПометка", стр.КонтактныеЛицаПометка);
		
		ТекНастройкиПоиска.Вставить(стр.Значение, СоответствиеПолей);
	КонецЦикла;
	
	ИнформацияПоКонтрагенту = Новый ТаблицаЗначений;
	ИнформацияПоКонтрагенту.Колонки.Добавить("Тип");
	ИнформацияПоКонтрагенту.Колонки.Добавить("Вид");
	ИнформацияПоКонтрагенту.Колонки.Добавить("Представление");
	
	Для каждого стр Из НастройкиПоиска Цикл
		
		стр.ПримерОтметка = 0;
		
		Если НЕ стр.КонтактныеЛицаПометка=Истина И НЕ стр.КонтрагентыПометка=Истина Тогда
			Продолжить;
		КонецЕсли;
		
		НовСтр = ИнформацияПоКонтрагенту.Добавить();
		НовСтр.Тип = стр.ЗначениеТип;
		НовСтр.Вид = стр.Значение;
		НовСтр.Представление = стр.ПримерПредставление;
		
	КонецЦикла; 
	
	СоответствиеЛогов = Новый Соответствие;
	НайденныйПокупатель = ОбменССайтом.НайтиКонтрагентаПоПолямПоиска(ТекНастройкиПоиска, ИнформацияПоКонтрагенту, ВариантПоиска, СоответствиеЛогов);
	
	Для каждого действие Из СоответствиеЛогов Цикл
		
		НайдСтроки = НастройкиПоиска.НайтиСтроки(Новый Структура("ЗначениеТип", действие.Значение.Получить("ЗначениеТип")));
		Для каждого стр Из НайдСтроки Цикл
			
			стр.ПримерОтметка = ?(действие.Значение.Получить("РезультатПоиска") = Истина, 2, 1);
		
		КонецЦикла;
		
	КонецЦикла;
	
	Если НайденныйПокупатель = Неопределено Тогда
		Элементы.ТекстРезультатПоиска.Заголовок = "По указанным данным ни один покупатель не найден, будет создан новый покупатель";
	Иначе
		Элементы.ТекстРезультатПоиска.Заголовок = Новый ФорматированнаяСтрока(
			"По указанным данным найден покупатель: ",
			Новый ФорматированнаяСтрока(Строка(НайденныйПокупатель),,,,ПолучитьНавигационнуюСсылку(НайденныйПокупатель))
		);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ИскатьПокупателя(Команда)
	
	ИскатьПокупателяНаСервере();
	Элементы.ПримерОтметка.Видимость = НЕ ВариантПоиска;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоискаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоискаПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти
