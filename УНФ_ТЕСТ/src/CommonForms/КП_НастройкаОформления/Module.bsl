&НаКлиенте 
Перем ЗакрытиеРазрешено;

&НаКлиенте
Перем ИдетРаскраскаЗаголовка;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТекстОбразцаЗаголовка", ПримерTitle);
	ТекущийПользователь=Параметры.ТекущийПользователь;
	ЭтоЗаполнениеИзФормыОбъекта=Параметры.ЭтоЗаполнениеИзФормыОбъекта;
	
	Если Параметры.Свойство("ИмяФормы") Тогда
		ИмяИсходнойФормы=Параметры.ИмяФормы;
	КонецЕсли;
	
	МассивОбъектов=Параметры.МассивОбъектов;
	Для Каждого ЭлементМассива Из МассивОбъектов Цикл
		Если СписокОбъектов.НайтиПоЗначению(ЭлементМассива)=Неопределено Тогда
			СписокОбъектов.Добавить(ЭлементМассива);
		КонецЕсли;
	КонецЦикла;
	
	Если ЭтоЗаполнениеИзФормыОбъекта Тогда
		АдресХраненияНастройкиЦветов=Параметры.АдресХраненияНастройкиЦветов;	
		Если ПустаяСтрока(АдресХраненияНастройкиЦветов) Тогда
			Отказ=Истина;
			Возврат;
		КонецЕсли;
		
		МассивНастройкиЦветов=ПолучитьИзВременногоХранилища(АдресХраненияНастройкиЦветов);
	
	Иначе
		//передан список
		Если МассивОбъектов.Количество()=1 Тогда
			ПервыйОбъект=МассивОбъектов[0];
			МассивНастройкиЦветов=КП_ОформлениеСтрокСервер.ПолучитьМассивЦветовОбъекта(ПервыйОбъект, ТекущийПользователь);
		Иначе
			МассивНастройкиЦветов=Неопределено;
		КонецЕсли;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(МассивНастройкиЦветов) И МассивНастройкиЦветов.Количество()=4 Тогда
		ЦветТекстаСсылка=МассивНастройкиЦветов[0];
		ЦветФонаСсылка=МассивНастройкиЦветов[1];
		ШрифтЖирный=МассивНастройкиЦветов[2];
		ШрифтНаклонный=МассивНастройкиЦветов[3];         	
	КонецЕсли;
		
	Если Параметры.Свойство("Заголовок") Тогда
		ЭтаФорма.Заголовок=Параметры.Заголовок;
	Иначе
		ЭтаФорма.Заголовок="Настройка оформления";
	КонецЕсли;
	
	ЗаполнитьТаблицыЦветов();	
	
	Если ЗначениеЗаполнено(ЦветТекстаСсылка) Тогда
		ВыделитьЦветВТаблицеТекста(ЦветТекстаСсылка);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЦветФонаСсылка) Тогда
		ВыделитьЦветВТаблицеФона(ЦветФонаСсылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыделитьЦветВТаблицеТекста(ВыделяемыйЦвет)
	
	Для Каждого СтрокаТЧ Из ТаблицаЦветаТекста Цикл
		Если СтрокаТЧ.КолонкаЦвет=ВыделяемыйЦвет Тогда
			СтрокаТЧ.ЦветВыбран=Истина;
			Возврат;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Процедура ВыделитьЦветВТаблицеФона(ВыделяемыйЦвет)
	
	Для Каждого СтрокаТЧ Из ТаблицаЦветаФона Цикл
		Если СтрокаТЧ.КолонкаЦвет=ВыделяемыйЦвет Тогда
			СтрокаТЧ.ЦветВыбран=Истина;
			Возврат;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИдетРаскраскаЗаголовка=Истина;	
	
	УстановитьОформлениеЗаголовка();
	
	ПодключитьОбработчикОжидания("Подключаемый_ФормированиеТаблицЗавершено", 0.2, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ФормированиеТаблицЗавершено() Экспорт 
	ИдетРаскраскаЗаголовка=Ложь;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицыЦветов()
	
	ТаблицаЦветовОбщая=КП_ОформлениеСтрокСерверПС.ПолучитьТаблицуЦветов();
	
	ТаблицаЦветаТекста.Очистить();
	ТаблицаЦветаФона.Очистить();
	
	ЭтоПерваяТаблица=Истина;
	
	КоличествоЦветовВТаблице=ТаблицаЦветовОбщая.Количество()-1;
	Для НомерСтроки=0 По КоличествоЦветовВТаблице Цикл
		
		СтрокаТекста=ТаблицаЦветовОбщая[НомерСтроки];
		СтрокаФона=ТаблицаЦветовОбщая[КоличествоЦветовВТаблице-НомерСтроки];
		
		СтрокаТаблицы=ТаблицаЦветаТекста.Добавить();
		СтрокаТаблицы.КолонкаФон=СтрокаТекста.Ссылка;
		СтрокаТаблицы.КолонкаЦвет=СтрокаТекста.Ссылка;
		
		СтрокаТаблицы=ТаблицаЦветаФона.Добавить();
		СтрокаТаблицы.КолонкаФон=СтрокаФона.Ссылка;			
		СтрокаТаблицы.КолонкаЦвет=СтрокаФона.Ссылка;
		
	КонецЦикла;
	
	УстановитьОформлениеСтрокЦветом();
			
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия="СозданНовыйЦвет" Тогда
		ЗаполнитьТаблицыЦветов();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ДобавитьЦветСервером(ИмяКолонкиЦвета, ИмяОформляемойКолонки, СсылкаНаЦвет)
	//ОформлениеСтрок.ДобавитьНовыйЦветВОформление(Список, ИмяКолонкиЦвета, ИмяОформляемойКолонки, СсылкаНаЦвет);
	//ОформлениеСтрок.ДобавитьНовыйЦветВОформление(Список, ИмяКолонкиЦвета, ИмяОформляемойКолонки, СсылкаНаЦвет, "ЦветФона");
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Результат=Новый Структура;
	Результат.Вставить("ШрифтЖирный", ШрифтЖирный);
	Результат.Вставить("ШрифтНаклонный", ШрифтНаклонный);
	Результат.Вставить("ЦветТекста", ЦветТекстаСсылка);
	Результат.Вставить("ЦветФона", ЦветФонаСсылка);
	Результат.Вставить("АдресХраненияНастройкиЦветов", АдресХраненияНастройкиЦветов);
	
	ЗакрытиеРазрешено=Истина;
	ЭтаФорма.Закрыть(Результат);
	
КонецПроцедуры

Процедура УстановитьОформлениеСтрокЦветом()
	
	Для Каждого СтрокаТЧ Из ТаблицаЦветаТекста Цикл		
		СсылкаНаЦвет=СтрокаТЧ.КолонкаЦвет;
		ДобавитьНовыйЦветВОформление("ТаблицаЦветаТекста.КолонкаЦвет", "ТаблицаЦветаТекстаКолонкаЦвет", СсылкаНаЦвет, "ЦветТекста");		
		ДобавитьНовыйЦветВОформление("ТаблицаЦветаТекста.КолонкаФон", "ТаблицаЦветаТекстаКолонкаФон", СсылкаНаЦвет, "ЦветФона");		
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из ТаблицаЦветаФона Цикл		
		СсылкаНаЦвет=СтрокаТЧ.КолонкаФон;
		ДобавитьНовыйЦветВОформление("ТаблицаЦветаФона.КолонкаЦвет", "ТаблицаЦветаФонаКолонкаЦвет", СсылкаНаЦвет, "ЦветТекста");		
		ДобавитьНовыйЦветВОформление("ТаблицаЦветаФона.КолонкаФон", "ТаблицаЦветаФонаКолонкаФон", СсылкаНаЦвет, "ЦветФона");					
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНовыйЦветВОформление(ИмяКолонкиЦвета, ИмяОформляемойКолонки, СсылкаНаЦвет, ИмяПараметраЦвета) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЦветТекста=Новый Цвет(СсылкаНаЦвет.Red, СсылкаНаЦвет.Green, СсылкаНаЦвет.Blue);
	
	ЭлементОформления=ЭтаФорма.УсловноеОформление.Элементы.Добавить();
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение=Новый ПолеКомпоновкиДанных(ИмяКолонкиЦвета);
	ЭлементОтбора.ВидСравнения=ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение=СсылкаНаЦвет;
	ЭлементОтбора.Использование=Истина;	
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра(ИмяПараметраЦвета, ЦветТекста);
	
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(ИмяОформляемойКолонки);
	ПолеОформления.Использование = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЦветаТекстаПриАктивизацииСтроки(Элемент)
	Если ИдетРаскраскаЗаголовка ИЛИ Элементы.ТаблицаЦветаТекста.ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтрокаТЧ=Элементы.ТаблицаЦветаТекста.ТекущиеДанные;
	ЦветТекстаСсылка=СтрокаТЧ.КолонкаФон;	
	ИдетРаскраскаЗаголовка=Истина;	
	СнятьВыделениеСтрок();
	СтрокаТЧ.ЦветВыбран=Истина;
	УстановитьОформлениеЗаголовка();
	ИдетРаскраскаЗаголовка=Ложь;	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЦветаФонаПриАктивизацииСтроки(Элемент)
	Если ИдетРаскраскаЗаголовка ИЛИ Элементы.ТаблицаЦветаФона.ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;	
	СтрокаТЧ=Элементы.ТаблицаЦветаФона.ТекущиеДанные;
	ЦветФонаСсылка=СтрокаТЧ.КолонкаФон;	
	ИдетРаскраскаЗаголовка=Истина;	
	СнятьВыделениеСтрок();
	СтрокаТЧ.ЦветВыбран=Истина;
	УстановитьОформлениеЗаголовка();
	ИдетРаскраскаЗаголовка=Ложь;	
КонецПроцедуры

Процедура СнятьВыделениеСтрок()
	Для Каждого СтрокаТЧ Из ТаблицаЦветаТекста Цикл
		СтрокаТЧ.ЦветВыбран=Ложь;
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из ТаблицаЦветаФона Цикл
		СтрокаТЧ.ЦветВыбран=Ложь;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЗаголовка()
		
	Если ЗначениеЗаполнено(ЦветТекстаСсылка) Тогда
		ВыбранныйЦвет=Новый Цвет(ЦветТекстаСсылка.Red, ЦветТекстаСсылка.Green, ЦветТекстаСсылка.Blue);
		Элементы.ПримерTitle.ЦветТекста=ВыбранныйЦвет;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЦветФонаСсылка) Тогда
		ВыбранныйЦвет=Новый Цвет(ЦветФонаСсылка.Red, ЦветФонаСсылка.Green, ЦветФонаСсылка.Blue);
		Элементы.ПримерTitle.ЦветФона=ВыбранныйЦвет;
	КонецЕсли;
	
	ШрифтЗаголовка=Новый Шрифт(,,ШрифтЖирный, ШрифтНаклонный);
	Элементы.ПримерTitle.Шрифт=ШрифтЗаголовка;
	
КонецПроцедуры

&НаКлиенте
Процедура ШрифтЖирныйПриИзменении(Элемент)
	ИдетРаскраскаЗаголовка=Истина;	
	УстановитьОформлениеЗаголовка();
	ИдетРаскраскаЗаголовка=Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ШрифтНаклонныйПриИзменении(Элемент)
	ИдетРаскраскаЗаголовка=Истина;	
	УстановитьОформлениеЗаголовка();
	ИдетРаскраскаЗаголовка=Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРедактированиеЦветовНажатие(Элемент)
	ОткрытьФорму("Справочник.Colors.ФормаСписка");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И НЕ ЗакрытиеРазрешено Тогда 
		Если ЗавершениеРаботы Тогда
			ТекстПредупреждения="Настройки не сохранены.";
			Отказ=Истина;
			Возврат;
		КонецЕсли;
	
		ПоказатьВопрос(Новый ОписаниеОповещения("Оповещение_ПередЗакрытиемЗавершение", ЭтотОбъект, Новый Структура), 
			"Настройки не сохранены.", КП_ОбщееКлиент.КнопкиВопроса("Сохранить"),,,КП_ОбщееКлиент.ЗаголовокДиалога());
		Отказ=Истина;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте 
Процедура Оповещение_ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт 
	
	Если РезультатВопроса=КодВозвратаДиалога.Нет Тогда 		
		ЗакрытиеРазрешено=Истина; 
		ЭтаФорма.Закрыть(); 
		Возврат;
		
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Да Тогда 		
		Результат=Новый Структура;
		Результат.Вставить("ШрифтЖирный", ШрифтЖирный);
		Результат.Вставить("ШрифтНаклонный", ШрифтНаклонный);
		Результат.Вставить("ЦветТекста", ЦветТекстаСсылка);
		Результат.Вставить("ЦветФона", ЦветФонаСсылка);
		ЗакрытиеРазрешено=Истина;
		ЭтаФорма.Закрыть(Результат);
		Возврат;
						
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНовыйЦвет(Команда)
	
	ПараметрыФормы=Новый Структура;
	ОткрытьФорму("Справочник.КП_ЦветаЭлементов.ФормаОбъекта",ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЦвет(ЦветСсылка);
	ПараметрыФормы=Новый Структура("Ключ", ЦветСсылка);
	ОткрытьФорму("Справочник.КП_ЦветаЭлементов.ФормаОбъекта",ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЦветаТекстаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	РедактироватьЦветТекста(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЦветаФонаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	РедактироватьЦветФона(Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЦветТекста(Команда)
	
	СтрокаТЧ=Элементы.ТаблицаЦветаТекста.ТекущиеДанные;
	
	Если СтрокаТЧ=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныйЦвет=СтрокаТЧ.КолонкаЦвет;
	
	ОткрытьЦвет(ВыбранныйЦвет);

КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЦветФона(Команда)
	
	СтрокаТЧ=Элементы.ТаблицаЦветаФона.ТекущиеДанные;
	
	Если СтрокаТЧ=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныйЦвет=СтрокаТЧ.КолонкаЦвет;
	
	ОткрытьЦвет(ВыбранныйЦвет);

КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьЦвета(Команда)
	
	Если НЕ КП_ОбщееСерверПС.ЭтоРольПолныеПрава() Тогда
		КП_ОбщееКлиент.ПредупреждениеПользователю("Операция доступна для пользователя с полными правами");
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("Оповещение_ОтветНаВопросПерезаполнения", ЭтаФорма, Новый Структура), 
		"Перезаполнить все цвета по умолчанию?", 
		КП_ОбщееКлиент.КнопкиВопроса("Да, перезаполнить"),,,КП_ОбщееКлиент.ЗаголовокДиалога());
	
КонецПроцедуры

&НаКлиенте
Процедура Оповещение_ОтветНаВопросПерезаполнения(Результат, ДопПараметры) Экспорт
	Если Результат=КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ПерезаполнитьЦветаСервером();
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьЦветаСервером()
	Справочники.КП_ЦветаЭлементов.ЗаполнитьОсновныеЦвета();	
	ЗаполнитьТаблицыЦветов();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПоУмолчанию(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("Оповещение_ОтветНаВопросОбОчистке", ЭтотОбъект, Новый Структура), 
		"Очистить настройки и закрыть форму?", РежимДиалогаВопрос.ДаНет,,,КП_ОбщееКлиент.ЗаголовокДиалога());

КонецПроцедуры

&НаКлиенте 
Процедура Оповещение_ОтветНаВопросОбОчистке(РезультатВопроса, ДополнительныеПараметры) Экспорт 
	
	Если РезультатВопроса=КодВозвратаДиалога.Нет Тогда 		
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоЗаполнениеИзФормыОбъекта Тогда
		КП_ОформлениеСтрокСервер.УдалитьОформлениеСпискаОбъектов(СписокОбъектов, ТекущийПользователь);
	КонецЕсли;
			
	Результат=Новый Структура;
	Результат.Вставить("РежимПоУмолчанию", Истина);
	
	ЗакрытиеРазрешено=Истина;
	ЭтаФорма.Закрыть(Результат);
	
КонецПроцедуры

ИдетРаскраскаЗаголовка=Ложь;
ЗакрытиеРазрешено=Ложь;
