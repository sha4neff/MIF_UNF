&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокШаблоновСообщения=КП_ОбщееСервер.ПолучитьСписокШаблоновПоТипуПараметра();
	Элементы.Исполнитель.КнопкаОчистки=ЗначениеЗаполнено(Исполнитель);
	
	Если СписокШаблоновСообщения.Количество()>0 Тогда
		ШаблонСообщения=СписокШаблоновСообщения[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПроцессаПриИзменении(Элемент)
	СформироватьНаименование();
КонецПроцедуры

&НаКлиенте
Процедура ТочкаКБППриИзменении(Элемент)
	Если ЗначениеЗаполнено(ТочкаКБП) Тогда
		ВидПроцесса=КП_ОбщееСервер.ПолучитьЗначениеРеквизитаОбъекта(ТочкаКБП, "ВладелецТочки");
	КонецЕсли;
	
	СформироватьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонСообщенияПриИзменении(Элемент)
	СформироватьНаименование();
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительПриИзменении(Элемент)
	Элементы.Исполнитель.КнопкаОчистки=ЗначениеЗаполнено(Исполнитель);
	СформироватьНаименование();
КонецПроцедуры

Процедура СформироватьНаименование()
	Наименование="";//СокрЛП(ВидПроцесса);
	Наименование=Наименование+?(ПустаяСтрока(Наименование), "", " ")+СокрЛП(ШаблонСообщения);	
	Наименование=Наименование+?(ПустаяСтрока(Наименование), "", " ")+СокрЛП(ТочкаКБП);
	
	Если НЕ ПустаяСтрока(Исполнитель) Тогда
		Наименование=Наименование+?(ПустаяСтрока(Наименование), "", " ("+СокрЛП(Исполнитель)+")");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуУведомления(Команда)
	
	Если НЕ ЗначениеЗаполнено(ВидПроцесса) Тогда
		ПоказатьПредупреждение(Неопределено, "Укажите вид процесса");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТочкаКБП) Тогда
		ПоказатьПредупреждение(Неопределено, "Укажите точку действия");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ШаблонСообщения) Тогда
		ПоказатьПредупреждение(Неопределено, "Укажите шаблон сообщения");
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("Оповещение_ВопросОСоздании", ЭтаФорма, Новый Структура),
		"Создать настройку уведомления?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура Оповещение_ВопросОСоздании(Результат, ДопПараметры) Экспорт
	Если Результат=КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Если СоздатьНастройкуСервером() Тогда	
		Оповестить("КП_ПравилоОбработкиСобытияСоздано");
	    Состояние("Настройка успешно создана");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТочкаКБПНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;

	СтруктураОтбора=Новый Структура("ТипТочки", ПредопределенноеЗначение("Перечисление.КП_ВидыТочекБизнесПроцесса.Действие"));
	Если ЗначениеЗаполнено(ВидПроцесса) Тогда
		СтруктураОтбора.Вставить("ВладелецТочки", ВидПроцесса);
	КонецЕсли;
	
	ПараметрыФормы=Новый Структура("РежимВыбора, МножественныйВыбор", Истина, Ложь);
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	ПараметрыФормы.Вставить("ТолькоВидыПроцессов", Истина);
	
	ОткрытьФорму("Справочник.КП_ТочкиПроцессов.ФормаВыбора", ПараметрыФормы,,,,,
		Новый ОписаниеОповещения("Оповещение_ВыборТочкиПроцесса", ЭтаФорма, Новый Структура),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Оповещение_ВыборТочкиПроцесса(Результат, ДопПараметр) Экспорт
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ТочкаКБП=Результат;
	
	ТочкаКБППриИзменении(Неопределено);	
	
КонецПроцедуры

&НаСервере
Функция СоздатьНастройкуСервером()
	
	УстановитьПривилегированныйРежим(Истина);
	
	//сначала найдем или создадим действие рабочего процесса
	
	ПолноеИмяОснования="Задача.КП_Задача";
	
	Запрос=Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                    |	ДействияРабочегоПроцесса.Ссылка КАК Ссылка
	                    |ИЗ
	                    |	Справочник.ДействияРабочегоПроцесса КАК ДействияРабочегоПроцесса
	                    |ГДЕ
	                    |	ДействияРабочегоПроцесса.ПометкаУдаления = ЛОЖЬ
	                    |	И ДействияРабочегоПроцесса.ТипДействия = &ТипДействия
	                    |	И ДействияРабочегоПроцесса.ШаблонСообщения = &ШаблонСообщения
	                    |	И ДействияРабочегоПроцесса.ПолноеИмяОснования = &ПолноеИмяОснования");
	
	Запрос.УстановитьПараметр("ТипДействия", Перечисления.ТипыДействийРабочегоПроцесса.ОтправитьEmail);
	Запрос.УстановитьПараметр("ШаблонСообщения", ШаблонСообщения);
	Запрос.УстановитьПараметр("ПолноеИмяОснования", ПолноеИмяОснования);
	
	Выборка=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.Прямой);
	
	Если Выборка.Следующий() Тогда
		ДействиеСсылка=Выборка.Ссылка;
	Иначе
		
		ДействиеОбъект=Справочники.ДействияРабочегоПроцесса.СоздатьЭлемент();
		ДействиеОбъект.Наименование="Отправить E-mail по основанию: Задача";
		ДействиеОбъект.ТипДействия=Перечисления.ТипыДействийРабочегоПроцесса.ОтправитьEmail;
		ДействиеОбъект.ШаблонСообщения=ШаблонСообщения;
		ДействиеОбъект.ПолноеИмяОснования=ПолноеИмяОснования;
		
		НоваяСтрока=ДействиеОбъект.НастройкиЗаполнения.Добавить();
		НоваяСтрока.ИмяРеквизита="Контакт";
		НоваяСтрока.ВариантЗаполнения="ИзОснования";
		НоваяСтрока.Значение=Новый ХранилищеЗначения("Исполнитель");
		
		НоваяСтрока=ДействиеОбъект.НастройкиЗаполнения.Добавить();
		НоваяСтрока.ИмяРеквизита="ВидКонтактнойИнформации";
		НоваяСтрока.ВариантЗаполнения="Указанный";
		НоваяСтрока.Значение=Новый ХранилищеЗначения(Справочники.ВидыКонтактнойИнформации.EmailПользователя);
		
		НоваяСтрока=ДействиеОбъект.НастройкиЗаполнения.Добавить();
		НоваяСтрока.ИмяРеквизита="УчетнаяЗапись";
		НоваяСтрока.ВариантЗаполнения="Указанный";
		НоваяСтрока.Значение=Новый ХранилищеЗначения(Справочники.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты);
		
		НоваяСтрока=ДействиеОбъект.НастройкиЗаполнения.Добавить();
		НоваяСтрока.ИмяРеквизита="Ответственный";
		НоваяСтрока.ВариантЗаполнения="Указанный";
		НоваяСтрока.Значение=Новый ХранилищеЗначения(Справочники.Сотрудники.ПустаяСсылка());
		
		Попытка
			ДействиеОбъект.Записать();
		Исключение
			КП_ОбщееСервер.ЗаписатьОшибку(ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки;
		
		ДействиеСсылка=ДействиеОбъект.Ссылка;
		
	КонецЕсли;
	
	//создадим новое правило рабочего процесса
	
	КомпоновщикНастроек=Новый КомпоновщикНастроекКомпоновкиДанных;
	СКД=Задачи.КП_Задача.ПолучитьМакет("СКД_КритерииПравилаРабочегоПроцесса");
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ПоместитьВоВременноеХранилище(СКД, УникальныйИдентификатор)));
	КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	
	Для Каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		ЛевоеЗначение=Новый ПолеКомпоновкиДанных("ТочкаВидаПроцесса");
		Если ЭлементОтбора.ЛевоеЗначение=ЛевоеЗначение Тогда
			ЭлементОтбора.ПравоеЗначение=ТочкаКБП;
			ЭлементОтбора.Использование=Истина;
		КонецЕсли;
		Если ЗначениеЗаполнено(Исполнитель) Тогда
			ЛевоеЗначение=Новый ПолеКомпоновкиДанных("Исполнитель");
			Если ЭлементОтбора.ЛевоеЗначение=ЛевоеЗначение Тогда
				ЭлементОтбора.ПравоеЗначение=Исполнитель;
				ЭлементОтбора.Использование=Истина;
			КонецЕсли;
		КонецЕсли;		
	КонецЦикла;
	
	//установим значения отборов
	
	ХранилищеОтбора=Новый ХранилищеЗначения(КомпоновщикНастроек.Настройки);
	
	ПравилоОбъект=Справочники.ПравилаРабочегоПроцесса.СоздатьЭлемент();
	Наименование="Уведомление о задаче на E-mail";
	Наименование=Наименование+?(ПустаяСтрока(Наименование), "", ", ")+СокрЛП(ВидПроцесса);
	Если НЕ ПустаяСтрока(Исполнитель) Тогда
		Наименование=Наименование+?(ПустаяСтрока(Наименование), "", " (")+СокрЛП(Исполнитель)+")";
	КонецЕсли;
	
	ПравилоОбъект.Наименование=Наименование;
	ПравилоОбъект.Включено=Истина;
	ПравилоОбъект.Комментарий="Создано из мастера создания в подсистеме ""Процессы""";
	ПравилоОбъект.УсловиеСтарта=Перечисления.СобытияРабочегоПроцесса.КП_Задача;
	ПравилоОбъект.НастройкиОтбора=ХранилищеОтбора;
	
	СтрокаДействия=ПравилоОбъект.Действия.Добавить();
	СтрокаДействия.Действие=ДействиеСсылка;
	
	ПравилоОбъект.ПредставлениеОтбора=ОтчетыУНФКлиентСервер.ПредставлениеОтбора(КомпоновщикНастроек.Настройки.Отбор);
	
	Попытка
		ПравилоОбъект.Записать();
	Исключение
		ПравилоОбъект.ЗаписатьОшибку(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции
