#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("НастройкиВыгрузкиРеквизитов") Тогда
		ЗаполнитьСписокРеквизитов(Параметры.НастройкиВыгрузкиРеквизитов);
	ИначеЕсли Параметры.Свойство("НастройкиВыгрузкиДопРеквизитовЗаказов") Тогда
		ЗаполнитьСписокДопРеквизитовЗаказов(Параметры.НастройкиВыгрузкиДопРеквизитовЗаказов, Параметры.ЗаказПокупателя);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ВыбранныеРеквизиты = Новый Структура;
	
	Для каждого реквизит Из СписокРеквизитов Цикл
		
		Если реквизит.Пометка Тогда
			ВыбранныеРеквизиты.Вставить(реквизит.Значение, реквизит.Представление);
		КонецЕсли;
		
	КонецЦикла;
	
	Закрыть(ВыбранныеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗаполнитьСписокДопРеквизитовЗаказов(ТекущиеНастройки, ВладелецСвойств)
	
	Если ЗначениеЗаполнено(ТекущиеНастройки) Тогда
		СоответствиеВыбранныхРеквизитов = ОбменССайтом.ЧтениеJSONВСтруктуру(ТекущиеНастройки, Истина);
	Иначе
		СоответствиеВыбранныхРеквизитов = Новый Соответствие;
	КонецЕсли;
	
	ТаблицаСвойств = УправлениеСвойствамиСлужебный.СписокСвойствДляВидаОбъектов("Документ_ЗаказПокупателя", "ДополнительныеРеквизиты");
	Для каждого ДопРеквизит Из ТаблицаСвойств Цикл
		
		СвойстваРеквизитаСписка = Неопределено;
		ИмяРеквизита = ДопРеквизит.Свойство.ИдентификаторДляФормул;
		НаименованиеДляВыгрузки = СоответствиеВыбранныхРеквизитов.Получить(ИмяРеквизита);
		
		НовСтр = СписокРеквизитов.Добавить();
		НовСтр.Значение = ИмяРеквизита;
		
		Если НаименованиеДляВыгрузки = Неопределено Тогда
			НовСтр.Представление = ДопРеквизит.Наименование;
			НовСтр.Пометка = Ложь;
		Иначе
			НовСтр.Представление = НаименованиеДляВыгрузки;
			НовСтр.Пометка = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	СортироватьПоПометке();
	
КонецФункции

&НаСервере
Функция ЗаполнитьСписокРеквизитов(ТекущиеНастройки)
	
	Если ЗначениеЗаполнено(ТекущиеНастройки) Тогда
		СписокВыбранныхРеквизитов = ОбменССайтом.ЧтениеJSONВСтруктуру(ТекущиеНастройки, Ложь);
	Иначе
		СписокВыбранныхРеквизитов = Новый Структура;
	КонецЕсли;
	
	Для каждого реквизит Из Метаданные.Справочники.Номенклатура.Реквизиты Цикл
		
		СвойстваРеквизитаСписка = Неопределено;
		ЕстьРеквизит = СписокВыбранныхРеквизитов.Свойство(реквизит.Имя, СвойстваРеквизитаСписка);
		
		НовСтр = СписокРеквизитов.Добавить();
		НовСтр.Значение = реквизит.Имя;
		Если ЕстьРеквизит Тогда
			НовСтр.Представление = ?(ЗначениеЗаполнено(СвойстваРеквизитаСписка), СвойстваРеквизитаСписка, реквизит.Имя);
			НовСтр.Пометка = Истина;
		Иначе
			НовСтр.Представление = реквизит.Имя;
			НовСтр.Пометка = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	СортироватьПоПометке();
	
КонецФункции

&НаКлиенте
Процедура СписокРеквизитовПриИзменении(Элемент)
	
	СортироватьПоПометке();
	
КонецПроцедуры

&НаСервере
Процедура СортироватьПоПометке()
	
	ТЗРеквизитов = РеквизитФормыВЗначение("СписокРеквизитов");
	ТЗРеквизитов.Сортировать("Пометка Убыв, Значение Возр");
	
	ЗначениеВРеквизитФормы(ТЗРеквизитов, "СписокРеквизитов");
	
КонецПроцедуры

#КонецОбласти

