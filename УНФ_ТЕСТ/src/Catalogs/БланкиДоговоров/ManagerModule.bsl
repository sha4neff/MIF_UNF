#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ТекстПоставляемогоБланка(ИмяМакета) Экспорт
	
	МакетДоговора = Справочники.БланкиДоговоров.ПолучитьМакет(ИмяМакета);
	
	ТекстХТМЛ = МакетДоговора.ПолучитьТекст();
	
	Падежи = Новый Массив;
	Падежи.Добавить(Неопределено);
	Падежи.Добавить("именительный");
	Падежи.Добавить("родительный");
	Падежи.Добавить("дательный");
	Падежи.Добавить("винительный");
	Падежи.Добавить("творительный");
	Падежи.Добавить("предложный");
	
	Для каждого ПараметрПеречисление Из Перечисления.ПараметрыБланковДоговоровСКонтрагентами Цикл
		
		Для каждого Падеж Из Падежи Цикл
			Если Падеж = Неопределено Тогда
				ПредставлениеПадежа = "";
			Иначе
				ПредставлениеПадежа = " (" + Падеж + ")";
			КонецЕсли;
			
			Параметр = "{" + Строка(ПараметрПеречисление) + ПредставлениеПадежа + "}";
			ЧислоВхождений = СтрЧислоВхождений(ТекстХТМЛ, Параметр);
			Для НомерПараметра = 1 По ЧислоВхождений Цикл
				Если НомерПараметра = 1 Тогда
					Представление = "{" + Строка(ПараметрПеречисление) + ПредставлениеПадежа + "%deleteSymbols%" + "}";
					Идентификатор = "infoParameter" + Строка(ПараметрПеречисление) + НомерПараметра;
				Иначе
					Представление = "{" + Строка(ПараметрПеречисление) + НомерПараметра + ПредставлениеПадежа + "}";
					Идентификатор = "infoParameter" + Строка(ПараметрПеречисление) + НомерПараметра;
				КонецЕсли;
				
				ПервоеВхождение = Найти(ТекстХТМЛ, Параметр);
				
				ТекстХТМЛ = Лев(ТекстХТМЛ, ПервоеВхождение - 1) + Представление + Сред(ТекстХТМЛ, ПервоеВхождение + СтрДлина(Параметр));
				
			КонецЦикла;
			
			ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%deleteSymbols%", "");
			
		КонецЦикла;
	КонецЦикла;
	
	ВложенияФД = Новый Структура;
	ФД = Новый ФорматированныйДокумент;
	ФД.УстановитьHTML(ТекстХТМЛ, ВложенияФД);
	ФД.ПолучитьHTML(ТекстХТМЛ, ВложенияФД);
	
	Возврат ТекстХТМЛ;
	
КонецФункции

#КонецОбласти

#КонецЕсли
