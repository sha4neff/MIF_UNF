#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
		
		ГруппаОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(Список.Отбор.Элементы, 
			НСтр("ru = 'Отбор по организации'"), ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, "Организация", 
			ВидСравненияКомпоновкиДанных.Равно, Параметры.Организация, НСтр("ru = 'Маршруты переданной организации'"), Истина);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, "Предопределенный", 
			ВидСравненияКомпоновкиДанных.Равно, Истина, НСтр("ru = 'ПредопределенныеМаршруты'"), Истина);
			
		Заголовок = Организация;
	КонецЕсли;
	
	Параметры.Свойство("ВидПодписи", ВидПодписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если ЗначениеЗаполнено(Организация) Или ЗначениеЗаполнено(ВидПодписи) Тогда
		Отказ = Истина;
		ЗначенияЗаполнения = Новый Структура;
		Если ЗначениеЗаполнено(Организация) Тогда
			ЗначенияЗаполнения.Вставить("Организация", Организация);
		КонецЕсли;
		Если ЗначениеЗаполнено(ВидПодписи) Тогда
			ЗначенияЗаполнения.Вставить("ВидПодписи", ВидПодписи);
		КонецЕсли;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ОткрытьФорму("Справочник.МаршрутыПодписания.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор); 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьНачальныеНастройки(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ОбменСКонтрагентами") Тогда
		МодульОбменСКонтрагентами = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСКонтрагентамиСлужебныйВызовСервера");
		МодульОбменСКонтрагентами.ЗаполнитьНачальныеНастройкиМаршрутов();
	КонецЕсли;
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти
