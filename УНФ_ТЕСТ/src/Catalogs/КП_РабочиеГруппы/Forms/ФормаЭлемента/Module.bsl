#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Ключ.Пустая() Тогда
		Если Параметры.Свойство("Родитель") Тогда
			Объект.Родитель=Параметры.Родитель;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подбор(Команда)
	ПараметрыФормы=Новый Структура("ЗакрыватьПриВыборе, РежимВыбора, МножественныйВыбор", Ложь, Истина, Истина);
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, Элементы.Сотрудники); 
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Для Каждого ЭлементМассива Из ВыбранноеЗначение Цикл
		Если Объект.Сотрудники.НайтиСтроки(Новый Структура("Пользователь", ЭлементМассива)).Количество()>0 Тогда
			Продолжить; //сотрудник уже в списке
		КонецЕсли;
		НоваяСтрока=Объект.Сотрудники.Добавить();
		НоваяСтрока.Пользователь=ЭлементМассива;
		Модифицированность=Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка=Ложь;
	ОткрытьФормуАдресации(ВыбраннаяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуАдресации(ВыбраннаяСтрока)
	
	ПараметрыФормы=Новый Структура;
	ПараметрыЗаполнения=Новый Структура;
	
	СтрокаТЧ=Элементы.Сотрудники.ТекущиеДанные;
	
	ПараметрыЗаполнения.Вставить("ТочкаАдресации", СтрокаТЧ.ТочкаАдресации);
	
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ПараметрыЗаполнения);
	ПараметрыФормы.Вставить("ПоказыватьПараметрыЗадачи", Ложь);
	ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru='Элемент адресации' en='Addressing element';"));
	
	ОткрытьФорму("ОбщаяФорма.КП_ФормаАдресации", ПараметрыФормы,,,,, Новый ОписаниеОповещения("Оповещение_ОткрытьФормуАдресацииЗавершение", ЭтотОбъект, Новый Структура("СтрокаТЧ", СтрокаТЧ)));
	
КонецПроцедуры

&НаКлиенте
Процедура Оповещение_ОткрытьФормуАдресацииЗавершение(СтруктураРезультата, ДополнительныеПараметры) Экспорт
    
    СтрокаТЧ = ДополнительныеПараметры.СтрокаТЧ;
	
	Если СтруктураРезультата=Неопределено Тогда
        Возврат;
    КонецЕсли;
    
    //обновим данные
    СтрокаТЧ.ТочкаАдресации=СтруктураРезультата.ТочкаАдресации;

КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	//откроем форму адресации
	Отказ=Истина;
	
	СрокПоУмолчаниюЧасов=8;
	ПараметрыФормы=Новый Структура;
	ПараметрыЗаполнения=Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ПараметрыЗаполнения);
	ПараметрыФормы.Вставить("ПоказыватьПараметрыЗадачи", Ложь);
	ПараметрыФормы.Вставить("ЗаголовокФормы", НСтр("ru='Новый элемент адресации'; en='New addressing element';"));
	
	ОткрытьФорму("ОбщаяФорма.КП_ФормаАдресации", ПараметрыФормы,,,,, Новый ОписаниеОповещения("Оповещение_СотрудникиПередНачаломДобавленияЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура Оповещение_СотрудникиПередНачаломДобавленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если Результат=Неопределено Тогда
        Возврат;
    КонецЕсли;
    
    ДобавитьНовуюСтрокуАдресации(Результат);

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНовуюСтрокуАдресации(СтруктураРезультата)
	
	НоваяСтрока=Объект.Сотрудники.Добавить();
	
	НоваяСтрока.ТочкаАдресации=СтруктураРезультата.ТочкаАдресации;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("КП_РабочаяГруппаИзменена",,Объект.Ссылка);
КонецПроцедуры

#КонецОбласти
