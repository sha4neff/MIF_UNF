
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеНебольшойФирмойСервер.НастроитьВспомогательнуюФормуМобильныйКлиент(ЭтотОбъект);
		
	ВыделенныеОбъектыМассив = Новый Массив;
	Параметры.Свойство("ВыделенныеОбъекты",ВыделенныеОбъектыМассив);
	Если ВыделенныеОбъектыМассив.Количество() > 1 Тогда
		Заголовок = НСтр("ru = 'Общие теги'");
	Иначе
		Заголовок = НСтр("ru = 'Теги'");
	КонецЕсли;
	
	Если ВыделенныеОбъектыМассив <> Неопределено И ВыделенныеОбъектыМассив.Количество()> 0 Тогда
		Для Каждого ВыделенныйОбъект Из ВыделенныеОбъектыМассив Цикл
			НовыйОбъект = ВыделенныеОбъекты.Добавить();
			НовыйОбъект.Объект = ВыделенныйОбъект.Ссылка;
		КонецЦикла;
	КонецЕсли;
	
	ЭтоФормаРедактированияТегов = Истина;
	ТегированиеОбъектов.ПриСозданииПриЧтенииНаСервере(ЭтотОбъект, ВыделенныеОбъектыМассив, ЭтоФормаРедактированияТегов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	КомандаОКСервер();
	Если ТегиИзменены Тогда
		Если ТипЗнч(ВыделенныеОбъекты[0].Объект) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
			ПараметрыПредмета = "контакта, контактов, контактов";
		ИначеЕсли ТипЗнч(ВыделенныеОбъекты[0].Объект) = Тип("СправочникСсылка.Контрагенты") Тогда
			ПараметрыПредмета = "контрагента, контрагентов, контрагентов";
		ИначеЕсли ТипЗнч(ВыделенныеОбъекты[0].Объект) = Тип("СправочникСсылка.Лиды") Тогда
			ПараметрыПредмета = "лида, лидов, лидов";
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(
		НСтр("ru='Изменение тегов'"),
		,
		СтрШаблон(НСтр("ru='Теги изменены для 
			|%1 '"), СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ВыделенныеОбъекты.Количество(), ПараметрыПредмета)),
		БиблиотекаКартинок.Информация32);
	КонецЕсли;
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура КомандаОКСервер()
	
	ЗаписатьДанныеОбщихТегов();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОблакоТеговОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОблакоТеговОбработкаНавигационнойСсылкиСервер(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ОблакоТеговОбработкаНавигационнойСсылкиСервер(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ЭтоФормаРедактированияТегов = Истина;
	ТегированиеОбъектов.ОблакоТеговОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, ЭтоФормаРедактированияТегов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВводаТегаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПолеВводаТегаОбработкаВыбораСервер(Элемент.Имя, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВводаТегаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПолеВводаТегаОкончаниеВводаТекстаСервер(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПолеВводаТегаОбработкаВыбораСервер(ИмяЭлемента, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТегированиеОбъектов.ПолеВводаТегаОбработкаВыбора(ЭтотОбъект, ИмяЭлемента, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПолеВводаТегаОкончаниеВводаТекстаСервер(Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ТегированиеОбъектов.ПолеВводаТегаОкончаниеВводаТекста(ЭтотОбъект, Текст, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеОбщихТегов()
	
	НачатьТранзакцию();
	
	Попытка
		
		Для Каждого ВыделенныйОбъект Из ВыделенныеОбъекты Цикл
			
			ВладелецОбъект = ВыделенныйОбъект.Объект.ПолучитьОбъект();
			
			Для Каждого Тег Из УдаляемыеТеги Цикл
				Если ВладелецОбъект.Теги.Найти(Тег.Тег) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				ВладелецОбъект.Теги.Удалить(ВладелецОбъект.Теги.Найти(Тег.Тег));
			КонецЦикла;
			
			Для Каждого Тег Из ДанныеТегов Цикл
				
				Если ВладелецОбъект.Теги.Найти(Тег.Тег) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ВладелецОбъект.Теги.Добавить();
				НоваяСтрока.Тег = Тег.Тег;
			КонецЦикла;
			
			ВладелецОбъект.Записать();
			
		КонецЦикла;
		
		ТегиИзменены = Истина;
		Модифицированность = Ложь;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТегиИзменены = Ложь;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
