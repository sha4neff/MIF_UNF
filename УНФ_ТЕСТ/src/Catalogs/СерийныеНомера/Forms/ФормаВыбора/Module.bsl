
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		Если Не Параметры.Отбор.Владелец.ИспользоватьСерийныеНомера Тогда
			СообщениеПользователю = НСтр("ru = 'Учет серийных номеров по данной номенклатуре не ведется'");
			ОбщегоНазначения.СообщитьПользователю(СообщениеПользователю, , , , Отказ);
			Возврат;
		КонецЕсли;
		ЭтотОбъект.Заголовок = СтрШаблон(НСтр("ru = 'Серийные номера %1'"), Параметры.Отбор.Владелец);
	ИначеЕсли Параметры.Свойство("ТекущаяСтрока") И ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда
		ЭтотОбъект.Заголовок = СтрШаблон(НСтр("ru = 'Серийные номера %1'"), Параметры.ТекущаяСтрока.Владелец);
	КонецЕсли;
	
	Если Параметры.Свойство("ПоказатьПроданные") Тогда
		ПоказатьПроданные = Параметры.ПоказатьПроданные;
	Иначе
		ПоказатьПроданные = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("ПодборДляОплаты") Тогда
		ПодборДляОплаты = Параметры.ПодборДляОплаты;
		Элементы.ПоказатьПроданные.Видимость = Не ПодборДляОплаты;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ПоказатьПроданные", ПоказатьПроданные);
	Список.Параметры.УстановитьЗначениеПараметра("ПодборДляОплаты", ПодборДляОплаты);
	Элементы.Продан.Видимость = ПоказатьПроданные;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПроданныеПриИзменении(Элемент)
	
	Элементы.Продан.Видимость = ПоказатьПроданные;
	Список.Параметры.УстановитьЗначениеПараметра("ПоказатьПроданные", ПоказатьПроданные);
	
КонецПроцедуры

#КонецОбласти
