#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		Объект.АвторШаблона=Пользователи.ТекущийПользователь();
		Объект.ВыполнениеПроцент=100;
		
	Иначе
		РезультатДаНет=?(Объект.РезультатБулево, 1, 0);
		
	КонецЕсли;
	
	ОформитьЭлементыРезультата();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.РезультатБулево=(РезультатДаНет=1);
	ТекущийОбъект.РезультатТекстХранилище=Новый ХранилищеЗначения(РезультатТекстФорматированный);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеПроцентПриИзменении(Элемент)
	Если Объект.ВыполнениеПроцент>=100 Тогда
		Объект.ВыполнениеПроцент=100;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТочкаКБППриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(Объект.ОтчетИсполнителя) Тогда
		Объект.ОтчетИсполнителя=КП_ЗадачиПроцессов.ПолучитьОсновнойОтчетТочки(Объект.ТочкаКБП);
		ОформитьЭлементыРезультата();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	РезультатТекстФорматированный=ТекущийОбъект.РезультатТекстХранилище.Получить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетИсполнителяПриИзменении(Элемент)
	
	ОформитьЭлементыРезультата();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОформитьЭлементыРезультата()
	
	УстановитьВидимостьЭлементовРезультата(Ложь);
	
	СписокРезультатовПоОтчету=КП_ЗадачиПроцессов.ПолучитьСписокРезультатовПоОтчету(Объект.ОтчетИсполнителя);
	
	Для Каждого ЭлементСписка Из СписокРезультатовПоОтчету Цикл
		ВидОтчета=ЭлементСписка.Значение;
		НаименованиеРезультата=ЭлементСписка.Представление;
		
		ИмяРеквизита=КП_ЗадачиПроцессов.ПолучитьИмяРеквизитаОтчетаИзВидаОтчета(ВидОтчета);
		
		Попытка
			Если ИмяРеквизита="РезультатТекстФорматированный" Тогда
				ЭлементРезультата=Элементы.ГруппаРезультатТекстФорматированный;
			Иначе
				ЭлементРезультата=Элементы[ИмяРеквизита];
			КонецЕсли;
			
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
			Продолжить;
			
		КонецПопытки;
		
		ЭлементРезультата.Видимость=Истина;
		
		Если НЕ ПустаяСтрока(НаименованиеРезультата) Тогда
			ЭлементРезультата.Заголовок=НаименованиеРезультата;
			
		КонецЕсли;
		
	КонецЦикла; //по списку результатов
	
	Если Элементы.РезультатЛичнаяВстреча.Видимость И Элементы.РезультатТелефонныйЗвонок.Видимость Тогда
		Элементы.ДекорацияОтметкиВыполнения.Видимость=Истина;	
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьВидимостьЭлементовРезультата(ВидимостьЭлемента)
	
	Элементы.РезультатДаНет.Видимость=ВидимостьЭлемента;
	Элементы.РезультатДата.Видимость=ВидимостьЭлемента;
	Элементы.РезультатЧисло.Видимость=ВидимостьЭлемента;
	Элементы.ГруппаРезультатТекстФорматированный.Видимость=ВидимостьЭлемента;
	Элементы.РезультатФайл.Видимость=ВидимостьЭлемента;
	Элементы.РезультатЛичнаяВстреча.Видимость=ВидимостьЭлемента;
	Элементы.РезультатТелефонныйЗвонок.Видимость=ВидимостьЭлемента;
	Элементы.ДекорацияОтметкиВыполнения.Видимость=ВидимостьЭлемента;
	
КонецПроцедуры

#КонецОбласти
