#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьСписокСервером();
		Объект.НаименованиеГруппыРезультат="Результат выполнения";
		Объект.ТипКонтроляРезультата=0;
		
	КонецЕсли;
	
	УстановитьВидимостьФлажкаОтключитьКонтроль();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьНаименованиеНажатие(Элемент)
	Если ЗначениеЗаполнено(Объект.Наименование) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("Оповещение_НадписьНаименованиеНажатиеЗавершение", ЭтотОбъект), НСтр("en='Do you want to form the name by the selected types of reports?';ru='Сформировать наименование по выбранным видам отчетов?'"), РежимДиалогаВопрос.ДаНет,,,КП_ОбщееКлиент.ЗаголовокДиалога());
        Возврат;
	КонецЕсли;
	
	//сформируем наименование
	НадписьНаименованиеНажатиеФрагмент();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ТребуемыйРезультат", "Записан", Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ПустаяСтрока(Объект.ОписаниеТребуемогоРезультата) Тогда
		СформироватьОписаниеРезультата();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Оповещение_НадписьНаименованиеНажатиеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса=КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    НадписьНаименованиеНажатиеФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура НадписьНаименованиеНажатиеФрагмент()
    
    Перем СтрНаименование, ЭлементСтроки;
    
    СтрНаименование="";
    Для Каждого ЭлементСтроки Из Объект.ВидыОтчетов Цикл
        Если НЕ ЭлементСтроки.Использование Тогда
            Продолжить;
        КонецЕсли;
        
        СтрНаименование=СтрНаименование+?(СтрНаименование="", "", ", ")+СокрЛП(ЭлементСтроки.ВидОтчета);
        
    КонецЦикла;
    
    Объект.Наименование=СтрНаименование;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписок(Команда)
	
	Если Объект.ВидыОтчетов.Количество()=0 Тогда
		ЗаполнитьСписокСервером();
		
	Иначе	
		ПоказатьВопрос(Новый ОписаниеОповещения("Оповещение_ЗаполнитьСписокЗавершение", ЭтотОбъект), НСтр("en='The list will be cleared. Do you want to continue?';ru='Список будет очищен! Продолжить?'"), РежимДиалогаВопрос.ДаНет);	

	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура Оповещение_ЗаполнитьСписокЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса=КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
	
	ЗаполнитьСписокСервером();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСервером()
	Справочники.КП_ТребуемыеРезультатыИсполнителей.ЗаполнитьСписокВидов(Объект);

КонецПроцедуры

&НаКлиенте
Процедура НадписьОписаниеРезультатаНажатие(Элемент)
	ПоказатьВопрос(Новый ОписаниеОповещения("Оповещение_НадписьОписаниеРезультатаНажатиеЗавершение", ЭтотОбъект), НСтр("en='Do you want to generate a string describing the desired result?';ru='Сформировать строку описания требуемого результата?'"), РежимДиалогаВопрос.ДаНет,,,КП_ОбщееКлиент.ЗаголовокДиалога());
		
КонецПроцедуры

&НаКлиенте
Процедура Оповещение_НадписьОписаниеРезультатаНажатиеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если НЕ ПустаяСтрока(Объект.ОписаниеТребуемогоРезультата) И РезультатВопроса=КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    СформироватьОписаниеРезультата();

КонецПроцедуры

&НаСервере
Процедура СформироватьОписаниеРезультата()
	Объект.ОписаниеТребуемогоРезультата=Справочники.КП_ТребуемыеРезультатыИсполнителей.СформироватьОписаниеРезультата(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыОтчетовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ=Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыОтчетовПриАктивизацииСтроки(Элемент)
	СтрокаТЧ=Элементы.ВидыОтчетов.ТекущиеДанные;
	
	Если СтрокаТЧ=Неопределено Тогда
		Возврат;
		
	КонецЕсли;
	
	Элементы.ДекорацияКомментарийРезультата.Заголовок=СокрЛП(КП_ОбщееСервер.ПолучитьЗначениеРеквизитаОбъекта(СтрокаТЧ.ВидОтчета, "Комментарий"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыОтчетовИспользуетсяПриИзменении(Элемент)
	УстановитьВидимостьФлажкаОтключитьКонтроль();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьФлажкаОтключитьКонтроль()
	
	ВозможностьОтключенияЕсть=Ложь; //далее переменная может быть переопределена
	
	Если Объект.ТипКонтроляРезультата=2 Тогда
		//смысла в контроле нет
		Элементы.ОтключитьКонтрольРезультатаЕслиВыбранБулевыйРезультатЛожь.Видимость=Ложь;
		Объект.ОтключитьКонтрольРезультатаЕслиВыбранБулевыйРезультатЛожь=Ложь;
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ВозможностьОтключенияЕсть Тогда	
		СтруктураОтбора=Новый Структура("Использование, ВидОтчета", Истина, ПланыВидовХарактеристик.КП_РезультатыИсполнителейЗадач.БулевыйРезультат);
		Если Объект.ВидыОтчетов.НайтиСтроки(СтруктураОтбора).Количество()>0 Тогда
			//строка есть
			ВозможностьОтключенияЕсть=Истина;
		КонецЕсли;
	КонецЕсли;	
	
	Если НЕ ВозможностьОтключенияЕсть Тогда
		СтруктураОтбора=Новый Структура("Использование, ВидОтчета", Истина, ПланыВидовХарактеристик.КП_РезультатыИсполнителейЗадач.ВыборКнопками);
		Если Объект.ВидыОтчетов.НайтиСтроки(СтруктураОтбора).Количество()>0 Тогда
			//строка есть
			ВозможностьОтключенияЕсть=Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ВозможностьОтключенияЕсть Тогда
		Элементы.ОтключитьКонтрольРезультатаЕслиВыбранБулевыйРезультатЛожь.Видимость=Истина;
		
	Иначе 
		Элементы.ОтключитьКонтрольРезультатаЕслиВыбранБулевыйРезультатЛожь.Видимость=Ложь;
		Объект.ОтключитьКонтрольРезультатаЕслиВыбранБулевыйРезультатЛожь=Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипКонтроляРезультатаПриИзменении(Элемент)
	УстановитьВидимостьФлажкаОтключитьКонтроль();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыОтчетовОбязательныйПриИзменении(Элемент)
	СтрокаТЧ=Элементы.ВидыОтчетов.ТекущиеДанные;
	Если СтрокаТЧ.Обязательный Тогда
		СтрокаТЧ.Использование=Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

