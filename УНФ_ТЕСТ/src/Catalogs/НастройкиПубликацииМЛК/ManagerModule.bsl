#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ОнлайнЗаписьВключена() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиПубликацииМЛК.ИспользоватьОнлайнЗапись КАК ИспользоватьОнлайнЗапись
		|ИЗ
		|	Справочник.НастройкиПубликацииМЛК КАК НастройкиПубликацииМЛК";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	Возврат ВыборкаДетальныеЗаписи.ИспользоватьОнлайнЗапись;
	
КонецФункции

// См. ОбменДаннымиПереопределяемый.РегистрацияИзмененийНачальнойВыгрузкиДанных()
//
Процедура РегистрацияИзмененийНачальнойВыгрузкиДанных(Получатель, СтандартнаяОбработка, Отбор) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Массив;
	Отбор.Добавить(Метаданные.Справочники.Организации);
	Отбор.Добавить(Метаданные.Справочники.ВидыЦен);
	Отбор.Добавить(Метаданные.Справочники.Валюты);
	Отбор.Добавить(Метаданные.Справочники.СтруктурныеЕдиницы);
	
	УзлыОбмена = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Получатель);
	Для каждого ОбъектМетаданных Из Отбор Цикл
		ПланыОбмена.ЗарегистрироватьИзменения(УзлыОбмена, ОбъектМетаданных);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НастройкиИнтеграцииКабинетКлиента(НастройкаПубликацииМЛК) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииКабинетКлиента.НастройкаПубликации КАК НастройкаПубликации,
	|	НастройкиИнтеграцииКабинетКлиента.КодПриложения КАК КодПриложения,
	|	НастройкиИнтеграцииКабинетКлиента.АдресПриложения КАК АдресПриложения,
	|	НастройкиИнтеграцииКабинетКлиента.НомерОбласти КАК НомерОбласти,
	|	НастройкиИнтеграцииКабинетКлиента.КодУзлаОбмена КАК КодУзлаОбмена,
	|	НастройкиИнтеграцииКабинетКлиента.Пользователь КАК Пользователь,
	|	НастройкиИнтеграцииКабинетКлиента.EmailПриРегистрации КАК EmailПриРегистрации
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииКабинетКлиента КАК НастройкиИнтеграцииКабинетКлиента
	|ГДЕ
	|	НастройкиИнтеграцииКабинетКлиента.НастройкаПубликации = &НастройкаПубликации";
	Запрос.УстановитьПараметр("НастройкаПубликации", НастройкаПубликацииМЛК);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если НЕ Результат.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НастройкиПриложения = Новый Структура();
	НастройкиПриложения.Вставить("КодПриложения");
	НастройкиПриложения.Вставить("АдресПриложения");
	НастройкиПриложения.Вставить("НомерОбласти");
	НастройкиПриложения.Вставить("КодУзлаОбмена");
	НастройкиПриложения.Вставить("Пользователь");
	НастройкиПриложения.Вставить("EmailПриРегистрации");
	
	ЗаполнитьЗначенияСвойств(НастройкиПриложения, Результат);
	
	Если ПривилегированныйРежим() Тогда
		Пароль = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(НастройкаПубликацииМЛК, "ПарольПользователяИнтеграции");
		Если Пароль = Неопределено Тогда
			Пароль = "";
		КонецЕсли;
		НастройкиПриложения.Вставить("Пароль", Пароль);
	КонецЕсли;
	
	Возврат НастройкиПриложения;
	
КонецФункции

Процедура СохранитьНастройкиИнтеграцииКабинетКлиента(
		НастройкаПубликацииМЛК,
		ОписаниеПриложения = Неопределено,
		ДанныеАвторизации = Неопределено,
		УзелОбмена = Неопределено) Экспорт
	
	Если ОписаниеПриложения = Неопределено
		И ДанныеАвторизации = Неопределено
		И УзелОбмена = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НастройкиИнтеграцииКабинетКлиента.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НастройкаПубликации.Установить(НастройкаПубликацииМЛК);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() <> 0 Тогда
		Запись = НаборЗаписей[0];
	Иначе
		Запись = НаборЗаписей.Добавить();
	КонецЕсли;
	
	Запись.НастройкаПубликации = НастройкаПубликацииМЛК;
	Если ОписаниеПриложения <> Неопределено Тогда
		Запись.КодПриложения = ОписаниеПриложения.Код;
		Запись.АдресПриложения = ОписаниеПриложения.АдресПриложения;
		Запись.НомерОбласти = ОписаниеПриложения.НомерОбласти;
	КонецЕсли;
	Если ДанныеАвторизации <> Неопределено Тогда
		Запись.Пользователь = ДанныеАвторизации.Логин;
		//Запись.Пароль = ДанныеАвторизации.Пароль;
		Запись.EmailПриРегистрации = ДанныеАвторизации.EmailПользователя;
		
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(НастройкаПубликацииМЛК, ДанныеАвторизации.Пароль, "ПарольПользователяИнтеграции");
	КонецЕсли;
	Если УзелОбмена <> Неопределено Тогда
		Запись.КодУзлаОбмена = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УзелОбмена, "Код");
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура УдалитьНастройкиИнтеграцииКабинетКлиента(НастройкаПубликацииМЛК) Экспорт
	
	НастройкиИнтеграции = НастройкиИнтеграцииКабинетКлиента(НастройкаПубликацииМЛК);
	
	Если НастройкиИнтеграции = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УзелОбмена = УзелПланаОбменаПоКоду(НастройкиИнтеграции.КодУзлаОбмена);
	
	НачатьТранзакцию();
	Попытка
		НаборЗаписей = РегистрыСведений.НастройкиИнтеграцииКабинетКлиента.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.НастройкаПубликации.Установить(НастройкаПубликацииМЛК);
		НаборЗаписей.Записать(Истина);
		
		ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(НастройкаПубликацииМЛК, "ПарольПользователяИнтеграции");
		
		Если ЗначениеЗаполнено(УзелОбмена) Тогда
			ОбменДаннымиСервер.УдалитьНастройкуСинхронизации(УзелОбмена);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#Область ПроцессПубликацииНастроек

Процедура ПодготовитьПриложениеКабинетКлиентаСуществующийАбонентРежимСервиса(НастройкаПубликацииМЛК) Экспорт
	
	АктивироватьПромокодЕслиНеобходимоРежимСервиса();
	
	ПараметрыСоздания = ПрограммныйИнтерфейсСервиса.НовыйПараметрыСозданияПриложения();
	ПараметрыСоздания.КодКонфигурации = КодПриложенияКабинетКлиента();
	ОписаниеНовогоПриложения = ПрограммныйИнтерфейсСервиса.СоздатьПриложение(ПараметрыСоздания);
	
	НомерОбласти = ОписаниеНовогоПриложения.Код;
	ОписаниеНовогоПриложения = ОжидатьГотовностьСозданногоПриложенияСуществующегоАбонентаРежимСервиса(НомерОбласти);
	
	ОписаниеПользователяИнтеграции = СоздатьСлужебногоПользователяИнтеграцииРежимСервиса(НомерОбласти);
	
	ДанныеАвторизации = Новый Структура;
	ДанныеАвторизации.Вставить("EmailПользователя", "");
	ДанныеАвторизации.Вставить("Логин",  ОписаниеПользователяИнтеграции.Логин);
	ДанныеАвторизации.Вставить("Пароль", ОписаниеПользователяИнтеграции.Пароль);
	
	ОписаниеПриложенияДляНастроек = Новый Структура;
	ОписаниеПриложенияДляНастроек.Вставить("Код",             ОписаниеНовогоПриложения.КодКонфигурации);
	ОписаниеПриложенияДляНастроек.Вставить("АдресПриложения", ОписаниеНовогоПриложения.АдресПриложения);
	ОписаниеПриложенияДляНастроек.Вставить("НомерОбласти",    ОписаниеНовогоПриложения.Код);
	
	СохранитьНастройкиИнтеграцииКабинетКлиента(НастройкаПубликацииМЛК, ОписаниеПриложенияДляНастроек, ДанныеАвторизации);
	
	ОтправкаЗапросов.Подождать(4);
	
КонецПроцедуры

Процедура ПодготовитьПриложениеКабинетКлиентаСуществующийАбонент(НастройкаПубликацииМЛК, ЛогинФреш, ПарольФреш, КодАбонентаВладельца) Экспорт
	
	АктивироватьПромокодЕслиНеобходимо(ЛогинФреш, ПарольФреш, КодАбонентаВладельца);
	
	ДанныеАвторизации = ИнтеграцияСервисФреш.НовыйДанныеАвторизации();
	ДанныеАвторизации.Логин = ЛогинФреш;
	ДанныеАвторизации.Пароль = ПарольФреш;
	ДанныеАвторизации.КодАбонента = КодАбонентаВладельца;
	
	ОписаниеНовогоПриложения = ИнтеграцияСервисФреш.НовыйОписаниеСозданияПриложения();
	ОписаниеНовогоПриложения.Код = КодПриложенияКабинетКлиента();
	
	ОписаниеСозданогоПриложения = ИнтеграцияСервисФреш.СоздатьПриложение(ДанныеАвторизации, ОписаниеНовогоПриложения);
	НомерОбласти = ОписаниеСозданогоПриложения.НомерОбласти;
	ОписаниеСозданогоПриложения = ОжидатьГотовностьСозданногоПриложенияСуществующегоАбонента(ДанныеАвторизации, НомерОбласти);
	ОписаниеПользователяИнтеграции = СоздатьСлужебногоПользователяИнтеграции(ДанныеАвторизации, НомерОбласти);
	
	ДанныеАвторизации = Новый Структура;
	ДанныеАвторизации.Вставить("EmailПользователя", ЛогинФреш);
	ДанныеАвторизации.Вставить("Логин",  ОписаниеПользователяИнтеграции.Логин);
	ДанныеАвторизации.Вставить("Пароль", ОписаниеПользователяИнтеграции.Пароль);
	СохранитьНастройкиИнтеграцииКабинетКлиента(НастройкаПубликацииМЛК, ОписаниеСозданогоПриложения, ДанныеАвторизации);
	
	ОтправкаЗапросов.Подождать(4);
	
КонецПроцедуры

Функция ПодготовитьПриложениеКабинетКлиентаНовыйАбонент(НастройкаПубликацииМЛК, ЛогинФреш) Экспорт
	
	ИнтеграцияСервисФреш.СоздатьАбонента(ЛогинФреш);
	ОписаниеПриложения = ОжидатьГотовностьСозданногоПриложенияНовогоАбонента(ЛогинФреш);
	
	ОписаниеПользователяИнтеграции = ОписаниеПользователяИнтеграцииПоУмолчанию();
	
	ДанныеАвторизации = Новый Структура;
	ДанныеАвторизации.Вставить("EmailПользователя", ЛогинФреш);
	ДанныеАвторизации.Вставить("Логин",  ОписаниеПользователяИнтеграции.Логин);
	ДанныеАвторизации.Вставить("Пароль", ОписаниеПользователяИнтеграции.Пароль);
	СохранитьНастройкиИнтеграцииКабинетКлиента(НастройкаПубликацииМЛК, ОписаниеПриложения, ДанныеАвторизации);
	
	ОтправкаЗапросов.Подождать(4);
	
КонецФункции

Процедура ЗарегистрироватьДанныеДляНачальнойВыгрузки(НастройкаПубликацииМЛК) Экспорт
	
	НастройкиИнтеграции = НастройкиИнтеграцииКабинетКлиента(НастройкаПубликацииМЛК);
	УзелОбмена = УзелПланаОбменаПоКоду(НастройкиИнтеграции.КодУзлаОбмена);
	Если НЕ ЗначениеЗаполнено(УзелОбмена) Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru='Не удалось найти узел обмена по коду ""%1""'"),
			НастройкиИнтеграции.КодУзлаОбмена);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	УзелОбменаОбъект = УзелОбмена.ПолучитьОбъект();
	УзелОбменаОбъект.ДатаНачалаВыгрузкиДокументов = НачалоДня(ТекущаяДатаСеанса());
	УзелОбменаОбъект.Записать();
	
	ОбменДаннымиСервер.ЗарегистрироватьДанныеДляНачальнойВыгрузки(УзелОбмена);
	
КонецПроцедуры

Процедура АктуализироватьИзмененияПланаОбмена(НастройкаПубликацииМЛК, УзелОбмена) Экспорт
	
	ДанныеНастройки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаПубликацииМЛК, "НужноПереопубликовать,НужноПереопубликоватьКаталогТоваров,НужноПереопубликоватьХарактеристики,ВидЦенТоваров,ОрганизацияДляОстатков,Склады,ИспользоватьОстатки");
	НеобходимоЗарегистрироватьЦены = Ложь;
	НеобходимоЗарегистрироватьОстатки = Ложь;
	
	УзелОбменаОбъект = УзелОбмена.ПолучитьОбъект();
	УзелОбменаОбъект.Заблокировать();
	УзелОбменаИзменен = Ложь;
	
	Если УзелОбменаОбъект.ИспользоватьОтборПоНоменклатуре <> Истина Тогда
		УзелОбменаОбъект.ИспользоватьОтборПоНоменклатуре = Истина;
		УзелОбменаИзменен = Истина;
	КонецЕсли;
	
	НоменклатураВыгружаемаяБыло = ОтборНоменклатарыУзлаОбмена(УзелОбменаОбъект);
	ДобавитьОтборНоменклатурыВУзелОбмена(УзелОбменаОбъект, НастройкаПубликацииМЛК);
	НоменклатураВыгружаемаяСтало = ОтборНоменклатарыУзлаОбмена(УзелОбменаОбъект);
	
	НоменклатураИзменилась = Не ОбщегоНазначенияКлиентСервер.СпискиЗначенийИдентичны(
		НоменклатураВыгружаемаяБыло, НоменклатураВыгружаемаяСтало);
	Если НоменклатураИзменилась Тогда
		УзелОбменаИзменен = Истина;
	КонецЕсли;
	
	Если НоменклатураИзменилась Тогда
		НоваяНоменклатураДляВыгрузки = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			НоменклатураВыгружаемаяСтало, НоменклатураВыгружаемаяБыло);
	Иначе
		НоваяНоменклатураДляВыгрузки = Новый Массив;
	КонецЕсли;
	
	Если УзелОбменаОбъект.ВидыЦен.Найти(ДанныеНастройки.ВидЦенТоваров, "ВидЦен") = Неопределено Тогда
		УзелОбменаОбъект.ИспользоватьОтборПоВидамЦен = Истина;
		УзелОбменаОбъект.ВидыЦен.Очистить();
		Строка = УзелОбменаОбъект.ВидыЦен.Добавить();
		Строка.ВидЦен = ДанныеНастройки.ВидЦенТоваров;
		УзелОбменаИзменен = Истина;
		НеобходимоЗарегистрироватьЦены = Истина;
	КонецЕсли;
	
	СкладыНастройки = ДанныеНастройки.Склады.Выгрузить().ВыгрузитьКолонку("Склад");
	СкладыВНастройкеИУзлеСовпадают = ОбщегоНазначения.КоллекцииИдентичны(
		СкладыНастройки,
		УзелОбменаОбъект.Склады.Выгрузить().ВыгрузитьКолонку("Склад"));
	Если ДанныеНастройки.ИспользоватьОстатки
		И НЕ СкладыВНастройкеИУзлеСовпадают Тогда
		УзелОбменаОбъект.ВыгружатьОстаткиНоменклатуры = Истина;
		УзелОбменаОбъект.Склады.Очистить();
		Для каждого Склад Из СкладыНастройки Цикл
			Строка = УзелОбменаОбъект.Склады.Добавить();
			Строка.Склад = Склад;
		КонецЦикла;
		УзелОбменаИзменен = Истина;
		НеобходимоЗарегистрироватьОстатки = Истина;
	КонецЕсли;
	
	ОрганизацияОстатковВНастройкеИУзлеСовпадают = УзелОбменаОбъект.ОрганизацииСкладскихОстатков.Найти(
		ДанныеНастройки.ОрганизацияДляОстатков, "Организация") <> Неопределено;
	Если УзелОбменаОбъект.Склады.Количество() <> 0
		И ЗначениеЗаполнено(ДанныеНастройки.ОрганизацияДляОстатков)
		И Не ОрганизацияОстатковВНастройкеИУзлеСовпадают Тогда
		УзелОбменаОбъект.ОрганизацииСкладскихОстатков.Очистить();
		Строка = УзелОбменаОбъект.ОрганизацииСкладскихОстатков.Добавить();
		Строка.Организация = ДанныеНастройки.ОрганизацияДляОстатков;
		УзелОбменаИзменен = Истина;
	КонецЕсли;
	
	Если НоменклатураИзменилась Тогда
		ЗарегистрироватьКартинкиНоменклатуры(УзелОбмена, НастройкаПубликацииМЛК, НоваяНоменклатураДляВыгрузки);
	КонецЕсли;
	
	Если ДанныеНастройки.НужноПереопубликоватьКаталогТоваров
		ИЛИ НеобходимоЗарегистрироватьЦены Тогда
		ЗарегистрироватьЦеныНоменклатуры(УзелОбмена, НастройкаПубликацииМЛК, НоваяНоменклатураДляВыгрузки);
	КонецЕсли;
	
	Если ДанныеНастройки.ИспользоватьОстатки
		И (ДанныеНастройки.НужноПереопубликоватьКаталогТоваров
		ИЛИ НеобходимоЗарегистрироватьОстатки) Тогда
		ЗарегистрироватьОстаткиНоменклатуры(УзелОбмена, НастройкаПубликацииМЛК, НоваяНоменклатураДляВыгрузки);
	КонецЕсли;
	
	Если ДанныеНастройки.НужноПереопубликоватьХарактеристики Тогда
		ЗарегистрироватьХарактеристикиНоменклатуры(УзелОбмена, НастройкаПубликацииМЛК, НоваяНоменклатураДляВыгрузки);
	КонецЕсли;
	
	Если УзелОбменаИзменен Тогда
		УзелОбменаОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоСервис1СФреш() Экспорт
	
	ПодключениеВебСервер = "e1c://server/";
	
	СтрокаURI = ПолучитьНавигационнуюСсылкуИнформационнойБазы();
	Если СтрНачинаетсяС(СтрокаURI, ПодключениеВебСервер) Тогда
		СтрокаURI = Прав(СтрокаURI, СтрДлина(СтрокаURI) - СтрДлина(ПодключениеВебСервер));
	КонецЕсли;
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(СтрокаURI);
	Возврат СтрНайти(СтруктураURI.Хост, ИнтеграцияСервисФреш.НастройкиПодключения().Хост) <> 0;
	
КонецФункции

Функция ПолучитьНастройкиПубликацииМЛК() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиПубликацииМЛК.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НастройкиПубликацииМЛК КАК НастройкиПубликацииМЛК";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	Иначе
		Возврат Неопределено
	КонецЕсли;
	
КонецФункции

Функция СинхронизироватьУзелОбмена(УзелОбмена, РазрешитьДлительнуюОперацию) Экспорт
	
	ПараметрыОбмена = ОбменДаннымиСервер.ПараметрыОбмена();
	ПараметрыОбмена.ВидТранспортаСообщенийОбмена = Перечисления.ВидыТранспортаСообщенийОбмена.WS;
	ПараметрыОбмена.ВыполнятьЗагрузку = Истина;
	ПараметрыОбмена.ВыполнятьВыгрузку = Истина;
	
	Если РазрешитьДлительнуюОперацию Тогда
		ПараметрыОбмена.ДлительнаяОперация = Истина;
		ПараметрыОбмена.ДлительнаяОперацияРазрешена = Истина;
	КонецЕсли;
	
	Отказ = Ложь;
	ОбменДаннымиСервер.ВыполнитьОбменДаннымиДляУзлаИнформационнойБазы(УзелОбмена, ПараметрыОбмена, Отказ);
	
	Возврат Не Отказ;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КодПриложенияКабинетКлиента()
	
	Возврат "clients";
	
КонецФункции

Функция ПромокодАктивацииПриложенияКабинетКлиента()
	
	Возврат "kk_new";
	
КонецФункции

Функция ОписаниеПользователяИнтеграцииПоУмолчанию()
	
	ОписаниеПользователя = Новый Структура;
	ОписаниеПользователя.Вставить("Логин",  "kk_api");
	ОписаниеПользователя.Вставить("Пароль", "gm3NtMPQ");
	Возврат ОписаниеПользователя;
	
КонецФункции

Функция СоздатьСлужебногоПользователяИнтеграции(ДанныеАвторизации, НомерОбласти)
	
	ОписаниеПользователя = ИнтеграцияСервисФреш.ПараметрыСоздаваемогоПользователя();
	ОписаниеПользователя.Логин = НовыйЛогинСлужебногоПользователяИнтеграции(НомерОбласти);
	ОписаниеПользователя.ИмяПользователя = ОписаниеПользователя.Логин;
	ОписаниеПользователя.Пароль = Пользователи.СоздатьПароль();
	
	ИнтеграцияСервисФреш.СоздатьПользователя(ДанныеАвторизации, ОписаниеПользователя);
	ИнтеграцияСервисФреш.ДобавитьПльзователяВПриложение(
		ДанныеАвторизации,
		НомерОбласти,
		ОписаниеПользователя.Логин,
		Перечисления.ПраваПользователяПриложения.ДоступКAPI);
		
	Возврат ОписаниеПользователя;
	
КонецФункции

Функция СоздатьСлужебногоПользователяИнтеграцииРежимСервиса(НомерОбласти)
	
	ПараметрыСоздания = ПрограммныйИнтерфейсСервиса.НовыйПараметрыСозданияПользователя();
	ПараметрыСоздания.Логин = НовыйЛогинСлужебногоПользователяИнтеграции(НомерОбласти);
	ПараметрыСоздания.ПолноеИмя = ПараметрыСоздания.Логин;
	ПараметрыСоздания.Пароль = Пользователи.СоздатьПароль();
	ПараметрыСоздания.ПочтаОбязательна = Ложь;
	ПараметрыСоздания.РольПользователя = Перечисления.РолиПользователейАбонентов.ПользовательАбонента;
	ОписаниеПользователя = ПрограммныйИнтерфейсСервиса.СоздатьПользователяАбонента(ПараметрыСоздания);
	
	ПараметрыДобавления = ПрограммныйИнтерфейсСервиса.НовыйПараметрыДобавленияПользователяВПриложение();
	ПараметрыДобавления.КодПриложения = НомерОбласти;
	ПараметрыДобавления.Логин = ПараметрыСоздания.Логин;
	ПараметрыДобавления.Право = Перечисления.ПраваПользователяПриложения.ДоступКAPI;
	ПрограммныйИнтерфейсСервиса.ДобавитьПользователяВПриложение(ПараметрыДобавления);
	
	Возврат ПараметрыСоздания;
	
КонецФункции

Функция НовыйЛогинСлужебногоПользователяИнтеграции(НомерОбласти)
	
	НомерОбластиСтрокой = Формат(НомерОбласти, "ЧГ=0");
	Возврат СтрШаблон("kk_api_%1", НомерОбластиСтрокой);
	
КонецФункции

Процедура АктивироватьПромокодЕслиНеобходимо(ЛогинФреш, ПарольФреш, КодАбонентаВладельца)
	
	НастройкиПодключенияСервиса = ИнтеграцияСервисФреш.НастройкиПодключения();
	Если НЕ СтрНачинаетсяС(НастройкиПодключенияСервиса.Сервер, "https://1cfresh.com") Тогда
		Возврат;
	КонецЕсли;
	
	ПромокодКабинетКлиента = ПромокодАктивацииПриложенияКабинетКлиента();
	
	ДанныеАвторизации = ИнтеграцияСервисФреш.НовыйДанныеАвторизации();
	ДанныеАвторизации.Логин = ЛогинФреш;
	ДанныеАвторизации.Пароль = ПарольФреш;
	ДанныеАвторизации.КодАбонента = КодАбонентаВладельца;
	//АктивиронныеПромокодыАбонента = ИнтеграцияСервисФреш.АктивированныеПромокоды(ДанныеАвторизации);
	//
	//ОтборПромокоды = Новый Структура("Промокод,АктивированУспешно", ПромокодКабинетКлиента, Истина);
	//ПромокодУжеАктивирован = Ложь;
	//Если АктивиронныеПромокодыАбонента.Количество() <> 0 Тогда
	//	ПромокодУжеАктивирован = АктивиронныеПромокодыАбонента.НайтиСтроки(ОтборПромокоды).Количество() <> 0;
	//КонецЕсли;
	//
	//Если ПромокодУжеАктивирован Тогда
	//	Возврат;
	//КонецЕсли;
	
	ПриложениеКабинетКлиентаДоступно = Ложь;
	
	ДоступныеКонфигурации = ИнтеграцияСервисФреш.ДоступныеКонфигурации(ДанныеАвторизации);
	Для каждого ОписаниеКонфигурации Из ДоступныеКонфигурации Цикл
		Если ОписаниеКонфигурации.Код = КодПриложенияКабинетКлиента() Тогда
			ПриложениеКабинетКлиентаДоступно = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПриложениеКабинетКлиентаДоступно Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияСервисФреш.АктивироватьПромокод(ДанныеАвторизации, ПромокодКабинетКлиента);
	
КонецПроцедуры

Процедура АктивироватьПромокодЕслиНеобходимоРежимСервиса()
	
	ПриложениеКабинетКлиентаДоступно = Ложь;
	
	ДоступныеКонфигурации = ПрограммныйИнтерфейсСервиса.Конфигурации();
	Для каждого ОписаниеКонфигурации Из ДоступныеКонфигурации Цикл
		Если ОписаниеКонфигурации.Код = КодПриложенияКабинетКлиента() Тогда
			ПриложениеКабинетКлиентаДоступно = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПриложениеКабинетКлиентаДоступно Тогда
		Возврат;
	КонецЕсли;
	
	ПрограммныйИнтерфейсСервиса.ИспользоватьПромокод(ПромокодАктивацииПриложенияКабинетКлиента());
	
КонецПроцедуры

Функция ОжидатьГотовностьСозданногоПриложенияНовогоАбонента(ЛогинФреш)
	
	КоличествоПопыток = 6;
	Таймаут = 3;
	
	СозданиеНовыхПриложенийЗавершено = Ложь;
	
	НомерПопытки = 0;
	Пока НомерПопытки < КоличествоПопыток
		И НЕ СозданиеНовыхПриложенийЗавершено Цикл
		
		ОтправкаЗапросов.Подождать(Таймаут);
		Таймаут = Таймаут + НомерПопытки;
		НомерПопытки = НомерПопытки + 1;
		
		ОписаниеПриложения = ИнтеграцияСервисФреш.СозданныеПриложенияПриРегистрации(ЛогинФреш);
		Если ОписаниеПриложения <> Неопределено Тогда
			СозданиеНовыхПриложенийЗавершено = ИнтеграцияСервисФреш.ПриложениеГотовоКИспользованию(ОписаниеПриложения);
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ СозданиеНовыхПриложенийЗавершено Тогда
		ТекстОшибки = НСтр("ru='При публикации возникла непридвиденная ошибка.
		|Попробуйте еще раз.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат ОписаниеПриложения;
	
КонецФункции

Функция ОжидатьГотовностьСозданногоПриложенияСуществующегоАбонента(ДанныеАвторизации, НомерОбласти)
	
	КоличествоПопыток = 7;
	Таймаут = 3;
	
	ПриложениеПодготовлено = Ложь;
	
	НомерПопытки = 0;
	Пока НомерПопытки < КоличествоПопыток
		И НЕ ПриложениеПодготовлено Цикл
		
		ОтправкаЗапросов.Подождать(Таймаут);
		Таймаут = Таймаут + НомерПопытки;
		НомерПопытки = НомерПопытки + 1;
		
		ОписаниеПриложения = ИнтеграцияСервисФреш.ИнформацияОПриложении(ДанныеАвторизации, НомерОбласти);
		ПриложениеПодготовлено = ИнтеграцияСервисФреш.ПриложениеГотовоКИспользованию(ОписаниеПриложения);
	КонецЦикла;
	
	Если НЕ ПриложениеПодготовлено Тогда
		ТекстОшибки = НСтр("ru='При публикации возникла непридвиденная ошибка.
		|Попробуйте еще раз.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат ОписаниеПриложения;
	
КонецФункции

Функция ОжидатьГотовностьСозданногоПриложенияСуществующегоАбонентаРежимСервиса(НомерОбласти)
	
	КоличествоПопыток = 7;
	Таймаут = 3;
	
	ПриложениеПодготовлено = Ложь;
	
	НомерПопытки = 0;
	Пока НомерПопытки < КоличествоПопыток
		И НЕ ПриложениеПодготовлено Цикл
		
		ОтправкаЗапросов.Подождать(Таймаут);
		Таймаут = Таймаут + НомерПопытки;
		НомерПопытки = НомерПопытки + 1;
		
		ОписаниеПриложения = ПрограммныйИнтерфейсСервиса.СвойстваПриложения(НомерОбласти);
		ПриложениеПодготовлено = ИнтеграцияСервисФреш.ПриложениеГотовоКИспользованию(ОписаниеПриложения);
	КонецЦикла;
	
	Если НЕ ПриложениеПодготовлено Тогда
		ТекстОшибки = НСтр("ru='При публикации возникла непридвиденная ошибка.
		|Попробуйте еще раз.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат ОписаниеПриложения;
	
КонецФункции

Функция УзелПланаОбменаПоКоду(КодУзла)
	
	Возврат ОбменДаннымиСервер.УзелПланаОбменаПоКоду(
		Метаданные.ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.Имя,
		КодУзла);
	
КонецФункции

Функция ОтборНоменклатарыУзлаОбмена(УзелОбмена)
	
	Результат = Новый Массив;
	Для каждого Строка Из УзелОбмена.Номенклатура Цикл
		Результат.Добавить(Строка.Номенклатура);
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьОтборНоменклатурыВУзелОбмена(УзелОбмена, НастройкаПубликацииМЛК)
	
	УзелОбмена.Номенклатура.Очистить();
	
	ДанныеНастройки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаПубликацииМЛК, "КаталогТоваров");
	КаталогТоваров = ДанныеНастройки.КаталогТоваров.Получить();
	
	Если КаталогТоваров = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Строка Из КаталогТоваров Цикл
		НоваяСтрока = УзелОбмена.Номенклатура.Добавить();
		НоваяСтрока.Номенклатура = Строка.Номенклатура;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьКартинкиНоменклатуры(УзелОбмена, НастройкаПубликацииМЛК, Номенклатура)
	
	УзлыОбмена = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(УзелОбмена);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.ФайлКартинки КАК ФайлКартинки
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&Номенклатура)";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	ОсновныеКартинкиНоменклатуры = Запрос.Выполнить().Выбрать();
	Пока ОсновныеКартинкиНоменклатуры.Следующий() Цикл
		Если Не ЗначениеЗаполнено(ОсновныеКартинкиНоменклатуры.ФайлКартинки) Тогда
			Продолжить;
		КонецЕсли;
		ПланыОбмена.ЗарегистрироватьИзменения(УзлыОбмена, ОсновныеКартинкиНоменклатуры.ФайлКартинки);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьЦеныНоменклатуры(УзелОбмена, НастройкаПубликацииМЛК, Номенклатура)
	
	ДанныеНастройки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаПубликацииМЛК, "КаталогТоваров,ВидЦенТоваров,ИспользоватьХарактеристики");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Период КАК Период,
	|	ЦеныНоменклатурыСрезПоследних.ВидЦен КАК ВидЦен,
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатурыСрезПоследних.Характеристика КАК Характеристика,
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			&Период,
	|			ВидЦен = &ВидЦен
	|				И Номенклатура В (&МассивНоменклатуры)
	|				И (&ИспользоватьХарактеристики
	|					ИЛИ Характеристика = &ХарактеристикаПустаяСсылка)) КАК ЦеныНоменклатурыСрезПоследних";
	
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ВидЦен", ДанныеНастройки.ВидЦенТоваров);
	Запрос.УстановитьПараметр("МассивНоменклатуры", Номенклатура);
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", ДанныеНастройки.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ХарактеристикаПустаяСсылка", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	
	Результат = Запрос.Выполнить().Выбрать();
	
	УзлыОбмена = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(УзелОбмена);
	
	Пока Результат.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.ЦеныНоменклатуры.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Результат.Период);
		НаборЗаписей.Отбор.ВидЦен.Установить(Результат.ВидЦен);
		НаборЗаписей.Отбор.Номенклатура.Установить(Результат.Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(Результат.Характеристика);
		ПланыОбмена.ЗарегистрироватьИзменения(УзлыОбмена, НаборЗаписей);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьОстаткиНоменклатуры(УзелОбмена, НастройкаПубликацииМЛК, Номенклатура = Неопределено)
	
	ДанныеНастройки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаПубликацииМЛК, "КаталогТоваров,ОрганизацияДляОстатков,Склады,ИспользоватьХарактеристики");
	КаталогТоваров = ДанныеНастройки.КаталогТоваров.Получить();
	
	Если КаталогТоваров = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Номенклатура = Неопределено Тогда
		Номенклатура = Новый Массив;
		Для каждого Строка Из КаталогТоваров Цикл
			Номенклатура.Добавить(Строка.Номенклатура);
		КонецЦикла;
	КонецЕсли;
	Склады = ДанныеНастройки.Склады.Выгрузить().ВыгрузитьКолонку("Склад");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОстаткиТоваров.Организация КАК Организация,
	|	ОстаткиТоваров.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ОстаткиТоваров.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваров.Характеристика КАК Характеристика
	|ИЗ
	|	РегистрСведений.ОстаткиТоваров КАК ОстаткиТоваров
	|ГДЕ
	|	ОстаткиТоваров.Номенклатура В(&Номенклатура)
	|	И ОстаткиТоваров.СтруктурнаяЕдиница В(&Склады)
	|	И ОстаткиТоваров.Организация = &Организация
	|	И (&ИспользоватьХарактеристики
	|			ИЛИ ОстаткиТоваров.Характеристика = &ХарактеристикаПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Организация", ДанныеНастройки.ОрганизацияДляОстатков);
	Запрос.УстановитьПараметр("Склады", Склады);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", ДанныеНастройки.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ХарактеристикаПустаяСсылка", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	
	Результат = Запрос.Выполнить().Выбрать();
	
	УзлыОбмена = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(УзелОбмена);
	
	Пока Результат.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.ОстаткиТоваров.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Результат.Организация);
		НаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(Результат.СтруктурнаяЕдиница);
		НаборЗаписей.Отбор.Номенклатура.Установить(Результат.Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(Результат.Характеристика);
		НаборЗаписей.Отбор.Партия.Установить(Справочники.ПартииНоменклатуры.ПустаяСсылка());
		ПланыОбмена.ЗарегистрироватьИзменения(УзлыОбмена, НаборЗаписей);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьХарактеристикиНоменклатуры(УзелОбмена, НастройкаПубликацииМЛК, Номенклатура)
	
	УзлыОбмена = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(УзелОбмена);
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|ГДЕ
	|	ХарактеристикиНоменклатуры.Владелец В(&Номенклатура)";
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		ПланыОбмена.ЗарегистрироватьИзменения(УзлыОбмена, Результат.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
