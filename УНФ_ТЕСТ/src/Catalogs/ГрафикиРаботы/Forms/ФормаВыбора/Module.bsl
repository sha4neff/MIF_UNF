

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
	Список, "Недействителен", Ложь, , , Истина);
	
	Если Параметры.Свойство("Подсистема") Тогда
		ТипПодсистемы = Параметры.Подсистема;
	Иначе
		ТипПодсистемы = 0;
		Элементы.ГруппаСущностиПоГрафику.Видимость = Ложь;
		Элементы.ОтборПографику.Видимость = Ложь;
	КонецЕсли;
	
	Если ТипПодсистемы = 2 Тогда
		СущностиПоГрафику.ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиСрезПоследних.ГрафикРаботы КАК ГрафикРаботы,
		|	СотрудникиСрезПоследних.Сотрудник КАК РесурсПредприятия
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			,
		|			НЕ Сотрудник.Недействителен
		|				И НЕ ГрафикРаботы = ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)) КАК СотрудникиСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	СотрудникиСрезПоследних.Сотрудник.Наименование";
		
		Элементы.СписокСотрудникиПоГрафикуРесурсПредприятия.Заголовок = "Сотрудники по графику";
		Элементы.ОтборПографику.Видимость = Ложь;
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПографикуПриИзменении(Элемент)
	
	УстановитьОтборПоГрафику();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СущностиПоГрафику, 
	"ГрафикРаботы",
	ТекущиеДанные.Ссылка,
	ВидСравненияКомпоновкиДанных.Равно);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСотрудникиПоГрафикуВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СущностиПоГрафику.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда Возврат КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.РесурсПредприятия) = Тип("СправочникСсылка.КлючевыеРесурсы") Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.РесурсПредприятия);
		ОткрытьФорму("Справочник.КлючевыеРесурсы.ФормаОбъекта",ПараметрыФормы);
	ИначеЕсли ТипЗнч(ТекущиеДанные.РесурсПредприятия) = Тип("СправочникСсылка.Сотрудники") Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.РесурсПредприятия);
		ОткрытьФорму("Справочник.Сотрудники.ФормаОбъекта",ПараметрыФормы);
	Иначе
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.РесурсПредприятия);
		ОткрытьФорму("Справочник.Бригады.ФормаОбъекта",ПараметрыФормы);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьНедействительную(Команда)
	
	ПолеОтбора = Новый ПолеКомпоновкиДанных("Недействителен");
	НайденныйЭлемент = Неопределено;
	
	Для Каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
			Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементОтбора.ЛевоеЗначение = ПолеОтбора
			Тогда
			НайденныйЭлемент = ЭлементОтбора;
			Прервать
		КонецЕсли;
	КонецЦикла;
	
	Если НайденныйЭлемент = Неопределено
		Тогда
		Возврат;
	КонецЕсли;
	
	НовыйОтбор = НайденныйЭлемент;
	НовыйОтбор.Использование = Не НовыйОтбор.Использование;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПоГрафику()
	
	Если ОтборПоГрафику = "Сотрудники" Тогда
		Элементы.СущностиПоГрафикуРесурсПредприятия.Заголовок = "Сотрудники по графику";
		УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(СущностиПоГрафику, "ТипРесурса", 1, Истина, ВидСравненияКомпоновкиДанных.Равно);
	ИначеЕсли ОтборПоГрафику = "Бригады" Тогда
		Элементы.СущностиПоГрафикуРесурсПредприятия.Заголовок = "Бригады по графику";
		УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(СущностиПоГрафику, "ТипРесурса", 2, Истина, ВидСравненияКомпоновкиДанных.Равно);
	ИначеЕсли ОтборПоГрафику = "Ресурсы" Тогда
		Элементы.СущностиПоГрафикуРесурсПредприятия.Заголовок = "Ресурсы по графику";
		УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(СущностиПоГрафику, "ТипРесурса", 3, Истина, ВидСравненияКомпоновкиДанных.Равно);
	Иначе
		УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(СущностиПоГрафику, "ТипРесурса", 0, Ложь, ВидСравненияКомпоновкиДанных.Равно);
		Элементы.СущностиПоГрафикуРесурсПредприятия.Заголовок = "Все элементы по графику";
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

		НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
		РаботаСФормой.ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Список.Недействителен", Истина, ВидСравненияКомпоновкиДанных.Равно);
		РаботаСФормой.ДобавитьОформляемыеПоля(НовоеУсловноеОформление, "Наименование");
		РаботаСФормой.ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекстаТабличнойЧасти);

КонецПроцедуры

#КонецОбласти

