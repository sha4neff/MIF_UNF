#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция получает основной вид цен продажи из пользовательский настроек.
//
Функция ПолучитьОсновнойВидЦенПродажи() Экспорт
	
	ВидЦенПродажи = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.АвторизованныйПользователь(), "ОсновнойВидЦенПродажи");
	
	Возврат ?(ЗначениеЗаполнено(ВидЦенПродажи), ВидЦенПродажи, Справочники.ВидыЦен.Оптовая);
	
КонецФункции// ЗаполнитьВидЦен()

// Функция получает вид цен продажи по умолчанию для указанного контрагента.
//
// Принцип получения цены:
//	1. Контрагент -> Основной договор -> Вид цен;
//	2. Настройки пользователя -> Основной вид цен продажи;
//	3. Предопределенный вид цен: Оптовая цена;
//	
Функция ПолучитьВидЦенПоУмолчанию(Контрагент) Экспорт
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ДоговорПоУмолчанию(Контрагент);
		
		Если ЗначениеЗаполнено(ДоговорПоУмолчанию)
		И ЗначениеЗаполнено(ДоговорПоУмолчанию.ВидЦен) Тогда
		
			Возврат ДоговорПоУмолчанию.ВидЦен;
		Иначе
			Возврат ПолучитьОсновнойВидЦенПродажи();
		КонецЕсли;
		
	Иначе
		
		Возврат ПолучитьОсновнойВидЦенПродажи();
		
	КонецЕсли;
	
КонецФункции

// Функция получает организацию для указанного контрагента.
//
// Принцип получения организации:
//	1. Контрагент -> Основной договор -> Организация; (если вкл. синхронизация данных)
//	2. Настройки пользователя -> Основная организация;
//	3. Предопределенный элемент: Основная организация;
//
Функция ПолучитьОрганизациюПоУмолчанию(Контрагент)
	
	// ВНИМАНИЕ! Не путать с "УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию"
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ДоговорПоУмолчанию(Контрагент);
		Если ЗначениеЗаполнено(ДоговорПоУмолчанию)
		И ЗначениеЗаполнено(ДоговорПоУмолчанию.Организация) Тогда
		
			Возврат ДоговорПоУмолчанию.Организация;
		Иначе
		
			ОсновнаяОрганизация = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.АвторизованныйПользователь(), "ОсновнаяОрганизация");
			
			Возврат ?(ЗначениеЗаполнено(ОсновнаяОрганизация), ОсновнаяОрганизация, Справочники.Организации.ОрганизацияПоУмолчанию());
		КонецЕсли;
	Иначе
		
		ОсновнаяОрганизация = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.АвторизованныйПользователь(), "ОсновнаяОрганизация");
		
		Возврат ?(ЗначениеЗаполнено(ОсновнаяОрганизация), ОсновнаяОрганизация, Справочники.Организации.ОрганизацияПоУмолчанию());
		
	КонецЕсли;

	
КонецФункции //ПолучитьОрганизациюПоУмолчанию()

// Получает первый телефон контрагента.
//
Функция ПолучитьТелефонКонтрагента(Контрагент) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Возврат "";
	КонецЕсли;
	
	Если ТипЗнч(Контрагент) <> Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат "";
	КонецЕсли;
	
	КонтрагентОбъект = Контрагент.ПолучитьОбъект();
	
	Для каждого СтрокаКИ Из КонтрагентОбъект.КонтактнаяИнформация Цикл
		Если СтрокаКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон И Не ПустаяСтрока(СтрокаКИ.Представление) Тогда
			Возврат(СтрокаКИ.Представление);
		КонецЕсли;
	КонецЦикла;
	Возврат "";
	
КонецФункции

// Возвращает список контактов, связанных с контрагентом
//
// Параметры:
//  Контрагент - СправочникСсылка.Контрагент
// 
// Возвращаемое значение:
//   - Массив
//
Функция СвязанныеКонтакты(Контрагент) Экспорт
	
	Контакты = Новый Массив;
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Контакты;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КонтактныеЛица.Ссылка КАК Ссылка
		|ИЗ
		|	РегистрСведений.СвязиКонтрагентКонтакт.СрезПоследних(, Контрагент = &Контрагент) КАК СвязиКонтрагентКонтактСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК КонтактныеЛица
		|		ПО (КонтактныеЛица.Ссылка = СвязиКонтрагентКонтактСрезПоследних.Контакт)
		|ГДЕ
		|	КонтактныеЛица.ПометкаУдаления = ЛОЖЬ
		|	И СвязиКонтрагентКонтактСрезПоследних.СвязьНедействительна = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Контакты.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Контакты;
	
КонецФункции

// Определяет установлена ли связь между контрагентом и контактом.
//
// Параметры:
//  Контрагент	 - СправочникСсылка.Контрагент
//  Контакт		 - СправочникСсылка.КонтактноеЛицо
// 
// Возвращаемое значение:
//   - Булево
//
Функция КонтрагентСвязанСКонтактом(Контрагент, Контакт) Экспорт
	
	Возврат СвязанныеКонтакты(Контрагент).Найти(Контакт) <> Неопределено;
	
КонецФункции

// Добавляет новую связь контрагента и контакта.
//
// Параметры:
//  Контрагент	 - СправочникСсылка.Контрагенты
//  Контакт		 - СправочникСсылка.КонтактныеЛица
//
Процедура ДобавитьСвязьСКонтактом(Контрагент, Контакт) Экспорт
	
	Если КонтрагентСвязанСКонтактом(Контрагент, Контакт) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.СвязиКонтрагентКонтакт.НоваяСвязь(Контрагент, Контакт);
	
КонецПроцедуры

// Лид на основании которого был создан контрагент.
//
// Параметры:
//  Контрагент	 - СправочникСсылка.Контрагент
// 
// Возвращаемое значение:
//   - СправочникСсылка.Лиды
//
Функция ЛидОснование(Контрагент) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Справочники.Лиды.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Лиды.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Лиды КАК Лиды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвязиКонтрагентЛид КАК СвязиКонтрагентЛид
	|		ПО Лиды.Ссылка = СвязиКонтрагентЛид.Лид
	|ГДЕ
	|	СвязиКонтрагентЛид.Контрагент = &Контрагент";
	
	Запрос.Параметры.Вставить("Контрагент", Контрагент);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
КонецФункции

// Создает контрагента по переданной структуре
//
// Параметры:
//  - ДанныеЗаполнения - Структура - структура свойств контрагента
//
// Возвращаемое значение:
//  - Ссылка - ссылка на созданный элемент справочника
//
Функция СоздатьКонтрагента(ДанныеЗаполнения) Экспорт
	
	СуществующийКонтрагент = НайтиПоРеквизиту("ИНН", ДанныеЗаполнения.ИНН);
	
	Если СуществующийКонтрагент.Пустая() Тогда
		Контрагент = Справочники.Контрагенты.СоздатьЭлемент();
	Иначе
		Контрагент = СуществующийКонтрагент.ПолучитьОбъект();
		Контрагент.Заблокировать();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Контрагент, ДанныеЗаполнения);
	
	Контрагент.Заполнить(ДанныеЗаполнения);
	
	
	Контрагент.СчетУчетаРасчетовСПокупателем = ПланыСчетов.Управленческий.РасчетыСПокупателями;
	Контрагент.СчетУчетаАвансовПокупателя = ПланыСчетов.Управленческий.РасчетыПоАвансамПолученным;
	Контрагент.СчетУчетаРасчетовСПоставщиком = ПланыСчетов.Управленческий.РасчетыСПоставщиками;
	Контрагент.СчетУчетаАвансовПоставщику = ПланыСчетов.Управленческий.РасчетыПоАвансамВыданным;
	Контрагент.ВестиРасчетыПоДоговорам = Истина;
	Контрагент.ВестиРасчетыПоДокументам = Истина;
	Контрагент.ВестиРасчетыПоЗаказам = Истина;
	Контрагент.ВестиУчетОплатыПоСчетам = Истина;
	
	Если Константы.ОграничиватьДоступНаУровнеЗаписей.Получить() Тогда
		Контрагент.ГруппаДоступа = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнаяГруппаДоступаКонтрагентов");
	КонецЕсли;
	
	Контрагент.ВестиУчетОплатыПоСчетам = Истина;
	
	Если Контрагент.ЭтоНовый() Тогда
		Контрагент.Записать();
	КонецЕсли;
	
	КонтрагентСсылка = Контрагент.Ссылка;
	
	// Банковский счет
	Если ДанныеЗаполнения.Свойство("НомерСчета") 
		И ДанныеЗаполнения.Свойство("БИК") 
		И ЗначениеЗаполнено(ДанныеЗаполнения.НомерСчета)
		И ЗначениеЗаполнено(ДанныеЗаполнения.БИК) Тогда
		Контрагент.БанковскийСчетПоУмолчанию = Справочники.БанковскиеСчета.ПолучитьЭлемент(
			КонтрагентСсылка, ДанныеЗаполнения.НомерСчета, ДанныеЗаполнения.БИК);
	КонецЕсли;
	
	// Руководитель
	Если ДанныеЗаполнения.Свойство("Руководитель") 
		И ДанныеЗаполнения.Свойство("ДолжностьРуководителя")
		И ЗначениеЗаполнено(ДанныеЗаполнения.Руководитель)
		И ЗначениеЗаполнено(ДанныеЗаполнения.ДолжностьРуководителя) Тогда
		Контрагент.КонтактноеЛицоПодписант = Справочники.КонтактныеЛица.ПолучитьЭлемент(КонтрагентСсылка, ДанныеЗаполнения.Руководитель);
	КонецЕсли;
	
	Контрагент.Записать();
	Контрагент = Неопределено;
	
	Возврат КонтрагентСсылка;
	
КонецФункции

Функция ПолучателиЭлектронногоПисьма(Контрагент, ВидКонтактнойИнформации = Неопределено) Экспорт
	
	ПолучателиЭлектронногоПисьма = Новый ТаблицаЗначений;
	ПолучателиЭлектронногоПисьма.Колонки.Добавить("Контакт", Метаданные.ОпределяемыеТипы.КонтактСобытия.Тип);
	ПолучателиЭлектронногоПисьма.Колонки.Добавить("КакСвязаться", Новый ОписаниеТипов("Строка"));
	
	КонтактнаяИнформацияОбъекта = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Контрагент, ВидКонтактнойИнформации,, Ложь);
	
	Если ВидКонтактнойИнформации = Неопределено Тогда
		ОтборКонтактнойИнформации = Новый Структура("Тип", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
		СтрокиКонтактнойИнформации = КонтактнаяИнформацияОбъекта.НайтиСтроки(ОтборКонтактнойИнформации);
	Иначе
		СтрокиКонтактнойИнформации = КонтактнаяИнформацияОбъекта;
	КонецЕсли;
	
	Если СтрокиКонтактнойИнформации.Количество() <> 0 Тогда
		Для каждого Строка Из СтрокиКонтактнойИнформации Цикл
			НовыйПолучатель = ПолучателиЭлектронногоПисьма.Добавить();
			НовыйПолучатель.Контакт = Контрагент;
			НовыйПолучатель.КакСвязаться = Строка.Представление;
		КонецЦикла;
	Иначе
		ОсновнойКонтакт = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "КонтактноеЛицо");
		Если ЗначениеЗаполнено(ОсновнойКонтакт) Тогда
			ПолучателиЭлектронногоПисьма = Справочники.КонтактныеЛица.ПолучателиЭлектронногоПисьма(ОсновнойКонтакт);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПолучателиЭлектронногоПисьма;
	
КонецФункции

// Стандартные реквизиты для выбора полей при переводе лида в контрагента
//
// Возвращаемое значение:
//  - Соответствие - список реквизитов, Ключ - Имя реквизита, Значение - представление реквизита
//
Функция СтандартныеРеквизитыДляПереводаВПокупателя() Экспорт
	
	Реквизиты = Новый Соответствие;
	Реквизиты.Вставить("Наименование", НСтр("ru = 'Представление в программе'"));
	Реквизиты.Вставить("Комментарий",  НСтр("ru = 'Дополнительная информация'"));
	Реквизиты.Вставить("НаименованиеПолное", НСтр("ru = 'Юридическое название'"));
	Реквизиты.Вставить("ИсточникПривлеченияПокупателя", НСтр("ru = 'Источник'"));
	Реквизиты.Вставить("ФИО", НСтр("ru = 'ФИО физ лица'"));
	Реквизиты.Вставить("ИНН", НСтр("ru = 'ИНН'"));
	Реквизиты.Вставить("КПП", НСтр("ru = 'КПП'"));
	Реквизиты.Вставить("КодПоОКПО", НСтр("ru = 'ОКПО'"));
	Реквизиты.Вставить("РегистрационныйНомер", НСтр("ru = 'ОГРН'"));
	Реквизиты.Вставить("ВидГосударственногоОргана", НСтр("ru = 'Вид государственного органа'"));
	Реквизиты.Вставить("КодГосударственногоОргана", НСтр("ru = 'Код государственного органа'"));
	Реквизиты.Вставить("СвидетельствоСерияНомер", НСтр("ru = 'Серия и номер свидетельства'"));
	Реквизиты.Вставить("СвидетельствоДатаВыдачи", НСтр("ru = 'Дата выдачи свидетельства'"));
	Реквизиты.Вставить("ДокументУдостоверяющийЛичность", НСтр("ru = 'Документ удостоверяющий личность'"));
	Реквизиты.Вставить("Пол",          НСтр("ru = 'Пол'"));
	Реквизиты.Вставить("ДатаРождения", НСтр("ru = 'Дата рождения'"));
	
	Возврат Реквизиты;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЭтоГруппа ИЛИ
	|	ЗначениеРазрешено(Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

// См. АвтоподборКонтактовКлиент.Подключаемый_АвтоПодбор
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("Рекурсия") Тогда
		Если Параметры.ВыборГруппИЭлементов <> ИспользованиеГруппИЭлементов.Группы Тогда
			
			Если Не Параметры.Отбор.Свойство("Недействителен") Тогда
				Параметры.Отбор.Вставить("Недействителен", Ложь);
			КонецЕсли;
			
		Иначе
			
			Для Каждого КлючИЗначение Из Параметры.Отбор Цикл
				НайденныйРеквизит = Метаданные.Справочники.Контрагенты.Реквизиты.Найти(КлючИЗначение.Ключ);
				Если НайденныйРеквизит = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				Если НайденныйРеквизит.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента Тогда
					Параметры.Отбор.Удалить(КлючИЗначение.Ключ);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		Параметры.Вставить("Рекурсия");
		СтандартныйСписок = ПолучитьДанныеВыбора(Параметры);
		ДополнитьДанныеВыбора(СтандартныйСписок, Параметры.СтрокаПоиска);
		ДанныеВыбора = СтандартныйСписок;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает текст запроса для печати прайс-листа (ценовая группа)
//
Функция ПолучитьТекстЗапросаДляПечатиПрайсЛистаЦеноваяГруппа()
	
	Возврат 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ВыводитьКод = ЗНАЧЕНИЕ(Перечисление.ДаНет.Да)
	|			ТОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.Код
	|		ИНАЧЕ ЦеныНоменклатурыСрезПоследних.Номенклатура.Артикул
	|	КОНЕЦ КАК АртикулКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьПолноеНаменование = ЗНАЧЕНИЕ(Перечисление.ДаНет.Да)
	|			ТОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ЦеныНоменклатурыСрезПоследних.Номенклатура.Наименование
	|	КОНЕЦ КАК ПредставлениеНоменклатуры,
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатурыСрезПоследних.Характеристика КАК Характеристика,
	|	ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена,
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура.ЦеноваяГруппа КАК ЦеноваяГруппа
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			&Период,
	|			Актуальность
	|				И ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура.Наименование,
	|	Характеристика,
	|	АртикулКод
	|ИТОГИ ПО
	|	ЦеноваяГруппа";
	
КонецФункции // ПолучитьТекстЗапросаДляПечатиПрайсЛиста()

// Возвращает текст запроса для печати прайс-листа (иерархия номенклатуры)
//
Функция ПолучитьТекстЗапросаДляПечатиПрайсЛистаИерархияНоменклатуры()
	
	Возврат 
	"ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &ВыводитьКод = ЗНАЧЕНИЕ(Перечисление.ДаНет.Да)
	|			ТОГДА СправочникНоменклатура.Код
	|		ИНАЧЕ СправочникНоменклатура.Артикул
	|	КОНЕЦ КАК АртикулКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьПолноеНаменование = ЗНАЧЕНИЕ(Перечисление.ДаНет.Да)
	|			ТОГДА СправочникНоменклатура.НаименованиеПолное
	|		ИНАЧЕ СправочникНоменклатура.Наименование
	|	КОНЕЦ КАК ПредставлениеНоменклатуры,
	|	СправочникНоменклатура.Родитель КАК Родитель,
	|	СправочникНоменклатура.ЭтоГруппа КАК ЭтоГруппа,
	|	ЦеныНоменклатурыСрезПоследних.Характеристика КАК Характеристика,
	|	ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				&Период,
	|				Актуальность
	|					И ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО СправочникНоменклатура.Ссылка = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	СправочникНоменклатура.Ссылка ИЕРАРХИЯ,
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура.Наименование,
	|	Характеристика,
	|	АртикулКод";
	
КонецФункции // ПолучитьТекстЗапросаДляПечатиПрайсЛиста()

// Возвращает список реквизитов, которые разрешается редактировать
// с помощью обработки группового изменения объектов.
//
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	РедактируемыеРеквизиты = Новый Массив;
	
	РедактируемыеРеквизиты.Добавить("ГруппаДоступа");
	РедактируемыеРеквизиты.Добавить("ДатаСоздания");
	РедактируемыеРеквизиты.Добавить("Недействителен");
	РедактируемыеРеквизиты.Добавить("Покупатель");
	РедактируемыеРеквизиты.Добавить("Поставщик");
	РедактируемыеРеквизиты.Добавить("Пол");
	РедактируемыеРеквизиты.Добавить("ПрочиеОтношения");
	РедактируемыеРеквизиты.Добавить("СтранаРегистрации");
	РедактируемыеРеквизиты.Добавить("Ответственный");
	РедактируемыеРеквизиты.Добавить("ИсточникПривлеченияПокупателя");
	РедактируемыеРеквизиты.Добавить("СчетУчетаРасчетовСПокупателем");
	РедактируемыеРеквизиты.Добавить("СчетУчетаАвансовПокупателя");
	РедактируемыеРеквизиты.Добавить("СчетУчетаРасчетовСПоставщиком");
	РедактируемыеРеквизиты.Добавить("СчетУчетаАвансовПоставщику");
	
	Возврат РедактируемыеРеквизиты;
	
КонецФункции

// Функция возвращает список имен «ключевых» реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	Возврат Результат;
	
КонецФункции // ПолучитьБлокируемыеРеквизитыОбъекта()

// Процедура добавляет к списку выбора контрагентов, найденных по номерам телефона и адресамЭП,
// связанных с контрагентом контактов
// Процедура добавляет к списку выбора контрагентов, найденных по номерам телефона и адресамЭП,
// связанных с контрагентом контактов
Процедура ДополнитьДанныеВыбора(ДанныеВыбора, СтрокаПоиска)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ 10
	|	СвязиКонтрагентКонтакт.Контрагент КАК Контрагент,
	|	СвязиКонтрагентКонтакт.Контрагент.Представление КАК Представление,
	|	"""" КАК НомерТелефонаДляПоиска,
	|	КонтактныеЛица.АдресЭПДляПоиска КАК АдресЭПДляПоиска
	|ИЗ
	|	РегистрСведений.СвязиКонтрагентКонтакт.СрезПоследних КАК СвязиКонтрагентКонтакт
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК КонтактныеЛица
	|		ПО СвязиКонтрагентКонтакт.Контакт = КонтактныеЛица.Ссылка
	|ГДЕ
	|	КонтактныеЛица.АдресЭПДляПоиска ПОДОБНО &СтрокаПоиска
	|	И СвязиКонтрагентКонтакт.СвязьНедействительна = ЛОЖЬ
	|	И КонтактныеЛица.Недействителен = ЛОЖЬ";
	
	Если НЕ ЗапрещенныеСимволыВНомереТелефона(СтрокаПоиска) Тогда
		Запрос.Текст = Запрос.Текст + "
		|ОБЪЕДИНИТЬ ВСЕ
		|";
		Запрос.Текст = Запрос.Текст + "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 10
		|	СвязиКонтрагентКонтакт.Контрагент,
		|	СвязиКонтрагентКонтакт.Контрагент.Представление,
		|	КонтактныеЛица.НомерТелефонаДляПоиска,
		|	""""
		|ИЗ
		|	РегистрСведений.СвязиКонтрагентКонтакт.СрезПоследних КАК СвязиКонтрагентКонтакт
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК КонтактныеЛица
		|		ПО СвязиКонтрагентКонтакт.Контакт = КонтактныеЛица.Ссылка
		|ГДЕ
		|	(КонтактныеЛица.НомерТелефонаДляПоиска ПОДОБНО &СтрокаПоиска
		|			ИЛИ КонтактныеЛица.НомерТелефонаДляПоиска ПОДОБНО &ТелефонДляПоиска)
		|	И СвязиКонтрагентКонтакт.СвязьНедействительна = ЛОЖЬ
		|	И КонтактныеЛица.Недействителен = ЛОЖЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 10
		|	СвязиКонтрагентКонтакт.Контрагент,
		|	Контрагенты.Представление,
		|	Контрагенты.НомерТелефонаДляПоиска,
		|	""""
		|ИЗ
		|	РегистрСведений.СвязиКонтрагентКонтакт.СрезПоследних КАК СвязиКонтрагентКонтакт
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО СвязиКонтрагентКонтакт.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	Контрагенты.НомерТелефонаДляПоиска ПОДОБНО &ТелефонДляПоиска
		|	И СвязиКонтрагентКонтакт.СвязьНедействительна = ЛОЖЬ
		|	И Контрагенты.Недействителен = ЛОЖЬ";
		
	КонецЕсли; 
	
	СтрокаПоискаДляЗапроса =  "%"+СтрокаПоиска+"%";
	ТелефонДляПоиска = КонтактнаяИнформацияУНФ.ПреобразоватьНомерДляКонтактнойИнформации(СтрокаПоиска);
	ТелефонДляПоиска = СтрЗаменить(ТелефонДляПоиска, "+", "");
	Если СтрНачинаетсяС(ТелефонДляПоиска, "7") Тогда
		ПоисковоеВыражение = Сред(ТелефонДляПоиска, 2, СтрДлина(ТелефонДляПоиска) - 1);
	КонецЕсли;

	Запрос.УстановитьПараметр("ТелефонДляПоиска", ТелефонДляПоиска);
	Запрос.УстановитьПараметр("СтрокаПоиска",     СтрокаПоискаДляЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ДанныеВыбора <> Неопределено И ДанныеВыбора.НайтиПоЗначению(Выборка.Контрагент) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Представление = "";
		
		Позиция = Найти(НРег(Выборка.НомерТелефонаДляПоиска), НРег(СтрокаПоиска));
		
		Если Позиция>0 Тогда
			ТекстДо = Лев(Выборка.НомерТелефонаДляПоиска, Позиция-1);
			ТекстЦентр = Сред(Выборка.НомерТелефонаДляПоиска, Позиция, СтрДлина(СтрокаПоиска));
			ТекстПосле = Сред(Выборка.НомерТелефонаДляПоиска, Позиция+СтрДлина(СтрокаПоиска));
			ВыделенныйТекст = Новый ФорматированнаяСтрока(ТекстЦентр, Новый Шрифт(Новый Шрифт,,, Истина), WebЦвета.ЗеленыйЛес);
			Представление = Новый ФорматированнаяСтрока(ТекстДо, ВыделенныйТекст, ТекстПосле);
		Иначе
			Позиция = Найти(НРег(Выборка.АдресЭПДляПоиска), НРег(СтрокаПоиска));
			ТекстДо = Лев(Выборка.АдресЭПДляПоиска, Позиция-1);
			ТекстЦентр = Сред(Выборка.АдресЭПДляПоиска, Позиция, СтрДлина(СтрокаПоиска));
			ТекстПосле = Сред(Выборка.АдресЭПДляПоиска, Позиция+СтрДлина(СтрокаПоиска));
			ВыделенныйТекст = Новый ФорматированнаяСтрока(ТекстЦентр, Новый Шрифт(Новый Шрифт,,, Истина), WebЦвета.ЗеленыйЛес);
			Представление = Новый ФорматированнаяСтрока(ТекстДо, ВыделенныйТекст, ТекстПосле);
		КонецЕсли;
		
		Представление = Новый ФорматированнаяСтрока(Представление, " (",Выборка.Представление,")");
		ДанныеВыбора.Добавить(Выборка.Контрагент, Представление);
	КонецЦикла;
	
КонецПроцедуры

Функция ЗапрещенныеСимволыВНомереТелефона(СтрокаПоиска)
	
	РазрешенныеСимволыВНомереТелефона = "0123456789+-() ";
	Для НомерСимвола = 1 По СтрДлина(СтрокаПоиска) Цикл
		Если СтрНайти(РазрешенныеСимволыВНомереТелефона, Сред(СтрокаПоиска, НомерСимвола, 1)) = 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ПроверкаДублей

// Функция определяет наличие дублей у контрагента.
// ИНН - ИНН проверяемого контрагента, Тип - Строка(12)
// КПП - КПП проверяемого контрагента, Тип - Строка(9)
// Ссылка - Сам проверяемый контрагент, Тип - СправочникСсылка.Контрагенты
Функция ПроверитьДублиСправочникаКонтрагентыПоИННКПП(Знач ИНН, Знач КПП, ИсключаяСсылку = Неопределено, ПроверкаПриЗаписи = Ложь) Экспорт
	
	Дубли = Новый Массив;
	
	Запрос = Новый Запрос;
	// Если записываем элемент, то сначала проверим наличие дублей
	// в регистре. Операция выполняется только при событии объекта ПередЗаписью
	// В интерактивной проверке дублей не применяется, 
	// т.к. на регистр устанавливаются исключительные блокировки.
	Если ПроверкаПриЗаписи Тогда
		Дубли = ЕстьЗаписиВРегистреДублей(ИНН, КПП, ИсключаяСсылку);
	КонецЕсли;
	
	// Если при записи элемента ничего не нашлось в регистре дублей, 
	// или при интерактивной проверке выполним поиск дублей по справочнику Контрагенты
	Если Дубли.Количество() = 0 Тогда
		
		Запрос.Текст = 	"ВЫБРАТЬ
		               	|	Контрагенты.Ссылка
		               	|ИЗ
		               	|	Справочник.Контрагенты КАК Контрагенты
		               	|ГДЕ
		               	|	НЕ Контрагенты.ЭтоГруппа
		               	|	И НЕ Контрагенты.Ссылка = &Ссылка
		               	|	И Контрагенты.ИНН = &ИНН
		               	|	И Контрагенты.КПП = &КПП";
		
		Запрос.УстановитьПараметр("ИНН", СокрЛП(ИНН));
		Запрос.УстановитьПараметр("КПП", СокрЛП(КПП));
		Запрос.УстановитьПараметр("Ссылка", ИсключаяСсылку);
		
		УстановитьПривилегированныйРежим(Истина);
		ВыборкаДублей = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Пока ВыборкаДублей.Следующий() Цикл
			Дубли.Добавить(ВыборкаДублей.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Дубли;
	
КонецФункции

// Процедура возвращает массив дублей по записям в регистре Наличие дублей контрагентов
// На вход получает ИНН, КПП и ссылку на контрагента
Функция ЕстьЗаписиВРегистреДублей(ИНН, КПП, ИсключаяСсылку = Неопределено) Экспорт
	
	Дубли = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ИсключаяСсылку);
	Запрос.УстановитьПараметр("ИНН", СокрЛП(ИНН));
	Запрос.УстановитьПараметр("КПП", СокрЛП(КПП));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НаличиеДублейУКонтрагентов.Контрагент КАК Ссылка
	|ИЗ
	|	РегистрСведений.НаличиеДублейУКонтрагентов КАК НаличиеДублейУКонтрагентов
	|ГДЕ
	|	НЕ НаличиеДублейУКонтрагентов.Контрагент = &Ссылка
	|	И НаличиеДублейУКонтрагентов.КПП = &КПП
	|	И НаличиеДублейУКонтрагентов.ИНН = &ИНН";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДублей = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДублей.Следующий() Цикл
		Дубли.Добавить(ВыборкаДублей.Ссылка);
	КонецЦикла;
	
	Возврат Дубли;
	
КонецФункции

// Процедура делает движения в регистре дублей
// Ссылка - ссылка на эелемнт справочника Контрагенты
// ИНН - ИНН записываемого контрагента
// КПП - КПП записываемого контрагента
// НужноУдалить:
//				Истина - удалить запись по переданному контрагенту
//				Ложь   - сделать запись по переданному контрагенту
Процедура ВыполнитьДвиженияПоРегиструДублей(Ссылка, ИНН = "", КПП = "", НужноУдалить) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.НаличиеДублейУКонтрагентов.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.Контрагент = Ссылка;
	МенеджерЗаписи.ИНН        = ИНН;
	МенеджерЗаписи.КПП        = КПП;
	
	МенеджерЗаписи.Прочитать();
	
	ЗаписьСуществует = МенеджерЗаписи.Выбран();
	
	Если НужноУдалить И ЗаписьСуществует Тогда
		МенеджерЗаписи.Удалить();
	ИначеЕсли Не НужноУдалить И НЕ ЗаписьСуществует Тогда
		
		МенеджерЗаписи.Контрагент = Ссылка;
		МенеджерЗаписи.ИНН        = ИНН;
		МенеджерЗаписи.КПП        = КПП;
		
		МенеджерЗаписи.Активность = Истина;
		МенеджерЗаписи.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаДанныхИзВнешнегоИсточника

Процедура ДобавитьКИ(ЭлементСправочника, ПредставлениеКИ, ВидКИ)
	
	ЗначениеКИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(ПредставлениеКИ, ВидКИ);
	УправлениеКонтактнойИнформацией.ЗаписатьКонтактнуюИнформацию(ЭлементСправочника, ЗначениеКИ, ВидКИ, ВидКИ.Тип);
	
КонецПроцедуры

Процедура ПриОпределенииЗначенияПоУмолчанию(СправочникСсылка, ИмяРеквизита, ВходящиеДанные, СтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию)
	
	Если СтрокаСопоставлена 
		И НЕ ЗначениеЗаполнено(ВходящиеДанные) Тогда
		
		ЗначениеПоУмолчанию = СправочникСсылка[ИмяРеквизита];
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииОбразцовЗагрузкиДанных(НастройкиЗагрузкиДанных, УникальныйИдентификатор) Экспорт
	
	Образец_xlsx = ПолучитьМакет("ОбразецЗагрузкиДанных_xlsx");
	ОбразецЗагрузкиДанных_xlsx = ПоместитьВоВременноеХранилище(Образец_xlsx, УникальныйИдентификатор);
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_xlsx", ОбразецЗагрузкиДанных_xlsx);
	
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_mxl", "ОбразецЗагрузкиДанных_mxl");
	
	Образец_csv = ПолучитьМакет("ОбразецЗагрузкиДанных_csv");
	ОбразецЗагрузкиДанных_csv = ПоместитьВоВременноеХранилище(Образец_csv, УникальныйИдентификатор);
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_csv", ОбразецЗагрузкиДанных_csv);
	
КонецПроцедуры

Процедура ПоляЗагрузкиДанныхИзВнешнегоИсточника(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных) Экспорт
	
	//
	// Для группы полей действует правило: хотя бы одно поле в группе должно быть выбрано в колонках
	//
	
	ОписаниеТиповСтрока10 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(10));
	ОписаниеТиповСтрока11 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(11));
	ОписаниеТиповСтрока25 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(25));
	ОписаниеТиповСтрока50 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50));
	ОписаниеТиповСтрока100 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(100));
	ОписаниеТиповСтрока150 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(150));
	ОписаниеТиповСтрока200 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(200));
	ОписаниеТиповСтрока1000 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(1000));
	ОписаниеТиповЧисло10_0 = Новый ОписаниеТипов("Число", , , , Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный));
	ОписаниеТиповЧисло10_3 = Новый ОписаниеТипов("Число", , , , Новый КвалификаторыЧисла(10, 3, ДопустимыйЗнак.Неотрицательный));
	ОписаниеТиповЧисло15_2 = Новый ОписаниеТипов("Число", , , , Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный));
	ОписаниеТиповЧисло15_3 = Новый ОписаниеТипов("Число", , , , Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Неотрицательный));
	ОписаниеТиповДата = Новый ОписаниеТипов("Дата", , , , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	
	Если НастройкиЗагрузкиДанных.ФиксированныйШаблон Тогда
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "УИД", "УИД", ОписаниеТиповСтрока50, ОписаниеТиповСтрока50);
	КонецЕсли; 
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Родитель", "Группа", ОписаниеТиповСтрока100, ОписаниеТиповКолонка);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ИНН_КПП", 				"ИНН/КПП или ИНН", 				ОписаниеТиповСтрока25, ОписаниеТиповКолонка, "Контрагент", 1, , Истина);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "КонтрагентНаименование",	"Контрагент (наименование)",	ОписаниеТиповСтрока100, ОписаниеТиповКолонка, "Контрагент", 3, Истина, Истина);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "РасчетныйСчет",			"Контрагент (расчетный счет)",	ОписаниеТиповСтрока50, ОписаниеТиповКолонка, "Контрагент", 4, , Истина);
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "КодПоОКПО", "Код по ОКПО", ОписаниеТиповСтрока10, ОписаниеТиповСтрока10);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГруппыДоступаКонтрагентов") Тогда
		
		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.ГруппыДоступаКонтрагентов");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ГруппаДоступа", "Группа доступа контрагента", ОписаниеТиповСтрока200, ОписаниеТиповКолонка, , , Истина);
		
	КонецЕсли;
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("ПланСчетовСсылка.Управленческий");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СчетУчетаРасчетовСПокупателем", 	"Счет учета (расчеты с покупателем)",	ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СчетУчетаАвансовПокупателя", 		"Счет учета (авансы покупателя)",		ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СчетУчетаРасчетовСПоставщиком", 	"Счет учета (расчеты с поставщиком)",	ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СчетУчетаАвансовПоставщику", 		"Счет учета (авансы поставщика)",		ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Комментарий", "Комментарий", ОписаниеТиповСтрока200, ОписаниеТиповСтрока200);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("Булево");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ВестиРасчетыПоДоговорам",		"Вести расчеты по договорам",	ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ВестиРасчетыПоДокументам",	"Вести расчеты по документам",	ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ВестиРасчетыПоЗаказам",		"Вести расчеты по заказам",		ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ВестиУчетОплатыПоСчетам",		"Вести расчеты по счетам",		ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "АдресЮр", "Юр. адрес", ОписаниеТиповСтрока1000, ОписаниеТиповСтрока1000);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "АдресФакт", "Факт. адрес", ОписаниеТиповСтрока1000, ОписаниеТиповСтрока1000);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Телефон", "Телефон", ОписаниеТиповСтрока100, ОписаниеТиповСтрока100);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "АдресЭП", "E-mail", ОписаниеТиповСтрока100, ОписаниеТиповСтрока100);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("ПеречислениеСсылка.ВидыКонтрагентов");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ВидКонтрагента", "Вид контрагента", ОписаниеТиповСтрока25, ОписаниеТиповКолонка);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("Булево");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Покупатель",		"Покупатель",		ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Поставщик",		"Поставщик",		ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ПрочиеОтношения",	"ПрочиеОтношения",	ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ФИО",	"ФИО", ОписаниеТиповСтрока100, ОписаниеТиповСтрока100);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("ПеречислениеСсылка.ПолФизическогоЛица");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Пол",	"Пол физ. лица", ОписаниеТиповСтрока10, ОписаниеТиповКолонка);
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ДатаРождения", "Дата рождения физ. лица", ОписаниеТиповСтрока25, ОписаниеТиповДата);
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ДокументУдостоверяющийЛичность",	"Документ физ. лица", ОписаниеТиповСтрока200, ОписаниеТиповСтрока200);
	
	// ДополнительныеРеквизиты
	ЗагрузкаДанныхИзВнешнегоИсточника.ПодготовитьСоответствиеПоДополнительнымРеквизитам(НастройкиЗагрузкиДанных, Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Контрагенты);
	Если НастройкиЗагрузкиДанных.ОписаниеДополнительныхРеквизитов.Количество() > 0 Тогда
		
		ИмяПоля = ЗагрузкаДанныхИзВнешнегоИсточника.ИмяПоляДобавленияДополнительныхРеквизитов();
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, ИмяПоля, "Дополнительные реквизиты", ОписаниеТиповСтрока150, ОписаниеТиповСтрока11, , , , , , Истина, Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Контрагенты);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьЗагружаемыеДанныеИзВнешнегоИсточника(ПараметрыСопоставления, АдресРезультата = "") Экспорт
	
	ТаблицаСопоставленияДанных	= ПараметрыСопоставления.ТаблицаСопоставленияДанных;
	РазмерТаблицыДанных			= ТаблицаСопоставленияДанных.Количество();
	НастройкиЗагрузкиДанных		= ПараметрыСопоставления.НастройкиЗагрузкиДанных;
	ОбновлятьДанные				= НастройкиЗагрузкиДанных.ОбновлятьСуществующие;
	
	// ТаблицаСопоставленияДанных - Тип ДанныеФормыКоллекция
	Для каждого СтрокаТаблицыФормы Из ТаблицаСопоставленияДанных Цикл
		
		// Контрагент по ИНН, КПП, Наименованию, Расчетному счету
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьКонтрагентаПоУИД(СтрокаТаблицыФормы);
		ЭтаСтрокаСопоставлена = ЗначениеЗаполнено(СтрокаТаблицыФормы.Контрагент);
		
		// Родитель по наименованию
		ЗначениеПоУмолчанию = Справочники.Контрагенты.ПустаяСсылка();
		ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Контрагент, "Родитель", СтрокаТаблицыФормы.Родитель_ВходящиеДанные, ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьРодителя("Контрагенты", СтрокаТаблицыФормы.Родитель, СтрокаТаблицыФормы.Родитель_ВходящиеДанные, ЗначениеПоУмолчанию);
		
		// КодПоОКПО
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СкопироватьСтрокуВЗначениеСтроковогоТипа(СтрокаТаблицыФормы.КодПоОКПО, СтрокаТаблицыФормы.КодПоОКПО_ВходящиеДанные);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьГруппыДоступаКонтрагентов") Тогда
			
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьГруппуДоступа(СтрокаТаблицыФормы.ГруппаДоступа, СтрокаТаблицыФормы.ГруппаДоступа_ВходящиеДанные);
			
		КонецЕсли;
		
		// СчетУчетаРасчетовСПокупателем по коду, наименованию
		ЗначениеПоУмолчанию = ПланыСчетов.Управленческий.РасчетыСПокупателями;
		ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Контрагент, "СчетУчетаРасчетовСПокупателем", СтрокаТаблицыФормы.СчетУчетаРасчетовСПокупателем_ВходящиеДанные, ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСчетУчетаРасчетовСПокупателем(СтрокаТаблицыФормы.СчетУчетаРасчетовСПокупателем, СтрокаТаблицыФормы.СчетУчетаРасчетовСПокупателем_ВходящиеДанные, ЗначениеПоУмолчанию);
		
		// СчетУчетаАвансовПокупателя по коду, наименованию
		ЗначениеПоУмолчанию = ПланыСчетов.Управленческий.РасчетыПоАвансамПолученным;
		ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Контрагент, "СчетУчетаАвансовПокупателя", СтрокаТаблицыФормы.СчетУчетаАвансовПокупателя_ВходящиеДанные, ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСчетУчетаАвансовПокупателя(СтрокаТаблицыФормы.СчетУчетаАвансовПокупателя, СтрокаТаблицыФормы.СчетУчетаАвансовПокупателя_ВходящиеДанные, ЗначениеПоУмолчанию);
		
		// СчетУчетаРасчетовСПокупателем по коду, наименованию
		ЗначениеПоУмолчанию = ПланыСчетов.Управленческий.РасчетыСПоставщиками;
		ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Контрагент, "СчетУчетаРасчетовСПоставщиком", СтрокаТаблицыФормы.СчетУчетаРасчетовСПоставщиком_ВходящиеДанные, ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСчетУчетаРасчетовСПоставщиком(СтрокаТаблицыФормы.СчетУчетаРасчетовСПоставщиком, СтрокаТаблицыФормы.СчетУчетаРасчетовСПоставщиком_ВходящиеДанные, ЗначениеПоУмолчанию);
		
		// СчетУчетаАвансовПоставщику по коду, наименованию
		ЗначениеПоУмолчанию = ПланыСчетов.Управленческий.РасчетыПоАвансамВыданным;
		ПриОпределенииЗначенияПоУмолчанию(СтрокаТаблицыФормы.Контрагент, "СчетУчетаАвансовПоставщику", СтрокаТаблицыФормы.СчетУчетаАвансовПоставщику_ВходящиеДанные, ЭтаСтрокаСопоставлена, ОбновлятьДанные, ЗначениеПоУмолчанию);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСчетУчетаАвансовПоставщику(СтрокаТаблицыФормы.СчетУчетаАвансовПоставщику, СтрокаТаблицыФормы.СчетУчетаАвансовПоставщику_ВходящиеДанные, ЗначениеПоУмолчанию);
		
		// Комментарий
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СкопироватьСтрокуВЗначениеСтроковогоТипа(СтрокаТаблицыФормы.Комментарий, СтрокаТаблицыФормы.Комментарий_ВходящиеДанные);
		
		// ВестиРасчетыПоДоговорам
		СтрокаКСопоставлению = ?(ПустаяСтрока(СтрокаТаблицыФормы.ВестиРасчетыПоДоговорам_ВходящиеДанные), "ИСТИНА", СтрокаТаблицыФормы.ВестиРасчетыПоДоговорам_ВходящиеДанные);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ВестиРасчетыПоДоговорам, СтрокаКСопоставлению);
		
		// ВестиРасчетыПоДокументам
		СтрокаКСопоставлению = ?(ПустаяСтрока(СтрокаТаблицыФормы.ВестиРасчетыПоДокументам_ВходящиеДанные), "ИСТИНА", СтрокаТаблицыФормы.ВестиРасчетыПоДокументам_ВходящиеДанные);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ВестиРасчетыПоДокументам, СтрокаКСопоставлению);
		
		// ВестиРасчетыПоЗаказам
		СтрокаКСопоставлению = ?(ПустаяСтрока(СтрокаТаблицыФормы.ВестиРасчетыПоЗаказам_ВходящиеДанные), "ИСТИНА", СтрокаТаблицыФормы.ВестиРасчетыПоЗаказам_ВходящиеДанные);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ВестиРасчетыПоЗаказам, СтрокаКСопоставлению);
		
		// ВестиУчетОплатыПоСчетам
		СтрокаКСопоставлению = ?(ПустаяСтрока(СтрокаТаблицыФормы.ВестиУчетОплатыПоСчетам_ВходящиеДанные), "ИСТИНА", СтрокаТаблицыФормы.ВестиУчетОплатыПоСчетам_ВходящиеДанные);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ВестиУчетОплатыПоСчетам, СтрокаКСопоставлению);
		
		// КИ (Юр. адрес, факт. адрес, адрес ЭП, телефон)
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СкопироватьСтрокуВЗначениеСтроковогоТипа(СтрокаТаблицыФормы.АдресЮр, СтрокаТаблицыФормы.АдресЮр_ВходящиеДанные);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СкопироватьСтрокуВЗначениеСтроковогоТипа(СтрокаТаблицыФормы.АдресФакт, СтрокаТаблицыФормы.АдресФакт_ВходящиеДанные);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СкопироватьСтрокуВЗначениеСтроковогоТипа(СтрокаТаблицыФормы.АдресЭП, СтрокаТаблицыФормы.АдресЭП_ВходящиеДанные);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СкопироватьСтрокуВЗначениеСтроковогоТипа(СтрокаТаблицыФормы.Телефон, СтрокаТаблицыФормы.Телефон_ВходящиеДанные);
		
		// ВидыКонтрагентов
		ЗначениеПоУмолчанию = Перечисления.ВидыКонтрагентов.ЮридическоеЛицо;
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьВидыКонтрагентов(СтрокаТаблицыФормы.ВидКонтрагента, СтрокаТаблицыФормы.ВидКонтрагента_ВходящиеДанные, ЗначениеПоУмолчанию);
		
		// Покупатель, Поставщик, ПрочиеОтношения
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.Покупатель, СтрокаТаблицыФормы.Покупатель_ВходящиеДанные);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.Поставщик, СтрокаТаблицыФормы.Поставщик_ВходящиеДанные);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВБулево(СтрокаТаблицыФормы.ПрочиеОтношения, СтрокаТаблицыФормы.ПрочиеОтношения_ВходящиеДанные);
		
		Если НЕ СтрокаТаблицыФормы.Покупатель
			И НЕ СтрокаТаблицыФормы.Поставщик
			И НЕ СтрокаТаблицыФормы.ПрочиеОтношения Тогда
			
			СтрокаТаблицыФормы.Покупатель = Истина;
			СтрокаТаблицыФормы.Поставщик = Истина;
			СтрокаТаблицыФормы.ПрочиеОтношения = Истина;
			
		КонецЕсли;
		
		// ФИО
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СкопироватьСтрокуВЗначениеСтроковогоТипа(СтрокаТаблицыФормы.ФИО, СтрокаТаблицыФормы.ФИО_ВходящиеДанные);
		
		// Пол
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьПолФизическогоЛица(СтрокаТаблицыФормы.Пол, СтрокаТаблицыФормы.Пол_ВходящиеДанные);
		
		// ДатаРождения
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВДату(СтрокаТаблицыФормы.ДатаРождения, СтрокаТаблицыФормы.ДатаРождения_ВходящиеДанные);
		
		// ДокументУдостоверяющийЛичность
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СкопироватьСтрокуВЗначениеСтроковогоТипа(СтрокаТаблицыФормы.ДокументУдостоверяющийЛичность, СтрокаТаблицыФормы.ДокументУдостоверяющийЛичность_ВходящиеДанные);
		
		// Доп. реквизиты
		Если НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты.Количество() > 0 Тогда
			
			ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьДополнительныеРеквизиты(СтрокаТаблицыФормы, НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты);
			
		КонецЕсли;
		
		ПроверитьКорректностьДанныхВСтрокеТаблицы(СтрокаТаблицыФормы);
		
		ЗагрузкаДанныхИзВнешнегоИсточника.ПрогрессСопоставленияДанных(ТаблицаСопоставленияДанных.Индекс(СтрокаТаблицыФормы), РазмерТаблицыДанных);
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ТаблицаСопоставленияДанных, АдресРезультата);
	
КонецПроцедуры

Процедура ПроверитьКорректностьДанныхВСтрокеТаблицы(СтрокаТаблицыФормы, ПолноеИмяОбъектаЗаполнения = "") Экспорт
	
	СтрокаТаблицыФормы._СтрокаСопоставлена = ЗначениеЗаполнено(СтрокаТаблицыФормы.Контрагент);
	
	ИмяСлужебногоПоля = ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна();
	
	СтрокаТаблицыФормы[ИмяСлужебногоПоля] = СтрокаТаблицыФормы._СтрокаСопоставлена
											ИЛИ (НЕ СтрокаТаблицыФормы._СтрокаСопоставлена И НЕ ПустаяСтрока(СтрокаТаблицыФормы.КонтрагентНаименование));
	
КонецПроцедуры

Процедура ОбработатьПодготовленныеДанные(СтруктураДанных, ФоновоеЗаданиеАдресХранилища = "") Экспорт
	
	НастройкиЗагрузкиДанных 	= СтруктураДанных.НастройкиЗагрузкиДанных;
	НастройкиОбновленияСвойств	= НастройкиЗагрузкиДанных.НастройкиОбновленияСвойств;
	ОбновлятьСуществующие		= НастройкиЗагрузкиДанных.ОбновлятьСуществующие;
	СоздаватьЕслиНеСопоставлено = НастройкиЗагрузкиДанных.СоздаватьЕслиНеСопоставлено;
	ТаблицаСопоставленияДанных	= СтруктураДанных.ТаблицаСопоставленияДанных;
	РазмерТаблицыДанных			= ТаблицаСопоставленияДанных.Количество();
	КоличествоЗаписейТранзакции	= 0;
	ТранзакцияОткрыта			= Ложь;
	
	Попытка
		
		Для каждого СтрокаТаблицы Из ТаблицаСопоставленияДанных Цикл
			
			Если НЕ ТранзакцияОткрыта 
				И КоличествоЗаписейТранзакции = 0 Тогда
				
				НачатьТранзакцию();
				ТранзакцияОткрыта = Истина;
				
			КонецЕсли;
			
			ЗагрузкаВПриложениеВозможна = СтрокаТаблицы[ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна()];
			
			СогласованноеСостояниеСтроки = (СтрокаТаблицы._СтрокаСопоставлена И ОбновлятьСуществующие) 
				ИЛИ (НЕ СтрокаТаблицы._СтрокаСопоставлена И СоздаватьЕслиНеСопоставлено);
			
			Если ЗагрузкаВПриложениеВозможна И СогласованноеСостояниеСтроки Тогда
				
				КоличествоЗаписейТранзакции = КоличествоЗаписейТранзакции + 1;
				
				Если СтрокаТаблицы._СтрокаСопоставлена Тогда
					
					ЭлементСправочника = СтрокаТаблицы.Контрагент.ПолучитьОбъект();
					ЗаполнитьЗначенияСвойств(ЭлементСправочника, СтрокаТаблицы, НастройкиОбновленияСвойств.ИменаПолейОбновляемые);
					
				Иначе
					
					ЭлементСправочника = Справочники.Контрагенты.СоздатьЭлемент();
					
					Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, "УИД") И ЗначениеЗаполнено(СтрокаТаблицы.УИД) Тогда
						НовыйУИД = Новый УникальныйИдентификатор(СтрокаТаблицы.УИД);
						ЭлементСправочника.УстановитьСсылкуНового(Справочники.Контрагенты.ПолучитьСсылку(НовыйУИД));
					КонецЕсли; 
					
					ЗаполнитьЗначенияСвойств(ЭлементСправочника, СтрокаТаблицы, , ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СтандартныеИменаПолейНеподлежащихОбновлению(НастройкиЗагрузкиДанных));
					
					ЭлементСправочника.Родитель = СтрокаТаблицы.Родитель;
					ЭлементСправочника.СтранаРегистрации = Справочники.СтраныМира.Россия;
					
				КонецЕсли;
				
				ЭлементСправочника.Наименование = СтрокаТаблицы.КонтрагентНаименование;
				ЭлементСправочника.НаименованиеПолное = СтрокаТаблицы.КонтрагентНаименование;
				
				Если НЕ ПустаяСтрока(СтрокаТаблицы.ИНН_КПП) Тогда
					
					Разделители = Новый Массив;
					Разделители.Добавить("/");
					Разделители.Добавить("\");
					Разделители.Добавить("-");
					Разделители.Добавить("|");
					
					ИНН = "";
					КПП = "";
					
					Для каждого ЗначениеРазделителя Из Разделители Цикл
						
						ПозицияРазделителя = Найти(СтрокаТаблицы.ИНН_КПП, ЗначениеРазделителя);
						Если ПозицияРазделителя = 0 Тогда 
							
							Продолжить;
							
						КонецЕсли;
						
						ИНН = Лев(СтрокаТаблицы.ИНН_КПП, ПозицияРазделителя - 1);
						КПП = Сред(СтрокаТаблицы.ИНН_КПП, ПозицияРазделителя + 1);
						
					КонецЦикла;
					
					Если ПустаяСтрока(ИНН) И ПустаяСтрока(КПП) Тогда
						
						ИНН = СтрокаТаблицы.ИНН_КПП;
						
					КонецЕсли;
					
					ЭлементСправочника.ИНН = ИНН;
					ЭлементСправочника.КПП = КПП;
					
				КонецЕсли;
				
				Если НЕ ПустаяСтрока(СтрокаТаблицы.АдресЮр) Тогда
					
					ДобавитьКИ(ЭлементСправочника, СтрокаТаблицы.АдресЮр, Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
					
				КонецЕсли;
				
				Если НЕ ПустаяСтрока(СтрокаТаблицы.АдресФакт) Тогда
					
					ДобавитьКИ(ЭлементСправочника, СтрокаТаблицы.АдресФакт, Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
					
				КонецЕсли;
				
				Если НЕ ПустаяСтрока(СтрокаТаблицы.Телефон) Тогда
					
					ДобавитьКИ(ЭлементСправочника, СтрокаТаблицы.Телефон, Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
					
				КонецЕсли;
				
				Если НЕ ПустаяСтрока(СтрокаТаблицы.АдресЭП) Тогда
					
					ДобавитьКИ(ЭлементСправочника, СтрокаТаблицы.АдресЭП, Справочники.ВидыКонтактнойИнформации.EmailКонтрагента);
					
				КонецЕсли;
				
				Если НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты.Количество() > 0 Тогда
					
					ЗагрузкаДанныхИзВнешнегоИсточника.ОбработатьВыбранныеДополнительныеРеквизиты(ЭлементСправочника, СтрокаТаблицы._СтрокаСопоставлена, СтрокаТаблицы, НастройкиЗагрузкиДанных.ВыбранныеДополнительныеРеквизиты);
					
				КонецЕсли;
				
				ЭлементСправочника.Записать();
				
			КонецЕсли;
			
			ИндексТекущейстроки = ТаблицаСопоставленияДанных.Индекс(СтрокаТаблицы);
			ТекстПрогресса      = СтрШаблон(НСтр("ru ='Обработано %1 из %2 строк...'"), ИндексТекущейстроки, РазмерТаблицыДанных);
			ПроцентВыполнения   = Окр(ИндексТекущейстроки * 100 / РазмерТаблицыДанных);
			
			ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения, ТекстПрогресса);
			
			Если ТранзакцияОткрыта
				И КоличествоЗаписейТранзакции > ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.МаксимумЗаписейВОднойТранзакции() Тогда
				
				ЗафиксироватьТранзакцию();
				ТранзакцияОткрыта = Ложь;
				КоличествоЗаписейТранзакции = 0;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ТранзакцияОткрыта 
			И КоличествоЗаписейТранзакции > 0 Тогда
			
			ЗафиксироватьТранзакцию();
			ТранзакцияОткрыта = Ложь;
			
		КонецЕсли;
		
	Исключение
		
		ЗаписьЖурналаРегистрации(Нстр("ru='Загрузка данных'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Контрагенты, , ОписаниеОшибки());
		ОтменитьТранзакцию();
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ИнтерфейсПечати

Процедура ЗаполнитьПараметрыОбласти(ИмяОбласти, ЗначенияПараметров, СтруктураПечати, ВыводитьЛоготип = Ложь)
	
	Перем ОбластьНоменклатура, ОбластьХарактеристика, ОбластьЦена;
	
	СтруктураПечати.СтруктураОбластей.Свойство("Область" + ИмяОбласти + "Номенклатура",		ОбластьНоменклатура);
	СтруктураПечати.СтруктураОбластей.Свойство("Область" + ИмяОбласти + "Характеристика",	ОбластьХарактеристика);
	СтруктураПечати.СтруктураОбластей.Свойство("Область" + ИмяОбласти + "Цена",				ОбластьЦена);
	
	ОбластьНоменклатура.Параметры.Заполнить(ЗначенияПараметров);
	
	Если ВыводитьЛоготип Тогда
		
		ОбластьНоменклатура.Рисунки.Логотип.Картинка = ЗначенияПараметров.Картинка;
		
	КонецЕсли;
	
	СтруктураПечати.ТабличныйДокумент.Вывести(ОбластьНоменклатура);
	
	Если СтруктураПечати.ИспользоватьХарактеристики Тогда
		
		ОбластьХарактеристика.Параметры.Заполнить(ЗначенияПараметров);
		СтруктураПечати.ТабличныйДокумент.Присоединить(ОбластьХарактеристика);
		
	КонецЕсли;
	
	ОбластьЦена.Параметры.Заполнить(ЗначенияПараметров);
	СтруктураПечати.ТабличныйДокумент.Присоединить(ОбластьЦена);
	
КонецПроцедуры

Процедура ЗаполнитьЗаголовокПрайсЛиста(ИмяОбласти, СтруктураПечати)
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("Заголовок", "Прайс-лист");
	
	ЗаполнитьПараметрыОбласти(ИмяОбласти, ЗначенияПараметров, СтруктураПечати);
	
КонецПроцедуры

Процедура ЗаполнитьОтправителяПрайсЛиста(СтруктураПечати)
	
	ОрганизацияПоУмолчанию = ПолучитьОрганизациюПоУмолчанию(СтруктураПечати.Контрагент);
	
	СведенияООтправителе = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ОрганизацияПоУмолчанию, ТекущаяДатаСеанса());
	
	ЗначенияПараметров = 
		Новый Структура("Отправитель, ОтправительАдрес, ОтправительТелефон, ОтправительФакс, ОтправительEmail",
			СведенияООтправителе.Представление,
			СведенияООтправителе.ФактическийАдрес,
			СведенияООтправителе.Телефоны,
			СведенияООтправителе.Факс,
			СведенияООтправителе.ЭлектроннаяПочта
			);
			
	ВыводитьЛоготип = Ложь;
	ИмяОбласти 		= "ОтправительБезЛоготипа";
	
	Если ЗначениеЗаполнено(ОрганизацияПоУмолчанию.ФайлЛоготип) Тогда
		
		ДанныеКартинки = РаботаСФайлами.ДвоичныеДанныеФайла(ОрганизацияПоУмолчанию.ФайлЛоготип);
		Если ЗначениеЗаполнено(ДанныеКартинки) Тогда
			
			ВыводитьЛоготип = Истина;
			ИмяОбласти		= "ОтправительСЛоготипом";
			ЗначенияПараметров.Вставить("Картинка", Новый Картинка(ДанныеКартинки));
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьПараметрыОбласти(ИмяОбласти, ЗначенияПараметров, СтруктураПечати, ВыводитьЛоготип);
	
КонецПроцедуры

Процедура ЗаполнитьСформированоПрайсЛиста(ИмяОбласти, СтруктураПечати)
	
	ЗначенияПараметров = 
		Новый Структура("Сформировано",
			"Сформировано " + Формат(ТекущаяДатаСеанса(),"ДФ=dd.MM.yyyy")
			);
	
	ЗаполнитьПараметрыОбласти(ИмяОбласти, ЗначенияПараметров, СтруктураПечати);
	
КонецПроцедуры

Процедура ЗаполнитьШапкуПрайсЛиста(ИмяОбласти, СтруктураПечати)
	
	ВидЦен = ПолучитьВидЦенПоУмолчанию(СтруктураПечати.Контрагент);
	
	ЗначенияПараметров = 
		Новый Структура("АртикулКод, ВидЦен, Цена",
			?(Константы.ПрайсЛистВыводитьКод.Получить() = Перечисления.ДаНет.Да, "Код", "Артикул"),
			ВидЦен,
			"Цена (" + ВидЦен.ВалютаЦены + ")"
			);
	
	ЗаполнитьПараметрыОбласти(ИмяОбласти, ЗначенияПараметров, СтруктураПечати);
	
КонецПроцедуры

Процедура ЗаполнитьДеталиПрайсЛистаЦеноваяГруппа(СтруктураПечати)
	
	Запрос = Новый Запрос(ПолучитьТекстЗапросаДляПечатиПрайсЛистаЦеноваяГруппа());
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ВидЦен", ПолучитьВидЦенПоУмолчанию(СтруктураПечати.Контрагент));
	Запрос.УстановитьПараметр("ВыводитьКод", Константы.ПрайсЛистВыводитьКод.Получить());
	Запрос.УстановитьПараметр("ВыводитьПолноеНаменование", Константы.ПрайсЛистВыводитьПолноеНаименование.Получить());
	
	ЗначенияПараметров = 
		Новый Структура("ЦеноваяГруппа, АртикулКод, ПредставлениеНоменклатуры, Номенклатура, Характеристика, ЕдиницаИзмерения, Цена");
	
	ВыборкаЦеновыеГруппы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Пока ВыборкаЦеновыеГруппы.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(ЗначенияПараметров, ВыборкаЦеновыеГруппы);
		ЗаполнитьПараметрыОбласти("ЦеноваяГруппа", ЗначенияПараметров, СтруктураПечати);
		
		ВыборкаДетали = ВыборкаЦеновыеГруппы.Выбрать();
		Пока ВыборкаДетали.Следующий() Цикл
			
			ЗаполнитьЗначенияСвойств(ЗначенияПараметров, ВыборкаДетали);
			ЗаполнитьПараметрыОбласти("Детали", ЗначенияПараметров, СтруктураПечати);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДеталиПрайсЛистаИерархияНоменклатуры(СтруктураПечати, Выборка, ВывестиНоменклатуруБезРодителя = Ложь)
	
	ЗначенияПараметров = 
		Новый Структура("ЦеноваяГруппа, АртикулКод, ПредставлениеНоменклатуры, Номенклатура, Характеристика, ЕдиницаИзмерения, Цена");
	
	Если ВывестиНоменклатуруБезРодителя Тогда
		
		ЗначенияПараметров.ЦеноваяГруппа = НСтр("ru = '<...>'");
		ЗаполнитьПараметрыОбласти("ЦеноваяГруппа", ЗначенияПараметров, СтруктураПечати);
		ВывестиПустойРодитель = Ложь;
		СтруктураПечати.ТабличныйДокумент.НачатьГруппуСтрок();
		
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		// Сложные условия в операторе "Если" необходимы, для того, что бы номенклатура без родителя выводилась первой в прайс-листе
		Если Выборка.ЭтоГруппа 
			И НЕ ВывестиНоменклатуруБезРодителя Тогда
			
			ЗначенияПараметров.ЦеноваяГруппа = Выборка.Номенклатура;
			ЗаполнитьПараметрыОбласти("ЦеноваяГруппа", ЗначенияПараметров, СтруктураПечати);
			
			СтруктураПечати.ТабличныйДокумент.НачатьГруппуСтрок();
			ЗаполнитьДеталиПрайсЛистаИерархияНоменклатуры(СтруктураПечати, Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией));
			СтруктураПечати.ТабличныйДокумент.ЗакончитьГруппуСтрок();
			
		ИначеЕсли НЕ Выборка.ЭтоГруппа 
			И (ВывестиНоменклатуруБезРодителя 
				ИЛИ ЗначениеЗаполнено(Выборка.Родитель)) Тогда
			
			ЗаполнитьЗначенияСвойств(ЗначенияПараметров, Выборка);
			ЗаполнитьПараметрыОбласти("Детали", ЗначенияПараметров, СтруктураПечати);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВывестиНоменклатуруБезРодителя Тогда
		
		СтруктураПечати.ТабличныйДокумент.ЗакончитьГруппуСтрок();
		Выборка.Сбросить();
		ЗаполнитьДеталиПрайсЛистаИерархияНоменклатуры(СтруктураПечати, Выборка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыбратьДанныеПрайсЛистаИерархияНоменклатуры(СтруктураПечати)
	
	Запрос = Новый Запрос(ПолучитьТекстЗапросаДляПечатиПрайсЛистаИерархияНоменклатуры());
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ВидЦен", ПолучитьВидЦенПоУмолчанию(СтруктураПечати.Контрагент));
	Запрос.УстановитьПараметр("ВыводитьКод", Константы.ПрайсЛистВыводитьКод.Получить());
	Запрос.УстановитьПараметр("ВыводитьПолноеНаменование", Константы.ПрайсЛистВыводитьПолноеНаименование.Получить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ЗаполнитьДеталиПрайсЛистаИерархияНоменклатуры(СтруктураПечати, РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией), Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает структуру областей макета для формирования прайс-листа
//
Функция ЗаполнитьСтрутуруОбластейМакета(Макет)
	
	СтруктураОбластей = Новый Структура;
	
	СтруктураОбластей.Вставить("ОбластьОтступНоменклатура",			Макет.ПолучитьОбласть("Отступ|Номенклатура"));
	СтруктураОбластей.Вставить("ОбластьОтступХарактеристика",		Макет.ПолучитьОбласть("Отступ|Характеристика"));
	СтруктураОбластей.Вставить("ОбластьОтступЦена",					Макет.ПолучитьОбласть("Отступ|Цена"));
	
	СтруктураОбластей.Вставить("ОбластьЗаголовокНоменклатура",		Макет.ПолучитьОбласть("Заголовок|Номенклатура"));
	СтруктураОбластей.Вставить("ОбластьЗаголовокХарактеристика",	Макет.ПолучитьОбласть("Заголовок|Характеристика"));
	СтруктураОбластей.Вставить("ОбластьЗаголовокЦена",				Макет.ПолучитьОбласть("Заголовок|Цена"));
	
	СтруктураОбластей.Вставить("ОбластьОтправительБезЛоготипаНоменклатура",		Макет.ПолучитьОбласть("ОтправительБезЛоготипа|Номенклатура"));
	СтруктураОбластей.Вставить("ОбластьОтправительБезЛоготипаХарактеристика",	Макет.ПолучитьОбласть("ОтправительБезЛоготипа|Характеристика"));
	СтруктураОбластей.Вставить("ОбластьОтправительБезЛоготипаЦена",				Макет.ПолучитьОбласть("ОтправительБезЛоготипа|Цена"));
	
	СтруктураОбластей.Вставить("ОбластьОтправительСЛоготипомНоменклатура",		Макет.ПолучитьОбласть("ОтправительСЛоготипом|Номенклатура"));
	СтруктураОбластей.Вставить("ОбластьОтправительСЛоготипомХарактеристика",	Макет.ПолучитьОбласть("ОтправительСЛоготипом|Характеристика"));
	СтруктураОбластей.Вставить("ОбластьОтправительСЛоготипомЦена",				Макет.ПолучитьОбласть("ОтправительСЛоготипом|Цена"));
	
	СтруктураОбластей.Вставить("ОбластьСформированоНоменклатура",	Макет.ПолучитьОбласть("Сформировано|Номенклатура"));
	СтруктураОбластей.Вставить("ОбластьСформированоХарактеристика",	Макет.ПолучитьОбласть("Сформировано|Характеристика"));
	СтруктураОбластей.Вставить("ОбластьСформированоЦена",			Макет.ПолучитьОбласть("Сформировано|Цена"));
	
	СтруктураОбластей.Вставить("ОбластьШапкаТаблицыНоменклатура",	Макет.ПолучитьОбласть("ШапкаТаблицы|Номенклатура"));
	СтруктураОбластей.Вставить("ОбластьШапкаТаблицыХарактеристика",	Макет.ПолучитьОбласть("ШапкаТаблицы|Характеристика"));
	СтруктураОбластей.Вставить("ОбластьШапкаТаблицыЦена",			Макет.ПолучитьОбласть("ШапкаТаблицы|Цена"));
	
	СтруктураОбластей.Вставить("ОбластьЦеноваяГруппаНоменклатура",	Макет.ПолучитьОбласть("ЦеноваяГруппа|Номенклатура"));
	СтруктураОбластей.Вставить("ОбластьЦеноваяГруппаХарактеристика",Макет.ПолучитьОбласть("ЦеноваяГруппа|Характеристика"));
	СтруктураОбластей.Вставить("ОбластьЦеноваяГруппаЦена",			Макет.ПолучитьОбласть("ЦеноваяГруппа|Цена"));
	
	СтруктураОбластей.Вставить("ОбластьДеталиНоменклатура",			Макет.ПолучитьОбласть("Детали|Номенклатура"));
	СтруктураОбластей.Вставить("ОбластьДеталиХарактеристика",		Макет.ПолучитьОбласть("Детали|Характеристика"));
	СтруктураОбластей.Вставить("ОбластьДеталиЦена",					Макет.ПолучитьОбласть("Детали|Цена"));
	
	Возврат СтруктураОбластей;
	
КонецФункции //ЗаполнитьСтрутуруОбластейМакета()

// Процедура формирования прайс-листа
//
Функция СформироватьПрайсЛист(МассивОбъектов, ОбъектыПечати)
	
	ИспользоватьИерархиюНоменклатуры = Константы.ПрайсЛистИспользоватьИерархиюНоменклатуры.Получить();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПрайсЛист";
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПрайсЛист";
	
	СтруктураОбластей = ЗаполнитьСтрутуруОбластейМакета(УправлениеПечатью.МакетПечатнойФормы("Справочник.Контрагенты.ПФ_MXL_ПрайсЛист"));
	
	НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
	ИспользоватьХарактеристики = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики");
	
	СтруктураПечати =
		Новый Структура("ТабличныйДокумент, СтруктураОбластей, Контрагент, ИспользоватьХарактеристики",
			ТабличныйДокумент,
			СтруктураОбластей,
			МассивОбъектов,
			ИспользоватьХарактеристики);
			
	ИмяСекцииОтправителя = ?(ЗначениеЗаполнено(ПолучитьОрганизациюПоУмолчанию(СтруктураПечати.Контрагент).ФайлЛоготип), "ОтправительСЛоготипом", "ОтправительБезЛоготипа");
			
	// Заполним прайс-лист по секционно
	ЗаполнитьЗаголовокПрайсЛиста("Заголовок", СтруктураПечати);
	ЗаполнитьОтправителяПрайсЛиста(СтруктураПечати); // секция определяется динамически
	ЗаполнитьСформированоПрайсЛиста("Сформировано", СтруктураПечати);
	ЗаполнитьШапкуПрайсЛиста("ШапкаТаблицы", СтруктураПечати);
	
	Если ИспользоватьИерархиюНоменклатуры Тогда
		
		ВыбратьДанныеПрайсЛистаИерархияНоменклатуры(СтруктураПечати); // Вывод областей "Группа номенклатуры" и "Детали"
		
	Иначе
		
		ЗаполнитьДеталиПрайсЛистаЦеноваяГруппа(СтруктураПечати); // Вывод областей "ЦеноваяГруппа" и "Детали"
		
	КонецЕсли;
	
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, МассивОбъектов.Ссылка);
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // СформироватьПрайсЛист()

// Функция вызывает процедуру печати прайс-листа для контрагента
// 
//
Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати)
	
	Возврат СформироватьПрайсЛист(МассивОбъектов[0], ОбъектыПечати);
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//	ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//	МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//	ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//	КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//	ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПрайсЛист") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПрайсЛист", "Прайс-лист", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
	// Заполним получателей прайс-листа
	Получатели = Новый СписокЗначений;
	Получатели.Добавить(МассивОбъектов[0]);
	
	МассивПолучателей = Новый Массив;
	МассивПолучателей.Добавить(МассивОбъектов[0]);
	
	ПараметрыВывода.ПараметрыОтправки.Получатель = Получатели;
	ПараметрыВывода.ПараметрыОтправки.Тема = "Прайс-лист """ + ПолучитьОрганизациюПоУмолчанию(МассивОбъектов[0]).Наименование + """ от " + ТекущаяДатаСеанса() + ". Сформировал " + Пользователи.АвторизованныйПользователь() + ".";
	УправлениеНебольшойФирмойСервер.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивПолучателей, КоллекцияПечатныхФорм);
	
КонецПроцедуры // Печать()

// Заполняет список команд печати Заказа покупателя
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "КонтактнаяИнформация";
	КомандаПечати.Представление = НСтр("ru = 'Контактная информация'");
	КомандаПечати.Порядок = 1;
	КомандаПечати.Обработчик = "ПечатьДокументовУНФКлиент.ПечатьКонтактнойИнформации";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Конверт";
	КомандаПечати.Представление = НСтр("ru = 'Конверт'");
	КомандаПечати.Обработчик = "ПечатьДокументовУНФКлиент.ПечатьКонверта";
	КомандаПечати.Порядок = 4;
	
	ШаблоныПечатиОфисныхДокументов.ДобавитьКомандыПечати(
	Перечисления.НазначенияШаблоновПечатиОфисныхДокументов.ДоговорКонтрагента,
	КомандыПечати, "ФормаЭлемента,ФормаСписка", "КомандыПечатиДоговорКонтрагента", 99);
	
КонецПроцедуры

#КонецОбласти

#Область ШаблоныСообщений

// Вызывается при подготовке шаблонов сообщений и позволяет переопределить список реквизитов и вложений.
//
// Параметры:
//  Реквизиты               - ДеревоЗначений - список реквизитов шаблона.
//         ** Имя            - Строка - Уникальное имя общего реквизита.
//         ** Представление  - Строка - Представление общего реквизита.
//         ** Тип            - Тип    - Тип реквизита. По умолчанию строка.
//         ** Формат         - Строка - Формат вывода значения для чисел, дат, строк и булевых значений.
//  Вложения                - ТаблицаЗначений - печатные формы и вложения
//         ** Имя            - Строка - Уникальное имя вложения.
//         ** Представление  - Строка - Представление варианта.
//         ** ТипФайла       - Строка - Тип вложения, который соответствует расширению файла: "pdf", "png", "jpg", mxl"
//                                      и др.
//  ДополнительныеПараметры - Структура - дополнительные сведения о шаблоне сообщений.
//
Процедура ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, ДополнительныеПараметры) Экспорт
	
	
КонецПроцедуры

// Вызывается в момент создания сообщений по шаблону для заполнения значений реквизитов и вложений.
//
// Параметры:
//  Сообщение - Структура - структура с ключами:
//    * ЗначенияРеквизитов - Соответствие - список используемых в шаблоне реквизитов.
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * ЗначенияОбщихРеквизитов - Соответствие - список используемых в шаблоне общих реквизитов.
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * Вложения - Соответствие - значения реквизитов 
//      ** Ключ     - Строка - имя вложения в шаблоне;
//      ** Значение - ДвоичныеДанные, Строка - двоичные данные или адрес во временном хранилище вложения.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//  ДополнительныеПараметры - Структура -  Дополнительная информация о шаблоне сообщения.
//
Процедура ПриФормированииСообщения(Сообщение, ПредметСообщения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Заполняет список получателей SMS при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиSMS - ТаблицаЗначений - список получается SMS.
//     * НомерТелефона - Строка - номер телефона, куда будет отправлено сообщение SMS.
//     * Представление - Строка - представление получателя сообщения SMS.
//     * Контакт       - Произвольный - контакт, которому принадлежит номер телефона.
//  ПредметСообщения - ЛюбаяСсылка, Структура - ссылка на объект являющийся источником данных, либо структура,
//    * Предмет               - ЛюбаяСсылка - ссылка на объект являющийся источником данных
//    * ПроизвольныеПараметры - Соответствие - заполненный список произвольных параметров.//
//
Процедура ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, ПредметСообщения) Экспорт
	
КонецПроцедуры

// Заполняет список получателей письма при отправки сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиПисьма - ТаблицаЗначений - список получается письма.
//     * Адрес           - Строка - адрес электронной почты получателя.
//     * Представление   - Строка - представление получается письма.
//     * Контакт         - Произвольный - контакт, которому принадлежит адрес электрнной почты.
//  ПредметСообщения - ЛюбаяСсылка, Структура - ссылка на объект являющийся источником данных, либо структура,
//                                              если шаблон содержит произвольные параметры:
//    * Предмет               - ЛюбаяСсылка - ссылка на объект являющийся источником данных
//    * ПроизвольныеПараметры - Соответствие - заполненный список произвольных параметров.
//
Процедура ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, ПредметСообщения) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область КонтактнаяИнформацияУНФ

Процедура ЗаполнитьДанныеПанелиКонтактнаяИнформация(ВладелецКИ, ДанныеПанелиКонтактнойИнформации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактнаяИнформация.Ссылка КАК Ссылка,
	|	КонтактнаяИнформация.Тип КАК Тип,
	|	КонтактнаяИнформация.Вид КАК Вид,
	|	КонтактнаяИнформация.Представление КАК Представление,
	|	КонтактнаяИнформация.ЗначенияПолей КАК ЗначенияПолей,
	|	КонтактнаяИнформация.Значение КАК Значение
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Ссылка = &Контрагент";
	
	Запрос.УстановитьПараметр("Контрагент", ВладелецКИ);
	ДанныеКИ = Запрос.Выполнить().Выбрать();
	
	Пока ДанныеКИ.Следующий() Цикл
		НоваяСтрока = ДанныеПанелиКонтактнойИнформации.Добавить();
		Комментарий = УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(ДанныеКИ.Значение);
		НоваяСтрока.Отображение = Строка(ДанныеКИ.Вид) + ": " + ДанныеКИ.Представление + ?(ПустаяСтрока(Комментарий), "", ", " + Комментарий);
		НоваяСтрока.ИндексПиктограммы = КонтактнаяИнформацияПанельУНФ.ИндексПиктограммыПоТипу(ДанныеКИ.Тип);
		НоваяСтрока.ТипОтображаемыхДанных = "ЗначениеКИ";
		НоваяСтрока.ВладелецКИ = ВладелецКИ;
		НоваяСтрока.ПредставлениеКИ = ДанныеКИ.Представление;
		НоваяСтрока.ТипКИ = ДанныеКИ.Тип;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПорядокТиповКИ.Тип КАК Тип,
		|	ПорядокТиповКИ.Порядок КАК Порядок
		|ПОМЕСТИТЬ втПорядокТиповКИ
		|ИЗ
		|	&ПорядокТиповКИ КАК ПорядокТиповКИ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КонтактныеЛица.Ссылка КАК КонтактноеЛицо,
		|	КонтактныеЛица.Наименование КАК Наименование,
		|	СвязиКонтрагентКонтакт.Должность КАК Должность,
		|	ВЫБОР
		|		КОГДА КонтактныеЛица.Ссылка = &ОсновноеКонтактноеЛицо
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ПорядокКонтактов
		|ПОМЕСТИТЬ втКонтакты
		|ИЗ
		|	Справочник.КонтактныеЛица КАК КонтактныеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвязиКонтрагентКонтакт.СрезПоследних(, Контрагент = &Владелец) КАК СвязиКонтрагентКонтакт
		|		ПО (СвязиКонтрагентКонтакт.Контакт = КонтактныеЛица.Ссылка)
		|ГДЕ
		|	КонтактныеЛица.ПометкаУдаления = ЛОЖЬ
		|	И КонтактныеЛица.Недействителен = ЛОЖЬ
		|	И СвязиКонтрагентКонтакт.СвязьНедействительна = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втКонтакты.КонтактноеЛицо КАК КонтактноеЛицо,
		|	втКонтакты.Наименование КАК Наименование,
		|	втКонтакты.Должность КАК Должность
		|ИЗ
		|	втКонтакты КАК втКонтакты
		|
		|УПОРЯДОЧИТЬ ПО
		|	втКонтакты.ПорядокКонтактов,
		|	Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КонтактныеЛицаКонтактнаяИнформация.Ссылка КАК КонтактноеЛицо,
		|	КонтактныеЛицаКонтактнаяИнформация.Тип КАК Тип,
		|	КонтактныеЛицаКонтактнаяИнформация.Вид КАК Вид,
		|	КонтактныеЛицаКонтактнаяИнформация.Представление КАК Представление,
		|	КонтактныеЛицаКонтактнаяИнформация.Значение КАК Значение,
		|	ПРЕДСТАВЛЕНИЕ(КонтактныеЛицаКонтактнаяИнформация.Вид) КАК ПредставлениеВидаКИ
		|ИЗ
		|	Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокТиповКИ КАК втПорядокТиповКИ
		|		ПО КонтактныеЛицаКонтактнаяИнформация.Тип = втПорядокТиповКИ.Тип
		|ГДЕ
		|	КонтактныеЛицаКонтактнаяИнформация.Ссылка В
		|			(ВЫБРАТЬ
		|				втКонтакты.КонтактноеЛицо
		|			ИЗ
		|				втКонтакты)
		|
		|УПОРЯДОЧИТЬ ПО
		|	втПорядокТиповКИ.Порядок,
		|	КонтактныеЛицаКонтактнаяИнформация.Вид.Наименование";
	
	Запрос.УстановитьПараметр("Владелец", ВладелецКИ);
	Запрос.УстановитьПараметр("ОсновноеКонтактноеЛицо", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецКИ, "КонтактноеЛицо"));
	Запрос.УстановитьПараметр("ПорядокТиповКИ", КонтактнаяИнформацияУНФ.ПорядокТиповКИ());
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаКонтакты = МассивРезультатов[2].Выбрать();
	ВыборкаКонтактнаяИнформация = МассивРезультатов[3].Выбрать();
	ОтборКИ = Новый Структура("КонтактноеЛицо");
	
	Пока ВыборкаКонтакты.Следующий() Цикл
		
		НоваяСтрока = ДанныеПанелиКонтактнойИнформации.Добавить();
		НоваяСтрока.Отображение = Строка(ВыборкаКонтакты.КонтактноеЛицо);
		НоваяСтрока.ИндексПиктограммы = -1;
		НоваяСтрока.ТипОтображаемыхДанных = "КонтактноеЛицо";
		НоваяСтрока.ВладелецКИ = ВыборкаКонтакты.КонтактноеЛицо;
		
		ВыборкаКонтактнаяИнформация.Сбросить();
		ОтборКИ.КонтактноеЛицо = ВыборкаКонтакты.КонтактноеЛицо;
		
		Пока ВыборкаКонтактнаяИнформация.НайтиСледующий(ОтборКИ) Цикл
			
			НоваяСтрока = ДанныеПанелиКонтактнойИнформации.Добавить();
			Комментарий = УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(ВыборкаКонтактнаяИнформация.Значение);
			НоваяСтрока.Отображение = Строка(ВыборкаКонтактнаяИнформация.Вид) + ": " +ВыборкаКонтактнаяИнформация.Представление + ?(ПустаяСтрока(Комментарий), "", ", " + Комментарий);
			НоваяСтрока.ИндексПиктограммы = КонтактнаяИнформацияПанельУНФ.ИндексПиктограммыПоТипу(ВыборкаКонтактнаяИнформация.Тип);
			НоваяСтрока.ТипОтображаемыхДанных = "ЗначениеКИ";
			НоваяСтрока.ВладелецКИ = ВыборкаКонтакты.КонтактноеЛицо;
			НоваяСтрока.ПредставлениеКИ = ВыборкаКонтактнаяИнформация.Представление;
			НоваяСтрока.ТипКИ = ВыборкаКонтактнаяИнформация.Тип;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли