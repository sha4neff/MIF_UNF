
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.ЗначенияЗаполнения.Свойство("ОбъектЗагрузки") Тогда
		Объект.ОбъектЗагрузки = Справочники.ИдентификаторыОбъектовМетаданных.НайтиПоНаименованию(Параметры.ЗначенияЗаполнения.ОбъектЗагрузки);
	КонецЕсли;
	
	Если Параметры.ЗначенияЗаполнения.Свойство("Наименование") Тогда
		Объект.Наименование = Параметры.ЗначенияЗаполнения.Наименование;
	КонецЕсли;  
	
	Если Параметры.ЗначенияЗаполнения.Свойство("МаппингПолей") Тогда
		Для каждого строка Из Параметры.ЗначенияЗаполнения.МаппингПолей Цикл
			НоваяСтрока = Объект.МаппингПолей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, строка);
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеМетоды

&НаКлиенте
Процедура ПоляМаппингаВТабличнуюЧасть(ДобавляемыеПоля)
	
	ПараметрыОтбора = Новый Структура;
	Для каждого ЭлементСоответствия Из ДобавляемыеПоля Цикл
		
		ИмяПоляОтбора = ?(ТипЗнч(ЭлементСоответствия.Ключ) = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"), "ДополнительныйРеквизит", "ИмяПоля");
		
		ПараметрыОтбора.Очистить();
		ПараметрыОтбора.Вставить(ИмяПоляОтбора, ЭлементСоответствия.Ключ);
		
		Если Объект.МаппингПолей.НайтиСтроки(ПараметрыОтбора).Количество() > 0 Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		НоваяСтрока = Объект.МаппингПолей.Добавить();
		НоваяСтрока[ИмяПоляОтбора] = ЭлементСоответствия.Ключ;
		НоваяСтрока.ПредставлениеПоля = ЭлементСоответствия.Значение;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДоступныеПоляЗагрузкиДанныхИзВнешнегоИсточника()
	
	Объект.МаппингПолей.Очистить();
	
	ДоступныеПоляЗагрузки = Справочники.МаппингПолейЗагрузкиДанныхИзВнешнегоИсточника.ДоступныеПоляЗагрузкиДанныхИзВнешнегоИсточника(Объект.ОбъектЗагрузки, "", Истина);
	Для каждого ЭлементСтруктуры Из ДоступныеПоляЗагрузки Цикл
		
		НоваяСтрока = Объект.МаппингПолей.Добавить();
		НоваяСтрока.ИмяПоля = ЭлементСтруктуры.Ключ;
		НоваяСтрока.ПредставлениеПоля = ЭлементСтруктуры.Значение;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ЭлементыФормы

&НаКлиенте
Процедура ОбъектЗагрузкиПриИзменении(Элемент)
	
	Объект.МаппингПолей.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура ДобавитьПоле(Команда)
	
	ВыбранныеПоля = Новый Массив;
	Для каждого СтрокаТаблицы Из Объект.МаппингПолей Цикл
		
		ВыбранныеПоля.Добавить(?(ПустаяСтрока(СтрокаТаблицы.ИмяПоля), СтрокаТаблицы.ДополнительныйРеквизит, СтрокаТаблицы.ИмяПоля));
		
	КонецЦикла;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВыбранныеПоля", ВыбранныеПоля);
	ПараметрыОткрытия.Вставить("ОбъектЗагрузки", Объект.ОбъектЗагрузки);
	ПараметрыОткрытия.Вставить("ИмяТабличнойЧасти", Объект.ИмяТабличнойЧасти);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаДобавленныхПолей", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.МаппингПолейЗагрузкиДанныхИзВнешнегоИсточника.Форма.ФормаПодбораПолей", ПараметрыОткрытия, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаДобавленныхПолей(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Соответствие") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПоляМаппингаВТабличнуюЧасть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПустую(Команда)
	
	НоваяСтрока = Объект.МаппингПолей.Добавить();
	НоваяСтрока.ИмяПоля = Неопределено;
	НоваяСтрока.ПредставлениеПоля = НСтр("ru ='Не используется'");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоля(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаЗаполненияПолей", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru ='Табличная часть будет очищена и заполнена заново. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаполненияПолей(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ДоступныеПоляЗагрузкиДанныхИзВнешнегоИсточника();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти