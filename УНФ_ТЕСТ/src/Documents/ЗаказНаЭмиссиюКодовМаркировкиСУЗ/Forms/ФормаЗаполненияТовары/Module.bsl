#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеДоступностьюЭлементовФорм(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидПродукции = Параметры.ВидПродукции;
	Наименование = Параметры.Наименование;
	
	СобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	СтруктураРезультата = Новый Структура();
	
	СтруктураРезультата.Вставить("ПолноеОписаниеОстатков", ПолноеОписаниеОстатков);
	
	Если ЗаполнятьЦелевойПол Тогда
		СтруктураРезультата.Вставить("ЦелевойПол", ЦелевойПол);
	КонецЕсли;
	
	Если ЗаполнятьСпособВводаВОборот Тогда
		СтруктураРезультата.Вставить("СпособВводаВОборот", СпособВводаВОборот);
	КонецЕсли;
	
	Если ЗаполнятьТоварныйЗнак Тогда
		СтруктураРезультата.Вставить("ТоварныйЗнак", ТоварныйЗнак);
	КонецЕсли;
	
	Если ЗаполнятьМодель Тогда
		СтруктураРезультата.Вставить("Модель", Модель);
	КонецЕсли;
	
	Если ЗаполнятьВозрастнуюКатегорию Тогда
		СтруктураРезультата.Вставить("ВозрастнаяКатегория", ВозрастнаяКатегория);
	КонецЕсли;
	
	Если ЗаполнятьНаименование Тогда
		СтруктураРезультата.Вставить("Наименование", Наименование);
	КонецЕсли;
	
	Закрыть(СтруктураРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФорм

&НаКлиенте
Процедура ЗаполнятьЦелевойПолПриИзменении(Элемент)
	УправлениеДоступностьюЭлементовФорм(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнятьМодельПриИзменении(Элемент)
	УправлениеДоступностьюЭлементовФорм(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнятьТоварныйЗнакПриИзменении(Элемент)
	УправлениеДоступностьюЭлементовФорм(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнятьВозрастнуюКатегориюПриИзменении(Элемент)
	УправлениеДоступностьюЭлементовФорм(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнятьСпосовВводаВОборотПриИзменении(Элемент)
	УправлениеДоступностьюЭлементовФорм(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПолноеОписаниеОстатковПриИзменении(Элемент)
	
	Если ПолноеОписаниеОстатков Тогда
		ЗаполнятьЦелевойПол         = Ложь;
		ЗаполнятьСпособВводаВОборот = Ложь;
		СпособВводаВОборот          = Неопределено;
		ЦелевойПол                  = Неопределено;
	КонецЕсли;
	
	УправлениеДоступностьюЭлементовФорм(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнятьНаименованиеПриИзменении(Элемент)
	
	УправлениеДоступностьюЭлементовФорм(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностьюЭлементовФорм(Форма)
	
	Форма.Элементы.ГруппаЦелевойПол.ТолькоПросмотр         = Форма.ПолноеОписаниеОстатков;
	Форма.Элементы.ГруппаСпособВводаВОборот.ТолькоПросмотр = Форма.ПолноеОписаниеОстатков;
	
	ЭтоОбувь = (Форма.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Обувь"));
	ЭтоШины  = (Форма.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Шины"));
	ЭтоЛегкаяПромышленность = (Форма.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность"));
	ЭтоТабачнаяПродукция    = ИнтеграцияИСКлиентСервер.ЭтоПродукцияМОТП(Форма.ВидПродукции);
	
	Форма.Элементы.ЗаполнятьЦелевойПол.Видимость = ЭтоОбувь Или ЭтоЛегкаяПромышленность;
	Форма.Элементы.ЦелевойПол.Видимость    = ЭтоОбувь Или ЭтоЛегкаяПромышленность;
	
	Форма.Элементы.ЗаполнятьСпосовВводаВОборот.Видимость = ЭтоОбувь Или ЭтоЛегкаяПромышленность;
	Форма.Элементы.СпособВводаВОборот.Видимость          = ЭтоОбувь Или ЭтоЛегкаяПромышленность;
	
	Форма.Элементы.ЗаполнятьМодель.Видимость = ЭтоШины;
	Форма.Элементы.Модель.Видимость          = ЭтоШины;
	
	Форма.Элементы.ЗаполнятьВозрастнуюКатегорию.Видимость = ЭтоЛегкаяПромышленность;
	Форма.Элементы.ВозрастнаяКатегория.Видимость    = ЭтоЛегкаяПромышленность;
	
	Форма.Элементы.ЗаполнятьТоварныйЗнак.Видимость = Не ЭтоОбувь;
	Форма.Элементы.ТоварныйЗнак.Видимость          = Не ЭтоОбувь;
	
	Форма.Элементы.ЗаполнятьНаименование.Видимость = ЭтоТабачнаяПродукция;
	Форма.Элементы.Наименование.Видимость          = ЭтоТабачнаяПродукция;
	
	Форма.Элементы.СпособВводаВОборот.ТолькоПросмотр  = Не Форма.ЗаполнятьСпособВводаВОборот;
	Форма.Элементы.ЦелевойПол.ТолькоПросмотр          = Не Форма.ЗаполнятьЦелевойПол;
	Форма.Элементы.ВозрастнаяКатегория.ТолькоПросмотр = Не Форма.ЗаполнятьВозрастнуюКатегорию;
	Форма.Элементы.Модель.ТолькоПросмотр              = Не Форма.ЗаполнятьМодель;
	Форма.Элементы.ТоварныйЗнак.ТолькоПросмотр        = Не Форма.ЗаполнятьТоварныйЗнак;
	Форма.Элементы.ПолноеОписаниеОстатков.Видимость   = Не ЭтоТабачнаяПродукция;
	Форма.Элементы.Наименование.ТолькоПросмотр        = Не Форма.ЗаполнятьНаименование;
	
КонецПроцедуры

#КонецОбласти