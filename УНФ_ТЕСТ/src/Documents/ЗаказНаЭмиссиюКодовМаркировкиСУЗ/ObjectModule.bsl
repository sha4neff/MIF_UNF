#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Товары.Очистить();
		СерийныеНомера.Очистить();
	КонецЕсли;
	
	ПропускатьЗаполнениеGTIN = Ложь;
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("ДанныеПула") Тогда
			ЗаполнитьПоДаннымПулаКодовМаркировки(ДанныеЗаполнения);
		ИначеЕсли ДанныеЗаполнения.Свойство("ИмяФильтра")
			И ТипЗнч(ДанныеЗаполнения.Основание) = Тип("ДокументСсылка.ПеремаркировкаТоваровИСМП") Тогда
			
			ОтборПоСпособуВводаВОборот = Неопределено;
			Если ДанныеЗаполнения.ИмяФильтра = "ЗаполнениеПоСпособуВводаВОборот" Тогда
				СпособВводаВОборот         = ДанныеЗаполнения.ЗначениеФильтра;
				ОтборПоСпособуВводаВОборот = СпособВводаВОборот;
			КонецЕсли;
			
			ЗаполнитьПоПеремаркировке(ДанныеЗаполнения.Основание, ОтборПоСпособуВводаВОборот);
			ПропускатьЗаполнениеGTIN = Истина;
			
		ИначеЕсли ДанныеЗаполнения.Свойство("ИмяФильтра")
			И ТипЗнч(ДанныеЗаполнения.Основание) = Тип("ДокументСсылка.ПеремаркировкаТоваровИСМП") Тогда
			
		КонецЕсли;
	
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.МаркировкаТоваровИСМП") Тогда
		
		ЗаполнитьПоМаркировке(ДанныеЗаполнения);
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПеремаркировкаТоваровИСМП") Тогда
		
		ЗаполнитьПоПеремаркировке(ДанныеЗаполнения);
		ПропускатьЗаполнениеGTIN = Истина;
		
	КонецЕсли;
	
	ИнтеграцияИСМППереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	ЗаполнитьИдентификаторыСтрокТабличнойЧастиТовары();
	
	ЗаполнитьОбъектПоСтатистике(ПропускатьЗаполнениеGTIN);
	
	ИнтеграцияИСМППереопределяемый.ЗаполнитьКодыТНВЭДПоНоменклатуреВТабличнойЧасти(Товары);
	
	ЗаполнитьСпособФормированияСерийныхНомеровПоУмолчанию();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.ВозрастнаяКатегория");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Модель");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.ЦелевойПол");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.СпособВводаВОборот");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.КодТНВЭД");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.СрокГодности");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Наименование");
	МассивНепроверяемыхРеквизитов.Добавить("ПриобретенПроизведенПослеДатыОбязательнойМаркировки");
	
	ЭтоТабачнаяПродукция    = ИнтеграцияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукции);
	ЭтоШины                 = (ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Шины"));
	ЭтоОбувь                = (ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Обувь"));
	ЭтоЛегкаяПромышленность = (ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность"));
	ЭтоМолочнаяПродукция    = (ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукция"));
	
	ЭтоМаркировкаОстатков   = СпособВводаВОборот = ПредопределенноеЗначение("Перечисление.СпособыВводаВОборотСУЗ.МаркировкаОстатков");
	ШаблонСообщения         = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""Товары""'");
		
	Если ЭтоТабачнаяПродукция Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("СпособВводаВОборот");
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроизводственногоЗаказа");
		
		Если ЭтоМаркировкаОстатков Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъект");
			МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъектИдентификатор");
			МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъектИдентификатор");
			МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъектАдресСтрокой"); 
			МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроизводственногоЗаказа");
			МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроизводственнойЛинии");
			МассивНепроверяемыхРеквизитов.Добавить("ОжидаемаяДатаНачалаПроизводства");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Номенклатура");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Характеристика");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.GTIN");
			ПроверяемыеРеквизиты.Добавить("Товары.КодТНВЭД");
			
			Для Каждого СтрокаТовары Из Товары Цикл
				
				Если БезУчетаНоменклатуры И
					Не (ЗначениеЗаполнено(СтрокаТовары.Номенклатура)
					Или ЗначениеЗаполнено(СтрокаТовары.Наименование)) Тогда
					
					ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Наименование'"), СтрокаТовары.НомерСтроки);
					ОбщегоНазначения.СообщитьПользователю(
						ТекстСообщения,
						ЭтотОбъект,
						ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
							"Товары", СтрокаТовары.НомерСтроки, "Наименование"),,
						Отказ);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукции) Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъект");
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъектИдентификатор");
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъектИдентификатор");
		МассивНепроверяемыхРеквизитов.Добавить("ПроизводственныйОбъектАдресСтрокой"); 
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроизводственногоЗаказа");
		МассивНепроверяемыхРеквизитов.Добавить("ИдентификаторПроизводственнойЛинии");
		МассивНепроверяемыхРеквизитов.Добавить("ОжидаемаяДатаНачалаПроизводства");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Шаблон");
		
		Если ЭтоМолочнаяПродукция Тогда
			ПроверяемыеРеквизиты.Добавить("Товары.Шаблон");
			Для Каждого СтрокаТовары Из Товары Цикл
				Если ЗначениеЗаполнено(СтрокаТовары.СрокГодности)
					Или (СтрокаТовары.Шаблон <> Перечисления.ШаблоныКодовМаркировкиСУЗ.МолочнаяПродукция
					   И СтрокаТовары.Шаблон <> Перечисления.ШаблоныКодовМаркировкиСУЗ.СкоропортящаясяМолочнаяПродукция) Тогда
					Продолжить;
				КонецЕсли;
				ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Срок годности'"), СтрокаТовары.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						"Товары", СтрокаТовары.НомерСтроки, "СрокГодности"),,
					Отказ);
			КонецЦикла;
		КонецЕсли;
		
		Если ЭтоМаркировкаОстатков Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Номенклатура");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Характеристика");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.GTIN");
			
			ПроверяемыеРеквизиты.Добавить("Товары.КодТНВЭД");
			
			Если Не ПриобретенПроизведенПослеДатыОбязательнойМаркировки
				И Не Документы.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.ДокументОтправлен(Ссылка) Тогда
				
				ДатаОбязательнойМаркировки = ИнтеграцияИСМПКлиентСерверПовтИсп.ДатаОбязательнойМаркировкиПродукции(ВидПродукции);
				РеквизитыМетаданного       = Метаданные.Документы.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Реквизиты;
				
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Поле ""%1 %2"" не заполнено'"),
					РеквизитыМетаданного.ПриобретенПроизведенПослеДатыОбязательнойМаркировки.Синоним,
					Формат(ДатаОбязательнойМаркировки, "ДФ=dd.MM.yyyy;"));
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
					ЭтотОбъект,
					РеквизитыМетаданного.ПриобретенПроизведенПослеДатыОбязательнойМаркировки.Имя,,
					Отказ);
				
			КонецЕсли;
			
			Для Каждого СтрокаТовары Из Товары Цикл
				
				КонтрольGTIN             = Истина;
				КонтрольОписанияОстатков = Истина;
				КонтрольНоменклатуры     = Истина;
				
				ЭтоGTINМаркировкиОстатков = ШтрихкодированиеИСКлиентСервер.ЭтоШтрихкодВводаОстатков(СтрокаТовары.GTIN);
				ЭтоУпрощенноеОписаниеОстатков = 
					    ЗначениеЗаполнено(СтрокаТовары.ЦелевойПол)
					Или ЗначениеЗаполнено(СтрокаТовары.СпособВводаВОборот)
					Или ЗначениеЗаполнено(СтрокаТовары.Модель)
					Или ЗначениеЗаполнено(СтрокаТовары.ТоварныйЗнак)
					Или ЗначениеЗаполнено(СтрокаТовары.ВозрастнаяКатегория);
				
				Если ЭтоGTINМаркировкиОстатков И БезУчетаНоменклатуры Тогда
					КонтрольНоменклатуры = Ложь;
				КонецЕсли;
				
				Если ЭтоУпрощенноеОписаниеОстатков Тогда
					КонтрольGTIN         = Ложь;
					КонтрольНоменклатуры = Не БезУчетаНоменклатуры;
				Иначе
					КонтрольОписанияОстатков = Ложь;
				КонецЕсли;
				
				Если Не ЭтоУпрощенноеОписаниеОстатков И КонтрольGTIN
					И ВидПродукции = Перечисления.ВидыПродукцииИС.Фотоаппараты Тогда
					КонтрольGTIN         = Ложь;
					КонтрольНоменклатуры = Не БезУчетаНоменклатуры;
				КонецЕсли;
				
				Если КонтрольGTIN И Не ЗначениеЗаполнено(СтрокаТовары.GTIN) Тогда
					
					ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'GTIN'"), СтрокаТовары.НомерСтроки);
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
						ЭтотОбъект,
						ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
							"Товары", СтрокаТовары.НомерСтроки, "GTIN"),,
						Отказ);
						
				КонецЕсли;
				
				Если КонтрольОписанияОстатков Тогда
					
					Если ЭтоШины Тогда
						
						Если Не ЗначениеЗаполнено(СтрокаТовары.Модель) Тогда
							ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Модель'"), СтрокаТовары.НомерСтроки);
							ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
								ЭтотОбъект,
								ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
									"Товары", СтрокаТовары.НомерСтроки, "Модель"),,
								Отказ);
						КонецЕсли;
						
					ИначеЕсли ЭтоОбувь Тогда
						
						Если Не ЗначениеЗаполнено(СтрокаТовары.ЦелевойПол) Тогда
							ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Целевой пол'"), СтрокаТовары.НомерСтроки);
							ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
								ЭтотОбъект,
								ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
									"Товары", СтрокаТовары.НомерСтроки, "ЦелевойПол"),,
								Отказ);
						КонецЕсли;
						
						Если Не ЗначениеЗаполнено(СтрокаТовары.СпособВводаВОборот) Тогда
							ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Способ ввода в оборот'"), СтрокаТовары.НомерСтроки);
							ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
								ЭтотОбъект,
								ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
									"Товары", СтрокаТовары.НомерСтроки, "СпособВводаВОборот"),,
								Отказ);
						КонецЕсли;
						
					ИначеЕсли ЭтоЛегкаяПромышленность Тогда
						
						КодТНВЭД = ИнтеграцияИСМПСлужебный.КодТНВЭДДляПередачиВИСМП(
							СтрокаТовары.КодТНВЭД,
							ВидПродукции);
						
						Если ЗначениеЗаполнено(КодТНВЭД) Тогда
							
							ИмяПоляПроверки   = "ЦелевойПол";
							ПредставлениеПоля = НСтр("ru = 'Целевой пол'");
							Если ИнтеграцияИСМПСлужебный.КодТНВЭДТребуетВозрастнуюКатегорию(КодТНВЭД) Тогда
								ИмяПоляПроверки   = "ВозрастнаяКатегория";
								ПредставлениеПоля = НСтр("ru = 'Возрастная категория'");
							КонецЕсли;
							
							Если Не ЗначениеЗаполнено(СтрокаТовары[ИмяПоляПроверки]) Тогда
								ТекстСообщения = СтрШаблон(ШаблонСообщения, ПредставлениеПоля, СтрокаТовары.НомерСтроки);
								ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
									ЭтотОбъект,
									ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
										"Товары", СтрокаТовары.НомерСтроки, ИмяПоляПроверки),,
									Отказ);
							КонецЕсли;
							
							Если Не ЗначениеЗаполнено(СтрокаТовары.СпособВводаВОборот) Тогда
								ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Способ ввода в оборот'"), СтрокаТовары.НомерСтроки);
								ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
									ЭтотОбъект,
									ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
										"Товары", СтрокаТовары.НомерСтроки, "СпособВводаВОборот"),,
									Отказ);
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
				Если КонтрольНоменклатуры И Не ЗначениеЗаполнено(СтрокаТовары.Номенклатура) Тогда
					
					ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Номенклатура'"), СтрокаТовары.НомерСтроки);
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
						ЭтотОбъект,
						ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
							"Товары", СтрокаТовары.НомерСтроки, "Номенклатура"),,
						Отказ);
					
				КонецЕсли;
				
			КонецЦикла;
			
		ИначеЕсли СпособВводаВОборот = ПредопределенноеЗначение(
			"Перечисление.СпособыВводаВОборотСУЗ.ТрансграничнаяТорговля") Тогда
			ПроверяемыеРеквизиты.Добавить("Контрагент");
		КонецЕсли;
		
		Если БезУчетаНоменклатуры Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Номенклатура");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Характеристика");
		КонецЕсли;
		
	КонецЕсли;
	
	ИнтеграцияИСМППереопределяемый.ПриОпределенииОбработкиПроверкиЗаполнения(
		ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	ПроверкаСпособаФормированияСерийныхНомеров(Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ИдентификаторПроизводственногоЗаказа) Тогда
		ИдентификаторПроизводственногоЗаказа = Строка(Новый УникальныйИдентификатор());
	КонецЕсли;
	
	ШаблонВидаПродукции = Неопределено;
	Если ИнтеграцияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукции) Тогда
		ШаблонВидаПродукции = ИнтеграцияИСМПКлиентСервер.ШаблонКодаМаркировкиПоВидуПродукции(ВидПродукции);
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЧ.ИдентификаторСтроки) Тогда
			СтрокаТЧ.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор());
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СтрокаТЧ.Шаблон) Тогда
			СтрокаТЧ.Шаблон = ШаблонВидаПродукции;
		КонецЕсли;
	КонецЦикла;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияИСМП.ЗаписатьСтатусДокументаИСМППоУмолчанию(ЭтотОбъект);
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование = Неопределено;
	СерийныеНомера.Очистить();
	ИдентификаторПроизводственногоЗаказа = "";
	ИдентификаторПроизводственнойЛинии   = "";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаЗаполнения

Процедура ЗаполнитьСпособФормированияСерийныхНомеровПоУмолчанию()

	Для Каждого СтрокаТовары Из Товары Цикл
		СтрокаТовары.СпособФормированияСерийногоНомера = Перечисления.СпособыФормированияСерийногоНомераСУЗ.Автоматически;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверкаСпособаФормированияСерийныхНомеров(Отказ)
	
	ТаблицаПроверки = Товары.Выгрузить(, "GTIN, СпособФормированияСерийногоНомера");
	ТаблицаПроверки.Свернуть("GTIN, СпособФормированияСерийногоНомера");
	ТаблицаПроверки.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	ТаблицаПроверки.ЗаполнитьЗначения(1, "Количество");
	ТаблицаПроверки.Свернуть("GTIN", "Количество");
	
	Для Каждого СтрокаПроверки Из ТаблицаПроверки Цикл
	
		Если СтрокаПроверки.Количество < 2 Тогда
			Продолжить;
		КонецЕсли;
		ПоискСтрок = Товары.НайтиСтроки(Новый Структура("GTIN", СтрокаПроверки.GTIN));
		
		Для Каждого СтрокаТовары Из ПоискСтрок Цикл
			ТекстСообщения = НСтр(
				"ru = 'В строке %1 для GTIN %2 отличается способ формирования серийных номеров от других строк'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, СтрокаТовары.НомерСтроки, СтрокаТовары.GTIN);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"Товары", СтрокаТовары.НомерСтроки, "СпособФормированияСерийногоНомера"),,
				Отказ);
			Прервать;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьОбъектПоСтатистике(ПропускатьЗаполнениеGTIN = Ложь)
	
	ДанныеСтатистики = ЗаполнениеОбъектовПоСтатистикеИСМП.ДанныеЗаполненияЗаказаНаЭмиссиюКодовМаркировкиСУЗ(Организация);
	
	Для Каждого КлючИЗначение Из ДанныеСтатистики Цикл
		ЗаполнениеОбъектовПоСтатистикеИСМП.ЗаполнитьПустойРеквизит(ЭтотОбъект, ДанныеСтатистики, КлючИЗначение.Ключ);
	КонецЦикла;
	
	ЗаполнениеОбъектовПоСтатистикеИСМП.ЗаполнитьДанныеПоТоварамЗаказаНаЭмиссиюКодовМаркировкиСУЗ(
		ЭтотОбъект.Товары,
		ЭтотОбъект,,
		"GTIN");
	
КонецПроцедуры

Процедура ЗаполнитьПоПеремаркировке(ДанныеЗаполнения, ОтборПоСпособуВводаВОборот = Неопределено)
	
	Если Не ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Основание",          ДанныеЗаполнения);
		Запрос.УстановитьПараметр("Ссылка",             Ссылка);
		Запрос.УстановитьПараметр("СпособВводаВОборот", ОтборПоСпособуВводаВОборот);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПеремаркировкаТоваровИСМПТовары.Номенклатура                    КАК Номенклатура,
		|	ПеремаркировкаТоваровИСМПТовары.Характеристика                  КАК Характеристика,
		|	ПеремаркировкаТоваровИСМПТовары.КодМаркировки.ЗначениеШтрихкода КАК КодМаркировки,
		|	ПеремаркировкаТоваровИСМПТовары.Ссылка.ВидПродукции             КАК ВидПродукции
		|ИЗ
		|	Документ.ПеремаркировкаТоваровИСМП.Товары КАК ПеремаркировкаТоваровИСМПТовары
		|ГДЕ
		|	ПеремаркировкаТоваровИСМПТовары.Ссылка = &Основание
		|	И ПеремаркировкаТоваровИСМПТовары.НовыйКодМаркировки = ЗНАЧЕНИЕ(Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка)
		|	И ПеремаркировкаТоваровИСМПТовары.КодМаркировки <> ЗНАЧЕНИЕ(Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка)
		|	И ВЫБОР 
		|		КОГДА &СпособВводаВОборот = НЕОПРЕДЕЛЕНО
		|				ИЛИ &СпособВводаВОборот = ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.ПустаяСсылка) ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ПеремаркировкаТоваровИСМПТовары.СпособВводаВОборот = &СпособВводаВОборот
		|		КОНЕЦ";
		
		ТаблицаТовары = Запрос.Выполнить().Выгрузить();
		ТаблицаТовары.Колонки.Добавить("GTIN", Метаданные.ОпределяемыеТипы.GTIN.Тип);
		
		Для Каждого СтрокаТовары Из ТаблицаТовары Цикл
			ДанныеРазбора = РазборКодаМаркировкиИССлужебный.РазобратьКодМаркировки(СтрокаТовары.КодМаркировки, СтрокаТовары.ВидПродукции);
			Если ДанныеРазбора = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если ДанныеРазбора.СоставКодаМаркировки.Свойство("GTIN") Тогда
				СтрокаТовары.GTIN = ДанныеРазбора.СоставКодаМаркировки.GTIN;
			КонецЕсли;
		КонецЦикла;
		
		Запрос.УстановитьПараметр("ТаблицаТовары", ТаблицаТовары);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаТовары.Номенклатура   КАК Номенклатура,
		|	ТаблицаТовары.Характеристика КАК Характеристика,
		|	ТаблицаТовары.GTIN           КАК GTIN,
		|	1                            КАК Количество
		|ПОМЕСТИТЬ ТоварыПеремаркировки
		|ИЗ
		|	&ТаблицаТовары Как ТаблицаТовары
		|
		|;
		|/////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	ПеремаркировкаТоваровИСМПТовары.Номенклатура      КАК Номенклатура,
		|	ПеремаркировкаТоваровИСМПТовары.Характеристика    КАК Характеристика,
		|	ПеремаркировкаТоваровИСМПТовары.GTIN              КАК GTIN,
		|	СУММА(ПеремаркировкаТоваровИСМПТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ НеобходимоЗаказать
		|ИЗ
		|	ТоварыПеремаркировки КАК ПеремаркировкаТоваровИСМПТовары
		|СГРУППИРОВАТЬ ПО
		|	ПеремаркировкаТоваровИСМПТовары.Номенклатура,
		|	ПеремаркировкаТоваровИСМПТовары.Характеристика,
		|	ПеремаркировкаТоваровИСМПТовары.GTIN
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПулКодовМаркировкиСУЗ.Номенклатура,
		|	ПулКодовМаркировкиСУЗ.Характеристика,
		|	ПулКодовМаркировкиСУЗ.GTIN,
		|	СУММА(-1)
		|ИЗ
		|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировкиСУЗ
		|ГДЕ
		|	ПулКодовМаркировкиСУЗ.ДокументОснование = &Основание
		|	И ПулКодовМаркировкиСУЗ.ЗаказНаЭмиссию.ДокументОснование <> &Основание
		|	И ВЫБОР 
		|		КОГДА &СпособВводаВОборот = НЕОПРЕДЕЛЕНО
		|				ИЛИ &СпособВводаВОборот = ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.ПустаяСсылка) ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ПулКодовМаркировкиСУЗ.СпособВводаВОборот = &СпособВводаВОборот
		|		КОНЕЦ
		|СГРУППИРОВАТЬ ПО
		|	ПулКодовМаркировкиСУЗ.Номенклатура,
		|	ПулКодовМаркировкиСУЗ.Характеристика,
		|	ПулКодовМаркировкиСУЗ.GTIN
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказНаЭмиссию.Номенклатура,
		|	ЗаказНаЭмиссию.Характеристика,
		|	ЗаказНаЭмиссию.GTIN,
		|	-СУММА(ЗаказНаЭмиссию.Количество)
		|ИЗ
		|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Товары КАК ЗаказНаЭмиссию
		|ГДЕ
		|	ЗаказНаЭмиссию.Ссылка.ДокументОснование = &Основание
		|	И ЗаказНаЭмиссию.Ссылка <> &Ссылка
		|	И НЕ ЗаказНаЭмиссию.Ссылка.ПометкаУдаления
		|	И ВЫБОР 
		|		КОГДА &СпособВводаВОборот = НЕОПРЕДЕЛЕНО
		|				ИЛИ &СпособВводаВОборот = ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.ПустаяСсылка) ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЗаказНаЭмиссию.СпособВводаВОборот = &СпособВводаВОборот
		|		КОНЕЦ
		|СГРУППИРОВАТЬ ПО
		|	ЗаказНаЭмиссию.Номенклатура,
		|	ЗаказНаЭмиссию.Характеристика,
		|	ЗаказНаЭмиссию.GTIN
		|;
		|ВЫБРАТЬ
		|	НеобходимоЗаказать.Номенклатура      КАК Номенклатура,
		|	НеобходимоЗаказать.Характеристика    КАК Характеристика,
		|	НеобходимоЗаказать.GTIN              КАК GTIN,
		|	СУММА(НеобходимоЗаказать.Количество) КАК Количество,
		|	СУММА(НеобходимоЗаказать.Количество) КАК КоличествоУпаковок
		|ИЗ
		|	НеобходимоЗаказать КАК НеобходимоЗаказать
		|СГРУППИРОВАТЬ ПО
		|	НеобходимоЗаказать.Номенклатура,
		|	НеобходимоЗаказать.Характеристика,
		|	НеобходимоЗаказать.GTIN
		|ИМЕЮЩИЕ
		|	СУММА(НеобходимоЗаказать.Количество) > 0
		|";
		
		ЗаполнитьТабличнуюЧастьТовары(Запрос);
		
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ДокументОснование = ДанныеЗаполнения;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, 
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, "Организация,ВидПродукции"));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоМаркировке(ДанныеЗаполнения)
	
	Если Не ДополнительныеСвойства.Свойство("НеЗаполнятьШапку") Тогда
		
		ДокументОснование = ДанныеЗаполнения;
		
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДокументОснование,
			"Организация,
			|ВидПродукции,
			|ИдентификаторПроизводственногоЗаказа,
			|ИдентификаторПроизводственнойЛинии,
			|Операция,
			|Контрагент");
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
		
		Если Реквизиты.Операция = Перечисления.ВидыОперацийИСМП.ВводВОборотМаркировкаОстатков
			Или ОснованиеМаркировкиТоваровОтноситсяКМаркировкеОстатков() Тогда
			СпособВводаВОборот = Перечисления.СпособыВводаВОборотСУЗ.МаркировкаОстатков;
		ИначеЕсли Реквизиты.Операция = Перечисления.ВидыОперацийИСМП.ВводВОборотПроизводствоВнеЕАЭС Тогда
			СпособВводаВОборот = Перечисления.СпособыВводаВОборотСУЗ.Импорт;
		ИначеЕсли Реквизиты.Операция = Перечисления.ВидыОперацийИСМП.ВводВОборотТрансграничнаяТорговля Тогда
			СпособВводаВОборот = Перечисления.СпособыВводаВОборотСУЗ.ТрансграничнаяТорговля;
		Иначе
			СпособВводаВОборот = Перечисления.СпособыВводаВОборотСУЗ.Производство;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Основание", ДанныеЗаполнения);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр(
			"ПустыеЗначенияНоменклатуры",
			ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("Номенклатура"));
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	МаркировкаТоваровИСМПТовары.Номенклатура КАК Номенклатура,
		|	МаркировкаТоваровИСМПТовары.Характеристика КАК Характеристика,
		|	&ШаблонМаркировкаТовары КАК Шаблон,
		|	МаркировкаТоваровИСМПТовары.СрокГодности КАК СрокГодности,
		|	СУММА(Количество) КАК Количество,
		|	ВЫБОР
		|		КОГДА МаркировкаТоваровИСМПТовары.Номенклатура В (&ПустыеЗначенияНоменклатуры)
		|			ТОГДА МаркировкаТоваровИСМПТовары.GTIN
		|		ИНАЧЕ Неопределено
		|	КОНЕЦ КАК GTIN,
		|	ВЫБОР
		|		КОГДА МаркировкаТоваровИСМПТовары.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотМаркировкаОстатков)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК МаркировкаОстатков
		|ПОМЕСТИТЬ НеобходимоЗаказать
		|ИЗ
		|	Документ.МаркировкаТоваровИСМП.Товары КАК МаркировкаТоваровИСМПТовары
		|ГДЕ
		|	МаркировкаТоваровИСМПТовары.Ссылка = &Основание
		|СГРУППИРОВАТЬ ПО
		|	МаркировкаТоваровИСМПТовары.Номенклатура,
		|	МаркировкаТоваровИСМПТовары.Характеристика,
		|	&ШаблонМаркировкаТовары,
		|	МаркировкаТоваровИСМПТовары.СрокГодности,
		|	ВЫБОР
		|		КОГДА МаркировкаТоваровИСМПТовары.Номенклатура В (&ПустыеЗначенияНоменклатуры)
		|			ТОГДА МаркировкаТоваровИСМПТовары.GTIN
		|		ИНАЧЕ Неопределено
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА МаркировкаТоваровИСМПТовары.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотМаркировкаОстатков)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Штрихкоды.Номенклатура,
		|	Штрихкоды.Характеристика,
		|	&ШаблонМаркировкаШтрихкоды,
		|	МаркировкаТоваровИСМПШтрихкоды.СрокГодности,
		|	СУММА(ВЫБОР КОГДА Штрихкоды.Количество = 0 ТОГДА -1 ИНАЧЕ -Штрихкоды.Количество КОНЕЦ),
		|	Неопределено,
		|	ВЫБОР
		|		КОГДА МаркировкаТоваровИСМПШтрихкоды.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотМаркировкаОстатков)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|ИЗ
		|	Документ.МаркировкаТоваровИСМП.ШтрихкодыУпаковок КАК МаркировкаТоваровИСМПШтрихкоды
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК Штрихкоды
		|		ПО Штрихкоды.Ссылка = МаркировкаТоваровИСМПШтрихкоды.ШтрихкодУпаковки
		|ГДЕ
		|	МаркировкаТоваровИСМПШтрихкоды.Ссылка = &Основание
		|СГРУППИРОВАТЬ ПО
		|	Штрихкоды.Номенклатура,
		|	Штрихкоды.Характеристика,
		|	&ШаблонМаркировкаШтрихкоды,
		|	МаркировкаТоваровИСМПШтрихкоды.СрокГодности,
		|	ВЫБОР
		|		КОГДА МаркировкаТоваровИСМПШтрихкоды.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИСМП.ВводВОборотМаркировкаОстатков)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПулКодовМаркировкиСУЗ.Номенклатура,
		|	ПулКодовМаркировкиСУЗ.Характеристика,
		|	ВЫБОР
		|		КОГДА ПулКодовМаркировкиСУЗ.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.СкоропортящаясяМолочнаяПродукция)
		|				ИЛИ ПулКодовМаркировкиСУЗ.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.МолочнаяПродукция)
		|			ТОГДА ПулКодовМаркировкиСУЗ.Шаблон
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ПустаяСсылка)
		|	КОНЕЦ,
		|	ПулКодовМаркировкиСУЗ.СрокГодности,
		|	СУММА(-1),
		|	ВЫБОР
		|		КОГДА ПулКодовМаркировкиСУЗ.Номенклатура В (&ПустыеЗначенияНоменклатуры)
		|			ТОГДА ПулКодовМаркировкиСУЗ.GTIN
		|		ИНАЧЕ Неопределено
		|	КОНЕЦ,
		|	ПулКодовМаркировкиСУЗ.МаркировкаОстатков
		|ИЗ
		|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировкиСУЗ
		|ГДЕ
		|	ПулКодовМаркировкиСУЗ.ДокументОснование = &Основание
		|	И ПулКодовМаркировкиСУЗ.ЗаказНаЭмиссию.ДокументОснование <> &Основание
		|СГРУППИРОВАТЬ ПО
		|	ПулКодовМаркировкиСУЗ.Номенклатура,
		|	ПулКодовМаркировкиСУЗ.Характеристика,
		|	ВЫБОР
		|		КОГДА ПулКодовМаркировкиСУЗ.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.СкоропортящаясяМолочнаяПродукция)
		|				ИЛИ ПулКодовМаркировкиСУЗ.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.МолочнаяПродукция)
		|			ТОГДА ПулКодовМаркировкиСУЗ.Шаблон
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ПустаяСсылка)
		|	КОНЕЦ,
		|	ПулКодовМаркировкиСУЗ.СрокГодности,
		|	ВЫБОР
		|		КОГДА ПулКодовМаркировкиСУЗ.Номенклатура В (&ПустыеЗначенияНоменклатуры)
		|			ТОГДА ПулКодовМаркировкиСУЗ.GTIN
		|		ИНАЧЕ Неопределено
		|	КОНЕЦ,
		|	ПулКодовМаркировкиСУЗ.МаркировкаОстатков
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказНаЭмиссию.Номенклатура,
		|	ЗаказНаЭмиссию.Характеристика,
		|	ВЫБОР
		|		КОГДА ЗаказНаЭмиссию.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.СкоропортящаясяМолочнаяПродукция)
		|				ИЛИ ЗаказНаЭмиссию.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.МолочнаяПродукция)
		|			ТОГДА ЗаказНаЭмиссию.Шаблон
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ПустаяСсылка)
		|	КОНЕЦ,
		|	ЗаказНаЭмиссию.СрокГодности,
		|	-СУММА(ЗаказНаЭмиссию.Количество),
		|	ВЫБОР
		|		КОГДА ЗаказНаЭмиссию.Номенклатура В (&ПустыеЗначенияНоменклатуры)
		|			ТОГДА ЗаказНаЭмиссию.GTIN
		|		ИНАЧЕ Неопределено
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ЗаказНаЭмиссию.Ссылка.СпособВводаВОборот = ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.МаркировкаОстатков)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|ИЗ
		|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Товары КАК ЗаказНаЭмиссию
		|ГДЕ
		|	ЗаказНаЭмиссию.Ссылка.ДокументОснование = &Основание
		|	И ЗаказНаЭмиссию.Ссылка <> &Ссылка
		|	И ЗаказНаЭмиссию.Ссылка.Проведен
		|СГРУППИРОВАТЬ ПО
		|	ЗаказНаЭмиссию.Номенклатура,
		|	ЗаказНаЭмиссию.Характеристика,
		|	ВЫБОР
		|		КОГДА ЗаказНаЭмиссию.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.СкоропортящаясяМолочнаяПродукция)
		|				ИЛИ ЗаказНаЭмиссию.Шаблон = ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.МолочнаяПродукция)
		|			ТОГДА ЗаказНаЭмиссию.Шаблон
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ПустаяСсылка)
		|	КОНЕЦ,
		|	ЗаказНаЭмиссию.СрокГодности,
		|	ВЫБОР
		|		КОГДА ЗаказНаЭмиссию.Номенклатура В (&ПустыеЗначенияНоменклатуры)
		|			ТОГДА ЗаказНаЭмиссию.GTIN
		|		ИНАЧЕ Неопределено
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ЗаказНаЭмиссию.Ссылка.СпособВводаВОборот = ЗНАЧЕНИЕ(Перечисление.СпособыВводаВОборотСУЗ.МаркировкаОстатков)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|;
		|ВЫБРАТЬ
		|	НеобходимоЗаказать.Номенклатура           КАК Номенклатура,
		|	НеобходимоЗаказать.Характеристика         КАК Характеристика,
		|	НеобходимоЗаказать.Шаблон                 КАК Шаблон,
		|	НеобходимоЗаказать.СрокГодности           КАК СрокГодности,
		|	СУММА(НеобходимоЗаказать.Количество)      КАК Количество,
		|	СУММА(НеобходимоЗаказать.Количество)      КАК КоличествоУпаковок,
		|	НеобходимоЗаказать.GTIN                   КАК GTIN
		|ИЗ
		|	НеобходимоЗаказать КАК НеобходимоЗаказать
		|СГРУППИРОВАТЬ ПО
		|	НеобходимоЗаказать.Номенклатура,
		|	НеобходимоЗаказать.Характеристика,
		|	НеобходимоЗаказать.Шаблон,
		|	НеобходимоЗаказать.СрокГодности,
		|	НеобходимоЗаказать.GTIN
		|ИМЕЮЩИЕ
		|	СУММА(НеобходимоЗаказать.Количество) > 0
		|";
		
		Если ВидПродукции = Перечисления.ВидыПродукцииИС.МолочнаяПродукция Тогда
			ИнтеграцияИСМПВЕТИС.ДоработатьЗапросЗаполненияЗаказаНаЭмиссиюПоМаркировке(Запрос);
		КонецЕсли;
		
		Подстановка = "ЗНАЧЕНИЕ(Перечисление.ШаблоныКодовМаркировкиСУЗ.ПустаяСсылка)";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ШаблонМаркировкаТовары", Подстановка);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ШаблонМаркировкаШтрихкоды", Подстановка);
		ЗаполнитьТабличнуюЧастьТовары(Запрос);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоДаннымПулаКодовМаркировки(ДанныеЗаполнения)
	
	ДокументОснование = ДанныеЗаполнения.ДокументОснование;
	ВидПродукции      = ДанныеЗаполнения.ВидПродукции;
	
	Для Каждого СтрокаТЧ Из ДанныеЗаполнения.Товары Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), СтрокаТЧ);
	КонецЦикла;
	
	ДополнительныеСвойства.Вставить("НеЗаполнятьТабличнуюЧасть");
	ИнтеграцияИСМППереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДокументОснование, "", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьТовары(Запрос)
	
	Выборка = Запрос.Выполнить().Выбрать();
	Товары.Очистить();
	Пока Выборка.следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьИдентификаторыСтрокТабличнойЧастиТовары()
	
	Для Каждого СтрокаТовары Из Товары Цикл
		
		Если ПустаяСтрока(СтрокаТовары.ИдентификаторСтроки) Тогда
			СтрокаТовары.ИдентификаторСтроки = Новый УникальныйИдентификатор();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОснованиеМаркировкиТоваровОтноситсяКМаркировкеОстатков()
	
	ОснованиеМаркировки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ДокументОснование");
	Если Не ЗначениеЗаполнено(ОснованиеМаркировки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЯвляетсяОснованиемДляМаркировкиОстатков = Ложь;
	
	ИнтеграцияИСМППереопределяемый.ЯвляетсяОснованиемДляМаркировкиОстатков(
		ОснованиеМаркировки,
		ЯвляетсяОснованиемДляМаркировкиОстатков);
	
	Возврат ЯвляетсяОснованиемДляМаркировкиОстатков;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли