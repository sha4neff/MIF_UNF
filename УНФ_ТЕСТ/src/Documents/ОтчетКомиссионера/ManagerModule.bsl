#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Контрагент)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Определяет список команд заполнения.
//
// Параметры:
//   КомандыЗаполнения - ТаблицаЗначений - Таблица с командами заполнения. Для изменения.
//       См. описание 1 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения().
//
Процедура ДобавитьКомандыЗаполнения(КомандыЗаполнения, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОВОДКИ

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаУправленческий(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ТаблицаУправленческий.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУправленческий.Период КАК Период,
	|	ТаблицаУправленческий.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаУправленческий.СчетУчетаРасчетовСПокупателем КАК СчетДт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПокупателем.Валютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПокупателем.Валютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалДт,
	|	ТаблицаУправленческий.СчетУчетаПродажи КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	0 КАК СуммаВалКт,
	|	ТаблицаУправленческий.Сумма КАК Сумма,
	|	&ОтражениеВыручки КАК Содержание
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаУправленческий
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	1,
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчетаАвансовПокупателя,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПокупателяВалютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПокупателяВалютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПокупателемВалютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПокупателемВалютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	&ЗачетПредоплаты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаДокумента.Период КАК Период,
	|		ТаблицаДокумента.Организация КАК Организация,
	|		ТаблицаДокумента.СчетУчетаАвансовПокупателя КАК СчетУчетаАвансовПокупателя,
	|		ТаблицаДокумента.СчетУчетаАвансовПокупателяВалютный КАК СчетУчетаАвансовПокупателяВалютный,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПокупателемВалютный КАК СчетУчетаРасчетовСПокупателемВалютный,
	|		ТаблицаДокумента.ВалютаРасчетов КАК ВалютаРасчетов,
	|		СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВал,
	|		СУММА(ТаблицаДокумента.Сумма) КАК Сумма
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Период КАК Период,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.СчетУчетаАвансовПокупателя КАК СчетУчетаАвансовПокупателя,
	|			ТаблицаДокумента.СчетУчетаАвансовПокупателя.Валютный КАК СчетУчетаАвансовПокупателяВалютный,
	|			ТаблицаДокумента.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|			ТаблицаДокумента.СчетУчетаРасчетовСПокупателем.Валютный КАК СчетУчетаРасчетовСПокупателемВалютный,
	|			ТаблицаДокумента.ВалютаРасчетов КАК ВалютаРасчетов,
	|			ТаблицаДокумента.СуммаВал КАК СуммаВал,
	|			ТаблицаДокумента.Сумма КАК Сумма
	|		ИЗ
	|			ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПокупателя,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПокупателя.Валютный,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПокупателем,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПокупателем.Валютный,
	|			ТаблицаДокумента.Валюта,
	|			0,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаДокумента
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаДокумента.Период,
	|		ТаблицаДокумента.Организация,
	|		ТаблицаДокумента.СчетУчетаАвансовПокупателя,
	|		ТаблицаДокумента.СчетУчетаАвансовПокупателяВалютный,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПокупателемВалютный,
	|		ТаблицаДокумента.ВалютаРасчетов
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаДокумента.Сумма) >= 0.005
	|			ИЛИ СУММА(ТаблицаДокумента.Сумма) <= -0.005
	|			ИЛИ СУММА(ТаблицаДокумента.СуммаВал) >= 0.005
	|			ИЛИ СУММА(ТаблицаДокумента.СуммаВал) <= -0.005)) КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	1,
	|	ТаблицаУправленческий.Дата,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СчетУчета
	|		ИНАЧЕ &ОтрицательнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА &ПоложительнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ ТаблицаУправленческий.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы < 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаУправленческий.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.СчетУчета КАК СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.СчетУчетаВалютный КАК СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Валюта КАК Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.СчетУчета КАК СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный КАК СчетУчетаВалютный,
	|			ТаблицаДокумента.Валюта КАК Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПокупателями
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаУправленческий
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("ЗачетПредоплаты", НСтр("ru = 'Зачет предоплаты'"));
	Запрос.УстановитьПараметр("ОтражениеВыручки", НСтр("ru = 'Выручка от продажи'"));
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл  
		НоваяПроводка = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка, Выборка);
	КонецЦикла;
	
КонецПроцедуры // СформироватьТаблицаУправленческий()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасы(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	ЭтоОперацияВозврат = Ложь;
	
	Если ДокументСсылкаОтчетКомиссионера.ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионера Тогда
		Запрос.Текст = ТекстЗапросаОтчетКомиссионераОПродажахЗапасы();
		
		ТаблицаДокументовОснования = Новый СписокЗначений;
		ТаблицаДокументовОснования.Добавить(ДокументСсылкаОтчетКомиссионера);
		СебестоимостьПоОстаткам = Истина;
		
	Иначе
		Запрос.Текст = ТекстЗапросаОтчетКомиссионераОВозвратахЗапасы();
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетКомиссионера);
		
		ЗапросОснования = Новый Запрос;
		ЗапросОснования.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		
		ЗапросОснования.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаПокупатели.ОтчетКомиссионера КАК ОтчетКомиссионера
		|ИЗ
		|	ВременнаяТаблицаПокупатели КАК ТаблицаПокупатели
		|ГДЕ
		|	НЕ ТаблицаПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
		|	И НЕ ТаблицаПокупатели.ОтчетКомиссионера = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПокупатели.ОтчетКомиссионера";
		
		ЗапросОснования.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетКомиссионера);
		
		ТаблицаДокументовОснования = ЗапросОснования.Выполнить().Выгрузить();
		ЭтоОперацияВозврат = Истина;
		
		СебестоимостьПоОстаткам = ?(ДокументСсылкаОтчетКомиссионера.Покупатели.Найти(Документы.ОтчетКомиссионера.ПустаяСсылка()) = Неопределено, Ложь, Истина);
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОтчетОтКомиссионера", НСтр("ru = 'Отчет комиссионера'"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", РезультатЗапроса.Выгрузить());
	
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	// Установка исключительной блокировки контролируемых остатков запасов.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Запасы");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	// Получение остатков запасов по стоимости.
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Запасы.Период КАК ДатаОстатка,
	|	Запасы.Регистратор КАК Регистратор,
	|	Запасы.Организация КАК Организация,
	|	Запасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Запасы.СчетУчета КАК СчетУчета,
	|	Запасы.Номенклатура КАК Номенклатура,
	|	Запасы.Характеристика КАК Характеристика,
	|	Запасы.Партия КАК Партия,
	|	Запасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	Запасы.Количество КАК КоличествоОстаток,
	|	Запасы.Сумма КАК СуммаОстаток
	|ПОМЕСТИТЬ ДвиженияПоОснованиям
	|ИЗ
	|	РегистрНакопления.Запасы КАК Запасы
	|ГДЕ
	|	Запасы.ДокументПродажи В(&ТаблицаДокументовОснования)
	|	И ВЫБОР
	|			КОГДА &ЭтоОперацияВозврат
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ПериодКонтроля КАК ДатаОстатка,
	|	ЗапасыОстатки.Организация КАК Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|	ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыОстатки.Характеристика КАК Характеристика,
	|	ЗапасыОстатки.Партия КАК Партия,
	|	ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|	СУММА(ЗапасыОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	&ТекущийДокумент КАК Регистратор
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗапасыОстатки.Организация КАК Организация,
	|		ЗапасыОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ЗапасыОстатки.СчетУчета КАК СчетУчета,
	|		ЗапасыОстатки.Номенклатура КАК Номенклатура,
	|		ЗапасыОстатки.Характеристика КАК Характеристика,
	|		ЗапасыОстатки.Партия КАК Партия,
	|		ЗапасыОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|		СУММА(ЗапасыОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|		СУММА(ЗапасыОстатки.СуммаОстаток) КАК СуммаОстаток
	|	ИЗ
	|		РегистрНакопления.Запасы.Остатки(
	|				&МоментКонтроля,
	|				&СебестоимостьПоОстаткам
	|					И (Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
	|						(ВЫБРАТЬ
	|							ТаблицаЗапасы.Организация,
	|							ТаблицаЗапасы.СтруктурнаяЕдиница,
	|							ТаблицаЗапасы.СчетУчета,
	|							ТаблицаЗапасы.Номенклатура,
	|							ТаблицаЗапасы.Характеристика,
	|							ТаблицаЗапасы.Партия,
	|							ТаблицаЗапасы.ЗаказПокупателя
	|						ИЗ
	|							ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы)) КАК ЗапасыОстатки
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ЗапасыОстатки.Организация,
	|		ЗапасыОстатки.СтруктурнаяЕдиница,
	|		ЗапасыОстатки.СчетУчета,
	|		ЗапасыОстатки.Номенклатура,
	|		ЗапасыОстатки.Характеристика,
	|		ЗапасыОстатки.Партия,
	|		ЗапасыОстатки.ЗаказПокупателя
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаЗапасы.Организация,
	|		ДвиженияДокументаЗапасы.СтруктурнаяЕдиница,
	|		ДвиженияДокументаЗапасы.СчетУчета,
	|		ДвиженияДокументаЗапасы.Номенклатура,
	|		ДвиженияДокументаЗапасы.Характеристика,
	|		ДвиженияДокументаЗапасы.Партия,
	|		ДвиженияДокументаЗапасы.ЗаказПокупателя,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Количество, 0)
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаЗапасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ЕСТЬNULL(ДвиженияДокументаЗапасы.Сумма, 0)
	|			ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаЗапасы.Сумма, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.Запасы КАК ДвиженияДокументаЗапасы
	|	ГДЕ
	|		ДвиженияДокументаЗапасы.Регистратор = &ТекущийДокумент
	|		И ВЫБОР
	|				КОГДА &СебестоимостьПоОстаткам
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ) КАК ЗапасыОстатки
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &СебестоимостьПоОстаткам
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыОстатки.Организация,
	|	ЗапасыОстатки.СтруктурнаяЕдиница,
	|	ЗапасыОстатки.СчетУчета,
	|	ЗапасыОстатки.Номенклатура,
	|	ЗапасыОстатки.Характеристика,
	|	ЗапасыОстатки.Партия,
	|	ЗапасыОстатки.ЗаказПокупателя
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДвиженияПоОснованиям.ДатаОстатка,
	|	ДвиженияПоОснованиям.Организация,
	|	ДвиженияПоОснованиям.СтруктурнаяЕдиница,
	|	ДвиженияПоОснованиям.СчетУчета,
	|	ДвиженияПоОснованиям.Номенклатура,
	|	ДвиженияПоОснованиям.Характеристика,
	|	ДвиженияПоОснованиям.Партия,
	|	ДвиженияПоОснованиям.ЗаказПокупателя,
	|	ДвиженияПоОснованиям.КоличествоОстаток,
	|	ДвиженияПоОснованиям.СуммаОстаток,
	|	ДвиженияПоОснованиям.Регистратор
	|ИЗ
	|	ДвиженияПоОснованиям КАК ДвиженияПоОснованиям";
	
	ПериодКонтроля = СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата;
	Запрос.УстановитьПараметр("ПериодКонтроля", ПериодКонтроля);
	Запрос.УстановитьПараметр("ТаблицаДокументовОснования", ТаблицаДокументовОснования);
	Запрос.УстановитьПараметр("СебестоимостьПоОстаткам", СебестоимостьПоОстаткам);
	Запрос.УстановитьПараметр("ЭтоОперацияВозврат", ЭтоОперацияВозврат);
	Запрос.УстановитьПараметр("ТекущийДокумент", ДокументСсылкаОтчетКомиссионера);
	Запрос.УстановитьПараметр("МоментКонтроля", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаЗапасыОстатки = РезультатЗапроса.Выгрузить();
	ТаблицаЗапасыОстатки.Индексы.Добавить("Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя, Регистратор");
	
	ВременнаяТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.СкопироватьКолонки();
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы[н];
		
		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("Регистратор", СтрокаТаблицаЗапасы.Документ);
		СтруктураДляПоиска.Вставить("Организация", СтрокаТаблицаЗапасы.Организация);
		СтруктураДляПоиска.Вставить("СтруктурнаяЕдиница", СтрокаТаблицаЗапасы.СтруктурнаяЕдиница);
		СтруктураДляПоиска.Вставить("СчетУчета", СтрокаТаблицаЗапасы.СчетУчета);
		СтруктураДляПоиска.Вставить("Номенклатура", СтрокаТаблицаЗапасы.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика", СтрокаТаблицаЗапасы.Характеристика);
		СтруктураДляПоиска.Вставить("Партия", СтрокаТаблицаЗапасы.Партия);
		СтруктураДляПоиска.Вставить("ЗаказПокупателя", СтрокаТаблицаЗапасы.ЗаказПокупателя);
		
		КоличествоТребуется = ?(ЭтоОперацияВозврат, -СтрокаТаблицаЗапасы.Количество, СтрокаТаблицаЗапасы.Количество);
		
		Если КоличествоТребуется > 0  Тогда
			
			МесяцПродажиИВозвратаСовпадает = Истина;
			
			Если ЭтоОперацияВозврат И СтрокаТаблицаЗапасы.РучнаяСебестоимость > 0 Тогда
				
				// Себестоимость указана явно в документе
				СуммаКСписанию = СтрокаТаблицаЗапасы.РучнаяСебестоимость;
				
			Иначе
				
				МассивСтрокОстатков = ТаблицаЗапасыОстатки.НайтиСтроки(СтруктураДляПоиска);
				
				КоличествоОстаток = 0;
				СуммаОстаток = 0;
				
				Если МассивСтрокОстатков.Количество() > 0 Тогда
					КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток;
					СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток;
				КонецЕсли;
				
				Если ЭтоОперацияВозврат И МассивСтрокОстатков.Количество() > 0 Тогда
					МесяцПродажиИВозвратаСовпадает = (НачалоМесяца(ПериодКонтроля) = НачалоМесяца(МассивСтрокОстатков[0].ДатаОстатка));
				КонецЕсли; 
				
				Если КоличествоОстаток > 0 И КоличествоОстаток > КоличествоТребуется Тогда
					
					СуммаКСписанию = Окр(СуммаОстаток * КоличествоТребуется / КоличествоОстаток , 2, 1);
					
					МассивСтрокОстатков[0].КоличествоОстаток = МассивСтрокОстатков[0].КоличествоОстаток - КоличествоТребуется;
					МассивСтрокОстатков[0].СуммаОстаток = МассивСтрокОстатков[0].СуммаОстаток - СуммаКСписанию;
					
				ИначеЕсли КоличествоОстаток = КоличествоТребуется Тогда
					
					СуммаКСписанию = СуммаОстаток;
					
					МассивСтрокОстатков[0].КоличествоОстаток = 0;
					МассивСтрокОстатков[0].СуммаОстаток = 0;
					
				Иначе
					СуммаКСписанию = 0;
				КонецЕсли;
				
			КонецЕсли;
			
			// Добавим строку по заказу.
			СтрокаТаблицыРасход = ВременнаяТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасы);
			
			СтрокаТаблицыРасход.Сумма = ?(ЭтоОперацияВозврат, -СуммаКСписанию, СуммаКСписанию);
			СтрокаТаблицыРасход.Количество = ?(ЭтоОперацияВозврат, -КоличествоТребуется, КоличествоТребуется);
			
			Если ЭтоОперацияВозврат И НЕ МесяцПродажиИВозвратаСовпадает Тогда
				СтрокаТаблицыРасход.ФиксированнаяСтоимость = Истина;
			КонецЕсли; 
			
			Если Окр(СуммаКСписанию, 2, 1) <> 0 Тогда
				
				// Сформируем проводки.
				СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицаЗапасы);
				СтрокаТаблицаУправленческий.Содержание = ?(ЭтоОперацияВозврат, НСтр("ru = 'Сторнирование себестоимости'"), НСтр("ru='Себестоимость продажи'"));
				СтрокаТаблицаУправленческий.Сумма = ?(ЭтоОперацияВозврат, -СуммаКСписанию, СуммаКСписанию);
				
				Если СтрокаТаблицаЗапасы.УдержатьКомиссионноеВознаграждение Тогда
					
					СтрокаТаблицаУправленческий = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицаУправленческий, СтрокаТаблицаЗапасы);
					СтрокаТаблицаУправленческий.СчетКт = ПланыСчетов.Управленческий.РасчетыСПокупателями;
					СтрокаТаблицаУправленческий.Содержание = ?(ЭтоОперацияВозврат, НСтр("ru = 'Сторнирование комиссионное вознаграждение'"), НСтр("ru='Комиссионное вознаграждение'"));
					СтрокаТаблицаУправленческий.Сумма = ?(ЭтоОперацияВозврат, -СтрокаТаблицаЗапасы.СуммаВознаграждения, СтрокаТаблицаЗапасы.СуммаВознаграждения);
					СтрокаТаблицаУправленческий.СуммаВалКт = ?(ЭтоОперацияВозврат, -СтрокаТаблицаЗапасы.СуммаВознаграждения, СтрокаТаблицаЗапасы.СуммаВознаграждения);
					СтрокаТаблицаУправленческий.ВалютаКт = ДокументСсылкаОтчетКомиссионера.Договор.ВалютаРасчетов;
					
				КонецЕсли;
				
				// Продвигаем себестоимость продаж.
				СтрокаТаблицаПродажи = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПродажи.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицаПродажи, СтрокаТаблицаЗапасы);
				
				СтрокаТаблицаПродажи.Количество = 0;
				СтрокаТаблицаПродажи.Сумма = 0;
				СтрокаТаблицаПродажи.СуммаНДС = 0;
				СтрокаТаблицаПродажи.Себестоимость = ?(ЭтоОперацияВозврат, -СуммаКСписанию, СуммаКСписанию);
				
				Если СтрокаТаблицаЗапасы.УдержатьКомиссионноеВознаграждение Тогда
					
					// Нужно увеличить себестоимость продажи на сумму вознаграждения.
					НоваяСтрока	= СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПродажи.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицаЗапасы);
					
					НоваяСтрока.Количество		= 0;
					НоваяСтрока.Сумма			= 0;
					НоваяСтрока.СуммаНДС		= 0;
					НоваяСтрока.Себестоимость	= СтрокаТаблицаЗапасы.СуммаВознаграждения;
					
				КонецЕсли;
				
				// Продвигаем доходы и расходы.
				СтрокаДоходыИРасходы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДоходыИРасходы, СтрокаТаблицаЗапасы);
				
				СтрокаДоходыИРасходы.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.ПодразделениеПродажи;
				СтрокаДоходыИРасходы.СчетУчета = СтрокаТаблицаЗапасы.СчетУчетаСебестоимость;
				СтрокаДоходыИРасходы.СуммаДоходов = 0;
				СтрокаДоходыИРасходы.СуммаРасходов = ?(ЭтоОперацияВозврат, -СуммаКСписанию, СуммаКСписанию);
				СтрокаДоходыИРасходы.Сумма = ?(ЭтоОперацияВозврат, -СуммаКСписанию, СуммаКСписанию);
				СтрокаДоходыИРасходы.СодержаниеПроводки = ?(ЭтоОперацияВозврат, НСтр("ru = 'Сторнирование расходов'"), НСтр("ru='Отражение расходов'"));
				
				Если ЗначениеЗаполнено(ДокументСсылкаОтчетКомиссионера.Проект) Тогда
					СтрокаДоходыИРасходы.Проект = ДокументСсылкаОтчетКомиссионера.Проект;
				КонецЕсли;
				
				Если СтрокаТаблицаЗапасы.УдержатьКомиссионноеВознаграждение Тогда
					
					// Нужно увеличить себестоимость продажи на сумму вознаграждения.
					НоваяСтрока= СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицаЗапасы);
					
					НоваяСтрока.СтруктурнаяЕдиница = СтрокаТаблицаЗапасы.ПодразделениеПродажи;
					НоваяСтрока.СчетУчета = СтрокаТаблицаЗапасы.СчетУчетаСебестоимость;
					НоваяСтрока.СуммаДоходов = 0;
					НоваяСтрока.СуммаРасходов = СтрокаТаблицаЗапасы.СуммаВознаграждения;
					НоваяСтрока.Сумма = СтрокаТаблицаЗапасы.СуммаВознаграждения;
					НоваяСтрока.СодержаниеПроводки = ?(ЭтоОперацияВозврат, НСтр("ru = 'Сторнирование расходов'"), НСтр("ru='Отражение расходов'"));
					
					Если ЗначениеЗаполнено(ДокументСсылкаОтчетКомиссионера.Проект) Тогда
						СтрокаДоходыИРасходы.Проект = ДокументСсылкаОтчетКомиссионера.Проект;
					КонецЕсли;
				
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
			
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы = ВременнаяТаблицаЗапасы;
	
КонецПроцедуры // СформироватьТаблицаЗапасы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПродажи(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Если ДокументСсылкаОтчетКомиссионера.ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионера Тогда
		Запрос.Текст = ТекстЗапросаОтчетКомиссионераОПродажах();
	Иначе
		Запрос.Текст = ТекстЗапросаОтчетКомиссионераОВозвратах();
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетКомиссионера);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПродажи", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаПродажи()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыПереданные(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыПереданные.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗапасыПереданные.Период КАК Период,
	|	ТаблицаЗапасыПереданные.Организация КАК Организация,
	|	ТаблицаЗапасыПереданные.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыПереданные.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыПереданные.Контрагент КАК Контрагент,
	|	ТаблицаЗапасыПереданные.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасыПереданные.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ТаблицаЗапасыПереданные.ЗаказПокупателя
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Заказ,
	|	ТаблицаЗапасыПереданные.Партия КАК Партия,
	|	ТаблицаЗапасыПереданные.ТипПриемаПередачи КАК ТипПриемаПередачи,
	|	СУММА(ТаблицаЗапасыПереданные.Количество) КАК Количество,
	|	СУММА(ТаблицаЗапасыПереданные.СуммаРасчетовПринятыеПереданные) КАК СуммаРасчетов
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыПереданные
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыПереданные.Период,
	|	ТаблицаЗапасыПереданные.Организация,
	|	ТаблицаЗапасыПереданные.Номенклатура,
	|	ТаблицаЗапасыПереданные.Характеристика,
	|	ТаблицаЗапасыПереданные.Партия,
	|	ТаблицаЗапасыПереданные.Контрагент,
	|	ТаблицаЗапасыПереданные.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасыПереданные.ЗаказПокупателя <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ТаблицаЗапасыПереданные.ЗаказПокупателя
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаЗапасыПереданные.ТипПриемаПередачи";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыПереданные", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыПереданные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходы(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	МАКСИМУМ(ТаблицаДоходыИРасходы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДоходыИРасходы.Период КАК Период,
	|	ТаблицаДоходыИРасходы.Организация КАК Организация,
	|	ТаблицаДоходыИРасходы.ПодразделениеПродажи КАК СтруктурнаяЕдиница,
	|	ТаблицаДоходыИРасходы.Проект КАК Проект,
	|	ТаблицаДоходыИРасходы.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ТаблицаДоходыИРасходы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаДоходыИРасходы.СчетУчетаПродажи КАК СчетУчета,
	|	НЕОПРЕДЕЛЕНО КАК Аналитика,
	|	ВЫРАЗИТЬ(&ОтражениеДоходов КАК СТРОКА(100)) КАК СодержаниеПроводки,
	|	СУММА(ТаблицаДоходыИРасходы.Сумма) КАК СуммаДоходов,
	|	0 КАК СуммаРасходов,
	|	СУММА(ТаблицаДоходыИРасходы.Сумма) КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДоходыИРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДоходыИРасходы.Период,
	|	ТаблицаДоходыИРасходы.Организация,
	|	ТаблицаДоходыИРасходы.ПодразделениеПродажи,
	|	ТаблицаДоходыИРасходы.Проект,
	|	ТаблицаДоходыИРасходы.НаправлениеДеятельностиПродажи,
	|	ТаблицаДоходыИРасходы.ЗаказПокупателя,
	|	ТаблицаДоходыИРасходы.СчетУчетаПродажи
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА &ПоложительнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ &ОтрицательнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	ВЫРАЗИТЬ(&КурсоваяРазница КАК СТРОКА(100)),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Валюта КАК Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.Валюта КАК Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПокупателями
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПокупателями.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПокупателями.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	ЭтоОперацияВозврат = ?(ДокументСсылкаОтчетКомиссионера.ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах, Истина, Ложь);
	
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	Запрос.УстановитьПараметр("ОтражениеДоходов", ?(ЭтоОперацияВозврат, НСтр("ru = 'Сторнирование доходов'"), НСтр("ru='Отражение доходов'")));
	Запрос.УстановитьПараметр("КурсоваяРазница", ?(ЭтоОперацияВозврат, НСтр("ru = 'Сторнирование курсовой разницы'"), НСтр("ru='Курсовая разница'")));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПокупателями(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства)
	
	ВидОперацииВозврат = ДокументСсылкаОтчетКомиссионера.ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетКомиссионера);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ВозникновениеОбязательствПокупателя", ?(ВидОперацииВозврат, НСтр("ru='Сторнирование обязательств покупателя'"), НСтр("ru='Возникновение обязательств покупателя'")));
	Запрос.УстановитьПараметр("ЗачетАванса", НСтр("ru='Зачет предоплаты'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("УчетВалютныхОпераций", СтруктураДополнительныеСвойства.УчетВалютныхОпераций);
	
	Если Не ВидОперацииВозврат Тогда
		Запрос.Текст = ТекстЗапросаОтчетКомиссионераОПродажахРасчетыСПокупателями();
	Иначе
		Запрос.Текст = ТекстЗапросаОтчетКомиссионераОВозвратахРасчетыСПокупателями();
	КонецЕсли;
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПокупателями.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПокупателями.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыСПокупателями.Договор КАК Договор,
	|	ВременнаяТаблицаРасчетыСПокупателями.Документ КАК Документ,
	|	ВременнаяТаблицаРасчетыСПокупателями.Заказ КАК Заказ,
	|	ВременнаяТаблицаРасчетыСПокупателями.ТипРасчетов КАК ТипРасчетов
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПокупателями";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПокупателями");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	Если Не ВидОперацииВозврат Тогда
		РасчетыПроведениеДокументов.ПодготовитьТаблицуВзаиморасчетовСУчетомАвансов(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства, Ложь);
	КонецЕсли;
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПокупателями(Запрос.МенеджерВременныхТаблиц, Истина, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПокупателями", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыСПокупателями()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Если ДокументСсылкаОтчетКомиссионера.ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионера Тогда
		Запрос.Текст = ТекстЗапросаОтчетКомиссионераОПродажахДоходыИРасходыОтложенные();
	Иначе
		Запрос.Текст = ТекстЗапросаОтчетКомиссионераОВозвратахДоходыИРасходыОтложенные();
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетКомиссионера);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаЗапасыДоходыИРасходыОтложенные = МассивРезультатов[0].Выгрузить();
	ВыборкаРезультатаЗапроса = МассивРезультатов[1].Выбрать();

	ТаблицаПредоплатаДоходыИРасходыОтложенные = ТаблицаЗапасыДоходыИРасходыОтложенные.Скопировать();
	ТаблицаПредоплатаДоходыИРасходыОтложенные.Очистить();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		СуммаКСписанию = ВыборкаРезультатаЗапроса.СуммаКСписанию;
		Для каждого СтрокаЗапасыДоходыИРасходыОтложенные Из ТаблицаЗапасыДоходыИРасходыОтложенные Цикл
			
			Если СуммаКСписанию = 0 Тогда
				Продолжить
			Иначе
				
				Если ДокументСсылкаОтчетКомиссионера.ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионера Тогда
					
					Если СтрокаЗапасыДоходыИРасходыОтложенные.СуммаДоходов <= СуммаКСписанию Тогда
						СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
						СуммаКСписанию = СуммаКСписанию - СтрокаЗапасыДоходыИРасходыОтложенные.СуммаДоходов;
					ИначеЕсли СтрокаЗапасыДоходыИРасходыОтложенные.СуммаДоходов > СуммаКСписанию Тогда
						СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
						СтрокаПредоплатаДоходыИРасходыОтложенные.СуммаДоходов = СуммаКСписанию;
						СуммаКСписанию = 0;
					КонецЕсли;
					
				Иначе
					
					Если СтрокаЗапасыДоходыИРасходыОтложенные.СуммаДоходов > СуммаКСписанию Тогда
						СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
						СуммаКСписанию = СуммаКСписанию - СтрокаЗапасыДоходыИРасходыОтложенные.СуммаДоходов;
					ИначеЕсли СтрокаЗапасыДоходыИРасходыОтложенные.СуммаДоходов <= СуммаКСписанию Тогда
						СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
						СтрокаПредоплатаДоходыИРасходыОтложенные.СуммаДоходов = СуммаКСписанию;
						СуммаКСписанию = 0;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтрокаПредоплатаДоходыИРасходыОтложенные Из ТаблицаПредоплатаДоходыИРасходыОтложенные Цикл
		СтрокаЗапасыДоходыИРасходыОтложенные = ТаблицаЗапасыДоходыИРасходыОтложенные.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗапасыДоходыИРасходыОтложенные, СтрокаПредоплатаДоходыИРасходыОтложенные);
		СтрокаЗапасыДоходыИРасходыОтложенные.ВидДвижения = ВидДвиженияНакопления.Расход;
	КонецЦикла;
	
	ВыборкаРезультатаЗапроса = МассивРезультатов[2].Выбрать();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		Статья = ВыборкаРезультатаЗапроса.Статья;
	Иначе
		Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей;
	КонецЕсли;

	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	Таблица.Период КАК Период,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Документ КАК Документ,
	|	&Статья КАК Статья,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Таблица.СуммаДоходов КАК СуммаДоходов
	|ПОМЕСТИТЬ ВременнаяТаблицаПредоплатаДоходыИРасходыОтложенные
	|ИЗ
	|	&Таблица КАК Таблица";
	Запрос.УстановитьПараметр("Таблица", ТаблицаПредоплатаДоходыИРасходыОтложенные);
	Запрос.УстановитьПараметр("Статья", Статья);
	
	Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыОтложенные", ТаблицаЗапасыДоходыИРасходыОтложенные);
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыОтложенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ТаблицаДокумента.Сумма КАК СуммаДоходов
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыНераспределенные", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыНераспределенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетКомиссионера);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.ДокументДата КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	-ТаблицаДокумента.Сумма КАК СуммаДоходов
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Период,
	|	Таблица.Организация,
	|	Таблица.НаправлениеДеятельности,
	|	Таблица.Статья,
	|	Таблица.СуммаДоходов
	|ИЗ
	|	ВременнаяТаблицаПредоплатаДоходыИРасходыОтложенные КАК Таблица";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыКассовыйМетод", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыКассовыйМетод()

// Формирует таблицу значений, содержащую данные для проведения по регистру "ОплатаДокументов".
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаОплатаДокументов(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	МассивДоступныхОпераций = Неопределено;
	РасчетыПроведениеДокументов.СформироватьТаблицаОплатаДокументовНакладные(ДокументСсылка, СтруктураДополнительныеСвойства, МассивДоступныхОпераций);
	
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОтчетКомиссионера.Договор.ЭтоДоговорОбслуживания
	|				И ОтчетКомиссионера.Договор.ДоговорОбслуживанияНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВестиУчетРасходовПоДоговорамОбслуживания,
	|	ОтчетКомиссионера.Договор.ДоговорОбслуживанияНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОтчетКомиссионера.Контрагент КАК Контрагент,
	|	ОтчетКомиссионера.Дата КАК Дата,
	|	ОтчетКомиссионера.Договор КАК Договор,
	|	ОтчетКомиссионера.УдержатьКомиссионноеВознаграждение КАК УдержатьКомиссионноеВознаграждение,
	|	ОтчетКомиссионера.Подразделение КАК Подразделение,
	|	ОтчетКомиссионера.Ответственный КАК Ответственный,
	|	ОтчетКомиссионера.НДСВключатьВСтоимость КАК НДСВключатьВСтоимость,
	|	ОтчетКомиссионера.ВалютаДокумента КАК ВалютаДокумента,
	|	ОтчетКомиссионера.Курс КАК Курс,
	|	ОтчетКомиссионера.Кратность КАК Кратность,
	|	ОтчетКомиссионера.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ОтчетКомиссионера.Проект КАК Проект
	|ПОМЕСТИТЬ ВременнаяТаблицаШапка
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК ОтчетКомиссионера
	|ГДЕ
	|	ОтчетКомиссионера.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Ссылка КАК Ссылка,
	|	ОтчетКомиссионераЗапасы.НомерСтроки КАК НомерСтроки,
	|	ОтчетКомиссионераЗапасы.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|			ТОГДА ОтчетКомиссионераЗапасы.Количество * -1
	|		ИНАЧЕ ОтчетКомиссионераЗапасы.Количество
	|	КОНЕЦ КАК Количество,
	|	ОтчетКомиссионераЗапасы.Характеристика КАК Характеристика,
	|	ОтчетКомиссионераЗапасы.СерийныеНомера КАК СерийныеНомера,
	|	ОтчетКомиссионераЗапасы.Цена КАК Цена,
	|	ВЫБОР
	|		КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|			ТОГДА ОтчетКомиссионераЗапасы.Сумма * -1
	|		ИНАЧЕ ОтчетКомиссионераЗапасы.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ОтчетКомиссионераЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|			ТОГДА ОтчетКомиссионераЗапасы.СуммаНДС * -1
	|		ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаНДС
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|			ТОГДА ОтчетКомиссионераЗапасы.Всего * -1
	|		ИНАЧЕ ОтчетКомиссионераЗапасы.Всего
	|	КОНЕЦ КАК Всего,
	|	ОтчетКомиссионераЗапасы.ЦенаПередачи КАК ЦенаПередачи,
	|	ВЫБОР
	|		КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|			ТОГДА ОтчетКомиссионераЗапасы.СуммаПередачи * -1
	|		ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаПередачи
	|	КОНЕЦ КАК СуммаПередачи,
	|	ВЫБОР
	|		КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|			ТОГДА ОтчетКомиссионераЗапасы.СуммаНДСПередачи * -1
	|		ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаНДСПередачи
	|	КОНЕЦ КАК СуммаНДСПередачи,
	|	ВЫБОР
	|		КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|			ТОГДА ОтчетКомиссионераЗапасы.СуммаВознаграждения * -1
	|		ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаВознаграждения
	|	КОНЕЦ КАК СуммаВознаграждения,
	|	ВЫБОР
	|		КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|			ТОГДА ОтчетКомиссионераЗапасы.СуммаНДСВознаграждения * -1
	|		ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаНДСВознаграждения
	|	КОНЕЦ КАК СуммаНДСВознаграждения,
	|	ОтчетКомиссионераЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ОтчетКомиссионераЗапасы.Партия КАК Партия,
	|	ОтчетКомиссионераЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОтчетКомиссионераЗапасы.КлючСвязи КАК КлючСвязи,
	|	ОтчетКомиссионераЗапасы.Себестоимость КАК Себестоимость,
	|	ОтчетКомиссионераЗапасы.КлючСвязиСерийныеНомера КАК КлючСвязиСерийныеНомера,
	|	ОтчетКомиссионераЗапасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ОтчетКомиссионераЗапасы.НомерГТД КАК НомерГТД,
	|	ОтчетКомиссионераЗапасы.ПрямоеСписаниеГТД КАК ПрямоеСписаниеГТД
	|ПОМЕСТИТЬ ОтчетКомиссионераЗапасы
	|ИЗ
	|	&ОтчетКомиссионераЗапасыРазвернутая КАК ОтчетКомиссионераЗапасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетКомиссионераЗапасы.НомерСтроки КАК НомерСтроки,
	|	ОтчетКомиссионераЗапасы.КлючСвязи КАК КлючСвязи,
	|	ОтчетКомиссионераЗапасы.КлючСвязиСерийныеНомера КАК КлючСвязиСерийныеНомера,
	|	ОтчетКомиссионераЗапасы.Ссылка КАК Документ,
	|	ВременнаяТаблицаШапка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаШапка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ВременнаяТаблицаШапка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ВременнаяТаблицаШапка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ВременнаяТаблицаШапка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ВременнаяТаблицаШапка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	ВременнаяТаблицаШапка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ВременнаяТаблицаШапка.Договор КАК Договор,
	|	ВременнаяТаблицаШапка.Контрагент КАК СтруктурнаяЕдиница,
	|	ВременнаяТаблицаШапка.УдержатьКомиссионноеВознаграждение КАК УдержатьКомиссионноеВознаграждение,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПередачаКомиссионеру) КАК ТипПриемаПередачи,
	|	ВременнаяТаблицаШапка.Подразделение КАК ПодразделениеПродажи,
	|	ВременнаяТаблицаШапка.Ответственный КАК Ответственный,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности
	|		ИНАЧЕ НоменклатураЗапасов.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиПродажи,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж
	|		ИНАЧЕ НоменклатураЗапасов.НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж
	|	КОНЕЦ КАК СчетУчетаПродажи,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж
	|		ИНАЧЕ НоменклатураЗапасов.НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж
	|	КОНЕЦ КАК СчетУчетаСебестоимость,
	|	НоменклатураЗапасов.СчетУчетаЗапасов КАК СчетУчета,
	|	ОтчетКомиссионераЗапасы.Номенклатура КАК Номенклатура,
	|	ОтчетКомиссионераЗапасы.НомерГТД КАК НомерГТД,
	|	ОтчетКомиссионераЗапасы.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА ОтчетКомиссионераЗапасы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|			ТОГДА ОтчетКомиссионераЗапасы.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ОтчетКомиссионераЗапасы.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА ОтчетКомиссионераЗапасы.Количество
	|		ИНАЧЕ ОтчетКомиссионераЗапасы.Количество * ЕдиницыИзмерения.Коэффициент
	|	КОНЕЦ КАК Количество,
	|	ОтчетКомиссионераЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|						ТОГДА ОтчетКомиссионераЗапасы.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаНДС * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ОтчетКомиссионераЗапасы.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаНДС * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСПродажи,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ОтчетКомиссионераЗапасы.Всего * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ОтчетКомиссионераЗапасы.Всего * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|						ТОГДА ОтчетКомиссионераЗапасы.СуммаНДС * КурсыРегВалюты.Курс * ВременнаяТаблицаШапка.Кратность / (ВременнаяТаблицаШапка.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаНДС
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ОтчетКомиссионераЗапасы.Всего * КурсыРегВалюты.Курс * ВременнаяТаблицаШапка.Кратность / (ВременнаяТаблицаШапка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ОтчетКомиссионераЗапасы.Всего
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ОтчетКомиссионераЗапасы.Всего
	|			ИНАЧЕ ОтчетКомиссионераЗапасы.Всего * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРег,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|						ТОГДА ОтчетКомиссионераЗапасы.СуммаНДСПередачи * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаНДСПередачи * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СебестоимостьНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ВЫБОР
	|						КОГДА ВременнаяТаблицаШапка.СуммаВключаетНДС
	|							ТОГДА ОтчетКомиссионераЗапасы.СуммаПередачи * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|						ИНАЧЕ (ОтчетКомиссионераЗапасы.СуммаПередачи + ОтчетКомиссионераЗапасы.СуммаНДСПередачи) * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.СуммаВключаетНДС
	|						ТОГДА ОтчетКомиссионераЗапасы.СуммаПередачи * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|					ИНАЧЕ (ОтчетКомиссионераЗапасы.СуммаПередачи + ОтчетКомиссионераЗапасы.СуммаНДСПередачи) * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Себестоимость,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|						ТОГДА ОтчетКомиссионераЗапасы.СуммаНДСВознаграждения * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаНДСВознаграждения * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСВознаграждения,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ВЫБОР
	|						КОГДА ВременнаяТаблицаШапка.СуммаВключаетНДС
	|							ТОГДА ОтчетКомиссионераЗапасы.СуммаВознаграждения * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|						ИНАЧЕ (ОтчетКомиссионераЗапасы.СуммаВознаграждения + ОтчетКомиссионераЗапасы.СуммаНДСВознаграждения) * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.СуммаВключаетНДС
	|						ТОГДА ОтчетКомиссионераЗапасы.СуммаВознаграждения * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|					ИНАЧЕ (ОтчетКомиссионераЗапасы.СуммаВознаграждения + ОтчетКомиссионераЗапасы.СуммаНДСВознаграждения) * ВременнаяТаблицаШапка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ВременнаяТаблицаШапка.Кратность)
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаВознаграждения,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ВЫБОР
	|						КОГДА ВременнаяТаблицаШапка.СуммаВключаетНДС
	|							ТОГДА ОтчетКомиссионераЗапасы.СуммаВознаграждения * КурсыРегВалюты.Курс * ВременнаяТаблицаШапка.Кратность / (ВременнаяТаблицаШапка.Курс * КурсыРегВалюты.Кратность)
	|						ИНАЧЕ (ОтчетКомиссионераЗапасы.СуммаВознаграждения + ОтчетКомиссионераЗапасы.СуммаНДСВознаграждения) * КурсыРегВалюты.Курс * ВременнаяТаблицаШапка.Кратность / (ВременнаяТаблицаШапка.Курс * КурсыРегВалюты.Кратность)
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.СуммаВключаетНДС
	|						ТОГДА ОтчетКомиссионераЗапасы.СуммаВознаграждения
	|					ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаВознаграждения + ОтчетКомиссионераЗапасы.СуммаНДСВознаграждения
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаВознагражденияВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА ВЫБОР
	|						КОГДА ВременнаяТаблицаШапка.СуммаВключаетНДС
	|							ТОГДА ОтчетКомиссионераЗапасы.СуммаВознаграждения
	|						ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаВознаграждения + ОтчетКомиссионераЗапасы.СуммаНДСВознаграждения
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВременнаяТаблицаШапка.СуммаВключаетНДС
	|						ТОГДА ОтчетКомиссионераЗапасы.СуммаВознаграждения * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность
	|					ИНАЧЕ (ОтчетКомиссионераЗапасы.СуммаВознаграждения + ОтчетКомиссионераЗапасы.СуммаНДСВознаграждения) * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаВознагражденияРег,
	|	ОтчетКомиссионераЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВременнаяТаблицаШапка.СуммаВключаетНДС
	|				ТОГДА ОтчетКомиссионераЗапасы.СуммаПередачи
	|			ИНАЧЕ ОтчетКомиссионераЗапасы.СуммаПередачи + ОтчетКомиссионераЗапасы.СуммаНДСПередачи
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРасчетовПринятыеПереданные,
	|	ОтчетКомиссионераЗапасы.ПрямоеСписаниеГТД КАК ПрямоеСписаниеГТД,
	|	ОтчетКомиссионераЗапасы.Себестоимость КАК РучнаяСебестоимость,
	|	ВременнаяТаблицаШапка.Проект КАК Проект
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасы
	|ИЗ
	|	ОтчетКомиссионераЗапасы КАК ОтчетКомиссионераЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыУпрВалюты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаНациональнаяВалюта.Значение
	|					ИЗ
	|						Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта)) КАК КурсыРегВалюты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураЗапасов
	|		ПО ОтчетКомиссионераЗапасы.Номенклатура = НоменклатураЗапасов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ОтчетКомиссионераЗапасы.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка,
	|	Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОтчетКомиссионераЗапасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДокумента.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.Ссылка.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПокупателя КАК СчетУчетаАвансовПокупателя,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|	ТаблицаДокумента.Ссылка.Договор КАК Договор,
	|	ТаблицаДокумента.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Документ) = ТИП(Документ.Взаимозачет)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей)
	|		ИНАЧЕ ТаблицаДокумента.Документ.Статья
	|	КОНЕЦ КАК Статья,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ.Дата
	|		ИНАЧЕ ТаблицаДокумента.Ссылка.Дата
	|	КОНЕЦ КАК ДокументДата,
	|	ТаблицаДокумента.Заказ КАК Заказ,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее) КАК НаправлениеДеятельностиПродажи,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс) КАК ТипРасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг) КАК ТипРасчетовКуда,
	|	&Ссылка КАК ДокументКуда,
	|	ТаблицаДокумента.Документ КАК Документ,
	|	СУММА(ВЫБОР
	|			КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|				ТОГДА -1 * (ВЫРАЗИТЬ(ТаблицаДокумента.СуммаРасчетов * ТаблицаДокумента.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * ТаблицаДокумента.Кратность) КАК ЧИСЛО(15, 2)))
	|			ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаРасчетов * ТаблицаДокумента.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * ТаблицаДокумента.Кратность) КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов * -1
	|			ИНАЧЕ ТаблицаДокумента.СуммаРасчетов
	|		КОНЕЦ) КАК СуммаВал,
	|	СУММА(ВЫБОР
	|			КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|				ТОГДА ВЫБОР
	|						КОГДА ТаблицаДокумента.Ссылка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|							ТОГДА ТаблицаДокумента.СуммаРасчетов * -1
	|						ИНАЧЕ -1 * (ВЫРАЗИТЬ(ТаблицаДокумента.СуммаРасчетов * ТаблицаДокумента.Курс / ТаблицаДокумента.Кратность КАК ЧИСЛО(15, 2)))
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ТаблицаДокумента.Ссылка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|						ТОГДА ТаблицаДокумента.СуммаРасчетов
	|					ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаРасчетов * ТаблицаДокумента.Курс / ТаблицаДокумента.Кратность КАК ЧИСЛО(15, 2))
	|				КОНЕЦ
	|		КОНЕЦ) КАК СуммаРег,
	|	ТаблицаДокумента.ОтчетКомиссионера КАК ОтчетКомиссионера
	|ПОМЕСТИТЬ ВременнаяТаблицаПредоплата
	|ИЗ
	|	Документ.ОтчетКомиссионера.Предоплата КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыВалютУчетаСрезПоследних
	|		ПО (ИСТИНА),
	|	Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.Документ,
	|	ТаблицаДокумента.Ссылка.Дата,
	|	ТаблицаДокумента.Ссылка.Контрагент,
	|	ТаблицаДокумента.Ссылка.Договор,
	|	ТаблицаДокумента.Заказ,
	|	ТаблицаДокумента.Ссылка.Договор.ВалютаРасчетов,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПокупателя,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПоставщику,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|				ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Документ) = ТИП(Документ.Взаимозачет)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей)
	|		ИНАЧЕ ТаблицаДокумента.Документ.Статья
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ.Дата
	|		ИНАЧЕ ТаблицаДокумента.Ссылка.Дата
	|	КОНЕЦ,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДоговорам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоЗаказам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам,
	|	ТаблицаДокумента.ОтчетКомиссионера
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетКомиссионераСерийныеНомера.КлючСвязи КАК КлючСвязи,
	|	ОтчетКомиссионераСерийныеНомера.СерийныйНомер КАК СерийныйНомер
	|ПОМЕСТИТЬ ВременнаяТаблицаСерийныеНомера
	|ИЗ
	|	Документ.ОтчетКомиссионера.СерийныеНомера КАК ОтчетКомиссионераСерийныеНомера
	|ГДЕ
	|	ОтчетКомиссионераСерийныеНомера.Ссылка = &Ссылка
	|	И &ИспользоватьСерийныеНомера
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетКомиссионераПокупатели.Ссылка КАК Ссылка,
	|	ОтчетКомиссионераПокупатели.НомерСтроки КАК НомерСтроки,
	|	ОтчетКомиссионераПокупатели.Покупатель КАК Покупатель,
	|	ОтчетКомиссионераПокупатели.Всего КАК Всего,
	|	ОтчетКомиссионераПокупатели.ВыставленСФ КАК ВыставленСФ,
	|	ОтчетКомиссионераПокупатели.ДатаСФ КАК ДатаСФ,
	|	ОтчетКомиссионераПокупатели.СчетФактура КАК СчетФактура,
	|	ОтчетКомиссионераПокупатели.КлючСвязи КАК КлючСвязи,
	|	ОтчетКомиссионераПокупатели.ОтчетКомиссионера КАК ОтчетКомиссионера
	|ПОМЕСТИТЬ ВременнаяТаблицаПокупатели
	|ИЗ
	|	Документ.ОтчетКомиссионера.Покупатели КАК ОтчетКомиссионераПокупатели
	|ГДЕ
	|	ОтчетКомиссионераПокупатели.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА НЕ ОтчетКомиссионераПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|				И ОтчетКомиссионераПокупатели.ОтчетКомиссионера.Проведен
	|			ТОГДА ОтчетКомиссионераПокупатели.ОтчетКомиссионера
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|	КОНЕЦ КАК ОтчетКомиссионера
	|ПОМЕСТИТЬ ВременнаяТаблицаПокупателиДокументыВозврата
	|ИЗ
	|	Документ.ОтчетКомиссионера.Покупатели КАК ОтчетКомиссионераПокупатели
	|ГДЕ
	|	ОтчетКомиссионераПокупатели.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетКомиссионера);
	Запрос.УстановитьПараметр("ВидОперации", ДокументСсылкаОтчетКомиссионера.ВидОперации);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ИспользоватьПартии", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьПартии);
	
	ОтчетКомиссионераЗапасы = РаспределеннаяТаблицаЗапасы(ДокументСсылкаОтчетКомиссионера);
	Запрос.УстановитьПараметр("ОтчетКомиссионераЗапасыРазвернутая", ОтчетКомиссионераЗапасы);
	
	Запрос.УстановитьПараметр("ИспользоватьСерийныеНомера", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьСерийныеНомера);
	
	Запрос.ВыполнитьПакет(); 
	
	// Формирование проводок документа.
	УправлениеНебольшойФирмойСервер.СформироватьТаблицуПроводок(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаРасчетыСПокупателями(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПродажи(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыПереданные(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаДоходыИРасходы(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаЗапасы(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыВРазрезеГТД(СтруктураДополнительныеСвойства);
	
	// Серийные номера
	СформироватьТаблицаСерийныеНомера(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаОплатаДокументов(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаУправленческий(ДокументСсылкаОтчетКомиссионера, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента() 

Функция РаспределеннаяТаблицаЗапасы(ДокументСсылкаОтчетКомиссионера) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетКомиссионера);
	
	ЭтоВозврат = ?(ДокументСсылкаОтчетКомиссионера.ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах, Истина, Ложь);
	
	Если Не ЭтоВозврат Тогда
		
		Запрос.УстановитьПараметр("Организация", ДокументСсылкаОтчетКомиссионера.Организация);
		Запрос.УстановитьПараметр("Контрагент", ДокументСсылкаОтчетКомиссионера.Контрагент);
		Запрос.УстановитьПараметр("Договор", ДокументСсылкаОтчетКомиссионера.Договор);
		Запрос.УстановитьПараметр("МоментВремени", Новый Граница(ДокументСсылкаОтчетКомиссионера.МоментВремени(), ВидГраницы.Включая));
		
		Запрос.Текст = ТекстЗапросаРаспределениеОтчетКомиссионераОПродажах();
		ИндексОперации = 0;
		
	Иначе
		Запрос.Текст = ТекстЗапросаРаспределениеОтчетКомиссионераОВозвратах();
		ИндексОперации = 2;
	КонецЕсли;
	
	ВыборкаПакет = Запрос.ВыполнитьПакет();
	
	ТаблицаКРаспределению = ВыборкаПакет[0 + ИндексОперации].Выгрузить();
	ТаблицаРаспределенная = ВыборкаПакет[1 + ИндексОперации].Выгрузить();
	ОстаткиУКомиссионера  = ВыборкаПакет[3 + ИндексОперации].Выгрузить();
	
	ОтчетКомиссионераЗапасы = ДокументСсылкаОтчетКомиссионера.Запасы.Выгрузить();
	
	Если Не ТаблицаКРаспределению.Количество() Тогда
		Возврат ОтчетКомиссионераЗапасы;
	КонецЕсли;
	
	ИспользоватьСерийныеНомераОстатки = РаботаССерийнымиНомерами.ИспользоватьСерийныеНомераОстатки();
	
	Если ЭтоВозврат Тогда
		ПараметрыОтбораОстатки = Новый Структура("Номенклатура, Характеристика, Партия, ОтчетКомиссионера, КлючСвязи");
		ПараметрыОтбораЗапасы = Новый Структура("Номенклатура, Характеристика, Партия, КлючСвязи");
	Иначе
		ПараметрыОтбораОстатки = Новый Структура("Номенклатура, Характеристика, Партия");
		ПараметрыОтбораЗапасы = Новый Структура("Номенклатура, Характеристика, Партия");
	КонецЕсли;
	
	СтрокиКУдалению = Новый СписокЗначений;
	
	Для Каждого СтрокаКРаспределению Из ТаблицаКРаспределению Цикл
		
		ПараметрыОтбораОстатки.Номенклатура = СтрокаКРаспределению.Номенклатура;
		ПараметрыОтбораОстатки.Характеристика = СтрокаКРаспределению.Характеристика;
		ПараметрыОтбораОстатки.Партия = СтрокаКРаспределению.Партия;
		
		ПараметрыОтбораЗапасы.Номенклатура = СтрокаКРаспределению.Номенклатура;
		ПараметрыОтбораЗапасы.Характеристика = СтрокаКРаспределению.Характеристика;
		ПараметрыОтбораЗапасы.Партия = СтрокаКРаспределению.Партия;
		
		Если ЭтоВозврат Тогда
			ПараметрыОтбораОстатки.ОтчетКомиссионера = СтрокаКРаспределению.ОтчетКомиссионера;
			ПараметрыОтбораОстатки.КлючСвязи = СтрокаКРаспределению.КлючСвязи;
			
			ПараметрыОтбораЗапасы.КлючСвязи = СтрокаКРаспределению.КлючСвязи;
		КонецЕсли;
		
		// Если по комбинации есть строки с выбранным Заказом, значит пользователь сознательно
		// оставил эту строку без заказа.
		ЕстьРаспределение = ТаблицаРаспределенная.НайтиСтроки(ПараметрыОтбораОстатки);
		Если ЕстьРаспределение.Количество() > 0 Тогда Продолжить КонецЕсли;
		
		// Если нет доступных остатков в разрезе Заказов, тогда оставляем без изменения
		ДоступныеОстаткиПоНоменклатуре = ОстаткиУКомиссионера.НайтиСтроки(ПараметрыОтбораОстатки);
		Если Не ДоступныеОстаткиПоНоменклатуре.Количество() Тогда Продолжить КонецЕсли;
		
		СтрокиТаблицаЗапасы = ОтчетКомиссионераЗапасы.НайтиСтроки(ПараметрыОтбораЗапасы);
		
		Для Каждого СтрокаТаблицыЗапасы Из СтрокиТаблицаЗапасы Цикл
			
			Коэффициент = ?(ТипЗнч(СтрокаТаблицыЗапасы.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения"), СтрокаТаблицыЗапасы.ЕдиницаИзмерения.Коэффициент, 1);
			РаспределяемоеКоличествоСтроки = СтрокаТаблицыЗапасы.Количество*Коэффициент;
			РаспределяемоеКоличествоСебестоимость = СтрокаТаблицыЗапасы.Себестоимость;
			
			СтавкаНДС = ?(ЗначениеЗаполнено(СтрокаТаблицыЗапасы.СтавкаНДС), СтрокаТаблицыЗапасы.СтавкаНДС.Ставка, 0);
			
			ИндексСтрокиОстатка = 0;
			
			Для Каждого СтрокаОстатка Из ДоступныеОстаткиПоНоменклатуре Цикл
				
				Если РаспределяемоеКоличествоСтроки <= СтрокаОстатка.Количество Тогда
					
					СтрокаТаблицыЗапасы.ЗаказПокупателя = СтрокаОстатка.ЗаказПокупателя;
					СтрокаОстатка.Количество = СтрокаОстатка.Количество - РаспределяемоеКоличествоСтроки;
					
					Если СтрокаОстатка.Количество = 0 Тогда СтрокиКУдалению.Добавить(ИндексСтрокиОстатка) КонецЕсли;
					
					Прервать;
					
				КонецЕсли;
				
				НоваяСтрока = ОтчетКомиссионераЗапасы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыЗапасы);
				
				ЕдиницаСебестоимости = ?(РаспределяемоеКоличествоСебестоимость <= 0, 0, РаспределяемоеКоличествоСебестоимость / НоваяСтрока.Количество);
				
				НоваяСтрока.Количество = СтрокаОстатка.Количество;
				
				НоваяСтрока.Себестоимость = ЕдиницаСебестоимости * НоваяСтрока.Количество;
				РаспределяемоеКоличествоСебестоимость = РаспределяемоеКоличествоСебестоимость - НоваяСтрока.Себестоимость;
				
				СтрокаОстатка.Количество = 0;
				СтрокиКУдалению.Добавить(ИндексСтрокиОстатка);
				НоваяСтрока.ЗаказПокупателя = СтрокаОстатка.ЗаказПокупателя;
				
				РаспределяемоеКоличествоСтроки = РаспределяемоеКоличествоСтроки - НоваяСтрока.Количество*Коэффициент;
				
				СтрокаТаблицыЗапасы.Количество = РаспределяемоеКоличествоСтроки/Коэффициент;
				
				РаботаСКомиссионерамиКомитентамиСервер.РассчитатьСуммуВСтрокеТабличнойЧастиЗапасыОтчетКомиссионера(ДокументСсылкаОтчетКомиссионера, ИспользоватьСерийныеНомераОстатки, СтрокаТаблицыЗапасы);
				РаботаСКомиссионерамиКомитентамиСервер.РассчитатьСуммуВСтрокеТабличнойЧастиЗапасыОтчетКомиссионера(ДокументСсылкаОтчетКомиссионера, ИспользоватьСерийныеНомераОстатки, НоваяСтрока);
				
				СтрокаТаблицыЗапасы.СуммаПередачи = Окр(СтрокаТаблицыЗапасы.ЦенаПередачи * СтрокаТаблицыЗапасы.Количество, 2, РежимОкругления.Окр15как20);
				
				СтрокаТаблицыЗапасы.СуммаНДСПередачи = ?(
				ДокументСсылкаОтчетКомиссионера.СуммаВключаетНДС,
				СтрокаТаблицыЗапасы.СуммаПередачи - (СтрокаТаблицыЗапасы.СуммаПередачи) / ((СтавкаНДС + 100) / 100),
				СтрокаТаблицыЗапасы.СуммаПередачи * СтавкаНДС / 100
				);
				
				НоваяСтрока.СуммаПередачи = СтрокаОстатка.СуммаРасчетов;
				
				НоваяСтрока.СуммаНДСПередачи = ?(
				ДокументСсылкаОтчетКомиссионера.СуммаВключаетНДС,
				НоваяСтрока.СуммаПередачи - (НоваяСтрока.СуммаПередачи) / ((СтавкаНДС + 100) / 100),
				НоваяСтрока.СуммаПередачи * СтавкаНДС / 100
				);
				
				РаботаСКомиссионерамиКомитентамиСервер.РассчитатьКомиссионноеВознаграждениеЗапасыОтчетКомиссионера(ДокументСсылкаОтчетКомиссионера, СтрокаТаблицыЗапасы);
				РаботаСКомиссионерамиКомитентамиСервер.РассчитатьСуммуВСтрокеТабличнойЧастиЗапасыОтчетКомиссионера(ДокументСсылкаОтчетКомиссионера, ИспользоватьСерийныеНомераОстатки, НоваяСтрока);
				
				ИндексСтрокиОстатка = ИндексСтрокиОстатка + 1;
				
			КонецЦикла;
			
			СтрокиКУдалению.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
			
			Если СтрокиКУдалению.Количество() Тогда
				Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
					ДоступныеОстаткиПоНоменклатуре.Удалить(СтрокаКУдалению.Значение)
				КонецЦикла;
				СтрокиКУдалению.Очистить();
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ОтчетКомиссионераЗапасы;
	
КонецФункции

Функция ТекстЗапросаРаспределениеОтчетКомиссионераОПродажах()
	
	ЗапросТекст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетКомиссионераЗапасы.Номенклатура КАК Номенклатура,
	|	ОтчетКомиссионераЗапасы.Характеристика КАК Характеристика,
	|	ОтчетКомиссионераЗапасы.Партия КАК Партия,
	|	ОтчетКомиссионераЗапасы.Себестоимость КАК Себестоимость
	|ИЗ
	|	Документ.ОтчетКомиссионера.Запасы КАК ОтчетКомиссионераЗапасы
	|ГДЕ
	|	ОтчетКомиссионераЗапасы.Ссылка = &Ссылка
	|	И ОтчетКомиссионераЗапасы.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	И НЕ ОтчетКомиссионераЗапасы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетКомиссионераЗапасы.Номенклатура КАК Номенклатура,
	|	ОтчетКомиссионераЗапасы.Характеристика КАК Характеристика,
	|	ОтчетКомиссионераЗапасы.Партия КАК Партия,
	|	ОтчетКомиссионераЗапасы.ЗаказПокупателя КАК Заказ,
	|	ОтчетКомиссионераЗапасы.Себестоимость КАК Себестоимость
	|ИЗ
	|	Документ.ОтчетКомиссионера.Запасы КАК ОтчетКомиссионераЗапасы
	|ГДЕ
	|	ОтчетКомиссионераЗапасы.Ссылка = &Ссылка
	|	И НЕ ОтчетКомиссионераЗапасы.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	И НЕ ОтчетКомиссионераЗапасы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗапасыПереданныеОстатки.Номенклатура КАК Номенклатура,
	|	ЗапасыПереданныеОстатки.Характеристика КАК Характеристика,
	|	ЗапасыПереданныеОстатки.Партия КАК Партия,
	|	ЗапасыПереданныеОстатки.Заказ КАК ЗаказПокупателя,
	|	ЗапасыПереданныеОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ЗапасыПереданныеОстатки.Заказ.ДатаОтгрузки ЕСТЬ NULL
	|			ТОГДА ДАТАВРЕМЯ(2099, 12, 31, 23, 59, 59)
	|		КОГДА ЗапасыПереданныеОстатки.Заказ.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ЗапасыПереданныеОстатки.Заказ.Дата
	|		ИНАЧЕ ЗапасыПереданныеОстатки.Заказ.ДатаОтгрузки
	|	КОНЕЦ КАК Дата,
	|	ЗапасыПереданныеОстатки.КоличествоОстаток КАК Количество,
	|	ЗапасыПереданныеОстатки.СуммаРасчетовОстаток КАК СуммаРасчетов
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	РегистрНакопления.ЗапасыПереданные.Остатки(
	|			&МоментВремени,
	|			Организация = &Организация
	|				И Контрагент = &Контрагент
	|				И Договор = &Договор
	|				И НЕ Заказ = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|				И ТипПриемаПередачи = ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПередачаКомиссионеру)
	|				И НЕ (Номенклатура, Характеристика, Партия, Заказ) В
	|						(ВЫБРАТЬ
	|							ОтчетКомиссионераЗапасы.Номенклатура КАК Номенклатура,
	|							ОтчетКомиссионераЗапасы.Характеристика КАК Характеристика,
	|							ОтчетКомиссионераЗапасы.Партия КАК Партия,
	|							ОтчетКомиссионераЗапасы.ЗаказПокупателя КАК Заказ
	|						ИЗ
	|							Документ.ОтчетКомиссионера.Запасы КАК ОтчетКомиссионераЗапасы
	|						ГДЕ
	|							ОтчетКомиссионераЗапасы.Ссылка = &Ссылка
	|							И НЕ ОтчетКомиссионераЗапасы.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка))) КАК ЗапасыПереданныеОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗапасыПереданныеОстатки.Заказ,
	|	ЗапасыПереданныеОстатки.Номенклатура,
	|	ЗапасыПереданныеОстатки.Характеристика,
	|	ЗапасыПереданныеОстатки.Партия,
	|	ВЫБОР
	|		КОГДА ЗапасыПереданныеОстатки.Заказ.ДатаОтгрузки ЕСТЬ NULL
	|			ТОГДА ДАТАВРЕМЯ(2099, 12, 31, 23, 59, 59)
	|		КОГДА ЗапасыПереданныеОстатки.Заказ.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ЗапасыПереданныеОстатки.Заказ.Дата
	|		ИНАЧЕ ЗапасыПереданныеОстатки.Заказ.ДатаОтгрузки
	|	КОНЕЦ,
	|	ЗапасыПереданныеОстатки.Номенклатура.ЕдиницаИзмерения,
	|	ЗапасыПереданныеОстатки.КоличествоОстаток,
	|	ЗапасыПереданныеОстатки.СуммаРасчетовОстаток
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапасыПереданные.Номенклатура,
	|	ЗапасыПереданные.Характеристика,
	|	ЗапасыПереданные.Партия,
	|	ЗапасыПереданные.Заказ,
	|	ЗапасыПереданные.Номенклатура.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ЗапасыПереданные.Заказ.ДатаОтгрузки ЕСТЬ NULL
	|			ТОГДА ДАТАВРЕМЯ(2099, 12, 31, 23, 59, 59)
	|		КОГДА ЗапасыПереданные.Заказ.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ЗапасыПереданные.Заказ.Дата
	|		ИНАЧЕ ЗапасыПереданные.Заказ.ДатаОтгрузки
	|	КОНЕЦ,
	|	ЗапасыПереданные.Количество,
	|	ЗапасыПереданные.СуммаРасчетов
	|ИЗ
	|	РегистрНакопления.ЗапасыПереданные КАК ЗапасыПереданные
	|ГДЕ
	|	ЗапасыПереданные.Регистратор = &Ссылка
	|	И ЗапасыПереданные.ТипПриемаПередачи = ЗНАЧЕНИЕ(Перечисление.ТипыПриемаПередачиТоваров.ПередачаКомиссионеру)
	|	И ЗапасыПереданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтОстатки.Номенклатура КАК Номенклатура,
	|	ВтОстатки.Характеристика КАК Характеристика,
	|	ВтОстатки.Партия КАК Партия,
	|	ВтОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВтОстатки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВтОстатки.Дата КАК Дата,
	|	СУММА(ВтОстатки.Количество) КАК Количество,
	|	СУММА(ВтОстатки.СуммаРасчетов) КАК СуммаРасчетов
	|ИЗ
	|	ВтОстатки КАК ВтОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтОстатки.Характеристика,
	|	ВтОстатки.Номенклатура,
	|	ВтОстатки.ЕдиницаИзмерения,
	|	ВтОстатки.Дата,
	|	ВтОстатки.Партия,
	|	ВтОстатки.ЗаказПокупателя
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказПокупателя";
	
	Возврат ЗапросТекст;
	
КонецФункции

Функция ТекстЗапросаРаспределениеОтчетКомиссионераОВозвратах()
	
	ЗапросТекст = 
	"ВЫБРАТЬ
	|	ОтчетКомиссионераПокупатели.ОтчетКомиссионера КАК ОтчетКомиссионера,
	|	ОтчетКомиссионераПокупатели.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВТОтчетыКомиссионера
	|ИЗ
	|	Документ.ОтчетКомиссионера.Покупатели КАК ОтчетКомиссионераПокупатели
	|ГДЕ
	|	ОтчетКомиссионераПокупатели.Ссылка = &Ссылка
	|	И НЕ ОтчетКомиссионераПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетКомиссионераЗапасы.Номенклатура КАК Номенклатура,
	|	ОтчетКомиссионераЗапасы.Характеристика КАК Характеристика,
	|	ОтчетКомиссионераЗапасы.Партия КАК Партия,
	|	ОтчетКомиссионераЗапасы.Себестоимость КАК Себестоимость,
	|	ВТОтчетыКомиссионера.ОтчетКомиссионера КАК ОтчетКомиссионера,
	|	ОтчетКомиссионераЗапасы.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ КРаспределению
	|ИЗ
	|	Документ.ОтчетКомиссионера.Запасы КАК ОтчетКомиссионераЗапасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтчетыКомиссионера КАК ВТОтчетыКомиссионера
	|		ПО ОтчетКомиссионераЗапасы.КлючСвязи = ВТОтчетыКомиссионера.КлючСвязи
	|ГДЕ
	|	ОтчетКомиссионераЗапасы.Ссылка = &Ссылка
	|	И ОтчетКомиссионераЗапасы.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	И ОтчетКомиссионераЗапасы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КРаспределению.Номенклатура КАК Номенклатура,
	|	КРаспределению.Характеристика КАК Характеристика,
	|	КРаспределению.Партия КАК Партия,
	|	КРаспределению.Себестоимость КАК Себестоимость,
	|	КРаспределению.ОтчетКомиссионера КАК ОтчетКомиссионера,
	|	КРаспределению.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	КРаспределению КАК КРаспределению
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетКомиссионераЗапасы.Номенклатура КАК Номенклатура,
	|	ОтчетКомиссионераЗапасы.Характеристика КАК Характеристика,
	|	ОтчетКомиссионераЗапасы.Партия КАК Партия,
	|	ОтчетКомиссионераЗапасы.ЗаказПокупателя КАК Заказ,
	|	ОтчетКомиссионераЗапасы.Себестоимость КАК Себестоимость,
	|	ВТОтчетыКомиссионера.ОтчетКомиссионера КАК ОтчетКомиссионера,
	|	ОтчетКомиссионераЗапасы.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	Документ.ОтчетКомиссионера.Запасы КАК ОтчетКомиссионераЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтчетыКомиссионера КАК ВТОтчетыКомиссионера
	|		ПО ОтчетКомиссионераЗапасы.КлючСвязи = ВТОтчетыКомиссионера.КлючСвязи
	|ГДЕ
	|	ОтчетКомиссионераЗапасы.Ссылка = &Ссылка
	|	И НЕ ОтчетКомиссионераЗапасы.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	И ОтчетКомиссионераЗапасы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОтчетКомиссионера
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Продажи.Номенклатура КАК Номенклатура,
	|	Продажи.Характеристика КАК Характеристика,
	|	Продажи.Партия КАК Партия,
	|	Продажи.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(Продажи.Количество) КАК Количество,
	|	СУММА(Продажи.Сумма) КАК СуммаРасчетов,
	|	КРаспределению.ОтчетКомиссионера КАК ОтчетКомиссионера,
	|	Продажи.ЗаказПокупателя КАК ЗаказПокупателя,
	|	КРаспределению.КлючСвязи КАК КлючСвязи
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	КРаспределению КАК КРаспределению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи КАК Продажи
	|		ПО КРаспределению.ОтчетКомиссионера = Продажи.Документ
	|			И КРаспределению.Номенклатура = Продажи.Номенклатура
	|			И КРаспределению.Характеристика = Продажи.Характеристика
	|			И КРаспределению.Партия = Продажи.Партия
	|ГДЕ
	|	НЕ Продажи.Регистратор = &Ссылка
	|	И НЕ Продажи.ЗаказПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	И Продажи.Документ В
	|			(ВЫБРАТЬ
	|				ОтчетКомиссионераПокупатели.ОтчетКомиссионера КАК Документ
	|			ИЗ
	|				Документ.ОтчетКомиссионера.Покупатели КАК ОтчетКомиссионераПокупатели
	|			ГДЕ
	|				ОтчетКомиссионераПокупатели.Ссылка = &Ссылка
	|				И НЕ ОтчетКомиссионераПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	Продажи.Характеристика,
	|	Продажи.Партия,
	|	Продажи.ЗаказПокупателя,
	|	Продажи.Номенклатура,
	|	КРаспределению.ОтчетКомиссионера,
	|	Продажи.Номенклатура.ЕдиницаИзмерения,
	|	КРаспределению.КлючСвязи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтОстатки.Номенклатура КАК Номенклатура,
	|	ВтОстатки.Характеристика КАК Характеристика,
	|	ВтОстатки.Партия КАК Партия,
	|	ВтОстатки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ВтОстатки.Количество) КАК Количество,
	|	СУММА(ВтОстатки.СуммаРасчетов) КАК СуммаРасчетов,
	|	ВтОстатки.ОтчетКомиссионера КАК ОтчетКомиссионера,
	|	ВтОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВтОстатки.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	ВтОстатки КАК ВтОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтОстатки.Партия,
	|	ВтОстатки.ЕдиницаИзмерения,
	|	ВтОстатки.Характеристика,
	|	ВтОстатки.ЗаказПокупателя,
	|	ВтОстатки.Номенклатура,
	|	ВтОстатки.ОтчетКомиссионера,
	|	ВтОстатки.КлючСвязи";
	
	Возврат ЗапросТекст;
	
КонецФункции

Функция ТекстЗапросаОтчетКомиссионераОПродажах()
	
	ЗапросТекст =
	"ВЫБРАТЬ
	|	ТаблицаПродажи.Период КАК Период,
	|	ТаблицаПродажи.Организация КАК Организация,
	|	ТаблицаПродажи.Номенклатура КАК Номенклатура,
	|	ТаблицаПродажи.Характеристика КАК Характеристика,
	|	ТаблицаПродажи.Партия КАК Партия,
	|	ТаблицаПродажи.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаПродажи.Документ КАК Документ,
	|	ТаблицаПродажи.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаПродажи.ПодразделениеПродажи КАК Подразделение,
	|	ТаблицаПродажи.Проект КАК Проект,
	|	ТаблицаПродажи.Ответственный КАК Ответственный,
	|	СУММА(ТаблицаПродажи.Количество) КАК Количество,
	|	СУММА(ТаблицаПродажи.СуммаНДСПродажи) КАК СуммаНДС,
	|	СУММА(ТаблицаПродажи.Сумма) КАК Сумма,
	|	0 КАК Себестоимость,
	|	СУММА(ТаблицаПродажи.Сумма) КАК СуммаБезСкидки
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаПродажи
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродажи.Период,
	|	ТаблицаПродажи.Организация,
	|	ТаблицаПродажи.Номенклатура,
	|	ТаблицаПродажи.Характеристика,
	|	ТаблицаПродажи.Партия,
	|	ТаблицаПродажи.ЗаказПокупателя,
	|	ТаблицаПродажи.Документ,
	|	ТаблицаПродажи.СтавкаНДС,
	|	ТаблицаПродажи.ПодразделениеПродажи,
	|	ТаблицаПродажи.Проект,
	|	ТаблицаПродажи.Ответственный";
	
	Возврат ЗапросТекст;
	
КонецФункции

Функция ТекстЗапросаОтчетКомиссионераОВозвратах()
	
	ЗапросТекст =
	"ВЫБРАТЬ
	|	ТаблицаПродажи.Период КАК Период,
	|	ТаблицаПродажи.Организация КАК Организация,
	|	ТаблицаПродажи.Номенклатура КАК Номенклатура,
	|	ТаблицаПродажи.Характеристика КАК Характеристика,
	|	ТаблицаПродажи.Партия КАК Партия,
	|	ТаблицаПродажи.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА НЕ ОтчетКомиссионераПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|				И ОтчетКомиссионераПокупатели.ОтчетКомиссионера.Проведен
	|			ТОГДА ОтчетКомиссионераПокупатели.ОтчетКомиссионера
	|		ИНАЧЕ ТаблицаПродажи.Документ
	|	КОНЕЦ КАК Документ,
	|	ТаблицаПродажи.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаПродажи.ПодразделениеПродажи КАК Подразделение,
	|	ТаблицаПродажи.Проект КАК Проект,
	|	ТаблицаПродажи.Ответственный КАК Ответственный,
	|	СУММА(ТаблицаПродажи.Количество) КАК Количество,
	|	СУММА(ТаблицаПродажи.СуммаНДСПродажи) КАК СуммаНДС,
	|	СУММА(ТаблицаПродажи.Сумма) КАК Сумма,
	|	0 КАК Себестоимость,
	|	СУММА(ТаблицаПродажи.Сумма) КАК СуммаБезСкидки
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаПродажи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионера.Покупатели КАК ОтчетКомиссионераПокупатели
	|		ПО ТаблицаПродажи.КлючСвязи = ОтчетКомиссионераПокупатели.КлючСвязи
	|ГДЕ
	|	ОтчетКомиссионераПокупатели.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродажи.Период,
	|	ТаблицаПродажи.Организация,
	|	ТаблицаПродажи.Номенклатура,
	|	ТаблицаПродажи.Характеристика,
	|	ТаблицаПродажи.Партия,
	|	ТаблицаПродажи.ЗаказПокупателя,
	|	ТаблицаПродажи.СтавкаНДС,
	|	ТаблицаПродажи.ПодразделениеПродажи,
	|	ТаблицаПродажи.Проект,
	|	ТаблицаПродажи.Ответственный,
	|	ВЫБОР
	|		КОГДА НЕ ОтчетКомиссионераПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|				И ОтчетКомиссионераПокупатели.ОтчетКомиссионера.Проведен
	|			ТОГДА ОтчетКомиссионераПокупатели.ОтчетКомиссионера
	|		ИНАЧЕ ТаблицаПродажи.Документ
	|	КОНЕЦ";
	
	Возврат ЗапросТекст;
	
КонецФункции

Функция ТекстЗапросаОтчетКомиссионераОПродажахЗапасы()
	
	ЗапросТекст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаЗапасы.Документ КАК Документ,
	|	ТаблицаЗапасы.Документ КАК ДокументПродажи,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПродажи,
	|	ТаблицаЗапасы.ПодразделениеПродажи КАК Подразделение,
	|	ТаблицаЗапасы.Ответственный КАК Ответственный,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.ПодразделениеПродажи КАК ПодразделениеПродажи,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаЗапасы.УдержатьКомиссионноеВознаграждение КАК УдержатьКомиссионноеВознаграждение,
	|	ТаблицаЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаЗапасы.Количество) КАК Количество,
	|	СУММА(ТаблицаЗапасы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ТаблицаЗапасы.Сумма) КАК Сумма,
	|	СУММА(ТаблицаЗапасы.СуммаВознаграждения) КАК СуммаВознаграждения,
	|	ЛОЖЬ КАК ФиксированнаяСтоимость,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетДт,
	|	ТаблицаЗапасы.СчетУчета КАК СчетКт,
	|	&ОтчетОтКомиссионера КАК Содержание,
	|	&ОтчетОтКомиссионера КАК СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.Документ,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.ПодразделениеПродажи,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя,
	|	ТаблицаЗапасы.СтавкаНДС,
	|	ТаблицаЗапасы.УдержатьКомиссионноеВознаграждение,
	|	ТаблицаЗапасы.Ответственный,
	|	ТаблицаЗапасы.Документ,
	|	ТаблицаЗапасы.ПодразделениеПродажи,
	|	ТаблицаЗапасы.ЗаказПокупателя,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СчетУчета";
	
	Возврат ЗапросТекст;
	
КонецФункции

Функция ТекстЗапросаОтчетКомиссионераОВозвратахЗапасы()
	
	ЗапросТекст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ВЫБОР
	|		КОГДА НЕ ОтчетКомиссионераПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|				И ОтчетКомиссионераПокупатели.ОтчетКомиссионера.Проведен
	|			ТОГДА ОтчетКомиссионераПокупатели.ОтчетКомиссионера
	|		ИНАЧЕ ТаблицаЗапасы.Документ
	|	КОНЕЦ КАК Документ,
	|	ВЫБОР
	|		КОГДА НЕ ОтчетКомиссионераПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|				И ОтчетКомиссионераПокупатели.ОтчетКомиссионера.Проведен
	|			ТОГДА ОтчетКомиссионераПокупатели.ОтчетКомиссионера
	|		ИНАЧЕ ТаблицаЗапасы.Документ
	|	КОНЕЦ КАК ДокументПродажи,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПродажи,
	|	ТаблицаЗапасы.ПодразделениеПродажи КАК Подразделение,
	|	ТаблицаЗапасы.Ответственный КАК Ответственный,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.ПодразделениеПродажи КАК ПодразделениеПродажи,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаЗапасы.УдержатьКомиссионноеВознаграждение КАК УдержатьКомиссионноеВознаграждение,
	|	ТаблицаЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаЗапасы.Количество) КАК Количество,
	|	СУММА(ТаблицаЗапасы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ТаблицаЗапасы.Сумма) КАК Сумма,
	|	СУММА(ТаблицаЗапасы.СуммаВознаграждения) КАК СуммаВознаграждения,
	|	ЛОЖЬ КАК ФиксированнаяСтоимость,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость КАК СчетДт,
	|	ТаблицаЗапасы.СчетУчета КАК СчетКт,
	|	СУММА(ТаблицаЗапасы.РучнаяСебестоимость) КАК РучнаяСебестоимость,
	|	&ОтчетОтКомиссионера КАК Содержание,
	|	&ОтчетОтКомиссионера КАК СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионера.Покупатели КАК ОтчетКомиссионераПокупатели
	|		ПО ТаблицаЗапасы.КлючСвязи = ОтчетКомиссионераПокупатели.КлючСвязи
	|ГДЕ
	|	ОтчетКомиссионераПокупатели.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.НаправлениеДеятельностиПродажи,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.ПодразделениеПродажи,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.ЗаказПокупателя,
	|	ТаблицаЗапасы.СтавкаНДС,
	|	ТаблицаЗапасы.УдержатьКомиссионноеВознаграждение,
	|	ТаблицаЗапасы.Ответственный,
	|	ВЫБОР
	|		КОГДА НЕ ОтчетКомиссионераПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|				И ОтчетКомиссионераПокупатели.ОтчетКомиссионера.Проведен
	|			ТОГДА ОтчетКомиссионераПокупатели.ОтчетКомиссионера
	|		ИНАЧЕ ТаблицаЗапасы.Документ
	|	КОНЕЦ,
	|	ТаблицаЗапасы.ПодразделениеПродажи,
	|	ТаблицаЗапасы.ЗаказПокупателя,
	|	ТаблицаЗапасы.СчетУчетаСебестоимость,
	|	ТаблицаЗапасы.СчетУчета,
	|	ВЫБОР
	|		КОГДА НЕ ОтчетКомиссионераПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|				И ОтчетКомиссионераПокупатели.ОтчетКомиссионера.Проведен
	|			ТОГДА ОтчетКомиссионераПокупатели.ОтчетКомиссионера
	|		ИНАЧЕ ТаблицаЗапасы.Документ
	|	КОНЕЦ";
	
	Возврат ЗапросТекст;
	
КонецФункции

Функция ТекстЗапросаОтчетКомиссионераОПродажахРасчетыСПокупателями()
	
	ЗапросТекст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.Период КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем КАК СчетУчета,
	|	ТаблицаДокумента.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ТаблицаДокумента.ВалютаРасчетов КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг) КАК ТипРасчетов,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.УдержатьКомиссионноеВознаграждение
	|				ТОГДА ТаблицаДокумента.Сумма - ТаблицаДокумента.СуммаВознаграждения
	|			ИНАЧЕ ТаблицаДокумента.Сумма
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.УдержатьКомиссионноеВознаграждение
	|				ТОГДА ТаблицаДокумента.СуммаВал - ТаблицаДокумента.СуммаВознагражденияВал
	|			ИНАЧЕ ТаблицаДокумента.СуммаВал
	|		КОНЕЦ) КАК СуммаВал,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ &УчетВалютныхОпераций
	|				ТОГДА 0
	|			КОГДА ТаблицаДокумента.УдержатьКомиссионноеВознаграждение
	|				ТОГДА ТаблицаДокумента.СуммаРег - ТаблицаДокумента.СуммаВознагражденияРег
	|			ИНАЧЕ ТаблицаДокумента.СуммаРег
	|		КОНЕЦ) КАК СуммаРег,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.УдержатьКомиссионноеВознаграждение
	|				ТОГДА ТаблицаДокумента.Сумма - ТаблицаДокумента.СуммаВознаграждения
	|			ИНАЧЕ ТаблицаДокумента.Сумма
	|		КОНЕЦ) КАК СуммаДляОстатка,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.УдержатьКомиссионноеВознаграждение
	|				ТОГДА ТаблицаДокумента.СуммаВал - ТаблицаДокумента.СуммаВознагражденияВал
	|			ИНАЧЕ ТаблицаДокумента.СуммаВал
	|		КОНЕЦ) КАК СуммаВалДляОстатка,
	|	ВЫРАЗИТЬ(&ВозникновениеОбязательствПокупателя КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПокупателями
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаАвансовПокупателя,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетов,
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	СУММА(ВЫБОР
	|			КОГДА НЕ &УчетВалютныхОпераций
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаДокумента.СуммаРег
	|		КОНЕЦ),
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	ВЫРАЗИТЬ(&ЗачетАванса КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ТипРасчетов,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.СчетУчетаАвансовПокупателя
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.ДокументКуда
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетовКуда,
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	СУММА(ВЫБОР
	|			КОГДА НЕ &УчетВалютныхОпераций
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаДокумента.СуммаРег
	|		КОНЕЦ),
	|	-СУММА(ТаблицаДокумента.Сумма),
	|	-СУММА(ТаблицаДокумента.СуммаВал),
	|	ВЫРАЗИТЬ(&ЗачетАванса КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.ДокументКуда
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетовКуда
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	Договор,
	|	Валюта,
	|	Документ,
	|	Заказ,
	|	ТипРасчетов,
	|	СчетУчета";
	
	Возврат ЗапросТекст;
	
КонецФункции

Функция ТекстЗапросаОтчетКомиссионераОВозвратахРасчетыСПокупателями()
	
	ЗапросТекст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.Период КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем КАК СчетУчета,
	|	ТаблицаДокумента.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ТаблицаПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|						ТОГДА ТаблицаПокупатели.ОтчетКомиссионера
	|					ИНАЧЕ ТаблицаДокумента.Документ
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ТаблицаДокумента.ВалютаРасчетов КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг) КАК ТипРасчетов,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.УдержатьКомиссионноеВознаграждение
	|				ТОГДА ТаблицаДокумента.Сумма - ТаблицаДокумента.СуммаВознаграждения
	|			ИНАЧЕ ТаблицаДокумента.Сумма
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.УдержатьКомиссионноеВознаграждение
	|				ТОГДА ТаблицаДокумента.СуммаВал - ТаблицаДокумента.СуммаВознагражденияВал
	|			ИНАЧЕ ТаблицаДокумента.СуммаВал
	|		КОНЕЦ) КАК СуммаВал,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ &УчетВалютныхОпераций
	|				ТОГДА 0
	|			КОГДА ТаблицаДокумента.УдержатьКомиссионноеВознаграждение
	|				ТОГДА ТаблицаДокумента.СуммаРег - ТаблицаДокумента.СуммаВознагражденияРег
	|			ИНАЧЕ ТаблицаДокумента.СуммаРег
	|		КОНЕЦ) КАК СуммаРег,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.УдержатьКомиссионноеВознаграждение
	|				ТОГДА ТаблицаДокумента.Сумма - ТаблицаДокумента.СуммаВознаграждения
	|			ИНАЧЕ ТаблицаДокумента.Сумма
	|		КОНЕЦ) КАК СуммаДляОстатка,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.УдержатьКомиссионноеВознаграждение
	|				ТОГДА ТаблицаДокумента.СуммаВал - ТаблицаДокумента.СуммаВознагражденияВал
	|			ИНАЧЕ ТаблицаДокумента.СуммаВал
	|		КОНЕЦ) КАК СуммаВалДляОстатка,
	|	ВЫРАЗИТЬ(&ВозникновениеОбязательствПокупателя КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПокупателями
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаПокупатели КАК ТаблицаПокупатели
	|		ПО ТаблицаДокумента.КлючСвязи = ТаблицаПокупатели.КлючСвязи
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.ЗаказПокупателя
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ТаблицаПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|						ТОГДА ТаблицаПокупатели.ОтчетКомиссионера
	|					ИНАЧЕ ТаблицаДокумента.Документ
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаАвансовПокупателя,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетов,
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	СУММА(ВЫБОР
	|			КОГДА НЕ &УчетВалютныхОпераций
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаДокумента.СуммаРег
	|		КОНЕЦ),
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	ВЫРАЗИТЬ(&ЗачетАванса КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ТипРасчетов,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.СчетУчетаАвансовПокупателя,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ТаблицаПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|						ТОГДА ТаблицаПокупатели.ОтчетКомиссионера
	|					ИНАЧЕ ТаблицаДокумента.ДокументКуда
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетовКуда,
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	СУММА(ВЫБОР
	|			КОГДА НЕ &УчетВалютныхОпераций
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаДокумента.СуммаРег
	|		КОНЕЦ),
	|	-СУММА(ТаблицаДокумента.Сумма),
	|	-СУММА(ТаблицаДокумента.СуммаВал),
	|	ВЫРАЗИТЬ(&ЗачетАванса КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаПокупателиДокументыВозврата КАК ТаблицаПокупатели
	|		ПО ТаблицаДокумента.ОтчетКомиссионера = ТаблицаПокупатели.ОтчетКомиссионера
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетовКуда,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ТаблицаПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|						ТОГДА ТаблицаПокупатели.ОтчетКомиссионера
	|					ИНАЧЕ ТаблицаДокумента.ДокументКуда
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	Договор,
	|	Валюта,
	|	Документ,
	|	Заказ,
	|	ТипРасчетов,
	|	СчетУчета";
	
	Возврат ЗапросТекст;
	
КонецФункции

Функция ТекстЗапросаОтчетКомиссионераОВозвратахДоходыИРасходыОтложенные()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.КлючСвязи КАК КлючСвязи,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ОтчетКомиссионераПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка)
	|						ТОГДА ОтчетКомиссионераПокупатели.ОтчетКомиссионера
	|					ИНАЧЕ &Ссылка
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Сумма - ТаблицаДокумента.СуммаВознаграждения КАК СуммаДоходов,
	|	ТаблицаДокумента.СуммаВознаграждения КАК СуммаВознаграждения
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионера.Покупатели КАК ОтчетКомиссионераПокупатели
	|		ПО ТаблицаДокумента.КлючСвязи = ОтчетКомиссионераПокупатели.КлючСвязи,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И ОтчетКомиссионераПокупатели.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация КАК Организация,
	|	СУММА(ТаблицаДокумента.Сумма) КАК СуммаКСписанию
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Статья КАК Статья
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаОтчетКомиссионераОПродажахДоходыИРасходыОтложенные()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Сумма - ТаблицаДокумента.СуммаВознаграждения КАК СуммаДоходов,
	|	ТаблицаДокумента.СуммаВознаграждения КАК СуммаВознаграждения
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация КАК Организация,
	|	СУММА(ТаблицаДокумента.Сумма) КАК СуммаКСписанию
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Статья КАК Статья
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылкаОтчетКомиссионера, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	
	Если НЕ УправлениеНебольшойФирмойСервер.ВыполнитьКонтрольОстатков() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Если временные таблицы "ДвиженияЗапасыИзменение", "ДвиженияЗапасыПереданныеИзменение"
	// содержат записи, необходимо выполнить контроль реализации товаров.
	
	Если СтруктураВременныеТаблицы.ДвиженияЗапасыИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыВРазрезеГТДИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыПереданныеВРазрезеГТДИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыПереданныеИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияРасчетыСПокупателямиИзменение
		Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияЗапасыИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.СтруктурнаяЕдиница) КАК СтруктурнаяЕдиницаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.СчетУчета) КАК СчетУчетаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыИзменение.ЗаказПокупателя) КАК ЗаказПокупателяПредставление,
		|	ЗапасыОстатки.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.СуммаОстаток, 0) КАК СуммаОстатокЗапасы
		|ИЗ
		|	ДвиженияЗапасыИзменение КАК ДвиженияЗапасыИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(
		|				&МоментКонтроля,
		|				(Организация, СтруктурнаяЕдиница, СчетУчета, Номенклатура, Характеристика, Партия, ЗаказПокупателя) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|						ДвиженияЗапасыИзменение.СчетУчета КАК СчетУчета,
		|						ДвиженияЗапасыИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыИзменение.ЗаказПокупателя КАК ЗаказПокупателя
		|					ИЗ
		|						ДвиженияЗапасыИзменение КАК ДвиженияЗапасыИзменение)) КАК ЗапасыОстатки
		|		ПО ДвиженияЗапасыИзменение.Организация = ЗапасыОстатки.Организация
		|			И ДвиженияЗапасыИзменение.СтруктурнаяЕдиница = ЗапасыОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыИзменение.СчетУчета = ЗапасыОстатки.СчетУчета
		|			И ДвиженияЗапасыИзменение.Номенклатура = ЗапасыОстатки.Номенклатура
		|			И ДвиженияЗапасыИзменение.Характеристика = ЗапасыОстатки.Характеристика
		|			И ДвиженияЗапасыИзменение.Партия = ЗапасыОстатки.Партия
		|			И ДвиженияЗапасыИзменение.ЗаказПокупателя = ЗапасыОстатки.ЗаказПокупателя
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыПереданныеИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Контрагент) КАК КонтрагентПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Договор) КАК ДоговорПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.Заказ) КАК ЗаказПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеИзменение.ТипПриемаПередачи) КАК ТипПриемаПередачиПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыПереданныеОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыПереданныеИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыПереданныеОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыПереданные,
		|	ЕСТЬNULL(ЗапасыПереданныеОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыПереданные,
		|	ЕСТЬNULL(ДвиженияЗапасыПереданныеИзменение.СуммаРасчетовИзменение, 0) + ЕСТЬNULL(ЗапасыПереданныеОстатки.СуммаРасчетовОстаток, 0) КАК СуммаРасчетовЗапасыПереданные,
		|	ЕСТЬNULL(ЗапасыПереданныеОстатки.СуммаРасчетовОстаток, 0) КАК СуммаРасчетовОстатокЗапасыПереданные
		|ИЗ
		|	ДвиженияЗапасыПереданныеИзменение КАК ДвиженияЗапасыПереданныеИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыПереданные.Остатки(
		|				&МоментКонтроля,
		|				(Организация, Номенклатура, Характеристика, Партия, Контрагент, Договор, Заказ, ТипПриемаПередачи) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыПереданныеИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыПереданныеИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыПереданныеИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыПереданныеИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыПереданныеИзменение.Контрагент КАК Контрагент,
		|						ДвиженияЗапасыПереданныеИзменение.Договор КАК Договор,
		|						ДвиженияЗапасыПереданныеИзменение.Заказ КАК Заказ,
		|						ДвиженияЗапасыПереданныеИзменение.ТипПриемаПередачи КАК ТипПриемаПередачи
		|					ИЗ
		|						ДвиженияЗапасыПереданныеИзменение КАК ДвиженияЗапасыПереданныеИзменение)) КАК ЗапасыПереданныеОстатки
		|		ПО ДвиженияЗапасыПереданныеИзменение.Организация = ЗапасыПереданныеОстатки.Организация
		|			И ДвиженияЗапасыПереданныеИзменение.Номенклатура = ЗапасыПереданныеОстатки.Номенклатура
		|			И ДвиженияЗапасыПереданныеИзменение.Характеристика = ЗапасыПереданныеОстатки.Характеристика
		|			И ДвиженияЗапасыПереданныеИзменение.Партия = ЗапасыПереданныеОстатки.Партия
		|			И ДвиженияЗапасыПереданныеИзменение.Контрагент = ЗапасыПереданныеОстатки.Контрагент
		|			И ДвиженияЗапасыПереданныеИзменение.Договор = ЗапасыПереданныеОстатки.Договор
		|			И ДвиженияЗапасыПереданныеИзменение.Заказ = ЗапасыПереданныеОстатки.Заказ
		|			И ДвиженияЗапасыПереданныеИзменение.ТипПриемаПередачи = ЗапасыПереданныеОстатки.ТипПриемаПередачи
		|ГДЕ
		|	(ЕСТЬNULL(ЗапасыПереданныеОстатки.КоличествоОстаток, 0) < 0
		|			ИЛИ ЕСТЬNULL(ЗапасыПереданныеОстатки.СуммаРасчетовОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРасчетыСПокупателямиИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПокупателямиИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПокупателямиИзменение.Контрагент) КАК КонтрагентПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПокупателямиИзменение.Договор) КАК ДоговорПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПокупателямиИзменение.Договор.ВалютаРасчетов) КАК ВалютаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПокупателямиИзменение.Документ) КАК ДокументПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПокупателямиИзменение.Заказ) КАК ЗаказПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов) КАК ТипРасчетовПредставление,
		|	ЛОЖЬ КАК ДвиженияДенежныхДокументов,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалИзменение КАК СуммаВалИзменение,
		|	ВЫБОР
		|		КОГДА ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПриЗаписи >= 0
		|			ТОГДА ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПриЗаписи - ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0)
		|		ИНАЧЕ ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) + ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПриЗаписи
		|	КОНЕЦ КАК СуммаПолученныхАвансов,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалИзменение + ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) КАК СуммаНепогашеннойЗадолженности,
		|	ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов КАК ТипРасчетов
		|ИЗ
		|	ДвиженияРасчетыСПокупателямиИзменение КАК ДвиженияРасчетыСПокупателямиИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПокупателями.Остатки(
		|				&МоментКонтроля,
		|				(Организация, Контрагент, Договор, Документ, Заказ, ТипРасчетов) В
		|					(ВЫБРАТЬ
		|						ДвиженияРасчетыСПокупателямиИзменение.Организация КАК Организация,
		|						ДвиженияРасчетыСПокупателямиИзменение.Контрагент КАК Контрагент,
		|						ДвиженияРасчетыСПокупателямиИзменение.Договор КАК Договор,
		|						ДвиженияРасчетыСПокупателямиИзменение.Документ КАК Документ,
		|						ДвиженияРасчетыСПокупателямиИзменение.Заказ КАК Заказ,
		|						ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов КАК ТипРасчетов
		|					ИЗ
		|						ДвиженияРасчетыСПокупателямиИзменение КАК ДвиженияРасчетыСПокупателямиИзменение)) КАК РасчетыСПокупателямиОстатки
		|		ПО ДвиженияРасчетыСПокупателямиИзменение.Организация = РасчетыСПокупателямиОстатки.Организация
		|			И ДвиженияРасчетыСПокупателямиИзменение.Контрагент = РасчетыСПокупателямиОстатки.Контрагент
		|			И ДвиженияРасчетыСПокупателямиИзменение.Договор = РасчетыСПокупателямиОстатки.Договор
		|			И ДвиженияРасчетыСПокупателямиИзменение.Документ = РасчетыСПокупателямиОстатки.Документ
		|			И ДвиженияРасчетыСПокупателямиИзменение.Заказ = РасчетыСПокупателямиОстатки.Заказ
		|			И ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов = РасчетыСПокупателямиОстатки.ТипРасчетов
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &ЭтоВозврат
		|					И ДвиженияРасчетыСПокупателямиИзменение.Документ = &Ссылка
		|				ТОГДА ЛОЖЬ
		|			ИНАЧЕ ВЫБОР
		|					КОГДА ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
		|						ТОГДА ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) > 0
		|					ИНАЧЕ ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) < 0
		|				КОНЕЦ
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыВРазрезеГТДИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД) КАК НомерГТДПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения) КАК СтранаПроисхожденияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыВРазрезеГТДОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыВРазрезеГТДИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыВРазрезеГТД,
		|	ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыВРазрезеГТД
		|ИЗ
		|	ДвиженияЗапасыВРазрезеГТДИзменение КАК ДвиженияЗапасыВРазрезеГТДИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыВРазрезеГТД.Остатки(
		|				&МоментКонтроля,
		|				(Организация, НомерГТД, Номенклатура, Характеристика, Партия, СтранаПроисхождения) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД КАК НомерГТД,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения КАК СтранаПроисхождения
		|					ИЗ
		|						ДвиженияЗапасыВРазрезеГТДИзменение КАК ДвиженияЗапасыВРазрезеГТДИзменение)) КАК ЗапасыВРазрезеГТДОстатки
		|		ПО ДвиженияЗапасыВРазрезеГТДИзменение.Организация = ЗапасыВРазрезеГТДОстатки.Организация
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД = ЗапасыВРазрезеГТДОстатки.НомерГТД
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура = ЗапасыВРазрезеГТДОстатки.Номенклатура
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика = ЗапасыВРазрезеГТДОстатки.Характеристика
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Партия = ЗапасыВРазрезеГТДОстатки.Партия
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения = ЗапасыВРазрезеГТДОстатки.СтранаПроисхождения
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.НомерГТД) КАК НомерГТДПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.СтранаПроисхождения) КАК СтранаПроисхожденияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыПереданныеВРазрезеГТДОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыПереданныеВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыВРазрезеГТД,
		|	ЕСТЬNULL(ЗапасыПереданныеВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыВРазрезеГТД
		|ИЗ
		|	ДвиженияЗапасыПереданныеВРазрезеГТДИзменение КАК ДвиженияЗапасыПереданныеВРазрезеГТДИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыПереданныеВРазрезеГТД.Остатки(
		|				&МоментКонтроля,
		|				(Организация, НомерГТД, Номенклатура, Характеристика, Партия, СтранаПроисхождения) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.НомерГТД КАК НомерГТД,
		|						ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.СтранаПроисхождения КАК СтранаПроисхождения
		|					ИЗ
		|						ДвиженияЗапасыПереданныеВРазрезеГТДИзменение КАК ДвиженияЗапасыПереданныеВРазрезеГТДИзменение)) КАК ЗапасыПереданныеВРазрезеГТДОстатки
		|		ПО ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Организация = ЗапасыПереданныеВРазрезеГТДОстатки.Организация
		|			И ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.НомерГТД = ЗапасыПереданныеВРазрезеГТДОстатки.НомерГТД
		|			И ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Номенклатура = ЗапасыПереданныеВРазрезеГТДОстатки.Номенклатура
		|			И ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Характеристика = ЗапасыПереданныеВРазрезеГТДОстатки.Характеристика
		|			И ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.Партия = ЗапасыПереданныеВРазрезеГТДОстатки.Партия
		|			И ДвиженияЗапасыПереданныеВРазрезеГТДИзменение.СтранаПроисхождения = ЗапасыПереданныеВРазрезеГТДОстатки.СтранаПроисхождения
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыПереданныеВРазрезеГТДОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки");
		
		ЭтоВозврат =  ?(ДокументСсылкаОтчетКомиссионера.ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах, Истина, Ложь);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("МоментКонтроля", ДополнительныеСвойства.ДляПроведения.МоментКонтроля);
		Запрос.УстановитьПараметр("ЭтоВозврат", ЭтоВозврат);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаОтчетКомиссионера);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		Если НЕ МассивРезультатов[0].Пустой()
			ИЛИ НЕ МассивРезультатов[1].Пустой()
			ИЛИ НЕ МассивРезультатов[2].Пустой()
			ИЛИ НЕ МассивРезультатов[3].Пустой()
			ИЛИ НЕ МассивРезультатов[4].Пустой()
			Тогда
			
			ДокументОбъектОтчетКомиссионера = ДокументСсылкаОтчетКомиссионера.ПолучитьОбъект();
			
		КонецЕсли;
		
		// Отрицательный остаток учета запасов.
		Если НЕ ЭтоВозврат И НЕ МассивРезультатов[0].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[0].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструЗапасы(ДокументОбъектОтчетКомиссионера, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток запасов переданных.
		Если НЕ ЭтоВозврат И НЕ МассивРезультатов[1].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[1].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструЗапасыПереданные(ДокументОбъектОтчетКомиссионера, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		Если Не ЭтоВозврат Тогда
			// Отрицательный остаток по расчетам с покупателями.
			Если Не ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения И НЕ МассивРезультатов[2].Пустой() Тогда
				ВыборкаИзРезультатаЗапроса = МассивРезультатов[2].Выбрать();
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструРасчетыСПокупателями(ДокументОбъектОтчетКомиссионера, ВыборкаИзРезультатаЗапроса, Отказ);
			КонецЕсли;
		КонецЕсли;
		
		// Отрицательный остаток по остаткам запасов в разрезе номеров ГТД.
		Если НЕ ЭтоВозврат И Константы.КонтролироватьОстаткиПоНомерамГТД.Получить()
			И НЕ МассивРезультатов[3].Пустой() Тогда
			
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[3].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструЗапасыВРазрезеГТД(ДокументОбъектОтчетКомиссионера, ВыборкаИзРезультатаЗапроса, Отказ);
			
		КонецЕсли;
		
		Если НЕ ЭтоВозврат И НЕ МассивРезультатов[4].Пустой() Тогда
				
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[4].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструЗапасыПереданныеВРазрезеГТД(ДокументОбъектОтчетКомиссионера, ВыборкаИзРезультатаЗапроса, Отказ);
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтроль()

// Выполняет контроль возникновения отрицательных оборотов.
//
Процедура ВыполнитьКонтрольВозвраты(ДокументСсылкаОтчетКомиссионера, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	
	Если НЕ УправлениеНебольшойФирмойСервер.ВыполнитьКонтрольОстатков() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Если Не СтруктураВременныеТаблицы.ДвиженияЗапасыПереданныеИзменение Тогда Возврат КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Продажи.Номенклатура КАК Номенклатура,
	|	Продажи.Характеристика КАК Характеристика,
	|	Продажи.Партия КАК Партия,
	|	Продажи.Контрагент КАК Контрагент,
	|	Продажи.Организация КАК Организация,
	|	Продажи.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СУММА(Продажи.Количество) КАК Количество,
	|	СУММА(Продажи.Сумма) КАК Сумма,
	|	Продажи.Документ.Договор КАК ДокументДоговор,
	|	Продажи.Номенклатура.ЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения
	|ПОМЕСТИТЬ ВтОстатокОборот
	|ИЗ
	|	РегистрНакопления.Продажи КАК Продажи
	|ГДЕ
	|	Продажи.Период <= &МоментКонтроля
	|	И Продажи.Документ В
	|			(ВЫБРАТЬ
	|				ОтчетКомиссионераПокупатели.ОтчетКомиссионера КАК ОтчетКомиссионера
	|			ИЗ
	|				Документ.ОтчетКомиссионера.Покупатели КАК ОтчетКомиссионераПокупатели
	|			ГДЕ
	|				ОтчетКомиссионераПокупатели.Ссылка = &Регистратор
	|				И НЕ ОтчетКомиссионераПокупатели.ОтчетКомиссионера = ЗНАЧЕНИЕ(Документ.ОтчетКомиссионера.ПустаяСсылка))
	|	И НЕ Продажи.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	Продажи.Организация,
	|	Продажи.ЗаказПокупателя,
	|	Продажи.Характеристика,
	|	Продажи.Контрагент,
	|	Продажи.Номенклатура,
	|	Продажи.Партия,
	|	Продажи.Документ.Договор,
	|	Продажи.Номенклатура.ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Продажи.Номенклатура КАК Номенклатура,
	|	Продажи.Характеристика КАК Характеристика,
	|	Продажи.Партия КАК Партия,
	|	Продажи.Контрагент КАК Контрагент,
	|	Продажи.Организация КАК Организация,
	|	Продажи.ЗаказПокупателя КАК ЗаказПокупателя,
	|	СУММА(Продажи.Количество) КАК Количество,
	|	СУММА(Продажи.Сумма) КАК Сумма,
	|	Продажи.Документ.Договор КАК ДокументДоговор,
	|	Продажи.Номенклатура.ЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения
	|ПОМЕСТИТЬ ВтОстатокОборотПоРегистратору
	|ИЗ
	|	РегистрНакопления.Продажи КАК Продажи
	|ГДЕ
	|	Продажи.Период <= &МоментКонтроля
	|	И Продажи.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	Продажи.Организация,
	|	Продажи.ЗаказПокупателя,
	|	Продажи.Характеристика,
	|	Продажи.Контрагент,
	|	Продажи.Номенклатура,
	|	Продажи.Партия,
	|	Продажи.Документ.Договор,
	|	Продажи.Номенклатура.ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияЗапасыПереданныеИзменение.НомерСтроки КАК НомерСтроки,
	|	ДвиженияЗапасыПереданныеИзменение.Организация КАК Организация,
	|	ДвиженияЗапасыПереданныеИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияЗапасыПереданныеИзменение.Характеристика КАК Характеристика,
	|	ДвиженияЗапасыПереданныеИзменение.Партия КАК Партия,
	|	ДвиженияЗапасыПереданныеИзменение.Контрагент КАК Контрагент,
	|	ДвиженияЗапасыПереданныеИзменение.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ДвиженияЗапасыПереданныеИзменение.Заказ = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		ИНАЧЕ ДвиженияЗапасыПереданныеИзменение.Заказ
	|	КОНЕЦ КАК ЗаказПопкутеля,
	|	ДвиженияЗапасыПереданныеИзменение.КоличествоИзменение КАК КоличествоИзменение,
	|	ДвиженияЗапасыПереданныеИзменение.СуммаРасчетовИзменение КАК СуммаРасчетовИзменение
	|ПОМЕСТИТЬ ВТЗапасыПереданныеИзменение
	|ИЗ
	|	ДвиженияЗапасыПереданныеИзменение КАК ДвиженияЗапасыПереданныеИзменение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТЗапасыПереданныеИзменение.Организация) КАК ОрганизацияПредставление,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТЗапасыПереданныеИзменение.Номенклатура) КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТЗапасыПереданныеИзменение.Характеристика) КАК ХарактеристикаПредставление,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТЗапасыПереданныеИзменение.Партия) КАК ПартияПредставление,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТЗапасыПереданныеИзменение.Контрагент) КАК КонтрагентПредставление,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТЗапасыПереданныеИзменение.Договор) КАК ДоговорПредставление,
	|	ВТЗапасыПереданныеИзменение.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТЗапасыПереданныеИзменение.КоличествоИзменение, 0) - ЕСТЬNULL(ВтОстатокОборотПоРегистратору.Количество, 0) = 0
	|			ТОГДА ЕСТЬNULL(ВТЗапасыПереданныеИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ВтОстатокОборот.Количество, 0)
	|		ИНАЧЕ ЕСТЬNULL(ВтОстатокОборот.Количество, 0) + ЕСТЬNULL(ВтОстатокОборотПоРегистратору.Количество, 0)
	|	КОНЕЦ КАК ОстатокЗапасыПереданные,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТЗапасыПереданныеИзменение.КоличествоИзменение, 0) - ЕСТЬNULL(ВтОстатокОборотПоРегистратору.Количество, 0) = 0
	|			ТОГДА ЕСТЬNULL(ВТЗапасыПереданныеИзменение.СуммаРасчетовИзменение, 0) + ЕСТЬNULL(ВтОстатокОборот.Сумма, 0)
	|		ИНАЧЕ ЕСТЬNULL(ВтОстатокОборотПоРегистратору.Сумма, 0) + ЕСТЬNULL(ВтОстатокОборот.Сумма, 0)
	|	КОНЕЦ КАК СуммаРасчетовЗапасыПереданные,
	|	ВтОстатокОборот.НоменклатураЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
	|	ЕСТЬNULL(ВтОстатокОборот.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ВтОстатокОборот.Сумма, 0) КАК Сумма,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВТЗапасыПереданныеИзменение.ЗаказПопкутеля) КАК ЗаказПредставление,
	|	ВТЗапасыПереданныеИзменение.КоличествоИзменение КАК КоличествоИзменение,
	|	ВтОстатокОборот.Количество КАК КоличествоОброт,
	|	ВтОстатокОборотПоРегистратору.Количество КАК КоличествоОборотРег
	|ПОМЕСТИТЬ ВТОборотПродажи
	|ИЗ
	|	ВТЗапасыПереданныеИзменение КАК ВТЗапасыПереданныеИзменение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтОстатокОборот КАК ВтОстатокОборот
	|		ПО ВТЗапасыПереданныеИзменение.Организация = ВтОстатокОборот.Организация
	|			И ВТЗапасыПереданныеИзменение.Договор = ВтОстатокОборот.ДокументДоговор
	|			И ВТЗапасыПереданныеИзменение.Номенклатура = ВтОстатокОборот.Номенклатура
	|			И ВТЗапасыПереданныеИзменение.Характеристика = ВтОстатокОборот.Характеристика
	|			И ВТЗапасыПереданныеИзменение.Партия = ВтОстатокОборот.Партия
	|			И ВТЗапасыПереданныеИзменение.Контрагент = ВтОстатокОборот.Контрагент
	|			И ВТЗапасыПереданныеИзменение.ЗаказПопкутеля = ВтОстатокОборот.ЗаказПокупателя
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтОстатокОборотПоРегистратору КАК ВтОстатокОборотПоРегистратору
	|		ПО ВТЗапасыПереданныеИзменение.Организация = ВтОстатокОборотПоРегистратору.Организация
	|			И ВТЗапасыПереданныеИзменение.Контрагент = ВтОстатокОборотПоРегистратору.Контрагент
	|			И ВТЗапасыПереданныеИзменение.ЗаказПопкутеля = ВтОстатокОборотПоРегистратору.ЗаказПокупателя
	|			И ВТЗапасыПереданныеИзменение.Договор = ВтОстатокОборотПоРегистратору.ДокументДоговор
	|			И ВТЗапасыПереданныеИзменение.Номенклатура = ВтОстатокОборотПоРегистратору.Номенклатура
	|			И ВТЗапасыПереданныеИзменение.Характеристика = ВтОстатокОборотПоРегистратору.Характеристика
	|			И ВТЗапасыПереданныеИзменение.Партия = ВтОстатокОборотПоРегистратору.Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОборотПродажи.ОрганизацияПредставление КАК ОрганизацияПредставление,
	|	ВТОборотПродажи.НоменклатураПредставление КАК НоменклатураПредставление,
	|	ВТОборотПродажи.ХарактеристикаПредставление КАК ХарактеристикаПредставление,
	|	ВТОборотПродажи.ПартияПредставление КАК ПартияПредставление,
	|	ВТОборотПродажи.КонтрагентПредставление КАК КонтрагентПредставление,
	|	ВТОборотПродажи.ДоговорПредставление КАК ДоговорПредставление,
	|	ВТОборотПродажи.ОстатокЗапасыПереданные КАК КоличествоОстатокЗапасыПереданные,
	|	ВТОборотПродажи.СуммаРасчетовЗапасыПереданные КАК СуммаРасчетовОстатокЗапасыПереданные,
	|	ВТОборотПродажи.НомерСтроки КАК НомерСтроки,
	|	ВТОборотПродажи.ЕдиницаИзмеренияПредставление КАК ЕдиницаИзмеренияПредставление,
	|	"""" КАК ТипПриемаПередачиПредставление,
	|	ВТОборотПродажи.Количество КАК ОстатокЗапасыПереданные,
	|	ВТОборотПродажи.Сумма КАК СуммаРасчетовЗапасыПереданные,
	|	ВТОборотПродажи.ЗаказПредставление КАК ЗаказПредставление,
	|	ВТОборотПродажи.КоличествоИзменение КАК КоличествоИзменение,
	|	ВТОборотПродажи.КоличествоОброт КАК КоличествоОброт,
	|	ВТОборотПродажи.КоличествоОборотРег КАК КоличествоОборотРег
	|ИЗ
	|	ВТОборотПродажи КАК ВТОборотПродажи
	|ГДЕ
	|	(ЕСТЬNULL(ВТОборотПродажи.ОстатокЗапасыПереданные, 0) < 0
	|			ИЛИ ЕСТЬNULL(ВТОборотПродажи.СуммаРасчетовЗапасыПереданные, 0) < 0)");
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МоментКонтроля", ДокументСсылкаОтчетКомиссионера.Дата);
	Запрос.УстановитьПараметр("Регистратор", ДокументСсылкаОтчетКомиссионера);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Если НЕ МассивРезультатов[4].Пустой()
		Тогда
		
		ДокументОбъектОтчетКомиссионера = ДокументСсылкаОтчетКомиссионера.ПолучитьОбъект();
		
	КонецЕсли;
	
	// Отрицательный остаток оборотов по регистру Товары переданные.
	Если НЕ МассивРезультатов[4].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[4].Выбрать();
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструЗапасыПереданные(ДокументОбъектОтчетКомиссионера, ВыборкаИзРезультатаЗапроса, Отказ);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтроль()

#Область ИнтерфейсПечати

Функция ДанныеДокументовРегУчет(МассивОбъектов, ИспользоватьФаксимиле, ПечатнаяФормаТолькоВРублях = Истина, Ошибки = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("НациональнаяВалюта", Константы.НациональнаяВалюта.Получить());
	Запрос.УстановитьПараметр("ПечатнаяФормаТолькоВРублях", ПечатнаяФормаТолькоВРублях);
	Запрос.УстановитьПараметр("ИспользоватьФаксимиле", ИспользоватьФаксимиле);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтчетКомиссионера.Ссылка КАК Ссылка,
	|	ОтчетКомиссионера.Дата КАК ДатаДокумента,
	|	ВЫРАЗИТЬ(ОтчетКомиссионера.Номер КАК СТРОКА(12)) КАК Номер,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ФаксимилеПустая,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСчетФактура.Продажа) КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА ОтчетКомиссионера.Организация.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОтчетКомиссионера.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ ОтчетКомиссионера.Организация
	|	КОНЕЦ КАК Организация,
	|	ОтчетКомиссионера.Организация.Префикс КАК Префикс,
	|	ОтчетКомиссионера.Организация.ФайлЛоготип КАК Логотип,
	|	ОтчетКомиссионера.Организация.ФайлФаксимильнаяПечать КАК ФаксимилеПечати,
	|	ВЫБОР
	|		КОГДА &ИспользоватьФаксимиле = ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДаНет.Да)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ДаНет.Нет)
	|	КОНЕЦ КАК ИспользоватьФаксимиле,
	|	ВЫБОР
	|		КОГДА ОтчетКомиссионера.Организация.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОтчетКомиссионера.Организация.ЦифровойИндексОбособленногоПодразделения
	|		КОГДА ОтчетКомиссионера.Подразделение.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				И ОтчетКомиссионера.Подразделение.ГоловнаяОрганизация = ОтчетКомиссионера.Организация
	|			ТОГДА ОтчетКомиссионера.Подразделение.ЦифровойИндексОбособленногоПодразделения
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ЦифровойИндексОбособленногоПодразделения,
	|	ВЫБОР
	|		КОГДА ОтчетКомиссионера.Организация.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОтчетКомиссионера.Организация
	|		КОГДА ОтчетКомиссионера.Подразделение.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|				И ОтчетКомиссионера.Подразделение.ГоловнаяОрганизация = ОтчетКомиссионера.Организация
	|			ТОГДА ОтчетКомиссионера.Подразделение
	|		ИНАЧЕ ОтчетКомиссионера.Организация
	|	КОНЕЦ КАК ОбособленноеПодразделениеПоставщика,
	|	ВЫБОР
	|		КОГДА ОтчетКомиссионера.Организация.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА ОтчетКомиссионера.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.НеОблагаетсяНДС)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СтатусУПД,
	|	ЛОЖЬ КАК ЭтоСводныйСчетФактура,
	|	ЛОЖЬ КАК ЭтоКорректировка,
	|	ОтчетКомиссионера.Организация.БанковскийСчетПоУмолчанию КАК БанковскийСчет,
	|	ПРЕДСТАВЛЕНИЕ(ОтчетКомиссионера.Подразделение) КАК ПредставлениеПодразделения,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеСкладаСписания,
	|	ОтчетКомиссионера.Организация КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА ОтчетКомиссионера.Контрагент.ГоловнойКонтрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ОтчетКомиссионера.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА ОтчетКомиссионера.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ОтчетКомиссионера.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ОтчетКомиссионера.Контрагент КАК ОбособленноеПодразделениеПокупателя,
	|	ОтчетКомиссионера.Контрагент КАК Грузополучатель,
	|	ОтчетКомиссионера.Контрагент.БанковскийСчетПоУмолчанию КАК БанковскийСчетКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК АдресДоставки,
	|	НЕОПРЕДЕЛЕНО КАК РасшифровкаПодписиКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьНомер,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьДата,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьВыдана,
	|	НЕОПРЕДЕЛЕНО КАК ДоверенностьЛицо,
	|	НЕОПРЕДЕЛЕНО КАК Основание,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ТОГДА НациональнаяВалюта.Значение
	|		ИНАЧЕ ОтчетКомиссионера.ВалютаДокумента
	|	КОНЕЦ КАК ВалютаДокумента,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ТОГДА НациональнаяВалюта.Значение.НаименованиеПолное
	|		ИНАЧЕ ОтчетКомиссионера.ВалютаДокумента.НаименованиеПолное
	|	КОНЕЦ КАК ВалютаНаименование,
	|	ВЫБОР
	|		КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|			ТОГДА НациональнаяВалюта.Значение.Код
	|		ИНАЧЕ ОтчетКомиссионера.ВалютаДокумента.Код
	|	КОНЕЦ КАК ВалютаКод,
	|	ОтчетКомиссионера.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ОтчетКомиссионера.НДСВключатьВСтоимость КАК НДСВключатьВСтоимость,
	|	ОтчетКомиссионера.Курс КАК Курс,
	|	ОтчетКомиссионера.Кратность КАК Кратность,
	|	ПРЕДСТАВЛЕНИЕ(ОтчетКомиссионера.Договор) КАК ПредставлениеОснования,
	|	ОтчетКомиссионера.Договор КАК ОснованиеПечатиСсылка,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеНомер,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеДата,
	|	НЕОПРЕДЕЛЕНО КАК ТранспортнаяНакладнаяНомер,
	|	НЕОПРЕДЕЛЕНО КАК ТранспортнаяНакладнаяДата,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьРуководителя,
	|	НЕОПРЕДЕЛЕНО КАК РасшифровкаПодписиРуководителя,
	|	НЕОПРЕДЕЛЕНО КАК ФаксимилеРуководителя,
	|	НЕОПРЕДЕЛЕНО КАК РасшифровкаПодписиГлавногоБухгалтера,
	|	НЕОПРЕДЕЛЕНО КАК ФаксимилеГлавногоБухгалтера,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьКладовщика,
	|	НЕОПРЕДЕЛЕНО КАК РасшифровкаПодписиКладовщика,
	|	НЕОПРЕДЕЛЕНО КАК ФаксимилеКладовщика,
	|	0 КАК Вес,
	|	0 КАК Объем,
	|	ОтчетКомиссионера.Запасы.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		НЕОПРЕДЕЛЕНО КАК Содержание,
	|		ВЫБОР
	|			КОГДА (ВЫРАЗИТЬ(ОтчетКомиссионера.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|				ТОГДА ОтчетКомиссионера.Запасы.Номенклатура.Наименование
	|			ИНАЧЕ ВЫРАЗИТЬ(ОтчетКомиссионера.Запасы.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		КОНЕЦ КАК ПредставлениеНоменклатуры,
	|		Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|		Номенклатура.Код КАК ЗапасКод,
	|		Номенклатура.Код КАК Код,
	|		Номенклатура.Артикул КАК Артикул,
	|		Характеристика КАК Характеристика,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионера.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И ОтчетКомиссионера.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА ОтчетКомиссионера.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Наименование
	|			ИНАЧЕ ОтчетКомиссионера.Запасы.Номенклатура.ЕдиницаИзмерения.Наименование
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Наименование,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионера.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И ОтчетКомиссионера.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА ОтчетКомиссионера.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору.Код
	|			ИНАЧЕ ОтчетКомиссионера.Запасы.Номенклатура.ЕдиницаИзмерения.Код
	|		КОНЕЦ КАК ЕдиницаИзмеренияПоОКЕИ_Код,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионера.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение <> ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|				ТОГДА ОтчетКомиссионера.Запасы.ЕдиницаИзмерения.Коэффициент
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК КоэффициентЕдиницыИзмерения,
	|		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество,
	|		ВЫБОР
	|			КОГДА ОтчетКомиссионера.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|					И ПечатьПользовательскихЕдиницИзмерения.Значение = ЗНАЧЕНИЕ(Перечисление.ПечатьПользовательскихЕдиницИзмерения.ФормироватьПечатныйДокументВПользовательскойЕдинице)
	|					И ОтчетКомиссионера.Запасы.ЕдиницаИзмерения.ЕдиницаИзмеренияПоКлассификатору <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	|				ТОГДА 1
	|			КОГДА ОтчетКомиссионера.Запасы.ЕдиницаИзмерения ССЫЛКА Справочник.КлассификаторЕдиницИзмерения
	|				ТОГДА 1
	|			ИНАЧЕ ОтчетКомиссионера.Запасы.ЕдиницаИзмерения.Коэффициент
	|		КОНЕЦ КАК КоличествоВОдномМесте,
	|		Количество КАК КоличествоМест,
	|		СтавкаНДС КАК СтавкаНДС,
	|		0 КАК МассаБрутто,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях
	|				ТОГДА ВЫБОР
	|						КОГДА ОтчетКомиссионера.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|								И ОтчетКомиссионера.Ссылка.ВалютаДокумента <> &НациональнаяВалюта
	|							ТОГДА ВЫРАЗИТЬ(ОтчетКомиссионера.Запасы.Цена * ОтчетКомиссионера.Курс / ОтчетКомиссионера.Кратность КАК ЧИСЛО(15, 2))
	|						ИНАЧЕ ОтчетКомиссионера.Запасы.Цена
	|					КОНЕЦ
	|			ИНАЧЕ ОтчетКомиссионера.Запасы.Цена
	|		КОНЕЦ КАК Цена,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях
	|				ТОГДА ВЫБОР
	|						КОГДА ОтчетКомиссионера.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|								И ОтчетКомиссионера.Ссылка.ВалютаДокумента <> &НациональнаяВалюта
	|							ТОГДА ВЫРАЗИТЬ(ОтчетКомиссионера.Запасы.Сумма * ОтчетКомиссионера.Курс / ОтчетКомиссионера.Кратность КАК ЧИСЛО(15, 2))
	|						ИНАЧЕ ОтчетКомиссионера.Запасы.Сумма
	|					КОНЕЦ
	|			ИНАЧЕ ОтчетКомиссионера.Запасы.Сумма
	|		КОНЕЦ КАК Сумма,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях
	|				ТОГДА ВЫБОР
	|						КОГДА ОтчетКомиссионера.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|								И ОтчетКомиссионера.Ссылка.ВалютаДокумента <> &НациональнаяВалюта
	|							ТОГДА ВЫРАЗИТЬ(ОтчетКомиссионера.Запасы.СуммаНДС * ОтчетКомиссионера.Курс / ОтчетКомиссионера.Кратность КАК ЧИСЛО(15, 2))
	|						ИНАЧЕ ОтчетКомиссионера.Запасы.СуммаНДС
	|					КОНЕЦ
	|			ИНАЧЕ ОтчетКомиссионера.Запасы.СуммаНДС
	|		КОНЕЦ КАК СуммаНДС,
	|		ВЫБОР
	|			КОГДА &ПечатнаяФормаТолькоВРублях
	|				ТОГДА ВЫБОР
	|						КОГДА ОтчетКомиссионера.Ссылка.Договор.РасчетыВУсловныхЕдиницах
	|								И ОтчетКомиссионера.Ссылка.ВалютаДокумента <> &НациональнаяВалюта
	|							ТОГДА ВЫРАЗИТЬ(ОтчетКомиссионера.Запасы.Всего * ОтчетКомиссионера.Курс / ОтчетКомиссионера.Кратность КАК ЧИСЛО(15, 2))
	|						ИНАЧЕ ОтчетКомиссионера.Запасы.Всего
	|					КОНЕЦ
	|			ИНАЧЕ ОтчетКомиссионера.Запасы.Всего
	|		КОНЕЦ КАК Всего,
	|		СтранаПроисхождения КАК СтранаСсылка,
	|		ПРЕДСТАВЛЕНИЕ(ОтчетКомиссионера.Запасы.СтранаПроисхождения) КАК СтранаПредставление,
	|		СтранаПроисхождения.Код КАК СтранаКод,
	|		НомерГТД.РегистрационныйНомер КАК ПредставлениеГТД,
	|		КлючСвязи КАК КлючСвязи
	|	) КАК ТаблицаЗапасы,
	|	ОтчетКомиссионера.Покупатели.(
	|		Покупатель КАК Покупатель,
	|		СчетФактура КАК СчетФактура,
	|		НЕОПРЕДЕЛЕНО КАК СведенияОПокупателе,
	|		НЕОПРЕДЕЛЕНО КАК СведенияОГрузополучателе,
	|		Всего КАК Всего,
	|		КлючСвязи КАК КлючСвязи
	|	) КАК ТаблицаПокупателей
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК ОтчетКомиссионера,
	|	Константа.НациональнаяВалюта КАК НациональнаяВалюта,
	|	Константа.ПечатьПользовательскихЕдиницИзмерения КАК ПечатьПользовательскихЕдиницИзмерения
	|ГДЕ
	|	ОтчетКомиссионера.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОтчетКомиссионера.Запасы.НомерСтроки";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ДанныеПолученныхДокументовРегУчет(МассивОбъектов, ИспользоватьФаксимиле, ПечатнаяФормаТолькоВРублях = Истина, Ошибки = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ИспользоватьФаксимиле", ИспользоватьФаксимиле);
	Запрос.УстановитьПараметр("ПечатнаяФормаТолькоВРублях", ПечатнаяФормаТолькоВРублях);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтчетКомиссионера.Ссылка КАК Ссылка
	|	,Неопределено КАК ДатаДокумента
	|	,Неопределено КАК Номер
	|	,Неопределено КАК ЦифровойИндексОбособленногоПодразделения
	|	,Неопределено КАК НомерИсправления
	|	,Неопределено КАК ДатаИсправления
	|	,Неопределено КАК ФаксимилеПустая
	|	,Значение(Перечисление.ВидыОперацийСчетФактура.Продажа) КАК ВидОперации
	|	,Выбор КОГДА ОтчетКомиссионера.Контрагент.ГоловнойКонтрагент <> Значение(Справочник.Контрагенты.ПустаяСсылка)
	|		ТОГДА ОтчетКомиссионера.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ОтчетКомиссионера.Контрагент КОНЕЦ КАК Организация
	|	,ОтчетКомиссионера.Контрагент КАК ОбособленноеПодразделениеПоставщика
	|	,ОтчетКомиссионера.Контрагент КАК Грузоотправитель
	|	,Неопределено КАК Префикс
	|	,Неопределено КАК Логотип
	|	,Неопределено КАК ФаксимилеПечати
	|	,Выбор КОГДА &ИспользоватьФаксимиле = ИСТИНА
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ДаНет.Да)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ДаНет.Нет) КОНЕЦ КАК ИспользоватьФаксимиле
	|	,Выбор КОГДА ОтчетКомиссионера.Контрагент.ВидКонтрагента = Значение(Перечисление.ВидыКонтрагентов.ФизическоеЛицо)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ КОНЕЦ КАК ЭтоФизическоеЛицо
	|	,Выбор КОГДА ОтчетКомиссионера.НалогообложениеНДС = Значение(Перечисление.ТипыНалогообложенияНДС.НеОблагаетсяНДС)
	|		ТОГДА 2
	|		ИНАЧЕ 1 КОНЕЦ КАК СтатусУПД
	|	,Ложь КАК ЭтоСводныйСчетФактура
	|	,Ложь КАК ЭтоКорректировка
	|	,ОтчетКомиссионера.Контрагент.БанковскийСчетПоУмолчанию КАК БанковскийСчет
	|	,Неопределено КАК ПредставлениеПодразделения
	|	,Неопределено КАК ПредставлениеСкладаСписания
	|	,Выбор КОГДА ОтчетКомиссионера.Организация.ГоловнаяОрганизация <> Значение(Справочник.Организации.ПустаяСсылка)
	|		ТОГДА ОтчетКомиссионера.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ ОтчетКомиссионера.Организация КОНЕЦ КАК Контрагент
	|	,ОтчетКомиссионера.Организация КАК ОбособленноеПодразделениеПокупателя
	|	,ОтчетКомиссионера.Организация КАК Грузополучатель
	|	,ОтчетКомиссионера.Организация.БанковскийСчетПоУмолчанию КАК БанковскийСчетКонтрагента
	|	,Неопределено КАК АдресДоставки
	|	,ОтчетКомиссионера.Организация.ПодписьРуководителя.РасшифровкаПодписи КАК РасшифровкаПодписиКонтрагента
	|	,Неопределено КАК ДоверенностьНомер
	|	,Неопределено КАК ДоверенностьДата
	|	,Неопределено КАК ДоверенностьВыдана
	|	,Неопределено КАК ДоверенностьЛицо
	|	,ОтчетКомиссионера.Договор.Представление КАК Основание
	|	,ОтчетКомиссионера.Договор.Представление КАК ПредставлениеОснования
	|	,ОтчетКомиссионера.Договор КАК ОснованиеПечатиСсылка
	|	,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА 
	|		ТОГДА НациональнаяВалюта.Значение
	|		ИНАЧЕ ОтчетКомиссионера.ВалютаДокумента КОНЕЦ КАК ВалютаДокумента
	|	,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА 
	|		ТОГДА НациональнаяВалюта.Значение.НаименованиеПолное
	|		ИНАЧЕ ОтчетКомиссионера.ВалютаДокумента.НаименованиеПолное КОНЕЦ КАК ВалютаНаименование
	|	,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА 
	|		ТОГДА НациональнаяВалюта.Значение.Код
	|		ИНАЧЕ ОтчетКомиссионера.ВалютаДокумента.Код КОНЕЦ КАК ВалютаКод
	|	,ОтчетКомиссионера.СуммаВключаетНДС КАК СуммаВключаетНДС
	|	,ОтчетКомиссионера.НДСВключатьВСтоимость КАК НДСВключатьВСтоимость
	|	,ОтчетКомиссионера.Курс КАК Курс
	|	,ОтчетКомиссионера.Кратность КАК Кратность
	|	,ОтчетКомиссионера.Договор.НомерДоговора КАК ОснованиеНомер
	|	,ОтчетКомиссионера.Договор.ДатаДоговора КАК ОснованиеДата
	|	,Неопределено КАК ТранспортнаяНакладнаяНомер
	|	,Неопределено КАК ТранспортнаяНакладнаяДата
	|	,Неопределено КАК ДолжностьРуководителя
	|	,Неопределено КАК РасшифровкаПодписиРуководителя
	|	,Неопределено КАК ФаксимилеРуководителя
	|	,Неопределено КАК РасшифровкаПодписиГлавногоБухгалтера
	|	,Неопределено КАК ФаксимилеГлавногоБухгалтера
	|	,Неопределено КАК ДолжностьКладовщика
	|	,Неопределено КАК РасшифровкаПодписиКладовщика
	|	,Неопределено КАК ФаксимилеКладовщика
	|	,0 КАК Вес
	|	,0 КАК Объем
	|	,Неопределено КАК НоменклатураДоставки
	|	,Неопределено КАК ПредставлениеНоменклатурыДоставки
	|	,Неопределено КАК КодНоменклатурыДоставки
	|	,Неопределено КАК АртикулНоменклатурыДоставки
	|	,Неопределено КАК СтоимостьДоставки
	|	,Неопределено КАК СтавкаНДСДоставки
	|	,Неопределено КАК СуммаНДСДоставки
	|	,Неопределено КАК ИдентификаторГосКонтракта
	|
	// ::: Табличная часть Расходы
	|	,ОтчетКомиссионера.Запасы.(
	|		1 КАК НомерСтроки
	|		,Неопределено КАК Номенклатура
	|		,""Комиссионное вознаграждение"" КАК Содержание
	|		,Неопределено КАК ПредставлениеНоменклатуры
	|		,Значение(Перечисление.ТипыНоменклатуры.Услуга) КАК ТипНоменклатуры
	|		,Неопределено КАК ЗапасКод
	|		,Неопределено КАК Код
	|		,Неопределено КАК Артикул
	|		,Неопределено КАК КодТНВЭД
	|		,Неопределено КАК ЕдиницаИзмеренияПоОКЕИ_Наименование
	|		,Неопределено КАК ЕдиницаИзмеренияПоОКЕИ_Код
	|		,Неопределено КАК Характеристика
	|		,Неопределено КАК ЕдиницаИзмерения
	|		,Неопределено КАК ВидУпаковки
	|		,1 КАК КоэффициентЕдиницыИзмерения
	|		,Неопределено КАК Количество
	|		,Неопределено КАК КоличествоВОдномМесте
	|		,Неопределено КАК КоличествоМест
	|		,СтавкаНДС КАК СтавкаНДС
	|		,0 КАК МассаБрутто
	|		,Неопределено КАК Цена
	
	// Сумма * Курс / Кратность ЛИБО Сумма
	|		,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|				ИЛИ (Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА 
	|						И Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение)
	|			ТОГДА
	|				ВЫРАЗИТЬ(
	|					ВЫБОР КОГДА ОтчетКомиссионера.СуммаВключаетНДС
	|						ТОГДА СуммаВознаграждения / ((СтавкаНДС.Ставка + 100) / 100)
	|						ИНАЧЕ СуммаВознаграждения КОНЕЦ * ОтчетКомиссионера.Курс / ОтчетКомиссионера.Кратность
	|				КАК Число(15, 2))
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(
	|					ВЫБОР КОГДА ОтчетКомиссионера.СуммаВключаетНДС
	|						ТОГДА СуммаВознаграждения / ((СтавкаНДС.Ставка + 100) / 100)
	|						ИНАЧЕ СуммаВознаграждения КОНЕЦ
	|				КАК Число(15, 2))
	|			КОНЕЦ КАК Сумма
	
	// СуммаНДС * Курс / Кратность ЛИБО СуммаНДС
	|		,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|				ИЛИ (Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА 
	|						И Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение)
	|			ТОГДА СуммаНДСВознаграждения * ОтчетКомиссионера.Курс / ОтчетКомиссионера.Кратность
	|			ИНАЧЕ СуммаНДСВознаграждения КОНЕЦ КАК СуммаНДС
	
	// Всего * Курс / Кратность ЛИБО Всего
	|		,Выбор КОГДА &ПечатнаяФормаТолькоВРублях = ИСТИНА
	|				ИЛИ (Ссылка.Договор.РасчетыВУсловныхЕдиницах = ИСТИНА 
	|						И Ссылка.ВалютаДокумента <> НациональнаяВалюта.Значение)
	|			ТОГДА 
	|				ВЫРАЗИТЬ(
	|					ВЫБОР КОГДА ОтчетКомиссионера.СуммаВключаетНДС
	|						ТОГДА СуммаВознаграждения / ((СтавкаНДС.Ставка + 100) / 100) + СуммаНДСВознаграждения
	|						ИНАЧЕ СуммаВознаграждения + СуммаНДСВознаграждения  КОНЕЦ * ОтчетКомиссионера.Курс / ОтчетКомиссионера.Кратность
	|				КАК Число(15, 2))
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(
	|					ВЫБОР КОГДА ОтчетКомиссионера.СуммаВключаетНДС
	|						ТОГДА СуммаВознаграждения / ((СтавкаНДС.Ставка + 100) / 100) + СуммаНДСВознаграждения
	|						ИНАЧЕ СуммаВознаграждения + СуммаНДСВознаграждения КОНЕЦ 
	|				КАК Число(15, 2))
	|			КОНЕЦ КАК Всего
	|		,Неопределено КАК СтранаСсылка
	|		,Неопределено КАК СтранаКод
	|		,Неопределено КАК СтранаПредставление
	|		,Неопределено КАК ПредставлениеГТД
	|	) КАК ТаблицаЗапасы
	|
	// ::: Табличная часть Платежные документы
	|	,Неопределено КАК ПлатежныеДокументы
	|
	|ИЗ Документ.ОтчетКомиссионера КАК ОтчетКомиссионера
	|	,Константа.НациональнаяВалюта КАК НациональнаяВалюта
	|ГДЕ ОтчетКомиссионера.Ссылка В(&МассивОбъектов)";
	
	ДанныеПечати = Запрос.Выполнить().Выгрузить();
	
	ИменаКолонокТЧ =
	"НомерСтроки
	|,Номенклатура
	|,Содержание
	|,ПредставлениеНоменклатуры
	|,ТипНоменклатуры
	|,ЗапасКод
	|,Код
	|,Артикул
	|,КодТНВЭД
	|,ЕдиницаИзмерения
	|,ЕдиницаИзмеренияПоОКЕИ_Наименование
	|,ЕдиницаИзмеренияПоОКЕИ_Код
	|,Характеристика
	|,ВидУпаковки
	|,КоэффициентЕдиницыИзмерения
	|,Количество
	|,КоличествоВОдномМесте
	|,КоличествоМест
	|,СтавкаНДС
	|,МассаБрутто
	|,СтранаСсылка
	|,СтранаКод
	|,СтранаПредставление
	|,ПредставлениеГТД";
	
	Для каждого ДанныеОбъекта Из ДанныеПечати Цикл
		
		ДанныеОбъекта.ТаблицаЗапасы.Свернуть(ИменаКолонокТЧ, "Цена, Сумма, СуммаНДС, Всего");
		
	КонецЦикла;
	
	Возврат ДанныеПечати;
	
КонецФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Обработка.ПечатьСчетФактура.СчетФактураПолученный";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура (полученный)'");;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;
	
	
КонецПроцедуры

#КонецОбласти

#Область РаботаССерийнымиНомерами

// Формирует таблицу значений, содержащую данные для проведения по регистру СерийныеНомераГарантии.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСерийныеНомера(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Если ДокументСсылка.СерийныеНомера.Количество()=0 Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерийныеНомераГарантии", Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаЗапасы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВременнаяТаблицаЗапасы.Период КАК ДатаСобытия,
	|	ЗНАЧЕНИЕ(Перечисление.ОперацииСерийныхНомеров.Расход) КАК Операция,		
	|	СерийныеНомера.СерийныйНомер КАК СерийныйНомер,
	|	ВременнаяТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ВременнаяТаблицаЗапасы.Партия КАК Партия,
	|	1 КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ВременнаяТаблицаЗапасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСерийныеНомера КАК СерийныеНомера
	|		ПО ВременнаяТаблицаЗапасы.КлючСвязиСерийныеНомера = СерийныеНомера.КлючСвязи";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерийныеНомераГарантии", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаСерийныеНомера()

Процедура СформироватьТаблицаЗапасыВРазрезеГТД(СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыНаСкладах.НомерСтроки) КАК НомерСтроки
	|	,ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
	|	,ТаблицаЗапасыНаСкладах.Период КАК Период
	|	,ТаблицаЗапасыНаСкладах.Организация КАК Организация
	|	,ТаблицаЗапасыНаСкладах.Номенклатура КАК Номенклатура
	|	,ТаблицаЗапасыНаСкладах.Характеристика КАК Характеристика
	|	,ТаблицаЗапасыНаСкладах.Партия КАК Партия
	|	,ТаблицаЗапасыНаСкладах.НомерГТД КАК НомерГТД
	|	,ТаблицаЗапасыНаСкладах.СтранаПроисхождения КАК СтранаПроисхождения
	|	,СУММА(ТаблицаЗапасыНаСкладах.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыНаСкладах
	|ГДЕ
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> Значение(Справочник.СтраныМира.Россия)
	|	И ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> Значение(Справочник.СтраныМира.ПустаяССылка)
	|	И ТаблицаЗапасыНаСкладах.НомерГТД <> Значение(Справочник.НомераГТД.ПустаяССылка)
	|	И ТаблицаЗапасыНаСкладах.ПрямоеСписаниеГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыНаСкладах.Период
	|	,ТаблицаЗапасыНаСкладах.Организация
	|	,ТаблицаЗапасыНаСкладах.Номенклатура
	|	,ТаблицаЗапасыНаСкладах.Характеристика
	|	,ТаблицаЗапасыНаСкладах.Партия
	|	,ТаблицаЗапасыНаСкладах.НомерГТД
	|	,ТаблицаЗапасыНаСкладах.СтранаПроисхождения
	|
	|;Выбрать
	|	МИНИМУМ(ТаблицаЗапасыПереданныеНаСкладах.НомерСтроки) КАК НомерСтроки
	|	,ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
	|	,ТаблицаЗапасыПереданныеНаСкладах.Период КАК Период
	|	,ТаблицаЗапасыПереданныеНаСкладах.Организация КАК Организация
	|	,ТаблицаЗапасыПереданныеНаСкладах.Номенклатура КАК Номенклатура
	|	,ТаблицаЗапасыПереданныеНаСкладах.Характеристика КАК Характеристика
	|	,ТаблицаЗапасыПереданныеНаСкладах.Партия КАК Партия
	|	,ТаблицаЗапасыПереданныеНаСкладах.НомерГТД КАК НомерГТД
	|	,ТаблицаЗапасыПереданныеНаСкладах.СтранаПроисхождения КАК СтранаПроисхождения
	|	,СУММА(ТаблицаЗапасыПереданныеНаСкладах.Количество) КАК Количество
	|	,ТаблицаЗапасыПереданныеНаСкладах.Документ КАК ДокументПередачи
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыПереданныеНаСкладах
	|ГДЕ
	|	ТаблицаЗапасыПереданныеНаСкладах.СтранаПроисхождения <> Значение(Справочник.СтраныМира.Россия)
	|	И ТаблицаЗапасыПереданныеНаСкладах.СтранаПроисхождения <> Значение(Справочник.СтраныМира.ПустаяССылка)
	|	И ТаблицаЗапасыПереданныеНаСкладах.НомерГТД <> Значение(Справочник.НомераГТД.ПустаяССылка)
	|	И НЕ ТаблицаЗапасыПереданныеНаСкладах.ПрямоеСписаниеГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыПереданныеНаСкладах.Период
	|	,ТаблицаЗапасыПереданныеНаСкладах.Организация
	|	,ТаблицаЗапасыПереданныеНаСкладах.Номенклатура
	|	,ТаблицаЗапасыПереданныеНаСкладах.Характеристика
	|	,ТаблицаЗапасыПереданныеНаСкладах.Партия
	|	,ТаблицаЗапасыПереданныеНаСкладах.НомерГТД
	|	,ТаблицаЗапасыПереданныеНаСкладах.СтранаПроисхождения
	|	,ТаблицаЗапасыПереданныеНаСкладах.Документ";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыВРазрезеГТД", РезультатЗапроса[0].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыПереданныеВРазрезеГТД", РезультатЗапроса[1].Выгрузить());
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СобытияДокументовУНФ.ПолучитьПредставлениеДокумента(Данные.Ссылка, Данные, Представление, СтандартнаяОбработка);
	
	Если Данные.Ссылка.ВидОперации = Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетКомиссионераОВозвратах Тогда
		Представление = СтрЗаменить(Представление, НСтр("ru='Отчет комиссионера'"), НСтр("ru='Отчет комиссионера о возвратах'"));
	Иначе
		Представление = СтрЗаменить(Представление, НСтр("ru='Отчет комиссионера'"), НСтр("ru='Отчет комиссионера о продажах'"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СобытияДокументовУНФ.ПолучитьПредставлениеПолейДокумента(,Поля, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаДанныхИзВнешнегоИсточника

Процедура ПоляЗагрузкиДанныхИзВнешнегоИсточника(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных) Экспорт
	
	//
	// Для группы полей действует правило: хотя бы одно поле в группе должно быть выбрано в колонках
	//
	
	ПолноеИмяОбъектаЗаполнения = НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения;
	
	ОписаниеТиповСтрока25 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(25));
	ОписаниеТиповСтрока30 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(30));
	ОписаниеТиповСтрока50 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50));
	ОписаниеТиповСтрока100 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(100));
	ОписаниеТиповСтрока150 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(150));
	ОписаниеТиповСтрока200 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(200));
	ОписаниеТиповСтрока1000 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(1000));
	ОписаниеТиповЧисло15_2 = Новый ОписаниеТипов("Число", , , , Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный));
	ОписаниеТиповЧисло15_3 = Новый ОписаниеТипов("Число", , , , Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Неотрицательный));
	
	ЭтоЗапасы = (ПолноеИмяОбъектаЗаполнения = "Документ.ОтчетКомиссионера.ТабличнаяЧасть.Запасы");
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Штрихкод", "Штрихкод", ОписаниеТиповСтрока200, ОписаниеТиповКолонка, "Номенклатура", 1, , Истина, ЭтоЗапасы);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Артикул", "Артикул", ОписаниеТиповСтрока25, ОписаниеТиповКолонка, "Номенклатура", 2, , Истина);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "НоменклатураНаименование", "Номенклатура (наименование)", ОписаниеТиповСтрока100, ОписаниеТиповКолонка, "Номенклатура", 3, , Истина);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "НоменклатураНаименованиеПолное","Номенклатура (полное наименование)", ОписаниеТиповСтрока1000, ОписаниеТиповКолонка, "Номенклатура", 5, , Истина);
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Содержание", "Содержание", ОписаниеТиповСтрока1000, ОписаниеТиповСтрока1000, , , , , НастройкиЗагрузкиДанных.СодержаниеВидимо);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
		
		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ХарактеристикаНаименование", "Характеристика (наименование)", ОписаниеТиповСтрока150, ОписаниеТиповКолонка, "Характеристика", 1);
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ХарактеристикаАртикул", "Характеристика (артикул)", ОписаниеТиповСтрока25, ОписаниеТиповКолонка, "Характеристика", 2);
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартии") Тогда
		
		ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.ПартииНоменклатуры");
		ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Партия", "Партия (наименование)", ОписаниеТиповСтрока150, ОписаниеТиповКолонка);
		
	КонецЕсли;
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Количество", "Количество", ОписаниеТиповСтрока25, ОписаниеТиповЧисло15_3, , , Истина);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.КлассификаторЕдиницИзмерения, СправочникСсылка.ЕдиницыИзмерения");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ЕдиницаИзмерения", "Ед. изм.", ОписаниеТиповСтрока25, ОписаниеТиповКолонка, , , , , ПолучитьФункциональнуюОпцию("УчетВРазличныхЕдиницахИзмерения"));
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Цена", "Цена", ОписаниеТиповСтрока25, ОписаниеТиповЧисло15_2, , , Ложь);
	
	ОписаниеТиповКолонка = Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС");
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СтавкаНДС", "СтавкаНДС", ОписаниеТиповСтрока25, ОписаниеТиповКолонка, , , Ложь);
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Сумма", "Сумма", ОписаниеТиповСтрока25, ОписаниеТиповЧисло15_2, , , Ложь);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "ЦенаПередачи", "ЦенаПередачи", ОписаниеТиповСтрока25, ОписаниеТиповЧисло15_2, , , Ложь);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СуммаПередачи", "СуммаПередачи", ОписаниеТиповСтрока25, ОписаниеТиповЧисло15_2, , , Ложь);
	
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СуммаВознаграждения", "СуммаВознаграждения", ОписаниеТиповСтрока25, ОписаниеТиповЧисло15_2, , , Ложь);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "СуммаНДСВознаграждения", "СуммаНДСВознаграждения", ОписаниеТиповСтрока25, ОписаниеТиповЧисло15_2, , , Ложь);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
	ОписаниеТиповКолонка = Новый ОписаниеТипов(МассивТипов);
	ЗагрузкаДанныхИзВнешнегоИсточника.ДобавитьПолеОписанияЗагрузки(ТаблицаПолейЗагрузки, "Заказ", "Заказ", ОписаниеТиповСтрока50, ОписаниеТиповКолонка);
	
КонецПроцедуры

Процедура ПриОпределенииОбразцовЗагрузкиДанных(НастройкиЗагрузкиДанных, УникальныйИдентификатор) Экспорт
	
	Образец_xlsx = ПолучитьМакет("ОбразецЗагрузкиДанных_xlsx");
	ОбразецЗагрузкиДанных_xlsx = ПоместитьВоВременноеХранилище(Образец_xlsx, УникальныйИдентификатор);
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_xlsx", ОбразецЗагрузкиДанных_xlsx);
	
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_mxl", "ОбразецЗагрузкиДанных_mxl");
	
	Образец_csv = ПолучитьМакет("ОбразецЗагрузкиДанных_csv");
	ОбразецЗагрузкиДанных_csv = ПоместитьВоВременноеХранилище(Образец_csv, УникальныйИдентификатор);
	НастройкиЗагрузкиДанных.Вставить("ОбразецЗагрузкиДанных_csv", ОбразецЗагрузкиДанных_csv);
	
КонецПроцедуры

Процедура СопоставитьЗагружаемыеДанныеИзВнешнегоИсточника(ПараметрыСопоставления, АдресРезультата) Экспорт
	
	ТаблицаСопоставленияДанных	= ПараметрыСопоставления.ТаблицаСопоставленияДанных;
	РазмерТаблицыДанных			= ТаблицаСопоставленияДанных.Количество();
	НастройкиЗагрузкиДанных		= ПараметрыСопоставления.НастройкиЗагрузкиДанных;
	НастройкиПоиска				= НастройкиЗагрузкиДанных.НастройкиПоиска;
	
	ПолноеИмяОбъектаЗаполнения = НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения;
	
	// ТаблицаСопоставленияДанных - Тип ДанныеФормыКоллекция
	Для каждого СтрокаТаблицыФормы Из ТаблицаСопоставленияДанных Цикл
		
		// Номенклатура по ШтрихКоду, Артикулу, Наименованию, НаименованиеПолное
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьНоменклатуру(СтрокаТаблицыФормы, НастройкиПоиска);
		
		Если ПолноеИмяОбъектаЗаполнения = "Документ.ОтчетКомиссионера.ТабличнаяЧасть.Запасы" Тогда
			
			Если СтрокаТаблицыФормы.Номенклатура.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Запас Тогда
				
				СтрокаТаблицыФормы.Номенклатура = Неопределено;
				
			КонецЕсли;
			
			Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
				
				Если ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура) Тогда
					
					// Характеристика по Владельцу и Наименованию
					ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьХарактеристику(СтрокаТаблицыФормы);
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ПолучитьФункциональнуюОпцию("ИспользоватьПартии") Тогда
				
				Если ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура) Тогда
					
					// Партия по Владельцу и Наименованию
					ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьПартию(СтрокаТаблицыФормы.Партия, СтрокаТаблицыФормы.Номенклатура, СтрокаТаблицыФормы.Штрихкод, СтрокаТаблицыФормы.Партия_ВходящиеДанные);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Количество
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.Количество, СтрокаТаблицыФормы.Количество_ВходящиеДанные, 1);
		
		// ЕдиницыИзмерения по Наименованию (так же рассмотреть возможность прикрутить пользовательские ЕИ)
		ЗначениеПоУмолчанию = ?(ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура), СтрокаТаблицыФормы.Номенклатура.ЕдиницаИзмерения, Справочники.КлассификаторЕдиницИзмерения.шт);
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьЕдиницыИзмерения(СтрокаТаблицыФормы.Номенклатура, СтрокаТаблицыФормы.ЕдиницаИзмерения, СтрокаТаблицыФормы.ЕдиницаИзмерения_ВходящиеДанные, ЗначениеПоУмолчанию);
		
		// Цена
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ПреобразоватьСтрокуВЧисло(СтрокаТаблицыФормы.Цена, СтрокаТаблицыФормы.Цена_ВходящиеДанные, 1);
		
		// СтавкаНДС по наименованию
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьСтавкуНДС(СтрокаТаблицыФормы.СтавкаНДС, СтрокаТаблицыФормы.СтавкаНДС_ВходящиеДанные, Неопределено);
		
		// Заказ по номеру, дате, признаку
		ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.СопоставитьЗаказ(СтрокаТаблицыФормы.Заказ, СтрокаТаблицыФормы.Заказ_ВходящиеДанные);
		
		ПроверитьКорректностьДанныхВСтрокеТаблицы(СтрокаТаблицыФормы, ПолноеИмяОбъектаЗаполнения);
		
		ЗагрузкаДанныхИзВнешнегоИсточника.ПрогрессСопоставленияДанных(ТаблицаСопоставленияДанных.Индекс(СтрокаТаблицыФормы), РазмерТаблицыДанных);
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ТаблицаСопоставленияДанных, АдресРезультата);
	
КонецПроцедуры

Процедура ПроверитьКорректностьДанныхВСтрокеТаблицы(СтрокаТаблицыФормы, ПолноеИмяОбъектаЗаполнения = "") Экспорт
	
	ИмяСлужебногоПоля = ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна();
	
	Если ПолноеИмяОбъектаЗаполнения = "Документ.ОтчетКомиссионера.ТабличнаяЧасть.Запасы" Тогда
		
		СтрокаТаблицыФормы[ИмяСлужебногоПоля] = ЗначениеЗаполнено(СтрокаТаблицыФормы.Номенклатура)
		И СтрокаТаблицыФормы.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас
		И НЕ СтрокаТаблицыФормы.Номенклатура.ЭтоНабор // Исключая наборы
		И СтрокаТаблицыФормы.Количество <> 0
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли