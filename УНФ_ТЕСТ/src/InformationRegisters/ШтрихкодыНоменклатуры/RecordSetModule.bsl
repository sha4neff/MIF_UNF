
#Область ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Константы.МиграцияШтрихкодовВыполнена.Получить() Тогда
		Возврат
	КонецЕсли;
	
	Если Не Количество() Тогда
		
		Если Не Отбор.Найти("ШтрихКод") = Неопределено Тогда
			
			НаборЗаписей = РегистрыСведений.УдалитьШтрихкодыНоменклатуры.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ШтрихКод.Установить(Отбор.ШтрихКод.Значение);
			НаборЗаписей.Записать(Истина);
			
		КонецЕсли;
		
		Возврат
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.УдалитьШтрихкодыНоменклатуры.СоздатьНаборЗаписей();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УдалитьШтрихкодыНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Попытка
		
		Блокировка.Заблокировать();
		
		Для Каждого ЭлементНабора Из ЭтотОбъект Цикл
			
			НаборЗаписей.Отбор.ШтрихКод.Установить(ЭлементНабора.Штрихкод);
			
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ЭлементНабора);
			НаборЗаписей.Записать(Истина);
			
			НаборЗаписей.Очистить();
			
		КонецЦикла;
		
	Исключение
		
		Отказ = Истина;
		
	КонецПопытки;

КонецПроцедуры

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

#КонецОбласти
