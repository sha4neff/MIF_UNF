
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ЕстьПравоНастройкиОбмена         = ОбменСКонтрагентамиСлужебныйВызовСервера.ЕстьПравоНастройкиОбмена();
	ИспользуютсяДоговорыКонтрагентов = ОбменСКонтрагентамиПовтИсп.ИспользуютсяДоговорыКонтрагентов();
	
	Элементы.НастройкиОтправкиСоздатьНастройкуОтправки.Видимость      = ЕстьПравоНастройкиОбмена;
	Элементы.НастройкиОтправкиСоздатьНастройкуПрямогоОбмена.Видимость = ЕстьПравоНастройкиОбмена;
	Элементы.НастройкиОтправкиОбновитьСтатусыПодключения.Видимость    = ЕстьПравоНастройкиОбмена;
	Элементы.ИнтеркампаниСоздатьНастройкуИнтеркампани.Видимость       = ЕстьПравоНастройкиОбмена;
	Элементы.НастройкиПолученияСоздатьНастройкуПолучения.Видимость    = ЕстьПравоНастройкиОбмена;
	Элементы.СоздатьНастройкуВнутреннегоЭДО.Видимость                 = ЕстьПравоНастройкиОбмена;
	Элементы.НастройкиОтправкиДоговор.Видимость                       = ИспользуютсяДоговорыКонтрагентов;
	
	НастроитьОтображениеСтраницСервер();
	УправлениеДоступностьюЭлементовУправления();
	
	Если ЗначениеЗаполнено(Параметры.Источник) Тогда
	
		Элементы.Отборы.Видимость = Ложь;
		ИмяСправочникаКонтрагенты = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяПрикладногоСправочника("Контрагенты");
		ИмяСправочникаОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяПрикладногоСправочника("Организации");
		ИмяСправочникаПартнеры = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяПрикладногоСправочника("Партнеры");
		
		ПараметрыФормы = Новый Структура;
		Если ТипЗнч(Параметры.Источник) = Тип("СправочникСсылка." + ИмяСправочникаКонтрагенты) Тогда
			Контрагент = Параметры.Источник;
			УстановитьОтборыКонтрагентаВДинамическихСписках(ЭтотОбъект);
		ИначеЕсли ТипЗнч(Параметры.Источник) = Тип("СправочникСсылка." + ИмяСправочникаОрганизации) Тогда
			Организация = Параметры.Источник;
			УстановитьОтборыОрганизацииВДинамическихСписках(ЭтотОбъект);
		ИначеЕсли ТипЗнч(Параметры.Источник) = Тип("СправочникСсылка." + ИмяСправочникаПартнеры) Тогда
			Партнер = Параметры.Источник;
			УстановитьОтборыПартнераВДинамическихСписках(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	ИмяРеквизитаПартнераКонтрагента = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ПартнерКонтрагента");
	
	НавигационнаяСсылка = "e1cib/app/" + ЭтотОбъект.ИмяФормы;

	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	КонтекстныеПодсказкиБЭД.КонтекстныеПодсказки_ПриСозданииНаСервере(ЭтотОбъект, 
																		Элементы.ПанельКонтекстныхНовостей, 
																		Элементы.ГруппаКонтекстныхПодсказок);
	СформироватьКонтекст();
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		УстановитьОтборыОрганизацииВДинамическихСписках(ЭтотОбъект);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		УстановитьОтборыКонтрагентаВДинамическихСписках(ЭтотОбъект);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтатусПриглашения) Тогда
		УстановитьОтборыСтатусаПодключенияВДинамическихСписках(ЭтотОбъект);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Партнер) Тогда
		УстановитьОтборыПартнераВДинамическихСписках(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьТекущиеДелаЭДО"
		Или ИмяСобытия = "ОбновитьСостояниеПриглашений"
		ИЛИ ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		Элементы.НастройкиОтправки.Обновить();
		Элементы.Интеркампани.Обновить();
		Элементы.НастройкиПолучения.Обновить();
		Элементы.НастройкиВнутреннегоЭДО.Обновить();
		
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант" Тогда
		
		Если Источник = "ИспользоватьОбменЭДМеждуОрганизациями"
			ИЛИ Источник = "ИспользоватьПрямойОбменЭлектроннымиДокументами"
			Или Источник = "ИспользоватьВнутренниеДокументыЭДО" Тогда
			
			НастроитьОтображениеСтраницСервер();
			
		КонецЕсли;
		
	КонецЕсли;

	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	КонтекстныеПодсказкиБЭДКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭлектронноеВзаимодействиеСлужебныйКлиент.ЗаблокироватьОткрытиеФормыНаМобильномКлиенте(Отказ);
	ОбменСКонтрагентамиСлужебныйКлиент.ЗаполнитьДанныеСлужбыПоддержки(ТелефонСлужбыПоддержки, АдресЭлектроннойПочтыСлужбыПоддержки);
	Элементы.Техподдержка.Заголовок = ОбменСКонтрагентамиСлужебныйКлиент.СформироватьГиперссылкуДляОбращенияВСлужбуПоддержки();
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	КонтекстныеПодсказкиБЭДКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	УстановитьОтборыОрганизацииВДинамическихСписках(ЭтотОбъект);
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	ОбновляемыеКатегории = Новый Массив;
	ОбновляемыеКатегории.Добавить(КонтекстныеПодсказкиБЭДКатегоризацияВызовСервера.Категория_КодОператораУчетнойЗаписиОрганизации());
	
	СформироватьКонтекст(ОбновляемыеКатегории);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	УстановитьОтборыКонтрагентаВДинамическихСписках(ЭтотОбъект);
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	ОбновляемыеКатегории = Новый Массив;
	ОбновляемыеКатегории.Добавить(КонтекстныеПодсказкиБЭДКатегоризацияВызовСервера.Категория_КодОператораКонтрагента());
	
	СформироватьКонтекст(ОбновляемыеКатегории);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПодключенияПриИзменении(Элемент)
	УстановитьОтборыСтатусаПодключенияВДинамическихСписках(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	УстановитьОтборыПартнераВДинамическихСписках(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиОтправки

&НаКлиенте
Процедура НастройкиОтправкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	Если ТекущиеДанные.ЭтоПрямойОбмен Тогда
		ОткрытьФормуНастроекПрямогоОбмена();
	Иначе
		ОткрытьФормуНастроекОтправки();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НастройкиОтправкиПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Для Каждого ТекущаяСтрока Из Строки Цикл
		
		ТекущиеДанные = ТекущаяСтрока.Значение.Данные;
		
		СтандартныйРежим = Истина;
		Если ТекущиеДанные.КоличествоИдентификаторовОтправителя > 1 Тогда
			СтандартныйРежим = Ложь;
			ТекущиеДанные.ИдентификаторОтправителя = ПредставлениеИдентификатораРасширенныйРежим(
				ТекущиеДанные.КоличествоИдентификаторовОтправителя);
		КонецЕсли;
		
		Если ТекущиеДанные.КоличествоИдентификаторовПолучателя > 1 Тогда
			СтандартныйРежим = Ложь;
			ТекущиеДанные.ИдентификаторПолучателя = ПредставлениеИдентификатораРасширенныйРежим(
				ТекущиеДанные.КоличествоИдентификаторовПолучателя);
		КонецЕсли;
		
		Если ТекущиеДанные.ЭтоПрямойОбмен Тогда
			ТекущиеДанные.СтатусПриглашенияПредставление = НСтр("ru = '<Не требуется>'");
			Продолжить;
		КонецЕсли;
		
		Если СтандартныйРежим Тогда
			Продолжить;
		КонецЕсли;
		
		Количество = 0;
		Если ТекущиеДанные.КоличествоОшибка Тогда
			СтатусПриглашения = Перечисления.СтатусыПриглашений.Ошибка;
			Количество = ТекущиеДанные.КоличествоОшибка;
		ИначеЕсли ТекущиеДанные.КоличествоТребуетсяСогласие Тогда
			СтатусПриглашения = Перечисления.СтатусыПриглашений.ТребуетсяСогласие;
			Количество = ТекущиеДанные.КоличествоТребуетсяСогласие;
		ИначеЕсли ТекущиеДанные.КоличествоТребуетсяОтправить Тогда
			СтатусПриглашения = Перечисления.СтатусыПриглашений.ТребуетсяОтправить;
			Количество = ТекущиеДанные.КоличествоТребуетсяОтправить;
		ИначеЕсли ТекущиеДанные.КоличествоОтклонено Тогда
			СтатусПриглашения = Перечисления.СтатусыПриглашений.Отклонено;
			Количество = ТекущиеДанные.КоличествоОтклонено;
		ИначеЕсли ТекущиеДанные.КоличествоОжидаемСогласия Тогда
			СтатусПриглашения = Перечисления.СтатусыПриглашений.ОжидаемСогласия;
			Количество = ТекущиеДанные.КоличествоОжидаемСогласия;
		Иначе
			СтатусПриглашения = Перечисления.СтатусыПриглашений.Принято;
		КонецЕсли;
		
		Если Количество > 0 Тогда
			ТекущиеДанные.СтатусПриглашенияПредставление = СтрШаблон("%1 (%2)", СтатусПриглашения, Количество);
		Иначе
			ТекущиеДанные.СтатусПриглашенияПредставление = Строка(СтатусПриглашения);
		КонецЕсли;
		
	КонецЦикла;
	
	СоответствиеПолей = Новый Структура;
	СоответствиеПолей.Вставить("ИдентификаторОрганизации", "ИдентификаторОтправителя");
	СоответствиеПолей.Вставить("ИдентификаторКонтрагента", "ИдентификаторПолучателя");
	ОбменСКонтрагентамиСлужебный.ЗаменитьПредставлениеСтатусовПриглашений(Строки, "СтатусПриглашенияСсылка", "СтатусПриглашенияПредставление", СоответствиеПолей);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиПолучения

&НаКлиенте
Процедура НастройкиПолученияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуНастроекПолучения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиПолучения

&НаКлиенте
Процедура ИнтеркампаниВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуНастроекИнтеркампани();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСтатусыПодключения(Команда)
	
	ОбработкаОповещения = Новый ОписаниеОповещения("ОбновитьСтатусыНастроекЗавершить", ЭтотОбъект);
	КонтекстОперации = ЭлектронноеВзаимодействиеСлужебныйКлиент.НовыйКонтекстОперации();
	ОбменСКонтрагентамиСлужебныйКлиент.ПолучитьНастройкиЭДОИПараметрыСертификатов(ОбработкаОповещения, Неопределено,,, КонтекстОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуОтправки(Команда)
	ОткрытьФормуНастроекОтправки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуПолучения(Команда)
	ОткрытьФормуНастроекПолучения(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуИнтеркампани(Команда)
	ОткрытьФормуНастроекИнтеркампани(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуПрямогоОбмена(Команда)
	ОткрытьФормуНастроекПрямогоОбмена(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСсылкуНаСтатьюПо1СБухфон(Команда)
	ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьИнструкциюПо1СБухфон();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОформлениеФормы

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	ЭлементУсловногоОформления = НастройкиПолучения.УсловноеОформление.Элементы.Добавить();
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Все учетные записи>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИдентификаторКонтрагента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ИдентификаторКонтрагента");
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////
	/////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	ЭлементУсловногоОформления = НастройкиПолучения.УсловноеОформление.Элементы.Добавить();
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Все учетные записи>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИдентификаторОрганизации");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ИдентификаторОрганизации");
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////

КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеСтраницСервер()
	
	ИспользуетсяДополнительнаяАналитикаПартнеры = ОбменСКонтрагентамиПовтИсп.ИспользуетсяДополнительнаяАналитикаКонтрагентовСправочникПартнеры();
	ИспользуетсяНесколькоОрганизацийЭД          = ЭлектронноеВзаимодействиеСлужебный.ИспользуетсяНесколькоОрганизаций();
	ИспользоватьОбменЭДМеждуОрганизациями       = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ЗначениеФункциональнойОпции("ИспользоватьОбменЭДМеждуОрганизациями");
	ИспользоватьВнутренниеДокументы             = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ЗначениеФункциональнойОпции("ИспользоватьВнутренниеДокументыЭДО");
	
	Элементы.МеждуСвоимиОрганизациями.Видимость = ИспользоватьОбменЭДМеждуОрганизациями;
	Элементы.СтраницаНастройкиВнутреннегоЭДО.Видимость = ИспользоватьВнутренниеДокументы;
	
	Элементы.Партнер.Видимость = Ложь;
	Если ИспользуетсяДополнительнаяАналитикаПартнеры
		И ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ЗначениеФункциональнойОпции(
			"ИспользоватьПартнеровИКонтрагентов") Тогда
		
		Элементы.Партнер.Видимость = Истина;
	КонецЕсли;
	
	Если ОбменСКонтрагентамиСлужебный.ИспользоватьПрямойОбмен() Тогда
		Элементы.НастройкиОтправкиГруппаСоздать.Вид = ВидГруппыФормы.Подменю;
		Элементы.НастройкиОтправкиГруппаСоздать.Картинка = БиблиотекаКартинок.СоздатьЭлементСписка;
		Элементы.НастройкиОтправкиСоздатьНастройкуОтправки.Заголовок = НСтр("ru = 'настройку обмена через сервис ЭДО'");
		Элементы.НастройкиОтправкиСоздатьНастройкуОтправки.Картинка = БиблиотекаКартинок.Пустая;
	Иначе
		Элементы.НастройкиОтправкиГруппаСоздать.Вид = ВидГруппыФормы.ГруппаКнопок;
		Элементы.НастройкиОтправкиСоздатьНастройкуОтправки.Заголовок = НСтр("ru = 'Создать'");
		Элементы.НастройкиОтправкиСоздатьНастройкуОтправки.Картинка = БиблиотекаКартинок.СоздатьЭлементСписка;
	КонецЕсли;
	
	НастройкиОтправки.Параметры.УстановитьЗначениеПараметра("ИспользоватьПрямойОбмен",
		ОбменСКонтрагентамиСлужебный.ИспользоватьПрямойОбмен());
	
КонецПроцедуры

&НаСервере
Процедура УправлениеДоступностьюЭлементовУправления()

	ЕстьПравоИзменения = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам);
	
	Элементы.НастройкиОтправкиСоздатьНастройкуОтправки.Доступность = ЕстьПравоИзменения;
	Элементы.НастройкиОтправкиСоздатьНастройкуПрямогоОбмена.Доступность = ЕстьПравоИзменения;
	Элементы.НастройкиПолученияСоздатьНастройкуПолучения.Доступность = ЕстьПравоИзменения;
	
КонецПроцедуры

#КонецОбласти

#Область ОткрытиеФормыНастроек

&НаКлиенте
Процедура ОткрытьФормуНастроекОтправки(ФормаСоздания = Ложь)
	
	ПараметрыОткрытия = Новый Структура;
	
	Если ФормаСоздания Тогда
		
		ПараметрыОткрытия.Вставить("Создание", Истина);
		ПараметрыОткрытия.Вставить("Организация", Организация);
		ПараметрыОткрытия.Вставить("Контрагент" , Контрагент);
		
	Иначе
		
		ТекущиеДанные = Элементы.НастройкиОтправки.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыОткрытия.Вставить("Организация", ТекущиеДанные.Организация);
		ПараметрыОткрытия.Вставить("Контрагент" , ТекущиеДанные.Контрагент);
		ПараметрыОткрытия.Вставить("Договор"    , ТекущиеДанные.Договор);
		
	КонецЕсли;
	
	ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьФормуНастройкиОтправкиЭДО(ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастроекПолучения(ФормаСоздания = Ложь)
	
	ПараметрыОткрытия = Новый Структура;
	
	Если ФормаСоздания Тогда
		ПараметрыОткрытия.Вставить("Создание", Истина);
		ПараметрыОткрытия.Вставить("Организация", Организация);
		ПараметрыОткрытия.Вставить("Контрагент" , Контрагент);
	Иначе
		
		ТекущиеДанные = Элементы.НастройкиПолучения.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыОткрытия.Вставить("Организация", ТекущиеДанные.Организация);
		ПараметрыОткрытия.Вставить("Контрагент" , ТекущиеДанные.Контрагент);
		ПараметрыОткрытия.Вставить("ИдентификаторКонтрагента"    , ТекущиеДанные.ИдентификаторКонтрагента);
		ПараметрыОткрытия.Вставить("ИдентификаторОрганизации"    , ТекущиеДанные.ИдентификаторОрганизации);
		
	КонецЕсли;
	
	ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьФормуНастройкиОтраженияВУчетеЭДО(ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастроекИнтеркампани(ФормаСоздания = Ложь)
	
	ПараметрыОткрытия = Новый Структура;
	
	Если ФормаСоздания Тогда
		ПараметрыОткрытия.Вставить("Создание", Истина);
		ПараметрыОткрытия.Вставить("Отправитель", Организация);
	Иначе
		
		ТекущиеДанные = Элементы.Интеркампани.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыОткрытия.Вставить("Отправитель", ТекущиеДанные.Отправитель);
		ПараметрыОткрытия.Вставить("Получатель" , ТекущиеДанные.Получатель);
		
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.Форма.НастройкиОтправкиДокументовИнтеркампани",
		ПараметрыОткрытия, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастроекПрямогоОбмена(ФормаСоздания = Ложь)
	
	ПараметрыОткрытия = Новый Структура;
	Если ФормаСоздания Тогда
		ПараметрыОткрытия.Вставить("Организация", Организация);
		ПараметрыОткрытия.Вставить("Контрагент" , Контрагент);
	Иначе
		ТекущиеДанные = Элементы.НастройкиОтправки.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ПараметрыОткрытия.Вставить("Организация", ТекущиеДанные.Организация);
		ПараметрыОткрытия.Вставить("Контрагент" , ТекущиеДанные.Контрагент);
		ПараметрыОткрытия.Вставить("Договор"    , ТекущиеДанные.Договор);
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.Форма.НастройкиОтправкиДокументовПрямойОбмен",
		ПараметрыОткрытия, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСОтборами

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборыОрганизацииВДинамическихСписках(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.НастройкиОтправки, "Организация",
		Форма.Организация, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Форма.Организация));
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.НастройкиПолучения, "Организация",
		Форма.Организация, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Форма.Организация));
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.Интеркампани, "Отправитель",
		Форма.Организация, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Форма.Организация));
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.НастройкиВнутреннегоЭДО, "Организация",
		Форма.Организация, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Форма.Организация));
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборыКонтрагентаВДинамическихСписках(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.НастройкиОтправки, "Контрагент",
		Форма.Контрагент, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Форма.Контрагент));
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.НастройкиПолучения, "Контрагент",
		Форма.Контрагент, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Форма.Контрагент));
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборыСтатусаПодключенияВДинамическихСписках(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.НастройкиОтправки, "СтатусПриглашенияСсылка",
		Форма.СтатусПриглашения, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Форма.СтатусПриглашения));
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборыПартнераВДинамическихСписках(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.НастройкиОтправки,
		"Контрагент." + Форма.ИмяРеквизитаПартнераКонтрагента, Форма.Партнер, ВидСравненияКомпоновкиДанных.Равно,,
		ЗначениеЗаполнено(Форма.Партнер));
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Форма.НастройкиПолучения,
		"Контрагент." + Форма.ИмяРеквизитаПартнераКонтрагента, Форма.Партнер, ВидСравненияКомпоновкиДанных.Равно,,
		ЗначениеЗаполнено(Форма.Партнер));
		
КонецПроцедуры
#КонецОбласти

&НаКлиенте
Процедура ОбновитьСтатусыНастроекЗавершить(Результат, Контекст) Экспорт
	
	СоотвСоглашенийИСтруктурСертификатов = Неопределено;
	Если ТипЗнч(Результат) <> Тип("Структура")
		ИЛИ Не Результат.Свойство("СоответствиеПрофилейИПараметровСертификатов", СоотвСоглашенийИСтруктурСертификатов) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Выполняется получение информации о приглашениях. Подождите...'");
	Состояние(НСтр("ru = 'Получение.'"), , ТекстСообщения);
	ОбменСКонтрагентамиСлужебныйВызовСервера.ОбновитьСтатусыПодключенияНастроекЭДО(СоотвСоглашенийИСтруктурСертификатов);
	
	Оповестить("ОбновитьСостояниеПриглашений");
	
	ЭлектронноеВзаимодействиеОбработкаОшибокКлиент.ОбработатьОшибки(Результат.КонтекстОперации);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеИдентификатораРасширенныйРежим(Количество)
	Возврат СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru = ';%1 идентификатор;;%1 идентификатора;%1 идентификаторов;%1 идентификатора'"), Количество);
КонецФункции

#Область КонтекстныеПодсказки

&НаСервере
Процедура СформироватьКонтекст(КатегорииПересчета = Неопределено) 
	
	Если Не КонтекстныеПодсказкиБЭДПовтИсп.ФункционалКонтекстныхПодсказокДоступен() Тогда 
		Возврат;
	КонецЕсли;
	
	Категория = КонтекстныеПодсказкиБЭДКатегоризацияВызовСервера.Категория_КодОператораКонтрагента();
	Если ЗначениеЗаполнено(Категория)  
			И ?(ЗначениеЗаполнено(КатегорииПересчета), КатегорииПересчета.Найти(Категория) <> Неопределено, Истина) Тогда 
		Значение = КонтекстныеПодсказкиБЭДКатегоризация.КодОператораКонтрагента(Контрагент); 
		КонтекстныеПодсказкиБЭДКлиентСервер.УстановитьЗначениеКатегорииКонтекстаФормы(ЭтаФорма, Категория, Значение);
	КонецЕсли;
	
	Категория = КонтекстныеПодсказкиБЭДКатегоризацияВызовСервера.Категория_КодОператораУчетнойЗаписиОрганизации();
	Если ЗначениеЗаполнено(Категория)  
			И ?(ЗначениеЗаполнено(КатегорииПересчета), КатегорииПересчета.Найти(Категория) <> Неопределено, Истина) Тогда 
		Значение = КонтекстныеПодсказкиБЭДКатегоризация.КодОператораУчетнойЗаписиОрганизации(Организация); 
		КонтекстныеПодсказкиБЭДКлиентСервер.УстановитьЗначениеКатегорииКонтекстаФормы(ЭтаФорма, Категория, Значение);
	КонецЕсли;
	
	КонтекстныеПодсказкиБЭД.ОтобразитьАктуальныеДляКонтекстаНовости(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПанельКонтекстныхНовостей_ЭлементУправленияНажатие(Элемент)
	
	КонтекстныеПодсказкиБЭДКлиент.ЭлементУправленияНажатие(ЭтотОбъект, Элемент);
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные).
//
// Параметры:
//  Нет.
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";	
	КонтекстныеПодсказкиБЭДКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПанельКонтекстныхНовостейОбработкаНавигационнойСсылки(Элемент, ПараметрНавигационнаяСсылка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	КонтекстныеПодсказкиБЭДКлиент.ПанельКонтекстныхНовостей_ЭлементПанелиНовостейОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		ПараметрНавигационнаяСсылка,
		СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти  

&НаКлиенте
Процедура НастройкиВнутреннегоЭДОВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьФормуНастройкиВнутреннегоЭДО(Элементы.НастройкиВнутреннегоЭДО.ТекущиеДанные.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНастройкуВнутреннегоЭДО(Команда)
	
	ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьФормуНастройкиВнутреннегоЭДО(Организация);
	
КонецПроцедуры

#КонецОбласти
