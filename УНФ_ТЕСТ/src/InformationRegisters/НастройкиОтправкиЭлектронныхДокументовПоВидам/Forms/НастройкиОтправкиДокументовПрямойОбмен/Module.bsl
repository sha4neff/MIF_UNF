#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Организация", Организация);
	Параметры.Свойство("Контрагент" , Контрагент);
	Параметры.Свойство("Договор"    , Договор);
	
	ЕстьПравоНастройкиОбмена = ОбменСКонтрагентамиСлужебныйВызовСервера.ЕстьПравоНастройкиОбмена();
	
	Если ЭтоНоваяНастройка() Тогда
		Если Не ЕстьПравоНастройкиОбмена Тогда
			ВызватьИсключение НСтр("ru = 'Недостаточно прав для выполнения операции'");
		КонецЕсли;
		Модифицированность = Истина;
		УстановитьПустойКлючНастройкиОтправки();
	Иначе
		ТолькоПросмотр = ЕстьПравоНастройкиОбмена;
		УстановитьКлючНастройкиОтправки();
	КонецЕсли;
	
	ВидимостьГиперссылкиЖурналРегистрации = Ложь;
	ЭлектронноеВзаимодействиеПереопределяемый.ЕстьПравоОткрытияЖурналаРегистрации(ВидимостьГиперссылкиЖурналРегистрации);
	Элементы.НадписьОшибкаПерейти.Видимость = ВидимостьГиперссылкиЖурналРегистрации;
	
	Если Не ЭлектронноеВзаимодействиеСлужебный.ИспользуетсяНесколькоОрганизаций()
		И НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = ЭлектронноеВзаимодействиеСлужебный.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ДатаНачалаЗагрузкиНастроек = ТекущаяДатаСеанса();
	ДлительнаяОперация = СинхронизироватьДанныеСервисаНастроек();
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	КонтекстныеПодсказкиБЭД.КонтекстныеПодсказки_ПриСозданииНаСервере(ЭтотОбъект, 
																		Элементы.ПанельКонтекстныхНовостей, 
																		Элементы.ГруппаКонтекстныхПодсказок);
	СформироватьКонтекст();
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьФормуПослеСинхронизацииДанныхСервисаНастроек();
	
	ДлительнаяОперация = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("СинхронизироватьДанныеСервисаНастроекЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
	НастроитьСтраницуОжидания();
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	КонтекстныеПодсказкиБЭДКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ИмяСобытияИзмененияУчетнойЗаписиЭДО() Тогда
		ЗаполнитьСписокУчетныхЗаписей();
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	КонтекстныеПодсказкиБЭДКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверитьЗначениеЗаполнено(Элементы.УчетнаяЗапись, Истина, Отказ);
	Если Не Отказ Тогда
		ПроверитьНаличиеДоступныхСертификатовУчетнойЗаписи(Отказ);
	КонецЕсли;
	
	ЕстьОшибки = Ложь;
	Если Не РасширенныйРежим
		И ЕстьОшибкиНастройкиТранспорта(Истина) Тогда
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	УстановитьПредупреждениеНастройкиТранспорта(ЕстьОшибки);
	
	Если ЕстьОшибки Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПриИзмененииОрганизации();
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	ОбновляемыеКатегории = Новый Массив;
	ОбновляемыеКатегории.Добавить(КонтекстныеПодсказкиБЭДКатегоризацияВызовСервера.Категория_КодОператораУчетнойЗаписиОрганизации());
	
	СформироватьКонтекст(ОбновляемыеКатегории); 
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	
КонецПроцедуры

&НаКлиенте
Процедура УчетнаяЗаписьПриИзменении(Элемент)
	
	ПриИзмененииУчетнойЗаписи();
	
КонецПроцедуры

&НаКлиенте
Процедура УчетнаяЗаписьОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьФормуУчетнойЗаписиЭДО(ИдентификаторОтправителя);
КонецПроцедуры

&НаКлиенте
Процедура УчетнаяЗаписьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Идентификатор_Создать() Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("УстановитьИдентификаторПослеСозданияУчетнойЗаписи", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	
	ОткрытьФорму("РегистрСведений.УчетныеЗаписиЭДО.Форма.УчетнаяЗаписьПрямогоОбмена",
		ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		ИдентификаторПолучателя = "";
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	ОбновляемыеКатегории = Новый Массив;
	ОбновляемыеКатегории.Добавить(КонтекстныеПодсказкиБЭДКатегоризацияВызовСервера.Категория_КодОператораКонтрагента());
	
	СформироватьКонтекст(ОбновляемыеКатегории); 
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами.КонтекстныеПодсказкиБЭД
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура("Организация, Контрагент", Организация, Контрагент);
	ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьФормуВыбораДоговора(
		ПараметрыФормы, Элемент, Неопределено, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьОшибкаПерейтиНажатие(Элемент)
	
	ОтборЖурнала = Новый Структура;
	Если ЗначениеЗаполнено(ДатаНачалаЗагрузкиНастроек) Тогда
		ОтборЖурнала.Вставить("ДатаНачала", ДатаНачалаЗагрузкиНастроек);
	КонецЕсли;
	ОтборЖурнала.Вставить("Уровень", "Ошибка");
	ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(ОтборЖурнала);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсходящиеДокументы

&НаКлиенте
Процедура ИсходящиеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ИсходящиеДокументы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено 
		Или Не ЕстьПравоНастройкиОбмена Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле = Элементы.ИсходящиеДокументыДополнительныеНастройки Тогда
		ОткрытьФормуРедактированияРегламентаЭДО(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ЗаписатьНастройки() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	УдалитьНастройкиОтправки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьКаталогТоваров(Команда)
	ОбменСКонтрагентамиСлужебныйКлиент.ОтправитьКаталогТоваров(Организация, Контрагент, Договор, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСоглашениеПоШаблону(Команда)
	ОбменСКонтрагентамиСлужебныйКлиент.СформироватьСоглашениеПоШаблону(Организация, Контрагент, Договор);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЭлектронныеДокументы(Команда)
	НастройкиЭДОСсылка = ОбменСКонтрагентамиСлужебныйВызовСервера.СсылкаНаОбъектНастройкиЭДО(
		Организация, Контрагент, Договор);
	ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьДеревоЭД(НастройкиЭДОСсылка, Неопределено, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРасширенныйРежим(Команда)
	
	ИзменитьРасширенныйРежимНачало();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЭтоНоваяНастройка()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИСТИНА КАК Выбран
		|ИЗ
		|	РегистрСведений.НастройкиОтправкиЭлектронныхДокументов КАК НастройкиОтправкиЭлектронныхДокументов
		|ГДЕ
		|	НастройкиОтправкиЭлектронныхДокументов.Отправитель = &Отправитель
		|	И НастройкиОтправкиЭлектронныхДокументов.Получатель = &Получатель
		|	И НастройкиОтправкиЭлектронныхДокументов.Договор = &Договор
		|	И НастройкиОтправкиЭлектронныхДокументов.ЭтоПрямойОбмен";
	
	Запрос.УстановитьПараметр("Отправитель", Организация);
	Запрос.УстановитьПараметр("Получатель", Контрагент);
	Запрос.УстановитьПараметр("Договор", Договор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции

&НаСервере
Функция СинхронизироватьДанныеСервисаНастроек()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление данных из сервиса настроек 1С:ЭДО'");
	ПараметрыВыполнения.ЗапуститьВФоне  = Истина;
	ПараметрыВыполнения.АдресРезультата = Новый УникальныйИдентификатор;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСКонтрагентамиСлужебный.ОбновитьКешиОператоровЭДОИФорматовВФоне", Неопределено, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура СинхронизироватьДанныеСервисаНастроекЗавершение(Результат, Контекст) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус <> "Выполнено" Тогда
		НастроитьСтраницуОшибки();
		Возврат;
	КонецЕсли;
	
	ПодготовитьФормуПослеСинхронизацииДанныхСервисаНастроек();
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуПослеСинхронизацииДанныхСервисаНастроек()
	
	ЗаполнитьСписокУчетныхЗаписей();
	
	ЗаполнитьНастройкиИсходящихДокументов();
	
	ЗаполнитьРежимРедактированияНастроек();
	
	ЗаполнитьУчетнуюЗаписьПоУмолчанию();
	
	НастроитьОсновнуюСтраницуФормы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокУчетныхЗаписей()
	
	СписокВыбора = Элементы.УчетнаяЗапись.СписокВыбора;
	СписокВыбора.Очистить();
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ОбменСКонтрагентамиСлужебный.ЗаполнитьСписокУчетныхЗаписейПрямогоОбмена(СписокВыбора, Организация);
	КонецЕсли;
	
	СписокВыбора.Добавить(Идентификатор_Создать(), НСтр("ru = 'Создать новую учетную запись'"),,
		БиблиотекаКартинок.СоздатьЭлементСписка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиИсходящихДокументов()
	
	СведенияОФорматах = ОбменСКонтрагентамиСлужебный.ФорматыЭлектронныхДокументов();
	
	Форматы = Новый Соответствие;
	Для Каждого СтрокаФормата Из СведенияОФорматах Цикл
		Форматы.Вставить(СтрокаФормата.ИдентификаторФормата, СтрокаФормата.ПредставлениеФормата);
	КонецЦикла;
	ФорматыЭлектронныхДокументов = Новый ФиксированноеСоответствие(Форматы);
	
	ТаблицаНастроек = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьНастройкиОтправкиДокументов();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписиЭД") Тогда
		ТаблицаНастроек.ЗаполнитьЗначения(Истина, "ОбменБезПодписи");
	КонецЕсли;
	
	НастройкиОтправки = ОбменСКонтрагентамиСлужебный.ТекущиеНастройкиОтправкиЭлектронныхДокументовПоВидам(
		КлючНастройкиОтправки.Отправитель,
		КлючНастройкиОтправки.Получатель,
		КлючНастройкиОтправки.Договор,
		ТаблицаНастроек);
	
	РасширенныйРежим = Ложь;
	
	Если НастройкиОтправки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПредставленияУчетныхЗаписей = Новый Соответствие;
	СписокВыбора = Элементы.УчетнаяЗапись.СписокВыбора;
	Для Каждого ЭлементСписка Из СписокВыбора Цикл
		ПредставленияУчетныхЗаписей.Вставить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	Для Каждого СтрокаНастроек Из НастройкиОтправки Цикл
		
		Если СтрокаНастроек.ВидДокумента = Перечисления.ВидыЭД.УПД
			ИЛИ СтрокаНастроек.ВидДокумента = Перечисления.ВидыЭД.УКД
			ИЛИ СтрокаНастроек.ВидДокумента = Перечисления.ВидыЭД.СчетФактура
			ИЛИ СтрокаНастроек.ВидДокумента = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ИсходящиеДокументы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНастроек);
		ЗаполнитьПредставлениеРегламентаЭДОВСтроке(ЭтотОбъект, НоваяСтрока);
		НоваяСтрока.УчетнаяЗаписьПредставление = ПредставленияУчетныхЗаписей[НоваяСтрока.ИдентификаторОтправителя];
		
		Если НоваяСтрока.ОбменБезПодписи
			И ОбменСКонтрагентамиСлужебный.ЭтоВидЭДБезТитула(НоваяСтрока.ВидДокумента) Тогда
			НоваяСтрока.ТребуетсяОтветнаяПодпись = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРежимРедактированияНастроек()
	
	Если ИсходящиеДокументы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РасширенныйРежим = Ложь;
	
	СтрокаНастроек = ИсходящиеДокументы[0];
	ИдентификаторОтправителя = СтрокаНастроек.ИдентификаторОтправителя;
	ИдентификаторПолучателя  = СтрокаНастроек.ИдентификаторПолучателя;
	АдресОтправителя         = СтрокаНастроек.АдресОтправителя;
	АдресПолучателя          = СтрокаНастроек.АдресПолучателя;
	СпособОбмена             = СтрокаНастроек.СпособОбменаЭД;
	
	Для Каждого СтрокаНастроек Из ИсходящиеДокументы Цикл
		
		Если ИдентификаторОтправителя <> СтрокаНастроек.ИдентификаторОтправителя
				ИЛИ ИдентификаторПолучателя <> СтрокаНастроек.ИдентификаторПолучателя
				ИЛИ АдресОтправителя <> СтрокаНастроек.АдресОтправителя
				ИЛИ АдресПолучателя <> СтрокаНастроек.АдресПолучателя Тогда
			РасширенныйРежим = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУчетнуюЗаписьПоУмолчанию()
	
	Если РасширенныйРежим Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбора = Элементы.УчетнаяЗапись.СписокВыбора;
	Если ПустаяСтрока(ИдентификаторОтправителя)
		И СписокВыбора.Количество() = 2 Тогда
		ИдентификаторОтправителя = СписокВыбора[0].Значение;
		ПриИзмененииУчетнойЗаписи();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОсновнуюСтраницуФормы()
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОсновная;
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Авто;
	
	ТолькоПросмотр = Не ОбменСКонтрагентамиСлужебныйВызовСервера.ЕстьПравоНастройкиОбмена();
	Элементы.Договор.Видимость = ОбменСКонтрагентамиПовтИсп.ИспользуютсяДоговорыКонтрагентов();
	
	УстановитьВидимостьРасширенныйРежим();
	
	УстановитьОтображениеТранспортныхНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСтраницуОжидания()
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОжидание;
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСтраницуОшибки()
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОшибка;
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьРасширенныйРежим()
	
	Если РасширенныйРежим Тогда
		Элементы.УчетнаяЗапись.Видимость = Ложь;
		Элементы.ГруппаНастройки.ТекущаяСтраница = Элементы.СтраницаИсходящиеДокументы;
		Элементы.ГруппаНастройки.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.СтраницаДополнительныеНастройки.Видимость = Ложь;
		Элементы.ИсходящиеДокументыУчетнаяЗаписьПредставление.Видимость = Истина;
	Иначе
		Элементы.УчетнаяЗапись.Видимость = Истина;
		Элементы.ГруппаНастройки.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
		Элементы.СтраницаДополнительныеНастройки.Видимость = Истина;
		Элементы.ИсходящиеДокументыУчетнаяЗаписьПредставление.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ФормаИзменитьРасширенныйРежим.Пометка = РасширенныйРежим;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаписатьНастройки()
	
	КонтекстОперации = ЭлектронноеВзаимодействиеСлужебныйКлиент.НовыйКонтекстОперации();
	Результат = ЗаписатьНастройкиНаСервере(КонтекстОперации);
	Если Результат Тогда
		Модифицированность = Ложь;
		ОповеститьОбИзмененииНастройкиОтправки();
	Иначе
		ЭлектронноеВзаимодействиеОбработкаОшибокКлиент.ОбработатьОшибки(КонтекстОперации);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗаписатьНастройкиНаСервере(КонтекстОперации)
	
	Если Не РасширенныйРежим Тогда
		ЗаполнитьИсходящиеДокументыОбщимиНастройками();
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не РасширенныйРежим И Не ИспользоватьПодпись Тогда
	ИначеЕсли Не ПроверитьСоответствиеСертификатовМаршрутам(КонтекстОперации) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.НастройкиОтправкиЭлектронныхДокументов");
		ЭлементБлокировки.УстановитьЗначение("Отправитель", Организация);
		ЭлементБлокировки.УстановитьЗначение("Получатель" , Контрагент);
		ЭлементБлокировки.УстановитьЗначение("Договор"    , Договор);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам");
		ЭлементБлокировки.УстановитьЗначение("Отправитель", Организация);
		ЭлементБлокировки.УстановитьЗначение("Получатель" , Контрагент);
		ЭлементБлокировки.УстановитьЗначение("Договор"    , Договор);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.НастройкиПолученияЭлектронныхДокументов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Отправитель", Контрагент );
		ЭлементБлокировки.УстановитьЗначение("Получатель" , Организация);
		ЭлементБлокировки.УстановитьЗначение("ИдентификаторОтправителя", "");
		ЭлементБлокировки.УстановитьЗначение("ИдентификаторПолучателя" , "");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		БлокировкаДанных.Заблокировать();
		
		Запись = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументов.СоздатьМенеджерЗаписи();
		Запись.Отправитель = Организация;
		Запись.Получатель  = Контрагент;
		Запись.Договор     = Договор;
		Запись.Прочитать();
		Если Запись.Выбран()
			И КлючНастройкиОтправкиИзменен() Тогда
			ТекстСообщения = НСтр("ru = 'Настройка отправки с указанными значениями организации, контрагента, договора уже существует.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			ЗафиксироватьТранзакцию();
			Возврат Ложь;
		КонецЕсли;
		
		Запись.Отправитель     = Организация;
		Запись.Получатель      = Контрагент;
		Запись.Договор         = Договор;
		Запись.ИспользоватьУПД = Ложь;
		Запись.ИспользоватьУКД = Ложь;
		Запись.ЭтоПрямойОбмен  = Истина;
		
		Если КлючНастройкиОтправкиПустой(ЭтотОбъект) Тогда
			Запись.ДатаНачалаДействия = ТекущаяДатаСеанса();
		КонецЕсли;
		
		Запись.Записать();
		
		НаборЗаписей = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Отправитель.Установить(Организация);
		НаборЗаписей.Отбор.Получатель.Установить(Контрагент);
		НаборЗаписей.Отбор.Договор.Установить(Договор);
		
		Для Каждого СтрокаТаблицы Из ИсходящиеДокументы Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТаблицы);
			
			НоваяЗапись.Отправитель = Организация;
			НоваяЗапись.Получатель  = Контрагент;
			НоваяЗапись.Договор     = Договор;
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
		Если ОбщиеНастройкиПолученияОтсутствуют() Тогда
			НаборЗаписей = РегистрыСведений.НастройкиПолученияЭлектронныхДокументов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Получатель.Установить(Организация);
			НаборЗаписей.Отбор.Отправитель.Установить(Контрагент);
			НаборЗаписей.Отбор.ИдентификаторОтправителя.Установить("");
			НаборЗаписей.Отбор.ИдентификаторПолучателя.Установить("");
			
			ТаблицаПредопределенногоПрофиля = ОбменСКонтрагентамиСлужебный.ТаблицаПредопределенногоПрофиля(
				"ПервоначальноеЗаполнение");
			Для Каждого СтрокаТаблицы Из ТаблицаПредопределенногоПрофиля Цикл
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Получатель       = Организация;
				НоваяЗапись.Отправитель      = Контрагент;
				НоваяЗапись.ВидДокумента     = СтрокаТаблицы.ВидДокумента;
				НоваяЗапись.ПрикладнойВидЭД  = СтрокаТаблицы.ПрикладнойВидЭД;
				НоваяЗапись.СпособОбработки  = СтрокаТаблицы.СпособОбработки;
			КонецЦикла;
			
			НаборЗаписей.Записать();
		КонецЕсли;
		
		УстановитьКлючНастройкиОтправки();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ВидОперации = НСтр("ru = 'Сохранение настроек отправки ЭДО'");
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(ВидОперации, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке),
			НСтр("ru = 'Не удалось сохранить Настройки отправки
				|Подробнее см. в журнале регистрации.'"));
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПроверитьСоответствиеСертификатовМаршрутам(КонтекстОперации)
	
	ИспользованныеУчетныеЗаписи = ИсходящиеДокументы.Выгрузить(, "ИдентификаторОтправителя").ВыгрузитьКолонку("ИдентификаторОтправителя");
	
	НаборыСертификатовУчетныхЗаписей = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УчетныеЗаписи", ИспользованныеУчетныеЗаписи);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО КАК УчетнаяЗапись,
	|	СертификатыУчетныхЗаписейЭДО.Сертификат КАК Сертификат
	|ИЗ
	|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
	|ГДЕ
	|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО В(&УчетныеЗаписи)
	|
	|УПОРЯДОЧИТЬ ПО
	|	УчетнаяЗапись,
	|	Сертификат
	|ИТОГИ ПО
	|	УчетнаяЗапись";
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаУчетныхЗаписей = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	УстановитьПривилегированныйРежим(Ложь);
	Пока ВыборкаУчетныхЗаписей.Следующий() Цикл
		СертификатыУчетныхЗаписей = Новый Массив;
		ИдентификаторНабора = "ИД_";
		Выборка = ВыборкаУчетныхЗаписей.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если СертификатыУчетныхЗаписей.Найти(Выборка.Сертификат) = Неопределено Тогда
				СертификатыУчетныхЗаписей.Добавить(Выборка.Сертификат);
				ИдентификаторНабора = ИдентификаторНабора + Строка(Выборка.Сертификат.УникальныйИдентификатор());
			КонецЕсли; 
		КонецЦикла;
		
		ИдентификаторНабора = СтрЗаменить(ИдентификаторНабора, "-", "_");
		СтруктураОписанияНабораСертификатов = Новый Структура("ИдентификаторНабора, Сертификаты", 
			ИдентификаторНабора, СертификатыУчетныхЗаписей);
		НаборыСертификатовУчетныхЗаписей.Вставить(ВыборкаУчетныхЗаписей.УчетнаяЗапись, СтруктураОписанияНабораСертификатов);
	КонецЦикла;
	
	УникальныеПараметрыПроверки = Новый Структура;
	Для Каждого СтрокаИсходящегоДокумента Из ИсходящиеДокументы Цикл
		
		Если Не СтрокаИсходящегоДокумента.Формировать
			ИЛИ СтрокаИсходящегоДокумента.ОбменБезПодписи Тогда
			Продолжить;
		КонецЕсли;
		
		МаршрутПодписания        = СтрокаИсходящегоДокумента.МаршрутПодписания;
		ВидЭД                    = СтрокаИсходящегоДокумента.ВидДокумента;
		УчетнаяЗаписьНастроекЭДО = СтрокаИсходящегоДокумента.ИдентификаторОтправителя;
		
		ПараметрыНабораСертификатов = НаборыСертификатовУчетныхЗаписей[ИдентификаторОтправителя];
		Если ПараметрыНабораСертификатов <> Неопределено Тогда
			ИдентификаторМаршрута   = СтрЗаменить(Строка(МаршрутПодписания.УникальныйИдентификатор()), "-", "_");
			ИдентификаторПараметров = ПараметрыНабораСертификатов.ИдентификаторНабора + Строка(ИдентификаторМаршрута);
			
			СтруктураОписанияПараметров = Неопределено;
			Если Не УникальныеПараметрыПроверки.Свойство(ИдентификаторПараметров, СтруктураОписанияПараметров) Тогда
				СтруктураОписанияПараметров = Новый Структура;
				СтруктураОписанияПараметров.Вставить("Сертификаты",       ПараметрыНабораСертификатов.Сертификаты);
				СтруктураОписанияПараметров.Вставить("МаршрутПодписания", МаршрутПодписания);
				СтруктураОписанияПараметров.Вставить("ВидыЭД",            Новый Массив);
				УникальныеПараметрыПроверки.Вставить(ИдентификаторПараметров, СтруктураОписанияПараметров);
			КонецЕсли;
			СтруктураОписанияПараметров.ВидыЭД.Добавить(ВидЭД);
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеНастройки = Новый Структура("Отправитель, Получатель, Договор");
	ОписаниеНастройки.Отправитель = Организация;
	ОписаниеНастройки.Получатель  = Контрагент;
	ОписаниеНастройки.Договор     = Договор;
	НастройкаОбмена = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументов.СоздатьКлючЗаписи(ОписаниеНастройки);
	ПредставлениеНастройки = ОбменСКонтрагентамиСлужебный.ПредставлениеНастройкиОтправкиЭлектронныхДокументов(ОписаниеНастройки);
	
	Отказ = Ложь;
	
	Для Каждого НаборПараметровПроверки Из УникальныеПараметрыПроверки Цикл
		МаршрутПодписания = НаборПараметровПроверки.Значение.МаршрутПодписания;
		Сертификаты       = НаборПараметровПроверки.Значение.Сертификаты;
		ВидыЭД            = НаборПараметровПроверки.Значение.ВидыЭД;
		
		РезультатыПроверки = ЭлектронноеВзаимодействиеСлужебный.РезультатыПроверкиМаршрутаПоПараметрамНастройки(
			МаршрутПодписания, Сертификаты, ВидыЭД);
			
		ЭлектронноеВзаимодействиеСлужебный.ВывестиРезультатыПроверкиМаршрута(РезультатыПроверки, 
			НастройкаОбмена, МаршрутПодписания, Отказ,, ПредставлениеНастройки, КонтекстОперации);
	КонецЦикла;
	
	Возврат Не Отказ;
	
КонецФункции

&НаСервере
Функция ОбщиеНастройкиПолученияОтсутствуют()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПолученияЭлектронныхДокументов.Получатель КАК Получатель
		|ИЗ
		|	РегистрСведений.НастройкиПолученияЭлектронныхДокументов КАК НастройкиПолученияЭлектронныхДокументов
		|ГДЕ
		|	НастройкиПолученияЭлектронныхДокументов.Получатель = &Получатель
		|	И НастройкиПолученияЭлектронныхДокументов.Отправитель = &Отправитель
		|	И НастройкиПолученияЭлектронныхДокументов.ИдентификаторОтправителя = &ИдентификаторОтправителя
		|	И НастройкиПолученияЭлектронныхДокументов.ИдентификаторПолучателя = &ИдентификаторПолучателя";
	
	Запрос.УстановитьПараметр("Получатель" , Организация);
	Запрос.УстановитьПараметр("Отправитель", Контрагент);
	Запрос.УстановитьПараметр("ИдентификаторОтправителя", "");
	Запрос.УстановитьПараметр("ИдентификаторПолучателя" , "");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции

&НаСервере
Процедура УстановитьКлючНастройкиОтправки()
	КлючНастройкиОтправки = Новый ФиксированнаяСтруктура("Отправитель, Получатель, Договор",
		Организация, Контрагент, Договор);
КонецПроцедуры

&НаСервере
Процедура УстановитьПустойКлючНастройкиОтправки()
	КлючНастройкиОтправки = Новый ФиксированнаяСтруктура("Отправитель, Получатель, Договор");
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КлючНастройкиОтправкиПустой(Форма)
	Возврат НЕ (ЗначениеЗаполнено(Форма.КлючНастройкиОтправки.Отправитель)
		ИЛИ ЗначениеЗаполнено(Форма.КлючНастройкиОтправки.Получатель)
		ИЛИ ЗначениеЗаполнено(Форма.КлючНастройкиОтправки.Договор));
КонецФункции

&НаСервере
Функция КлючНастройкиОтправкиИзменен()
	Возврат КлючНастройкиОтправки.Отправитель <> Организация
		ИЛИ КлючНастройкиОтправки.Получатель <> Контрагент
		ИЛИ КлючНастройкиОтправки.Договор <> Договор
КонецФункции

&НаКлиенте
Процедура ОповеститьОбИзмененииНастройкиОтправки()
	ТипЗначения = Тип("РегистрСведенийКлючЗаписи.НастройкиОтправкиЭлектронныхДокументов");
	ПараметрыЗаписи = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Новый Структура(КлючНастройкиОтправки));
	КлючЗаписи = Новый(ТипЗначения, ПараметрыЗаписи);
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(КлючЗаписи);
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОрганизации()
	
	ЗаполнитьСписокУчетныхЗаписей();
	
	СписокВыбора = Элементы.УчетнаяЗапись.СписокВыбора;
	Если СписокВыбора.Количество() = 2 Тогда
		ИдентификаторОтправителя = СписокВыбора[0].Значение;
	ИначеЕсли СписокВыбора.НайтиПоЗначению(ИдентификаторОтправителя) = Неопределено Тогда
		ИдентификаторОтправителя = "";
	КонецЕсли;
	
	ПриИзмененииУчетнойЗаписи();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииУчетнойЗаписи()
	
	ЗаполнитьНастройкиУчетнойЗаписи();
	
	УстановитьОтображениеТранспортныхНастроек();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиУчетнойЗаписи()
	
	Если ПустаяСтрока(ИдентификаторОтправителя) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетныеЗаписиЭДО.Организация КАК Организация,
		|	УчетныеЗаписиЭДО.СпособОбменаЭД КАК СпособОбменаЭД
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО = &ИдентификаторЭДО";
	
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторОтправителя);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = Выборка.Организация;
	КонецЕсли;
	
	СпособОбмена = Выборка.СпособОбменаЭД;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеТранспортныхНастроек()
	
	Если СпособОбмена = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту Тогда
		Элементы.АдресОтправителя.Видимость = Ложь;
		Элементы.АдресПолучателя.Заголовок  = НСтр("ru = 'Электронная почта получателя'");
	Иначе
		Элементы.АдресОтправителя.Видимость = Истина;
		Элементы.АдресОтправителя.Заголовок = НСтр("ru = 'Каталог отправителя'");
		Элементы.АдресПолучателя.Заголовок  = НСтр("ru = 'Каталог получателя'");;
	КонецЕсли;
	
	УстановитьПредупреждениеНастройкиТранспорта();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИдентификаторПослеСозданияУчетнойЗаписи(Идентификатор, Контекст) Экспорт
	
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОтправителя = Идентификатор;
	
	НастроитьОсновнуюСтраницуФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНаличиеДоступныхСертификатовУчетнойЗаписи(Отказ)
	
	УчетныеЗаписиДляПроверки = Новый Соответствие;
	
	Для Каждого СтрокаНастроек Из ИсходящиеДокументы Цикл
		Если СтрокаНастроек.ОбменБезПодписи Тогда
			Продолжить;
		КонецЕсли;
		УчетныеЗаписиДляПроверки.Вставить(
			СтрокаНастроек.ИдентификаторОтправителя, СтрокаНастроек.УчетнаяЗаписьПредставление);
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(УчетныеЗаписиДляПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыЭДО = Новый Массив;
	Для Каждого УчетнаяЗаписьДляПроверки Из УчетныеЗаписиДляПроверки Цикл
		ИдентификаторыЭДО.Добавить(УчетнаяЗаписьДляПроверки.Ключ);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО
		|ИЗ
		|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
		|ГДЕ
		|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО В(&ИдентификаторыЭДО)
		|	И РАЗНОСТЬДАТ(&ТекущаяДата, СертификатыУчетныхЗаписейЭДО.ДействителенДо, МИНУТА) > 0";
	
	Запрос.УстановитьПараметр("ИдентификаторыЭДО", ИдентификаторыЭДО);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		УчетныеЗаписиДляПроверки.Удалить(Выборка.ИдентификаторЭДО);
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(УчетныеЗаписиДляПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	Для Каждого УчетнаяЗаписьБезСертификатов Из УчетныеЗаписиДляПроверки Цикл
		ТекстСообщения = СтрШаблон(НСтр("ru = 'В учетной записи ""%1"" нет доступных сертификатов.'"),
			УчетнаяЗаписьБезСертификатов.Значение);
		СтруктураКлюча = Новый Структура("ИдентификаторЭДО", УчетнаяЗаписьБезСертификатов.Ключ);
		КлючЗаписи = РегистрыСведений.УчетныеЗаписиЭДО.СоздатьКлючЗаписи(СтруктураКлюча);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючЗаписи,,"СписокСертификатов");
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЕстьОшибкиНастройкиТранспорта(Выводить = Ложь)
	
	ЕстьОшибки = Ложь;
	
	ПроверитьЗначениеЗаполнено(Элементы.ИдентификаторПолучателя, Выводить, ЕстьОшибки);
	
	ПроверитьЗначениеЗаполнено(Элементы.АдресПолучателя, Выводить, ЕстьОшибки);
	
	Если СпособОбмена = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту Тогда
		ПроверитьКорректностьЭлектроннойПочты(Элементы.АдресПолучателя, Выводить, ЕстьОшибки);
	Иначе
		ПроверитьНедопустимыеСимволыВИмениФайла(Элементы.АдресПолучателя, Выводить, ЕстьОшибки);
		
		ПроверитьЗначениеЗаполнено(Элементы.АдресОтправителя, Выводить, ЕстьОшибки);
		ПроверитьНедопустимыеСимволыВИмениФайла(Элементы.АдресОтправителя, Выводить, ЕстьОшибки);
	КонецЕсли;
	
	Возврат ЕстьОшибки;
	
КонецФункции

&НаСервере
Процедура ПроверитьЗначениеЗаполнено(Элемент, Выводить, Отказ)
	
	ЗначениеРеквизита = ЭтотОбъект[Элемент.ПутьКДанным];
	Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	Если Выводить Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено.'"), Элемент.Заголовок);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,Элемент.ПутьКДанным);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНедопустимыеСимволыВИмениФайла(Элемент, Выводить, Отказ)
	
	ЗначениеРеквизита = ЭтотОбъект[Элемент.ПутьКДанным];
	Если Не ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	НедопустимыеСимволы = ОбщегоНазначенияКлиентСервер.НайтиНедопустимыеСимволыВИмениФайла(ЗначениеРеквизита);
	Если НедопустимыеСимволы.Количество() Тогда
		Отказ = Истина;
		Если Выводить Тогда
			ШаблонТекста = НСтр("ru = 'Наименование папки содержит запрещенные символы (%1)'");
			ТекстСообщения = СтрШаблон(ШаблонТекста, СтрСоединить(НедопустимыеСимволы, " "));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,Элемент.ПутьКДанным);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьКорректностьЭлектроннойПочты(Элемент, Выводить, Отказ)
	
	ЗначениеРеквизита = ЭтотОбъект[Элемент.ПутьКДанным];
	Если Не ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(ЗначениеРеквизита) Тогда
		Отказ = Истина;
		Если Выводить Тогда
			ТекстСообщения = НСтр("ru = 'Электронная почта указана не верно'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Элемент.ПутьКДанным);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредупреждениеНастройкиТранспорта(ЕстьОшибки = Неопределено)
	
	Если РасширенныйРежим Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьОшибки = Неопределено Тогда
		ЕстьОшибки = ЕстьОшибкиНастройкиТранспорта();
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		Элементы.СтраницаДополнительныеНастройки.Картинка = БиблиотекаКартинок.ПредупреждениеКрасноеБЭД16;
	Иначе
		Элементы.СтраницаДополнительныеНастройки.Картинка = Новый Картинка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ИмяСобытияИзмененияУчетнойЗаписиЭДО()
	КлючУчетнойЗаписиЭДО = ОбменСКонтрагентамиСлужебныйКлиент.КлючУчетнойЗаписиЭДО(ИдентификаторОтправителя);
	Возврат ОбменСКонтрагентамиСлужебныйКлиент.ИмяСобытияИзмененияОбъекта(КлючУчетнойЗаписиЭДО);
КонецФункции

#Область РасширенныйРежим

&НаКлиенте
Процедура ИзменитьРасширенныйРежимНачало()
	
	Если РасширенныйРежим Тогда
		Описание = Новый ОписаниеОповещения("ИзменитьРасширенныйРежимЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Описание, НСтр("ru = 'Изменения расширенного режима будут очищены.
			|Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ВключитьРасширенныйРежим();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРасширенныйРежимЗавершение(Результат, Контекст) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ВыключитьРасширенныйРежим();
	
КонецПроцедуры

&НаСервере
Процедура ВключитьРасширенныйРежим()
	
	РасширенныйРежим = Истина;
	
	ЗаполнитьИсходящиеДокументыОбщимиНастройками();
	
	УстановитьВидимостьРасширенныйРежим();
	
КонецПроцедуры

&НаСервере
Процедура ВыключитьРасширенныйРежим()
	
	РасширенныйРежим = Ложь;
	
	ЗаполнитьОбщиеНастройкиПоИсходящимДокументам();
	
	ЗаполнитьИсходящиеДокументыОбщимиНастройками();
	
	УстановитьВидимостьРасширенныйРежим();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИсходящиеДокументыОбщимиНастройками()
	
	ЭлементСписка = Элементы.УчетнаяЗапись.СписокВыбора.НайтиПоЗначению(ИдентификаторОтправителя);
	Если ЭлементСписка = Неопределено Тогда
		УчетнаяЗаписьПредставление = "";
	Иначе
		УчетнаяЗаписьПредставление = ЭлементСписка.Представление;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ИсходящиеДокументы Цикл
		СтрокаТаблицы.СпособОбменаЭД             = СпособОбмена;
		СтрокаТаблицы.ИдентификаторОтправителя   = ИдентификаторОтправителя;
		СтрокаТаблицы.ИдентификаторПолучателя    = ИдентификаторПолучателя;
		СтрокаТаблицы.УчетнаяЗаписьПредставление = УчетнаяЗаписьПредставление;
		СтрокаТаблицы.АдресОтправителя           = АдресОтправителя;
		СтрокаТаблицы.АдресПолучателя            = АдресПолучателя;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбщиеНастройкиПоИсходящимДокументам()
	
	Если ИсходящиеДокументы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы = ИсходящиеДокументы[0];
	
	СпособОбмена             = СтрокаТаблицы.СпособОбменаЭД;
	ИдентификаторОтправителя = СтрокаТаблицы.ИдентификаторОтправителя;
	ИдентификаторПолучателя  = СтрокаТаблицы.ИдентификаторПолучателя;
	ИспользоватьПодпись      = Не СтрокаТаблицы.ОбменБезПодписи;
	МаршрутПодписания        = СтрокаТаблицы.МаршрутПодписания;
	АдресОтправителя         = СтрокаТаблицы.АдресОтправителя;
	АдресПолучателя          = СтрокаТаблицы.АдресПолучателя;
	
КонецПроцедуры

#КонецОбласти

#Область РедактированиеРегламентаЭДО

&НаКлиенте
Процедура ОткрытьФормуРедактированияРегламентаЭДО(ТекущиеДанные)
	
	Оповещение = Новый ОписаниеОповещения("ПриЗавершенииРедактированияРегламентаЭДО", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация"               , Организация);
	ПараметрыФормы.Вставить("Контрагент"                , Контрагент);
	ПараметрыФормы.Вставить("Договор"                   , Договор);
	ПараметрыФормы.Вставить("СпособОбмена"              , ТекущиеДанные.СпособОбменаЭД);
	ПараметрыФормы.Вставить("АдресОтправителя"          , ТекущиеДанные.АдресОтправителя);
	ПараметрыФормы.Вставить("АдресПолучателя"           , ТекущиеДанные.АдресПолучателя);
	ПараметрыФормы.Вставить("УчетнаяЗаписьПредставление", ТекущиеДанные.УчетнаяЗаписьПредставление);
	ПараметрыФормы.Вставить("ИдентификаторОтправителя"  , ТекущиеДанные.ИдентификаторОтправителя);
	ПараметрыФормы.Вставить("ИдентификаторПолучателя"   , ТекущиеДанные.ИдентификаторПолучателя);
	ПараметрыФормы.Вставить("ВидДокумента"              , ТекущиеДанные.ВидДокумента);
	ПараметрыФормы.Вставить("ВерсияФормата"             , ТекущиеДанные.ВерсияФормата);
	ПараметрыФормы.Вставить("ИспользоватьПодпись"       , Не ТекущиеДанные.ОбменБезПодписи);
	ПараметрыФормы.Вставить("МаршрутПодписания"         , ТекущиеДанные.МаршрутПодписания);
	ПараметрыФормы.Вставить("ОжидатьОтветнуюПодпись"    , ТекущиеДанные.ТребуетсяОтветнаяПодпись);
	ПараметрыФормы.Вставить("ОжидатьИзвещение"          , ТекущиеДанные.ТребуетсяИзвещениеОПолучении);
	ПараметрыФормы.Вставить("ЗаполнениеКодаТовара"      , ТекущиеДанные.ЗаполнениеКодаТовара);
	ПараметрыФормы.Вставить("ПрикладнойВидЭД"           , ТекущиеДанные.ПрикладнойВидЭД);
	ПараметрыФормы.Вставить("РасширенныйРежим"          , РасширенныйРежим);
	ПараметрыФормы.Вставить("ВерсияФорматаУстановленаВручную", ТекущиеДанные.ВерсияФорматаУстановленаВручную);
	
	ОткрытьФорму("РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.Форма.НастройкиРегламентаПрямогоОбмена",
		ПараметрыФормы, УникальныйИдентификатор,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииРедактированияРегламентаЭДО(Результат, ТекущиеДанные) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ТекущиеДанные = Элементы.ИсходящиеДокументы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Организация <> Результат.Организация Тогда
		Организация = Результат.Организация;
		ПриИзмененииОрганизации();
	КонецЕсли;
	
	ТекущиеДанные.СпособОбменаЭД                  = Результат.СпособОбмена;
	ТекущиеДанные.ВерсияФормата                   = Результат.ВерсияФормата;
	ТекущиеДанные.АдресОтправителя                = Результат.АдресОтправителя;
	ТекущиеДанные.АдресПолучателя                 = Результат.АдресПолучателя;
	ТекущиеДанные.УчетнаяЗаписьПредставление      = Результат.УчетнаяЗаписьПредставление;
	ТекущиеДанные.ИдентификаторОтправителя        = Результат.ИдентификаторОтправителя;
	ТекущиеДанные.ИдентификаторПолучателя         = Результат.ИдентификаторПолучателя;
	ТекущиеДанные.ОбменБезПодписи                 = Не Результат.ИспользоватьПодпись;
	ТекущиеДанные.МаршрутПодписания               = Результат.МаршрутПодписания;
	ТекущиеДанные.ТребуетсяОтветнаяПодпись        = Результат.ОжидатьОтветнуюПодпись;
	ТекущиеДанные.ТребуетсяИзвещениеОПолучении    = Результат.ОжидатьИзвещение;
	ТекущиеДанные.ЗаполнениеКодаТовара            = Результат.ЗаполнениеКодаТовара;
	ТекущиеДанные.ВерсияФорматаУстановленаВручную = Результат.ВерсияФорматаУстановленаВручную;
	
	ЗаполнитьПредставлениеРегламентаЭДОВСтроке(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеРегламентаЭДОВСтроке(Форма, СтрокаТаблицы)
	
	ПредставлениеФормата = Форма.ФорматыЭлектронныхДокументов.Получить(СтрокаТаблицы.ВерсияФормата);
	
	МассивСтрок = Новый Массив;
	Если ПредставлениеФормата <> Неопределено Тогда
		МассивСтрок.Добавить(ПредставлениеФормата);
	Иначе
		МассивСтрок.Добавить(НСтр("ru = 'Произвольный документ'"));
	КонецЕсли;
	
	МассивСтрок.Добавить(?(СтрокаТаблицы.ТребуетсяОтветнаяПодпись,
		НСтр("ru = 'с ответной подписью'"), НСтр("ru = 'без ответной подписи'")));
	
	СтрокаТаблицы.ДополнительныеНастройки = СтрСоединить(МассивСтрок,", ");
	
КонецПроцедуры

#КонецОбласти

#Область УдалениеНастройкиОбмена

&НаКлиенте
Процедура УдалитьНастройкиОтправки()
	
	ВидыДокументов = Неопределено;
	Если КлючНастройкиОтправкиПустой(ЭтотОбъект)
		ИЛИ ЗначениеЗаполнено(КлючНастройкиОтправки.Договор) Тогда
		ТекстВопроса = НСтр("ru = 'Удалить настройку обмена с контрагентом?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Настройка обмена с контрагентом будет удалена. Отправка электронных документов контрагенту будет невозможна.
			|Продолжить?'");
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("УдалитьНастройкиОтправкиПослеВопроса", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНастройкиОтправкиПослеВопроса(Ответ, Контекст) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если КлючНастройкиОтправкиПустой(ЭтотОбъект) Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	Если УдалитьНастройкиОтправкиНаСервере(КлючНастройкиОтправки) Тогда
		ОповеститьОбИзмененииНастройкиОтправки();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УдалитьНастройкиОтправкиНаСервере(Знач КлючНастройкиОтправки)
	
	Организация = КлючНастройкиОтправки.Отправитель;
	Контрагент  = КлючНастройкиОтправки.Получатель;
	Договор     = КлючНастройкиОтправки.Договор;
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам");
		ЭлементБлокировкиДанных.УстановитьЗначение("Отправитель", Организация);
		ЭлементБлокировкиДанных.УстановитьЗначение("Получатель" , Контрагент);
		ЭлементБлокировкиДанных.УстановитьЗначение("Договор"    , Договор);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.НастройкиОтправкиЭлектронныхДокументов");
		ЭлементБлокировкиДанных.УстановитьЗначение("Отправитель", Организация);
		ЭлементБлокировкиДанных.УстановитьЗначение("Получатель" , Контрагент);
		ЭлементБлокировкиДанных.УстановитьЗначение("Договор"    , Договор);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументовПоВидам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Отправитель.Установить(Организация);
		НаборЗаписей.Отбор.Получатель.Установить(Контрагент);
		НаборЗаписей.Отбор.Договор.Установить(Договор);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.НастройкиОтправкиЭлектронныхДокументов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Отправитель.Установить(Организация);
		НаборЗаписей.Отбор.Получатель.Установить(Контрагент);
		НаборЗаписей.Отбор.Договор.Установить(Договор);
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ВидОперации = НСтр("ru = 'Удаление настроек отправки ЭДО'");
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(ВидОперации, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке),
			НСтр("ru = 'Не удалось удалить настройки отправки
				|Подробнее см. в журнале регистрации.'"));
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ТипыИдентификаторовУчетныхЗаписей

&НаКлиентеНаСервереБезКонтекста
Функция Идентификатор_Создать()
	Возврат "Создать";
КонецФункции

#КонецОбласти

#Область КонтекстныеПодсказки

&НаКлиенте
Процедура Подключаемый_ПанельКонтекстныхНовостей_ЭлементУправленияНажатие(Элемент)
	
	КонтекстныеПодсказкиБЭДКлиент.ЭлементУправленияНажатие(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьКонтекст(КатегорииПересчета = Неопределено)
	
	Если Не КонтекстныеПодсказкиБЭД.ФункционалКонтекстныхПодсказокДоступен() Тогда 
		Возврат;
	КонецЕсли;   
	
	Категория = КонтекстныеПодсказкиБЭДКатегоризацияВызовСервера.Категория_КодОператораКонтрагента();
	Если ЗначениеЗаполнено(Категория)  
			И ?(ЗначениеЗаполнено(КатегорииПересчета), КатегорииПересчета.Найти(Категория) <> Неопределено, Истина) Тогда 
		Значение = КонтекстныеПодсказкиБЭДКатегоризация.КодОператораКонтрагента(Контрагент); 
		КонтекстныеПодсказкиБЭДКлиентСервер.УстановитьЗначениеКатегорииКонтекстаФормы(ЭтаФорма, Категория, Значение);
	КонецЕсли;
	
	Категория = КонтекстныеПодсказкиБЭДКатегоризацияВызовСервера.Категория_КодОператораУчетнойЗаписиОрганизации();
	Если ЗначениеЗаполнено(Категория)  
			И ?(ЗначениеЗаполнено(КатегорииПересчета), КатегорииПересчета.Найти(Категория) <> Неопределено, Истина) Тогда 
		Значение = КонтекстныеПодсказкиБЭДКатегоризация.КодОператораУчетнойЗаписиОрганизации(Организация); 
		КонтекстныеПодсказкиБЭДКлиентСервер.УстановитьЗначениеКатегорииКонтекстаФормы(ЭтаФорма, Категория, Значение);
	КонецЕсли;  
	
	КонтекстныеПодсказкиБЭД.ОтобразитьАктуальныеДляКонтекстаНовости(ЭтотОбъект);
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные).
//
// Параметры:
//  Нет.
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";	
	КонтекстныеПодсказкиБЭДКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПанельКонтекстныхНовостейОбработкаНавигационнойСсылки(Элемент, ПараметрНавигационнаяСсылка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	КонтекстныеПодсказкиБЭДКлиент.ПанельКонтекстныхНовостей_ЭлементПанелиНовостейОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		ПараметрНавигационнаяСсылка,
		СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти 


#КонецОбласти