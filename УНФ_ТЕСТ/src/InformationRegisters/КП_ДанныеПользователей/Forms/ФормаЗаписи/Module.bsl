
&НаКлиенте
Процедура ВыбратьФотографию(Команда)
	АдресКартинкиФотографии="";
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ВыбратьФотографиюЗавершение", ЭтотОбъект), АдресКартинкиФотографии, , , УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФотографиюЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
    
    Если НЕ Результат Тогда
        Возврат;
    КонецЕсли;
	
	АдресКартинкиФотографии=Адрес;
    КартинкаФотография = АдресКартинкиФотографии;
	ФотографияИзменялась=Истина;
	Модифицированность=Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФотографию(Команда)
	АдресКартинкиФотографии="";
	КартинкаФотография = АдресКартинкиФотографии;
	ФотографияИзменялась=Истина;
	Модифицированность=Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ФотографияИзменялась Тогда
		Если ЭтоАдресВременногоХранилища(АдресКартинкиФотографии) Тогда
			Картинка = ПолучитьИзВременногоХранилища(АдресКартинкиФотографии);
			ТекущийОбъект.Фотография = Новый ХранилищеЗначения(Картинка, Новый СжатиеДанных(9));
		Иначе
			ТекущийОбъект.Фотография = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ПодписьИзменялась Тогда
		Если ЭтоАдресВременногоХранилища(АдресКартинкиПодписи) Тогда
			Картинка = ПолучитьИзВременногоХранилища(АдресКартинкиПодписи);
			ТекущийОбъект.ОбразецПодписи = Новый ХранилищеЗначения(Картинка, Новый СжатиеДанных(9));
		Иначе
			ТекущийОбъект.ОбразецПодписи = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Ключ.Пустой() Тогда
		КартинкаФотография = ПолучитьНавигационнуюСсылку(Запись.ИсходныйКлючЗаписи, "Фотография");
		КартинкаПодпись = ПолучитьНавигационнуюСсылку(Запись.ИсходныйКлючЗаписи, "ОбразецПодписи");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПодпись(Команда)
	АдресКартинкиПодписи="";
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ВыбратьПодписьЗавершение", ЭтотОбъект), АдресКартинкиПодписи, , ,УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПодписьЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
    
    Если НЕ Результат Тогда
        Возврат;
    КонецЕсли;
	
	АдресКартинкиПодписи=Адрес;
    КартинкаПодпись = АдресКартинкиПодписи;
	ПодписьИзменялась=Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПодпись(Команда)
	АдресКартинкиПодписи="";
	КартинкаПодпись = АдресКартинкиПодписи;
	ПодписьИзменялась=Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаПодписьНажатие(Элемент, СтандартнаяОбработка)
	ВыбратьПодпись(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаФотографияНажатие(Элемент, СтандартнаяОбработка)
	ВыбратьФотографию(Неопределено);    
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ФотографияИзменялась ИЛИ ПодписьИзменялась Тогда
		Оповестить("ИзмененыДополнительныеДанныеПользователя");
	КонецЕсли;
	
КонецПроцедуры


