#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаЗаписи" Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Пригласить", Ложь) Тогда
		ОбработкаПолученияФормыОтправкиПриглашения(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	Иначе
		ОбработкаПолученияФормыЗаписи(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НоваяТаблицаПриглашенийКОтправке() Экспорт
	
	Приглашения = Новый ТаблицаЗначений;
	Приглашения.Колонки.Добавить("ИдентификаторОрганизации");
	Приглашения.Колонки.Добавить("ИдентификаторКонтрагента");
	Приглашения.Колонки.Добавить("Организация");
	Приглашения.Колонки.Добавить("Контрагент");
	Приглашения.Колонки.Добавить("ЭлектроннаяПочта");
	Приглашения.Колонки.Добавить("ОператорЭДО");
	Приглашения.Колонки.Добавить("ТекстПриглашения");
	Приглашения.Колонки.Добавить("ТребуетсяСоглашение", Новый ОписаниеТипов("Булево"));
	Приглашения.Колонки.Добавить("СоздаватьНастройкиЭДО", Новый ОписаниеТипов("Булево"));
	Приглашения.Колонки.Добавить("EmailОрганизации");
	Приглашения.Колонки.Добавить("ПолучательИНН");
	Приглашения.Колонки.Добавить("ПолучательКПП");
	
	Возврат Приглашения;
	
КонецФункции

Функция НовыйСтатусПриглашения(ИдентификаторОрганизации, ИдентификаторКонтрагента, СтатусПодключения,
	Контрагент = Неопределено, ТекстПриглашения = Неопределено, ОписаниеОшибки = Неопределено) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.ПриглашенияКОбменуЭлектроннымиДокументами.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ИдентификаторКонтрагента = ИдентификаторКонтрагента;
	МенеджерЗаписи.ИдентификаторОрганизации = ИдентификаторОрганизации;
	МенеджерЗаписи.Прочитать();
	
	Если Не МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.ИдентификаторКонтрагента = ИдентификаторКонтрагента;
		МенеджерЗаписи.ИдентификаторОрганизации = ИдентификаторОрганизации;
	КонецЕсли;
	
	МенеджерЗаписи.Статус = СтатусПодключения;
	
	Если ЗначениеЗаполнено(ТекстПриглашения) Тогда
		МенеджерЗаписи.ТекстПриглашения = ТекстПриглашения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		МенеджерЗаписи.ОписаниеОшибки = ОписаниеОшибки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		МенеджерЗаписи.Контрагент = Контрагент;
	КонецЕсли;
	
	МенеджерЗаписи.ДатаИзмененияСтатуса = ТекущаяДатаСеанса();
	МенеджерЗаписи.Записать();
	
КонецФункции

Процедура ОтправитьСписокПриглашений(ПараметрыКоманды, АдресРезультата) Экспорт
	
	КоличествоОтправлено = 0;
	
	Организация    = ПараметрыКоманды.Организация;
	СписокМаркеров = ПараметрыКоманды.СписокМаркеров;
	Приглашения    = ПараметрыКоманды.Приглашения;
	
	КонтекстОперации = Неопределено;
	Если Не ПараметрыКоманды.Свойство("КонтекстОперации", КонтекстОперации) Тогда
		КонтекстОперации = ЭлектронноеВзаимодействиеСлужебный.НовыйКонтекстОперации();
	КонецЕсли;
	
	// Готовим таблицу с реквизитами контрагентов.
	ТаблицаПриглашений = Новый ТаблицаЗначений;
	ТаблицаПриглашений.Колонки.Добавить("Получатель");
	ТаблицаПриглашений.Колонки.Добавить("Идентификатор");
	ТаблицаПриглашений.Колонки.Добавить("Наименование");
	ТаблицаПриглашений.Колонки.Добавить("НаименованиеДляСообщенияПользователю");
	ТаблицаПриглашений.Колонки.Добавить("ИНН");
	ТаблицаПриглашений.Колонки.Добавить("КПП");
	ТаблицаПриглашений.Колонки.Добавить("АдресЭП", Новый ОписаниеТипов("Строка"));
	ТаблицаПриглашений.Колонки.Добавить("ТекстПриглашения");
	ТаблицаПриглашений.Колонки.Добавить("ВнешнийКод");
	ТаблицаПриглашений.Колонки.Добавить("ИдентификаторОрганизации");
	ТаблицаПриглашений.Колонки.Добавить("ИдентификаторКонтрагента");
	ТаблицаПриглашений.Колонки.Добавить("ОператорЭДО");
	ТаблицаПриглашений.Колонки.Добавить("ИмяФайлаСкана");
	ТаблицаПриглашений.Колонки.Добавить("ДанныеФайлаСкана");
	ТаблицаПриглашений.Колонки.Добавить("СоздаватьНастройкиЭДО", Новый ОписаниеТипов("Булево"));
	ТаблицаПриглашений.Колонки.Добавить("EmailОрганизации");
	
	МассивКонтрагентов = Приглашения.ВыгрузитьКолонку("Контрагент");
	МассивИдентификаторовОрганизации = Приглашения.ВыгрузитьКолонку("ИдентификаторОрганизации");
	
	// Получаем имена необходимых реквизитов
	ИмяРеквизитаИННКонтрагента = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННКонтрагента");
	ИмяРеквизитаКППКонтрагента = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППКонтрагента");
	ИмяРеквизитаНаименованиеКонтрагента = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("НаименованиеКонтрагента");
	ИмяРеквизитаВнешнийКодКонтрагента = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ВнешнийКодКонтрагента");
	ИмяРеквизитаНаименованиеКонтрагентаДляСообщенияПользователю = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("НаименованиеКонтрагентаДляСообщенияПользователю");
	
	Реквизиты = СтрШаблон("%1,%2,%3,%4,%5",
		ИмяРеквизитаНаименованиеКонтрагента,
		ИмяРеквизитаНаименованиеКонтрагентаДляСообщенияПользователю,
		ИмяРеквизитаИННКонтрагента,
		ИмяРеквизитаКППКонтрагента,
		ИмяРеквизитаВнешнийКодКонтрагента);
	
	ТекстЗапросаКонтрагентов = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Ссылка КАК Ссылка,
		|	&Реквизиты
		|ИЗ
		|	&ИмяСправочникаКонтрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Ссылка В (&МассивКонтрагентов)";
		
	ТекстЗапросаКонтрагентов = СтрЗаменить(ТекстЗапросаКонтрагентов, "&Реквизиты", Реквизиты);
	ТекстЗапросаКонтрагентов = СтрЗаменить(ТекстЗапросаКонтрагентов, "&ИмяСправочникаКонтрагенты",
		ОбщегоНазначения.ИмяТаблицыПоСсылке(МассивКонтрагентов[0]));
	
	МассивТекстовЗапроса = Новый Массив;
	МассивТекстовЗапроса.Добавить(ТекстЗапросаКонтрагентов);
	
	МассивТекстовЗапроса.Добавить(
		"ВЫБРАТЬ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
		|	УчетныеЗаписиЭДО.СпособОбменаЭД КАК СпособОбменаЭД,
		|	УчетныеЗаписиЭДО.ИмяФайлаСоглашенияНаРоуминг КАК ИмяФайлаСоглашенияНаРоуминг,
		|	УчетныеЗаписиЭДО.ДанныеСоглашенияНаРоуминг КАК ДанныеСоглашенияНаРоуминг
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО В (&МассивИдентификаторовЭДО)");
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.УстановитьПараметр("МассивКонтрагентов", МассивКонтрагентов);
	Запрос.УстановитьПараметр("МассивИдентификаторовЭДО", МассивИдентификаторовОрганизации);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ЗначениеРеквизитовПоКонтрагентам = Новый Соответствие;
	
	ВыборкаКонтрагентов = РезультатыЗапроса[0].Выбрать();
	Пока ВыборкаКонтрагентов.Следующий() Цикл
		ЗначенияРеквизитов = Новый Структура(Реквизиты);
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, ВыборкаКонтрагентов);
		ЗначениеРеквизитовПоКонтрагентам.Вставить(ВыборкаКонтрагентов.Ссылка, ЗначенияРеквизитов);
	КонецЦикла;
	
	ЗначенияРеквизитовПоУчетнымЗаписям = Новый Соответствие;
	Реквизиты = "СпособОбменаЭД,ИмяФайлаСоглашенияНаРоуминг,ДанныеСоглашенияНаРоуминг";
	
	ВыборкаУчетныхЗаписей = РезультатыЗапроса[1].Выбрать();
	Пока ВыборкаУчетныхЗаписей.Следующий() Цикл
		ЗначенияРеквизитов = Новый Структура(Реквизиты);
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, ВыборкаУчетныхЗаписей);
		Если ЗначениеЗаполнено(ВыборкаУчетныхЗаписей.ИмяФайлаСоглашенияНаРоуминг) Тогда
			ЗначенияРеквизитов.ДанныеСоглашенияНаРоуминг = Base64Строка(ВыборкаУчетныхЗаписей.ДанныеСоглашенияНаРоуминг.Получить());
		КонецЕсли;
		ЗначенияРеквизитовПоУчетнымЗаписям.Вставить(ВыборкаУчетныхЗаписей.ИдентификаторЭДО, ЗначенияРеквизитов);
	КонецЦикла;
	
	// Настройки, которые нужно создать без отправки приглашения (для случая одного идентификатора для разных контрагентов).
	ТаблицаНастроек = Новый ТаблицаЗначений;
	ТаблицаНастроек.Колонки.Добавить("Организация");
	ТаблицаНастроек.Колонки.Добавить("Контрагент");
	ТаблицаНастроек.Колонки.Добавить("ИдентификаторОрганизации");
	ТаблицаНастроек.Колонки.Добавить("ИдентификаторКонтрагента");
	
	Для Каждого Маркер Из СписокМаркеров Цикл
		
		ТаблицаПриглашений.Очистить();
		
		ДанныеУчетнойЗаписи = ЗначенияРеквизитовПоУчетнымЗаписям.Получить(Маркер.Ключ);
		Отправлено = Новый Соответствие;
		ТаблицаНастроек.Очистить();
		
		Отбор = Новый Структура("ИдентификаторОрганизации", Маркер.Ключ);
		Для Каждого СтрокаТаблицы Из Приглашения.НайтиСтроки(Отбор) Цикл
			
			Если Отправлено[СтрокаТаблицы.ИдентификаторКонтрагента] <> Неопределено Тогда
				Если СтрокаТаблицы.СоздаватьНастройкиЭДО Тогда
					СтрокаНастроек = ТаблицаНастроек.Добавить();
					СтрокаНастроек.Организация = СтрокаТаблицы.Организация;
					СтрокаНастроек.Контрагент = СтрокаТаблицы.Контрагент;
					СтрокаНастроек.ИдентификаторОрганизации = СтрокаТаблицы.ИдентификаторОрганизации;
					СтрокаНастроек.ИдентификаторКонтрагента = СтрокаТаблицы.ИдентификаторКонтрагента;
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			
			ПараметрыКонтрагента = ЗначениеРеквизитовПоКонтрагентам.Получить(СтрокаТаблицы.Контрагент);
			
			Если ЗначениеЗаполнено(СтрокаТаблицы.ПолучательИНН) Тогда
				ПараметрыКонтрагента[ИмяРеквизитаИННКонтрагента] = СтрокаТаблицы.ПолучательИНН;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТаблицы.ПолучательКПП) Тогда
				ПараметрыКонтрагента[ИмяРеквизитаКППКонтрагента] = СтрокаТаблицы.ПолучательКПП;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ПараметрыКонтрагента[ИмяРеквизитаИННКонтрагента]) Тогда
				ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'У контрагента ''%1'' не указан ИНН'"), СтрокаТаблицы.Контрагент));
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока                                      = ТаблицаПриглашений.Добавить();
			НоваяСтрока.Получатель                           = СтрокаТаблицы.Контрагент;
			НоваяСтрока.Идентификатор                        = СтрокаТаблицы.ИдентификаторКонтрагента;
			НоваяСтрока.Наименование                         = ПараметрыКонтрагента[ИмяРеквизитаНаименованиеКонтрагента];
			НоваяСтрока.НаименованиеДляСообщенияПользователю = ПараметрыКонтрагента[ИмяРеквизитаНаименованиеКонтрагентаДляСообщенияПользователю];
			НоваяСтрока.ИНН                                  = ПараметрыКонтрагента[ИмяРеквизитаИННКонтрагента];
			НоваяСтрока.КПП                                  = ПараметрыКонтрагента[ИмяРеквизитаКППКонтрагента];
			НоваяСтрока.АдресЭП                              = СтрокаТаблицы.ЭлектроннаяПочта;
			НоваяСтрока.ОператорЭДО                          = СтрокаТаблицы.ОператорЭДО;
			НоваяСтрока.EmailОрганизации                     = СтрокаТаблицы.EmailОрганизации;
			
			НоваяСтрока.ВнешнийКод                           = ?(ЗначениеЗаполнено(СтрокаТаблицы.ИдентификаторКонтрагента),
				СтрокаТаблицы.ИдентификаторКонтрагента, ПараметрыКонтрагента[ИмяРеквизитаВнешнийКодКонтрагента]);
			
			НоваяСтрока.ТекстПриглашения                     = ?(ЗначениеЗаполнено(СтрокаТаблицы.ТекстПриглашения),
				СтрокаТаблицы.ТекстПриглашения, ОбменСКонтрагентамиСлужебный.СтандартныйШаблонПриглашения());
			
			Если СтрокаТаблицы.ТребуетсяСоглашение
				И ЗначениеЗаполнено(ДанныеУчетнойЗаписи.ИмяФайлаСоглашенияНаРоуминг) Тогда
				НоваяСтрока.ИмяФайлаСкана = ДанныеУчетнойЗаписи.ИмяФайлаСоглашенияНаРоуминг;
				НоваяСтрока.ДанныеФайлаСкана = ДанныеУчетнойЗаписи.ДанныеСоглашенияНаРоуминг;
			КонецЕсли;
			
			НоваяСтрока.ИдентификаторОрганизации = СтрокаТаблицы.ИдентификаторОрганизации;
			НоваяСтрока.ИдентификаторКонтрагента = СтрокаТаблицы.ИдентификаторКонтрагента;
			
			НоваяСтрока.СоздаватьНастройкиЭДО = СтрокаТаблицы.СоздаватьНастройкиЭДО;
			
			Отправлено.Вставить(СтрокаТаблицы.ИдентификаторКонтрагента, Истина);
			
		КонецЦикла;
		
		Если ТаблицаПриглашений.Количество() > 0 Тогда
			ПараметрыПроцедуры = Новый Структура;
			ПараметрыПроцедуры.Вставить("ТаблицаПриглашений", ТаблицаПриглашений);
			ПараметрыПроцедуры.Вставить("Маркер"            , Маркер.Значение.МаркерРасшифрованный);
			ПараметрыПроцедуры.Вставить("СпособОбменаЭД"    , ДанныеУчетнойЗаписи.СпособОбменаЭД);
			
			Результат = ОбменСКонтрагентамиСлужебный.ОтправитьПриглашенияОператоруЭДО(ПараметрыПроцедуры, КонтекстОперации);
			
			КоличествоОтправлено = КоличествоОтправлено + Результат;
			
		КонецЕсли;
		
		Для каждого СтрокаНастроек Из ТаблицаНастроек Цикл
			
			НачатьТранзакцию();
			Попытка
				ОбменСКонтрагентамиСлужебный.СоздатьНастройкиОтправкиЭДО(
					СтрокаНастроек.Организация, СтрокаНастроек.Контрагент, Неопределено,
					СтрокаНастроек.ИдентификаторОрганизации, СтрокаНастроек.ИдентификаторКонтрагента);
				
				ОбменСКонтрагентамиСлужебный.СоздатьНастройкиОтраженияВУчетеЭДО(
					СтрокаНастроек.Организация, СтрокаНастроек.Контрагент, "", "");
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				Информация = ИнформацияОбОшибке();
				ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Создание настроек ЭДО при отправке приглашений'"),
					ПодробноеПредставлениеОшибки(Информация), КраткоеПредставлениеОшибки(Информация));
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЦикла;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("КоличествоОтправлено", КоличествоОтправлено);
	СтруктураВозврата.Вставить("КонтекстОперации", КонтекстОперации);
	
	ПоместитьВоВременноеХранилище(СтруктураВозврата, АдресРезультата);
	
КонецПроцедуры

Функция ДанныеПриглашения(ИдентификаторОрганизации, ИдентификаторКонтрагента) Экспорт
	
	Данные = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПриглашенияКОбменуЭлектроннымиДокументами.Статус КАК Статус,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.ТекстПриглашения КАК ТекстПриглашения,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.Email КАК Email,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.Контрагент КАК Контрагент,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.ИНН КАК ИНН,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.КПП КАК КПП,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.ИсходныйКонтрагент КАК ИсходныйКонтрагент
		|ИЗ
		|	РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияКОбменуЭлектроннымиДокументами
		|ГДЕ
		|	ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации = &ИдентификаторОрганизации
		|	И ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента = &ИдентификаторКонтрагента";
	
	Запрос.УстановитьПараметр("ИдентификаторКонтрагента", ИдентификаторКонтрагента);
	Запрос.УстановитьПараметр("ИдентификаторОрганизации", ИдентификаторОрганизации);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ТаблицаРезультат = РезультатЗапроса.Выгрузить();
	
	Если ТаблицаРезультат.Количество() > 0 Тогда
		
		Данные = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРезультат[0]);
		
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

Функция ПредставленияПриглашений(КлючиЗаписей) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(УчетныеЗаписиЭДО.Организация) КАК Организация,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.ИНН КАК ИННКонтрагента,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
		|	ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации КАК ИдентификаторОрганизации
		|ИЗ
		|	РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияКОбменуЭлектроннымиДокументами
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|		ПО ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации = УчетныеЗаписиЭДО.ИдентификаторЭДО
		|ГДЕ
		|	ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации В(&ИдентификаторОрганизации)
		|	И ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента В(&ИдентификаторКонтрагента)";
	
	ИдентификаторыОрганизации = Новый Массив;
	ИдентификаторыКонтрагента = Новый Массив;
	
	Для каждого КлючЗаписи Из КлючиЗаписей Цикл
		ИдентификаторыОрганизации.Добавить(КлючЗаписи.ИдентификаторОрганизации);
		ИдентификаторыКонтрагента.Добавить(КлючЗаписи.ИдентификаторКонтрагента);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ИдентификаторОрганизации", ИдентификаторыОрганизации);
	Запрос.УстановитьПараметр("ИдентификаторКонтрагента", ИдентификаторыКонтрагента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Представления = Новый Соответствие;
	
	Для каждого КлючЗаписи Из КлючиЗаписей Цикл
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ИдентификаторОрганизации", КлючЗаписи.ИдентификаторОрганизации);
		СтруктураПоиска.Вставить("ИдентификаторКонтрагента", КлючЗаписи.ИдентификаторКонтрагента);
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(СтруктураПоиска) Тогда
			ПредставлениеПриглашения = СтрШаблон(НСтр("ru = '%1 -> ИНН: %2'"), ВыборкаДетальныеЗаписи.Организация,
				ВыборкаДетальныеЗаписи.ИННКонтрагента);
			Представления.Вставить(КлючЗаписи, ПредставлениеПриглашения);
			ВыборкаДетальныеЗаписи.Сбросить();
		КонецЕсли;
	КонецЦикла;
	
	Возврат Представления;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаПолученияФормыЗаписи(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	КлючЗаписи = Неопределено;
	Если Параметры.Свойство("Ключ", КлючЗаписи)
		И ЗначениеЗаполнено(КлючЗаписи.ИдентификаторОрганизации)
		И ЗначениеЗаполнено(КлючЗаписи.ИдентификаторКонтрагента) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	УчетныеЗаписиЭДО.Организация,
			|	Контрагент,
			|   Email КАК ЭлектроннаяПочта,
			|   ПриглашенияКОбменуЭлектроннымиДокументами.ОператорЭДО КАК ОператорКонтрагента,
			|   ТекстПриглашения КАК ТекстПриглашения,
			|   Статус
			|ИЗ
			|	РегистрСведений.ПриглашенияКОбменуЭлектроннымиДокументами КАК ПриглашенияКОбменуЭлектроннымиДокументами
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
			|		ПО ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации = УчетныеЗаписиЭДО.ИдентификаторЭДО
			|ГДЕ
			|	ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторОрганизации = &ИдентификаторОрганизации
			|	И ПриглашенияКОбменуЭлектроннымиДокументами.ИдентификаторКонтрагента = &ИдентификаторКонтрагента";
		
		Запрос.УстановитьПараметр("ИдентификаторОрганизации", КлючЗаписи.ИдентификаторОрганизации);
		Запрос.УстановитьПараметр("ИдентификаторКонтрагента", КлючЗаписи.ИдентификаторКонтрагента);
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Выборка.Следующий() И Выборка.Статус <> Перечисления.СтатусыПриглашений.ТребуетсяОтправить Тогда
			Возврат;
		Иначе 
			// Добавляем параметры для того, чтобы корректно открылся помощник отправки приглашений.
			Параметры.Вставить("Организация");
			Параметры.Вставить("Контрагент");
			Параметры.Вставить("ЭлектроннаяПочта");
			Параметры.Вставить("ОператорКонтрагента");
			Параметры.Вставить("ТекстПриглашения");
			ЗаполнитьЗначенияСвойств(Параметры, Выборка);
		КонецЕсли;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ВыбраннаяФорма = "ПомощникОтправкиПриглашения";
	
КонецПроцедуры

Процедура ОбработкаПолученияФормыОтправкиПриглашения(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбраннаяФорма = "ПомощникОтправкиПриглашения";
	
	МассивКонтрагентов = Неопределено;
	Параметры.Свойство("МассивКонтрагентов", МассивКонтрагентов);
	Если Не ЗначениеЗаполнено(МассивКонтрагентов) Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеОбъекта = МассивКонтрагентов[0].Метаданные();
	
	Если МетаданныеОбъекта.Иерархический 
			И МетаданныеОбъекта.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Контрагенты.Ссылка КАК Ссылка
			|ИЗ
			|	ИмяТаблицыКонтрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.Ссылка В (&Контрагенты)
			|	И НЕ Контрагенты.ЭтоГруппа";
		ИмяТаблицыКонтрагенты = МетаданныеОбъекта.ПолноеИмя();
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяТаблицыКонтрагенты", ИмяТаблицыКонтрагенты);
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Контрагенты", МассивКонтрагентов);
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Возврат;
		КонецЕсли;
		МассивКонтрагентов = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Если МассивКонтрагентов.Количество() = 1 Тогда
		Параметры.Вставить("Контрагент", МассивКонтрагентов[0]);
	Иначе
		Параметры.Вставить("МассивКонтрагентов", МассивКонтрагентов);
		ВыбраннаяФорма = "ПомощникМассовойОтправкиПриглашений";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
