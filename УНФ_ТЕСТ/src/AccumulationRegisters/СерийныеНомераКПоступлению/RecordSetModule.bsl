#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью набора записей.
//
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Установка исключительной блокировки текущего набора записей регистратора.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СерийныеНомераКПоступлению.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Отбор.Регистратор.Значение);
	Блокировка.Заблокировать();
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияСерийныеНомераКПоступлениюИзменение")
			ИЛИ СтруктураВременныеТаблицы.Свойство("ДвиженияСерийныеНомераКПоступлениюИзменение")
			И НЕ СтруктураВременныеТаблицы.ДвиженияСерийныеНомераКПоступлениюИзменение Тогда

		// Если временная таблица "ДвиженияСерийныеНомераКПоступлениюИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияСерийныеНомераКПоступлениюПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		Запрос = Новый Запрос("ВЫБРАТЬ
			|	СерийныеНомераКПоступлению.НомерСтроки КАК НомерСтроки,
			|	СерийныеНомераКПоступлению.Номенклатура КАК Номенклатура,
			|	СерийныеНомераКПоступлению.Характеристика КАК Характеристика,
			|	СерийныеНомераКПоступлению.Партия КАК Партия,
			|	СерийныеНомераКПоступлению.СерийныйНомер КАК СерийныйНомер,
			|	СерийныеНомераКПоступлению.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
			|	ВЫБОР
			|		КОГДА СерийныеНомераКПоступлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА СерийныеНомераКПоступлению.Количество
			|		ИНАЧЕ -СерийныеНомераКПоступлению.Количество
			|	КОНЕЦ КАК КоличествоПередЗаписью
			|ПОМЕСТИТЬ ДвиженияСерийныеНомераКПоступлениюПередЗаписью
			|ИЗ
			|	РегистрНакопления.СерийныеНомераКПоступлению КАК СерийныеНомераКПоступлению
			|ГДЕ
			|	СерийныеНомераКПоступлению.Регистратор = &Регистратор
			|	И &Замещение");

		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);

		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();

	Иначе

	// Если временная таблица "ДвиженияСерийныеНомераКПоступлениюИзменение" существует и содержит записи
	// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
	// Текущее состояние набора и текущее состояние изменений помещаются во временную таблицу "ДвиженияСерийныеНомераКПоступлениюПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно первоначального.
		Запрос = Новый Запрос("ВЫБРАТЬ
			|	ДвиженияСерийныеНомераКПоступлениюИзменение.НомерСтроки КАК НомерСтроки,
			|	ДвиженияСерийныеНомераКПоступлениюИзменение.Номенклатура КАК Номенклатура,
			|	ДвиженияСерийныеНомераКПоступлениюИзменение.Характеристика КАК Характеристика,
			|	ДвиженияСерийныеНомераКПоступлениюИзменение.Партия КАК Партия,
			|	ДвиженияСерийныеНомераКПоступлениюИзменение.СерийныйНомер КАК СерийныйНомер,
			|	ДвиженияСерийныеНомераКПоступлениюИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
			|	ДвиженияСерийныеНомераКПоступлениюИзменение.КоличествоПередЗаписью КАК КоличествоПередЗаписью
			|ПОМЕСТИТЬ ДвиженияСерийныеНомераКПоступлениюПередЗаписью
			|ИЗ
			|	ДвиженияСерийныеНомераКПоступлениюИзменение КАК ДвиженияСерийныеНомераКПоступлениюИзменение
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СерийныеНомераКПоступлению.НомерСтроки,
			|	СерийныеНомераКПоступлению.Номенклатура,
			|	СерийныеНомераКПоступлению.Характеристика,
			|	СерийныеНомераКПоступлению.Партия,
			|	СерийныеНомераКПоступлению.СерийныйНомер,
			|	СерийныеНомераКПоступлению.СтруктурнаяЕдиница,
			|	ВЫБОР
			|		КОГДА СерийныеНомераКПоступлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|			ТОГДА СерийныеНомераКПоступлению.Количество
			|		ИНАЧЕ -СерийныеНомераКПоступлению.Количество
			|	КОНЕЦ
			|ИЗ
			|	РегистрНакопления.СерийныеНомераКПоступлению КАК СерийныеНомераКПоступлению
			|ГДЕ
			|	СерийныеНомераКПоступлению.Регистратор = &Регистратор
			|	И &Замещение");

		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);

		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();

	КонецЕсли;

	// Временная таблица "ДвиженияСерийныеНомераКПоступлениюИзменение" уничтожается
	// Удаляется информация о ее существовании.
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияСерийныеНомераКПоступлениюИзменение") Тогда

		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияСерийныеНомераКПоступлениюИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияСерийныеНомераКПоступлениюИзменение");

	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияСерийныеНомераКПоступлениюИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияСерийныеНомераКПоступлениюИзменение.НомерСтроки) КАК НомерСтроки,
	|	ДвиженияСерийныеНомераКПоступлениюИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияСерийныеНомераКПоступлениюИзменение.Характеристика КАК Характеристика,
	|	ДвиженияСерийныеНомераКПоступлениюИзменение.Партия КАК Партия,
	|	ДвиженияСерийныеНомераКПоступлениюИзменение.СерийныйНомер КАК СерийныйНомер,
	|	ДвиженияСерийныеНомераКПоступлениюИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СУММА(ДвиженияСерийныеНомераКПоступлениюИзменение.КоличествоПередЗаписью) КАК КоличествоПередЗаписью,
	|	СУММА(ДвиженияСерийныеНомераКПоступлениюИзменение.КоличествоИзменение) КАК КоличествоИзменение,
	|	СУММА(ДвиженияСерийныеНомераКПоступлениюИзменение.КоличествоПриЗаписи) КАК КоличествоПриЗаписи
	|ПОМЕСТИТЬ ДвиженияСерийныеНомераКПоступлениюИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияСерийныеНомераКПоступлениюПередЗаписью.НомерСтроки КАК НомерСтроки,
	|		ДвиженияСерийныеНомераКПоступлениюПередЗаписью.Номенклатура КАК Номенклатура,
	|		ДвиженияСерийныеНомераКПоступлениюПередЗаписью.Характеристика КАК Характеристика,
	|		ДвиженияСерийныеНомераКПоступлениюПередЗаписью.Партия КАК Партия,
	|		ДвиженияСерийныеНомераКПоступлениюПередЗаписью.СерийныйНомер КАК СерийныйНомер,
	|		ДвиженияСерийныеНомераКПоступлениюПередЗаписью.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ДвиженияСерийныеНомераКПоступлениюПередЗаписью.КоличествоПередЗаписью КАК КоличествоПередЗаписью,
	|		ДвиженияСерийныеНомераКПоступлениюПередЗаписью.КоличествоПередЗаписью КАК КоличествоИзменение,
	|		0 КАК КоличествоПриЗаписи
	|	ИЗ
	|		ДвиженияСерийныеНомераКПоступлениюПередЗаписью КАК ДвиженияСерийныеНомераКПоступлениюПередЗаписью
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДвиженияСерийныеНомераКПоступлениюПриЗаписи.НомерСтроки,
	|		ДвиженияСерийныеНомераКПоступлениюПриЗаписи.Номенклатура,
	|		ДвиженияСерийныеНомераКПоступлениюПриЗаписи.Характеристика,
	|		ДвиженияСерийныеНомераКПоступлениюПриЗаписи.Партия,
	|		ДвиженияСерийныеНомераКПоступлениюПриЗаписи.СерийныйНомер,
	|		ДвиженияСерийныеНомераКПоступлениюПриЗаписи.СтруктурнаяЕдиница,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияСерийныеНомераКПоступлениюПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияСерийныеНомераКПоступлениюПриЗаписи.Количество
	|			ИНАЧЕ ДвиженияСерийныеНомераКПоступлениюПриЗаписи.Количество
	|		КОНЕЦ,
	|		ДвиженияСерийныеНомераКПоступлениюПриЗаписи.Количество
	|	ИЗ
	|		РегистрНакопления.СерийныеНомераКПоступлению КАК ДвиженияСерийныеНомераКПоступлениюПриЗаписи
	|	ГДЕ
	|		ДвиженияСерийныеНомераКПоступлениюПриЗаписи.Регистратор = &Регистратор) КАК
	|		ДвиженияСерийныеНомераКПоступлениюИзменение
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияСерийныеНомераКПоступлениюИзменение.Номенклатура,
	|	ДвиженияСерийныеНомераКПоступлениюИзменение.Характеристика,
	|	ДвиженияСерийныеНомераКПоступлениюИзменение.Партия,
	|	ДвиженияСерийныеНомераКПоступлениюИзменение.СерийныйНомер,
	|	ДвиженияСерийныеНомераКПоступлениюИзменение.СтруктурнаяЕдиница
	|ИМЕЮЩИЕ
	|	СУММА(ДвиженияСерийныеНомераКПоступлениюИзменение.КоличествоИзменение) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Партия,
	|	СерийныйНомер,
	|	СтруктурнаяЕдиница");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияСерийныеНомераКПоступлениюИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияСерийныеНомераКПоступлениюИзменение", ВыборкаИзРезультатаЗапроса.Количество > 0);
	
	// Временная таблица "ДвиженияСерийныеНомераКПоступлениюПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияСерийныеНомераКПоступлениюПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли