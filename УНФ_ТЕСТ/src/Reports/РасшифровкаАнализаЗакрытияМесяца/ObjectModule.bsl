#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиОтчета.ПрограммноеИзменениеФормыОтчета = Истина;
	НастройкиОтчета.ПоказыватьГруппуСтрокиНаФормеОтчета = Ложь;
	НастройкиОтчета.ПоказыватьГруппуКолонкиНаФормеОтчета = Ложь;
	НастройкиОтчета.ПоказыватьНастройкиДиаграммыНаФормеОтчета = Ложь;
	НастройкиОтчета.ПоказыватьГруппуФильтрыНаФормеОтчета = Ложь;
	НастройкиОтчета.РазрешитьРедактироватьСКД = Ложь;
	
	ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов);
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ОтчетыУНФ.ПриКомпоновкеРезультата(КомпоновщикНастроек, СхемаКомпоновкиДанных, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ОбновитьНастройкиНаФорме(НастройкиОтчета, НастройкиСКД, Форма) Экспорт
	
	Если НЕ НастройкиОтчета.Свойство("КлючВарианта") Тогда
		Возврат;
	КонецЕсли; 	
	
	Если НастройкиОтчета.КлючВарианта = "ПоХарактеристикам" Тогда
		УдаляемыйОтбор = "Характеристика";
	ИначеЕсли НастройкиОтчета.КлючВарианта = "ПоПартиям" Тогда
		УдаляемыйОтбор = "Партия";
	ИначеЕсли НастройкиОтчета.КлючВарианта = "ПоСкладам" Тогда
		УдаляемыйОтбор = "СтруктурнаяЕдиница";
	ИначеЕсли НастройкиОтчета.КлючВарианта = "ПоЗаказам" Тогда
		УдаляемыйОтбор = "ЗаказПокупателя";
	Иначе
		УдаляемыйОтбор = Неопределено; 
	КонецЕсли;
	Если ЗначениеЗаполнено(УдаляемыйОтбор) Тогда
		УдаляемыйОтбор = Новый ПолеКомпоновкиДанных(УдаляемыйОтбор);
	КонецЕсли; 
	
	КУдалению = Новый Массив;
	Для каждого ЭлементОтбора Из НастройкиСКД.Отбор.Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) <> Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли; 
		Если ЭлементОтбора.ЛевоеЗначение = УдаляемыйОтбор Тогда
			КУдалению.Добавить(ЭлементОтбора);
		Иначе
			ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		КонецЕсли; 
	КонецЦикла;
	Для каждого ЭлементОтбора Из КУдалению Цикл
		НастройкиСКД.Отбор.Элементы.Удалить(ЭлементОтбора);
	КонецЦикла;
	
	Если НастройкиОтчета.КлючВарианта = "ПоЗаказам" Тогда
		// Только резервы
		НовыйЭлемент = НастройкиСКД.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		НовыйЭлемент.ЛевоеЗначение = УдаляемыйОтбор;
		НовыйЭлемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
		НовыйЭлемент.Использование = Истина;
		НовыйЭлемент.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов)
	
	Для каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		ВариантыОформления = НастройкиТекВарианта.Значение.ВариантыОформления;
		ОтчетыУНФ.ДобавитьВариантыОформленияКоличества(ВариантыОформления, "Остаток");
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти 

#Область Инициализация

ЭтоОтчетУНФ = Истина;

#КонецОбласти 

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли