#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиОтчета.ПрограммноеИзменениеФормыОтчета = Истина;
	НастройкиОтчета.ИспользоватьСравнение = Ложь;
	НастройкиОтчета.ИспользоватьПериодичность = Ложь;
	
	НастройкиВариантов["Основной"].Рекомендуемый = Истина;
	
	ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов);
	УстановитьТегиВариантов(НастройкиВариантов);
	ДобавитьОписанияСвязанныхПолей(НастройкиВариантов);
	
КонецПроцедуры

Процедура ОбновитьНастройкиНаФорме(НастройкиОтчета, НастройкиСКД, Форма) Экспорт
	
	// Связи параметров
	СтрНоменклатура = Форма.СтрокаОписанияПоляВызов("Параметр", "Номенклатура");
	Если СтрНоменклатура<>Неопределено И СтрНоменклатура.ВидЭлемента="Поле" Тогда
		ИмяРеквизитаНоменклатура = "";
		Для каждого ОписаниеРеквизита Из СтрНоменклатура.Реквизиты Цикл
			ИмяРеквизитаНоменклатура = ОписаниеРеквизита.Ключ;
			Прервать;
		КонецЦикла; 
		ИмяРеквизитаХарактеристика = "";
		СтрХарактеристика = Форма.СтрокаОписанияПоляВызов("Параметр", "Характеристика");
		Если НЕ ПустаяСтрока(ИмяРеквизитаНоменклатура) И СтрХарактеристика<>Неопределено И СтрХарактеристика.ВидЭлемента="Поле" Тогда
			Для каждого ОписаниеРеквизита Из СтрХарактеристика.Реквизиты Цикл
				ИмяРеквизитаХарактеристика = ОписаниеРеквизита.Ключ;
				Прервать;
			КонецЦикла; 
			Если НЕ ПустаяСтрока(ИмяРеквизитаХарактеристика) Тогда
				МассивСвязей = Новый Массив;
				МассивСвязей.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", ИмяРеквизитаНоменклатура));
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяРеквизитаХарактеристика, "СвязиПараметровВыбора", Новый ФиксированныйМассив(МассивСвязей));
				Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
					ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяРеквизитаХарактеристика, "Видимость", Ложь);
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
		СтрСпецификация = Форма.СтрокаОписанияПоляВызов("Параметр", "Спецификация");
		Если НЕ ПустаяСтрока(ИмяРеквизитаНоменклатура) И СтрСпецификация<>Неопределено И СтрСпецификация.ВидЭлемента="Поле" Тогда
			Для каждого ОписаниеРеквизита Из СтрСпецификация.Реквизиты Цикл
				ИмяРеквизитаСпецификация = ОписаниеРеквизита.Ключ;
				Прервать;
			КонецЦикла; 
			Если НЕ ПустаяСтрока(ИмяРеквизитаСпецификация) Тогда
				МассивСвязей = Новый Массив;
				МассивСвязей.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", ИмяРеквизитаНоменклатура));
				Если НЕ ПустаяСтрока(ИмяРеквизитаХарактеристика) Тогда
					МассивСвязей.Добавить(Новый СвязьПараметраВыбора("Отбор.ХарактеристикаПродукции", ИмяРеквизитаХарактеристика));
				КонецЕсли; 
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяРеквизитаСпецификация, "СвязиПараметровВыбора", Новый ФиксированныйМассив(МассивСвязей));
				МассивПараметров = Новый Массив;
				МассивПараметров.Добавить(Новый ПараметрВыбора("ЭтоШаблон", Истина));
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяРеквизитаСпецификация, "ПараметрыВыбора", Новый ФиксированныйМассив(МассивПараметров));
			КонецЕсли; 
		КонецЕсли; 
		// Отметка обязательного заполнения характеристики
		Номенклатура = ЗначениеПараметра("Номенклатура", НастройкиСКД);
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			ПриИзмененииПараметрНоменклатура(Форма, Номенклатура);
		КонецЕсли; 
	КонецЕсли;
	
	// Нестандартные реквизиты формы
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Тип", "Параметр");
	СтруктураОтбора.Вставить("Создан", Истина);
	СтруктураОтбора.Вставить("Поле", "ПоЗакупочнымЦенам");
	Идентификаторы = Форма.ИдентификаторыСтрокНастроекВызов(СтруктураОтбора, Истина);
	Если Идентификаторы.Количество()>0 Тогда
		СтрПараметр = Форма.ПоляНастроек.НайтиПоИдентификатору(Идентификаторы[0]);
		СтрПараметр.НестандартныйОбработчик = Истина;
		ПоЗакупочнымЦенам = ЗначениеПараметра("ПоЗакупочнымЦенам", НастройкиСКД);
		Если ПоЗакупочнымЦенам=Истина Тогда
			СтруктураОтбора.Вставить("Поле", "ВидЦен");
			СтрокиВидЦен = Форма.СтрокиДереваНастроекВызов(СтруктураОтбора, Истина);
			Если СтрокиВидЦен.Количество()>0 Тогда
				Для каждого ОписаниеРеквизита Из СтрокиВидЦен[0].Реквизиты Цикл
					ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ОписаниеРеквизита.Ключ, "Видимость", Ложь);
				КонецЦикла; 
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	СтруктураОтбора.Вставить("Поле", "Номенклатура");
	Идентификаторы = Форма.ИдентификаторыСтрокНастроекВызов(СтруктураОтбора, Истина);
	Если Идентификаторы.Количество()>0 Тогда
		СтрПараметр = Форма.ПоляНастроек.НайтиПоИдентификатору(Идентификаторы[0]);
		СтрПараметр.НестандартныйОбработчик = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриИзмененииНестандартногоРеквизита(Тип, ИмяПоля, СтруктураЗначений, НастройкиСКД, Форма, ИмяЭлемента) Экспорт
	
	Если Тип="Параметр" И ИмяПоля="ПоЗакупочнымЦенам" Тогда
		СтрПараметр = Форма.СтрокаОписанияПоляВызов("Параметр", "ПоЗакупочнымЦенам");
		Если СтрПараметр<>Неопределено Тогда
			Для каждого ОписаниеРеквизита Из СтрПараметр.Реквизиты Цикл
				ИмяРеквизита = ОписаниеРеквизита.Ключ;
			КонецЦикла; 
			Стр = Форма.СтрокаОписанияПоляВызов("Параметр", "ВидЦен");
			Если Стр<>Неопределено Тогда
				Для каждого ОписаниеРеквизита Из Стр.Реквизиты Цикл
					ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ОписаниеРеквизита.Ключ, "Видимость", НЕ СтруктураЗначений[ИмяРеквизита]);
				КонецЦикла; 
			КонецЕсли; 
			НастройкиСКД.ПараметрыДанных.УстановитьЗначениеПараметра(ИмяПоля, СтруктураЗначений[ИмяРеквизита]);
		КонецЕсли;
	ИначеЕсли Тип="Параметр" И ИмяПоля="Номенклатура" Тогда
		СтрПараметр = Форма.СтрокаОписанияПоляВызов("Параметр", "Номенклатура");
		Если СтрПараметр<>Неопределено Тогда
			Для каждого ОписаниеРеквизита Из СтрПараметр.Реквизиты Цикл
				ИмяРеквизита = ОписаниеРеквизита.Ключ;
			КонецЦикла; 
			Номенклатура = СтруктураЗначений[ИмяРеквизита];
			ПриИзмененииПараметрНоменклатура(Форма, Номенклатура);
			НастройкиСКД.ПараметрыДанных.УстановитьЗначениеПараметра(ИмяПоля, Номенклатура);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриКонтекстномОткрытии(Объект, ПолеСвязи, Отборы, Отказ) Экспорт
	
	Если ПолеСвязи = "Номенклатура" Тогда
		Отборы.Вставить("Номенклатура", Объект);
		Если ЗначениеЗаполнено(Объект) И Объект.ИспользоватьХарактеристики Тогда
			Характеристика = НоменклатураВДокументахСервер.ЗначенияНоменклатурыПоУмолчанию(Объект);
		Иначе
			Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		КонецЕсли;
		Отборы.Вставить("Характеристика", Характеристика);
		Если ЗначениеЗаполнено(Характеристика) Тогда
			Спецификация = УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(Объект, Характеристика);
		Иначе
			Спецификация = УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(Номенклатура);
		КонецЕсли;
		Отборы.Вставить("Спецификация", Спецификация);
		Отборы.Вставить("Количество", 1);
	КонецЕсли;
	
	Если ПолеСвязи = "Спецификация" Тогда
		Отборы.Вставить("Номенклатура", Объект.Владелец);
		Отборы.Вставить("Характеристика", Объект.ХарактеристикаПродукции);
		Отборы.Вставить("Спецификация", Объект);
		Отборы.Вставить("Количество", 1);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ПодготовитьВнешниеНаборыДанных(КомпоновщикНастроек.Настройки.ДополнительныеСвойства);
	МодифицироватьНастройкиПередФормированием();
	
	ОтчетыУНФ.ПриКомпоновкеРезультата(КомпоновщикНастроек, СхемаКомпоновкиДанных, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов)
	
	МассивПолейСумм = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("УчетнаяЦена,Стоимость"); 
	
	Для каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		
		ВариантыОформления = НастройкиТекВарианта.Значение.ВариантыОформления;
		ОтчетыУНФ.ДобавитьВариантыОформленияСумм(ВариантыОформления, МассивПолейСумм);
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура УстановитьТегиВариантов(НастройкиВариантов)
	
	НастройкиВариантов["Основной"].Теги = НСТР("ru = 'Производство,Продукция,Изделия'");
	
КонецПроцедуры

Процедура ДобавитьОписанияСвязанныхПолей(НастройкиВариантов)
	
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Основной"].СвязанныеПоля, "Спецификация", "Справочник.Спецификации", , Истина);
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Основной"].СвязанныеПоля, "Номенклатура", "Справочник.Номенклатура", , Истина);
	
КонецПроцедуры

Процедура ПодготовитьВнешниеНаборыДанных(ДополнительныеСвойства)
	
	Номенклатура = ЗначениеПараметра("Номенклатура");
	Характеристика = ЗначениеПараметра("Характеристика");
	Спецификация = ЗначениеПараметра("Спецификация");
	Количество = ЗначениеПараметра("Количество");
	Период = ЗначениеПараметра("Период");
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли; 
	ВидЦен = ЗначениеПараметра("ВидЦен");
	
	Результат = Новый Структура;
	
	СтруктураСостава = СформироватьСтруктуруСостава();
	СтруктураСостава.Номенклатура		= Номенклатура;
	СтруктураСостава.Характеристика		= Характеристика;
	СтруктураСостава.ЕдиницаИзмерения	= Номенклатура.ЕдиницаИзмерения;
	СтруктураСостава.Количество			= Количество;
	СтруктураСостава.Спецификация		= Спецификация;
	СтруктураСостава.ДатаОбработки		= Период;
	СтруктураСостава.ВидЦен				= ВидЦен;
	СтруктураСостава.Уровень			= 0;
	СтруктураСостава.УчетнаяЦена		= 0;
	СтруктураСостава.Стоимость			= 0;
	
	ОписаниеТипаЧисло = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(15, 3));
	
	ТаблицаСостава = Новый ТаблицаЗначений;
	
	ТаблицаСостава.Колонки.Добавить("Этап");
	ТаблицаСостава.Колонки.Добавить("Тип");
	ТаблицаСостава.Колонки.Добавить("Номенклатура");
	ТаблицаСостава.Колонки.Добавить("Характеристика");
	ТаблицаСостава.Колонки.Добавить("ЕдиницаИзмерения");
	ТаблицаСостава.Колонки.Добавить("Количество", ОписаниеТипаЧисло);
	ТаблицаСостава.Колонки.Добавить("Уровень");
	ТаблицаСостава.Колонки.Добавить("Узел");
	ТаблицаСостава.Колонки.Добавить("УчетнаяЦена", ОписаниеТипаЧисло);
	ТаблицаСостава.Колонки.Добавить("Стоимость", ОписаниеТипаЧисло);
	
	Разузлование(СтруктураСостава, ТаблицаСостава);
	
	ТипИдентификатор = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0, ДопустимыйЗнак.Неотрицательный));
	ТаблицаСостава.Колонки.Добавить("ИдентификаторСтроки", ТипИдентификатор);
	ТаблицаСостава.Колонки.Добавить("ИдентификаторРодителя", ТипИдентификатор);
	ТаблицаСостава.Колонки.Добавить("Собирается", Новый ОписаниеТипов("Булево"));
	
	ТаблицаДанных = ТаблицаСостава.СкопироватьКолонки();
	ТаблицаИерархии = ТаблицаСостава.СкопироватьКолонки();
	ЗаполнитьТаблицуИерархии(ТаблицаДанных, ТаблицаИерархии, ТаблицаСостава);
	ТаблицаДанных.Сортировать("ИдентификаторРодителя, ИдентификаторСтроки");
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаДанных", ТаблицаДанных);
	ВнешниеНаборыДанных.Вставить("ТаблицаИерархии", ТаблицаИерархии);
	КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ВнешниеНаборыДанных", ПоместитьВоВременноеХранилище(ВнешниеНаборыДанных));	
	
КонецПроцедуры

Функция ЗначениеПараметра(ИмяПараметра, НастройкиСКД = Неопределено)
	
	Если НастройкиСКД=Неопределено Тогда
		Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Иначе
		Параметр = НастройкиСКД.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	КонецЕсли; 
	Если Параметр<>Неопределено И Параметр.Использование Тогда
		Возврат Параметр.Значение;
	Иначе
		Возврат Неопределено;
	КонецЕсли; 
	
КонецФункции

Процедура ЗаполнитьТаблицуИерархии(ТаблицаДанных, ТаблицаИерархии, ТаблицаИсточник, БезИерархии = Ложь)
	
	СтекРодителей = Новый Массив;
	ТекущийУровень = 0;
	ПредыдущаяСтрока = Неопределено;
	Идентификатор = ТаблицаИерархии.Количество();
	Для каждого СтрокаСостава Из ТаблицаИсточник Цикл
		Если СтрокаСостава.Тип=Перечисления.ТипыНоменклатуры.Операция Тогда
			Продолжить;
		КонецЕсли;
		Идентификатор = Идентификатор + 1;
		СтрокаСостава.ИдентификаторСтроки = ?(ПредыдущаяСтрока=Неопределено, 1, Идентификатор);
		СтрокаСостава.Тип = НСтр("ru = 'Материалы'");
		Если ПредыдущаяСтрока=Неопределено Тогда
			// Корень
			ПредыдущаяСтрока = СтрокаСостава;
			Продолжить;
		ИначеЕсли БезИерархии Тогда
			СтрокаСостава.ИдентификаторРодителя = 1;
		ИначеЕсли ТекущийУровень=СтрокаСостава.Уровень Тогда
			СтрокаСостава.ИдентификаторРодителя = ПредыдущаяСтрока.ИдентификаторРодителя;
		ИначеЕсли ТекущийУровень<СтрокаСостава.Уровень Тогда
			СтрокаСостава.ИдентификаторРодителя = ПредыдущаяСтрока.ИдентификаторСтроки;
			ПредыдущаяСтрока.Собирается = Истина;
			Для ии = ТекущийУровень По СтрокаСостава.Уровень-1 Цикл
				СтекРодителей.Добавить(ПредыдущаяСтрока);
			КонецЦикла; 
			ТекущийУровень = СтрокаСостава.Уровень;
		ИначеЕсли ТекущийУровень>СтрокаСостава.Уровень Тогда
			Для ии = СтрокаСостава.Уровень По ТекущийУровень-1 Цикл
				СтекРодителей.Удалить(СтекРодителей.Количество()-1);
			КонецЦикла; 
			СтрокаСостава.ИдентификаторРодителя = СтекРодителей[СтекРодителей.Количество()-1].ИдентификаторСтроки;
			ТекущийУровень = СтрокаСостава.Уровень;
		КонецЕсли;
		Если ПредыдущаяСтрока.ИдентификаторСтроки>1 ИЛИ ТаблицаИерархии.Количество()=0 Тогда
			НоваяСтрока = ТаблицаДанных.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПредыдущаяСтрока);
			НоваяСтрокаИерархии = ТаблицаИерархии.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаИерархии, ПредыдущаяСтрока);
		КонецЕсли; 
		ПредыдущаяСтрока = СтрокаСостава;
	КонецЦикла;
	Если ПредыдущаяСтрока<>Неопределено И ПредыдущаяСтрока.Тип<>Перечисления.ТипыНоменклатуры.Операция Тогда
		НоваяСтрока = ТаблицаДанных.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ПредыдущаяСтрока);
		НоваяСтрокаИерархии = ТаблицаИерархии.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаИерархии, ПредыдущаяСтрока);
	КонецЕсли;
	
	// Операции
	Для каждого СтрокаСостава Из ТаблицаИсточник Цикл
		Если СтрокаСостава.Тип<>Перечисления.ТипыНоменклатуры.Операция Тогда
			Продолжить;
		КонецЕсли; 
		Идентификатор = Идентификатор + 1;
		СтрокаСостава.ИдентификаторСтроки = Идентификатор;
		СтрокаСостава.ИдентификаторРодителя = 0;
		СтрокаСостава.Тип = НСтр("ru = 'Тех. операции'");
		НоваяСтрока = ТаблицаДанных.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСостава);
		НоваяСтрокаИерархии = ТаблицаИерархии.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаИерархии, СтрокаСостава);
	КонецЦикла; 
	
КонецПроцедуры

Процедура МодифицироватьНастройкиПередФормированием()
	
	// Исключение элементов-изделий
	ПолеПоиска = Новый ПолеКомпоновкиДанных("Собирается");
	ЭлементОтбора = ОтчетыУНФКлиентСервер.НайтиПолеРекурсивно(ПолеПоиска, КомпоновщикНастроек.Настройки.Отбор.Элементы);
	Если ЭлементОтбора=Неопределено Тогда
		ЭлементОтбора = КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = ПолеПоиска;
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Ложь;
		ЭлементОтбора.Использование = Истина;
	КонецЕсли; 
	
	// Отключение видимости служебных колонок
	ПолеПоиска = Новый ПолеКомпоновкиДанных("ИдентификаторСтроки");
	ЭлементСтруктуры = ОтчетыУНФКлиентСервер.НайтиПолеРекурсивно(ПолеПоиска, КомпоновщикНастроек.Настройки.Структура);
	Если ЭлементСтруктуры<>Неопределено Тогда
		ЭлементСтруктуры.Выбор.Элементы.Очистить();
		Для каждого ВыбранноеПоле Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
			Если ВыбранноеПоле.Поле=ПолеПоиска Тогда
				Продолжить;
			КонецЕсли; 
			ВыбранныйЭлемент = ЭлементСтруктуры.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранныйЭлемент.Поле = ВыбранноеПоле.Поле;
			ВыбранныйЭлемент.Использование = Истина;
		КонецЦикла;
		Для каждого ВыбранноеПоле Из КомпоновщикНастроек.Настройки.Выбор.Элементы Цикл
			Если ТипЗнч(ВыбранноеПоле)=Тип("АвтоВыбранноеПолеКомпоновкиДанных") ИЛИ ВыбранноеПоле.Поле=ПолеПоиска Тогда
				Продолжить;
			КонецЕсли; 
			ВыбранныйЭлемент = ЭлементСтруктуры.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранныйЭлемент.Поле = ВыбранноеПоле.Поле;
			ВыбранныйЭлемент.Использование = Истина;
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриИзмененииПараметрНоменклатура(Форма, Номенклатура)
	
	Если НЕ ЗначениеЗаполнено(Номенклатура) ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
		ПроверятьЗаполнениеХарактеристики = Ложь;
	Иначе
		ПроверятьЗаполнениеХарактеристики = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ПроверятьЗаполнениеХарактеристики");
	КонецЕсли; 
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Тип", "Параметр");
	СтруктураОтбора.Вставить("Создан", Истина);
	СтруктураОтбора.Вставить("Поле", "Характеристика");
	Идентификаторы = Форма.ИдентификаторыСтрокНастроекВызов(СтруктураОтбора, Истина);
	Если Идентификаторы.Количество()>0 Тогда
		СтрПараметрХарактеристика = Форма.ПоляНастроек.НайтиПоИдентификатору(Идентификаторы[0]);
		Если СтрПараметрХарактеристика.Создан И СтрПараметрХарактеристика.ВидЭлемента="Поле" Тогда
			СтрПараметрХарактеристика.ДополнительныеПараметры.Вставить("АвтоОтметкаНезаполненного", ПроверятьЗаполнениеХарактеристики);
			Для каждого ОписаниеРеквизита Из СтрПараметрХарактеристика.Реквизиты Цикл
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ОписаниеРеквизита.Ключ, "АвтоОтметкаНезаполненного", ПроверятьЗаполнениеХарактеристики);
				Если НЕ ПроверятьЗаполнениеХарактеристики Тогда
					ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ОписаниеРеквизита.Ключ, "ОтметкаНезаполненного", Ложь);
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

Процедура МодифицироватьСхемуФО()
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЭтапыПроизводства") Тогда
		Для каждого Набор Из СхемаКомпоновкиДанных.НаборыДанных Цикл
			Поле = Набор.Поля.Найти("Этап");
			Если Поле=Неопределено ИЛИ НЕ Тип(Поле)=Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			Ограничения = Поле.ОграничениеИспользования;
			Ограничения.Группировка = Истина;
			Ограничения.Порядок = Истина;
			Ограничения.Условие = Истина;
			Ограничения = Поле.ОграничениеИспользованияРеквизитов;
			Ограничения.Группировка = Истина;
			Ограничения.Порядок = Истина;
			Ограничения.Условие = Истина;
		КонецЦикла; 
	КонецЕсли; 
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
		Для каждого Набор Из СхемаКомпоновкиДанных.НаборыДанных Цикл
			Поле = Набор.Поля.Найти("Характеристика");
			Если Поле=Неопределено ИЛИ НЕ Тип(Поле)=Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			Ограничения = Поле.ОграничениеИспользования;
			Ограничения.Группировка = Истина;
			Ограничения.Порядок = Истина;
			Ограничения.Условие = Истина;
			Ограничения = Поле.ОграничениеИспользованияРеквизитов;
			Ограничения.Группировка = Истина;
			Ограничения.Порядок = Истина;
			Ограничения.Условие = Истина;
		КонецЦикла; 
	КонецЕсли; 

КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРазузлования

// Формирование структуры с определенным составом полей для процедуры 
// разузлования.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Структура - структура с определенным составом полей для процедуры 
//              разузлования.
//
Функция СформироватьСтруктуруСостава()
	
	Структура = Новый Структура();
	
	// Поля описания текущего узла.
	Структура.Вставить("Этап");
	Структура.Вставить("Тип");
	Структура.Вставить("Номенклатура");
	Структура.Вставить("Характеристика");
	Структура.Вставить("ЕдиницаИзмерения");
	Структура.Вставить("Количество");
	Структура.Вставить("УчетнаяЦена");
	Структура.Вставить("Стоимость");
	Структура.Вставить("КоличествоПродукции");
	Структура.Вставить("Спецификация");
	
	Структура.Вставить("ТипСтрокиСостава");
	
	// Вспомогательные данные.
	Структура.Вставить("Объект");
	Структура.Вставить("ДатаОбработки", '00010101');
	Структура.Вставить("Уровень");
	Структура.Вставить("ВидЦен");
	
	Возврат Структура;
	
КонецФункции // обСформироватьСтруктуруСостава()

Функция ПолучитьСоставСпецификации(СтруктураСостава)
	
	ПоЗакупочнымЦенам = ЗначениеПараметра("ПоЗакупочнымЦенам");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПоЗакупочнымЦенам", ПоЗакупочнымЦенам);
	Запрос.УстановитьПараметр("ВидЦен", ?(ПоЗакупочнымЦенам, Справочники.ВидыЦен.Учетная, СтруктураСостава.ВидЦен));
	Запрос.УстановитьПараметр("Спецификация", СтруктураСостава.Спецификация);
	Запрос.УстановитьПараметр("Количество", СтруктураСостава.Количество);
	Запрос.УстановитьПараметр("ДатаОбработки", СтруктураСостава.ДатаОбработки);
	Запрос.УстановитьПараметр("ТаблицаПорядковОкругления", УправлениеНебольшойФирмойСервер.ТаблицаПорядковОкругления());
	
	ЭлементыЗапроса = Новый Массив;
	ЭлементыЗапроса.Добавить(
	"ВЫБРАТЬ
	|	ТаблицаПорядковОкругления.Порядок КАК Порядок,
	|	ТаблицаПорядковОкругления.Значение КАК Значение
	|ПОМЕСТИТЬ ТаблицаПорядковОкругления
	|ИЗ
	|	&ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПараметрическиеСпецификации")
		И ЗначениеЗаполнено(СтруктураСостава.Спецификация)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураСостава.Спецификация, "ЭтоШаблон") Тогда
		СпецификацияОбъект = СтруктураСостава.Спецификация.ПолучитьОбъект();
		Если ЗначениеЗаполнено(СтруктураСостава.Характеристика) Тогда
			СпецификацияОбъект.ХарактеристикаПродукции = СтруктураСостава.Характеристика;
		КонецЕсли; 
		Отказ = Ложь;
		ПроизводствоФормулыСервер.ЗаполнитьСпецификацию(СпецификацияОбъект, Отказ);
		Если Отказ Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru='Ошибка формирования: не удалось рассчитать состав параметрической спецификации %1.'"), СтруктураСостава.Спецификация);
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		Запрос.УстановитьПараметр("Состав", СпецификацияОбъект.Состав.Выгрузить());
		Запрос.УстановитьПараметр("Операции", СпецификацияОбъект.Операции.Выгрузить());
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СпецификацииСостав.ТипСтрокиСостава КАК ТипСтрокиСостава,
		|	СпецификацииСостав.Этап КАК Этап,
		|	СпецификацииСостав.Номенклатура КАК Номенклатура,
		|	СпецификацииСостав.Характеристика КАК Характеристика,
		|	СпецификацииСостав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СпецификацииСостав.Спецификация КАК Спецификация,
		|	СпецификацииСостав.Количество КАК Количество,
		|	СпецификацииСостав.КоличествоПродукции КАК КоличествоПродукции
		|ПОМЕСТИТЬ СоставПоШаблону
		|ИЗ
		|	&Состав КАК СпецификацииСостав
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СпецификацииОперации.Этап КАК Этап,
		|	СпецификацииОперации.Операция КАК Операция,
		|	СпецификацииОперации.Количество КАК Количество,
		|	СпецификацииОперации.НормаВремени КАК НормаВремени,
		|	СпецификацииОперации.КоличествоПродукции КАК КоличествоПродукции
		|ПОМЕСТИТЬ ОперацииПоШаблону
		|ИЗ
		|	&Операции КАК СпецификацииОперации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставПоШаблону.ТипСтрокиСостава КАК ТипСтрокиСостава,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас) КАК Тип,
		|	СоставПоШаблону.Этап КАК Этап,
		|	СоставПоШаблону.Номенклатура КАК Номенклатура,
		|	СоставПоШаблону.Характеристика КАК Характеристика,
		|	СоставПоШаблону.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СоставПоШаблону.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
		|			ТОГДА ВЫРАЗИТЬ(СоставПоШаблону.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Коэффициент,
		|	СоставПоШаблону.Спецификация КАК Спецификация,
		|	СоставПоШаблону.Количество * &Количество КАК Количество,
		|	СоставПоШаблону.КоличествоПродукции КАК КоличествоПродукции
		|ПОМЕСТИТЬ Состав
		|ИЗ
		|	СоставПоШаблону КАК СоставПоШаблону
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция),
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция),
		|	ОперацииПоШаблону.Этап,
		|	ОперацииПоШаблону.Операция,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	ОперацииПоШаблону.НормаВремени,
		|	1,
		|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка),
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ОперацииПоШаблону.Операция КАК Справочник.Номенклатура).ФиксированнаяСтоимость
		|			ТОГДА ВЫБОР
		|					КОГДА ОперацииПоШаблону.Количество = 0
		|						ТОГДА 1
		|					ИНАЧЕ ОперацииПоШаблону.Количество
		|				КОНЕЦ
		|		ИНАЧЕ ОперацииПоШаблону.НормаВремени
		|	КОНЕЦ * &Количество,
		|	ОперацииПоШаблону.КоличествоПродукции
		|ИЗ
		|	ОперацииПоШаблону КАК ОперацииПоШаблону";
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СпецификацииСостав.ТипСтрокиСостава КАК ТипСтрокиСостава,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Запас) КАК Тип,
		|	СпецификацииСостав.Этап КАК Этап,
		|	СпецификацииСостав.Номенклатура КАК Номенклатура,
		|	СпецификацииСостав.Характеристика КАК Характеристика,
		|	СпецификацииСостав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(СпецификацииСостав.ЕдиницаИзмерения) = ТИП(Справочник.ЕдиницыИзмерения)
		|			ТОГДА СпецификацииСостав.ЕдиницаИзмерения.Коэффициент
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Коэффициент,
		|	СпецификацииСостав.Спецификация КАК Спецификация,
		|	ЕСТЬNULL(СпецификацииСостав.Количество, 0) * &Количество КАК Количество,
		|	ЕСТЬNULL(СпецификацииСостав.КоличествоПродукции, 0) КАК КоличествоПродукции
		|ПОМЕСТИТЬ Состав
		|ИЗ
		|	Справочник.Спецификации.Состав КАК СпецификацииСостав
		|ГДЕ
		|	СпецификацииСостав.Ссылка = &Спецификация
		|	И НЕ СпецификацииСостав.Ссылка.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция),
		|	ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция),
		|	СпецификацииОперации.Этап,
		|	СпецификацииОперации.Операция,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	СпецификацииОперации.НормаВремени,
		|	1,
		|	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка),
		|	ВЫБОР
		|		КОГДА СпецификацииОперации.Операция.ФиксированнаяСтоимость
		|			ТОГДА ВЫБОР
		|					КОГДА СпецификацииОперации.Количество = 0
		|						ТОГДА 1
		|					ИНАЧЕ СпецификацииОперации.Количество
		|				КОНЕЦ
		|		ИНАЧЕ СпецификацииОперации.НормаВремени
		|	КОНЕЦ * &Количество,
		|	ЕСТЬNULL(СпецификацииОперации.КоличествоПродукции, 0)
		|ИЗ
		|	Справочник.Спецификации.Операции КАК СпецификацииОперации
		|ГДЕ
		|	СпецификацииОперации.Ссылка = &Спецификация
		|	И НЕ СпецификацииОперации.Ссылка.ПометкаУдаления";
	КонецЕсли;
	ЭлементыЗапроса.Добавить(ТекстЗапроса);
	ЭлементыЗапроса.Добавить(
	"ВЫБРАТЬ
	|	Состав.ТипСтрокиСостава КАК ТипСтрокиСостава,
	|	Состав.Этап КАК Этап,
	|	Состав.Тип КАК Тип,
	|	Состав.Номенклатура КАК Номенклатура,
	|	Состав.Характеристика КАК Характеристика,
	|	Состав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Состав.Спецификация КАК Спецификация,
	|	Состав.Количество КАК Количество,
	|	Состав.КоличествоПродукции КАК КоличествоПродукции,
	|	ВЫБОР
	|		КОГДА &ПоЗакупочнымЦенам
	|				И НЕ Состав.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция)
	|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ЗакупочныеЦены.Цена, 0) * Состав.Коэффициент КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ (ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА Состав.ЕдиницаИзмерения = ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения
	|						ТОГДА ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0)
	|					ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) * Состав.Коэффициент
	|				КОНЕЦ / ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01) КАК ЧИСЛО(15, 0))) * ЕСТЬNULL(ТаблицаПорядковОкругления.Значение, 0.01)
	|	КОНЕЦ КАК УчетнаяЦена,
	|	0 КАК Стоимость
	|ИЗ
	|	Состав КАК Состав
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				&ДатаОбработки,
	|				ВидЦен = &ВидЦен
	|					И (Номенклатура, Характеристика) В
	|						(ВЫБРАТЬ
	|							Состав.Номенклатура,
	|							Состав.Характеристика
	|						ИЗ
	|							Состав
	|						ГДЕ
	|							(НЕ &ПоЗакупочнымЦенам
	|								ИЛИ Состав.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция)))) КАК ЦеныНоменклатурыСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПорядковОкругления КАК ТаблицаПорядковОкругления
	|			ПО ЦеныНоменклатурыСрезПоследних.ВидЦен.ПорядокОкругления = ТаблицаПорядковОкругления.Порядок
	|		ПО Состав.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			И Состав.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
	|			И (НЕ &ПоЗакупочнымЦенам
	|				ИЛИ Состав.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция))
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Закупки.Номенклатура КАК Номенклатура,
	|			Закупки.Характеристика КАК Характеристика,
	|			МИНИМУМ(ВЫРАЗИТЬ(ВЫБОР
	|						КОГДА Закупки.Количество = 0
	|							ТОГДА 0
	|						ИНАЧЕ Закупки.Сумма * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность / Закупки.Количество
	|					КОНЕЦ КАК ЧИСЛО(15, 2))) КАК Цена
	|		ИЗ
	|			(ВЫБРАТЬ
	|				Закупки.Номенклатура КАК Номенклатура,
	|				Закупки.Характеристика КАК Характеристика,
	|				МАКСИМУМ(Закупки.Период) КАК Период
	|			ИЗ
	|				РегистрНакопления.Закупки КАК Закупки
	|			ГДЕ
	|				Закупки.Период < &ДатаОбработки
	|				И (Закупки.Номенклатура, Закупки.Характеристика) В
	|						(ВЫБРАТЬ
	|							Состав.Номенклатура,
	|							Состав.Характеристика
	|						ИЗ
	|							Состав КАК Состав
	|						ГДЕ
	|							&ПоЗакупочнымЦенам
	|							И НЕ Состав.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция))
	|			
	|			СГРУППИРОВАТЬ ПО
	|				Закупки.Номенклатура,
	|				Закупки.Характеристика) КАК ПоследниеЗакупки
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Закупки КАК Закупки
	|				ПО (Закупки.Номенклатура = ПоследниеЗакупки.Номенклатура)
	|					И (Закупки.Характеристика = ПоследниеЗакупки.Характеристика)
	|					И (Закупки.Период = ПоследниеЗакупки.Период),
	|			РегистрСведений.КурсыВалют.СрезПоследних(
	|					&ДатаОбработки,
	|					Валюта В
	|						(ВЫБРАТЬ
	|							ВалютаУчета.Значение
	|						ИЗ
	|							Константа.ВалютаУчета КАК ВалютаУчета)) КАК КурсыВалютСрезПоследних
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Закупки.Номенклатура,
	|			Закупки.Характеристика) КАК ЗакупочныеЦены
	|		ПО Состав.Номенклатура = ЗакупочныеЦены.Номенклатура
	|			И Состав.Характеристика = ЗакупочныеЦены.Характеристика
	|			И (&ПоЗакупочнымЦенам)
	|			И (НЕ Состав.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Операция))");
	
	Запрос.Текст = СтрСоединить(
	ЭлементыЗапроса,
	"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|");
		
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции // ПолучитьСоставСпецификации()

// Процедура добавляет новый узел в стек номенклатуры для разузлования.
//
// Параметры:
//  СтруктураСостава - Структура состава
//	СтекНоменклатуры - ТаблицаЗначений стек номенклатуры
//	СтекНоменклатурыСтекВходов - ТаблицаЗначений стек входов номенклатуры
//	НоваяСтрокаСтека - СтрокаТаблицыЗначений - строка стека
//	ТекСтрока     - СтрокаТаблицыЗначений - текущая строка.
//
Процедура ДобавитьУзел(СтруктураСостава, СтекНоменклатуры, СтекНоменклатурыСтекВходов, НоваяСтрокаСтека, ТекСтрока)
	
	НоваяСтрокаСтека = СтекНоменклатуры.Добавить();
	НоваяСтрокаСтека.Номенклатура	= ТекСтрока.Номенклатура;
	НоваяСтрокаСтека.Характеристика = ТекСтрока.Характеристика;
	НоваяСтрокаСтека.Спецификация	= ТекСтрока.Спецификация;
	НоваяСтрокаСтека.Уровень		= ТекСтрока.Уровень;
	
	// Инициализация вложенного стека.
	СтекНоменклатурыСтекВходов = СтекНоменклатурыСтекВходов.СкопироватьКолонки();
	НоваяСтрокаСтека.СтекВходов = СтекНоменклатурыСтекВходов;
	
	// Заполнение структуры состава.
	СтруктураСостава.Этап					= ТекСтрока.Этап;
	СтруктураСостава.Тип					= ТекСтрока.Тип;
	СтруктураСостава.ТипСтрокиСостава		= ТекСтрока.ТипСтрокиСостава;
	СтруктураСостава.Номенклатура			= ТекСтрока.Номенклатура;
	СтруктураСостава.Характеристика			= ТекСтрока.Характеристика;
	СтруктураСостава.ЕдиницаИзмерения		= ТекСтрока.ЕдиницаИзмерения;
	СтруктураСостава.Количество				= ТекСтрока.Количество / ?(ТекСтрока.КоличествоПродукции <> 0, ТекСтрока.КоличествоПродукции, 1);
	СтруктураСостава.КоличествоПродукции	= ТекСтрока.КоличествоПродукции;
	СтруктураСостава.Уровень				= НоваяСтрокаСтека.Уровень;
	СтруктураСостава.УчетнаяЦена			= ТекСтрока.УчетнаяЦена;
	СтруктураСостава.Стоимость				= СтруктураСостава.Количество * ТекСтрока.УчетнаяЦена;
		
	Если ТекСтрока.Спецификация.ПометкаУдаления Тогда
		СтруктураСостава.Спецификация = Справочники.Спецификации.ПустаяСсылка();
	Иначе
		СтруктураСостава.Спецификация = ТекСтрока.Спецификация;
	КонецЕсли;
		
КонецПроцедуры // ДобавитьУзел()

// Выполняет разузлование узла.
//
// Параметры:
//  СтруктураСостава - Структура, описывающая обрабатываемый узел
//	ТаблицаСостава - ТаблицаЗначений состава и операций
//  
Процедура ВыполнитьРазузлование(СтруктураСостава, ТаблицаСостава)
	
	НоваяСтрокаСостава = ТаблицаСостава.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрокаСостава, СтруктураСостава);
	НоваяСтрокаСостава.Узел				= Ложь;
	
	Если СтруктураСостава.ТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Узел
	 ИЛИ СтруктураСостава.ТипСтрокиСостава = Перечисления.ТипыСтрокСоставаСпецификации.Сборка
	 ИЛИ СтруктураСостава.Уровень = 0 Тогда
			
		НоваяСтрокаСостава.Узел			= Истина;
	 
	КонецЕсли;
		
КонецПроцедуры // ВыполнитьРазузлование()	

// Процедура разузлования.
//
// Параметры:
//  СтруктураСостава - Структура, описывающая обрабатываемый узел
//	Объект
//	ТаблицаСостава - ТаблицаЗначений состава и операций
//  
Процедура Разузлование(СтруктураСостава, ТаблицаСостава)
	
	// Инициализация стека номенклатуры.
	СтекНоменклатуры = Новый ТаблицаЗначений();
	СтекНоменклатуры.Колонки.Добавить("Номенклатура");
	СтекНоменклатуры.Колонки.Добавить("Характеристика");
	СтекНоменклатуры.Колонки.Добавить("Спецификация");
	СтекНоменклатуры.Колонки.Добавить("Уровень");
	
	СтекНоменклатуры.Колонки.Добавить("СтекВходов");
	
	СтекНоменклатуры.Индексы.Добавить("Номенклатура, Характеристика, Спецификация");
	
	// Инициализация таблицы Входы.
	СтекНоменклатурыСтекВходов = Новый ТаблицаЗначений();
	СтекНоменклатурыСтекВходов.Колонки.Добавить("ТипСтрокиСостава");
	СтекНоменклатурыСтекВходов.Колонки.Добавить("Этап");
	СтекНоменклатурыСтекВходов.Колонки.Добавить("Тип");
	СтекНоменклатурыСтекВходов.Колонки.Добавить("Номенклатура");
	СтекНоменклатурыСтекВходов.Колонки.Добавить("Характеристика");
	СтекНоменклатурыСтекВходов.Колонки.Добавить("ЕдиницаИзмерения");
	СтекНоменклатурыСтекВходов.Колонки.Добавить("Количество");
	СтекНоменклатурыСтекВходов.Колонки.Добавить("КоличествоПродукции");
	СтекНоменклатурыСтекВходов.Колонки.Добавить("Спецификация");
	СтекНоменклатурыСтекВходов.Колонки.Добавить("Уровень");
	СтекНоменклатурыСтекВходов.Колонки.Добавить("УчетнаяЦена");
	СтекНоменклатурыСтекВходов.Колонки.Добавить("Стоимость");
	
	СтруктураСостава.Уровень = 0;
	
	// Начальное заполнение стека.
	НоваяСтрокаСтека = СтекНоменклатуры.Добавить();
	НоваяСтрокаСтека.Номенклатура	= СтруктураСостава.Номенклатура;
	НоваяСтрокаСтека.Характеристика	= СтруктураСостава.Характеристика;
	НоваяСтрокаСтека.Спецификация	= СтруктураСостава.Спецификация;
	НоваяСтрокаСтека.Уровень		= СтруктураСостава.Уровень;
	
	НоваяСтрокаСтека.СтекВходов		= СтекНоменклатурыСтекВходов;
	
	ВыполнитьРазузлование(СтруктураСостава, ТаблицаСостава);
	
	// Пока есть, что разузловывать.
	Пока СтекНоменклатуры.Количество() <> 0 Цикл
		
		ВыборкаНоменклатуры = ПолучитьСоставСпецификации(СтруктураСостава);
		
		Если ВыборкаНоменклатуры<>Неопределено Тогда
			
			Пока ВыборкаНоменклатуры.Следующий() Цикл
				
				Если НЕ ЗначениеЗаполнено(ВыборкаНоменклатуры.Номенклатура) Тогда
					Продолжить;
				КонецЕсли;
				
				// Проверяем рекурсивный вход.
				СтруктураПоиска = Новый Структура;
				СтруктураПоиска.Вставить("Номенклатура",	ВыборкаНоменклатуры.Номенклатура);
				СтруктураПоиска.Вставить("Характеристика",	ВыборкаНоменклатуры.Характеристика);
				СтруктураПоиска.Вставить("Спецификация",	ВыборкаНоменклатуры.Спецификация);
				
				СтрокиРекурсивногоВхождения = СтекНоменклатуры.НайтиСтроки(СтруктураПоиска);
				
				Если СтрокиРекурсивногоВхождения.Количество() <> 0 Тогда
					
					Для каждого СтрокаРекВх Из СтрокиРекурсивногоВхождения Цикл
						
						ТекстСообщения = СтрШаблон(
						НСтр("ru = 'Обнаружено рекурсивное вхождение элемента %1 в элемент %2!'"),
						ВыборкаНоменклатуры.Номенклатура,
						СтруктураСостава.Номенклатура);
						УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(СтруктураСостава.Объект, ТекстСообщения);
						
					КонецЦикла;
					
					Продолжить;
					
				КонецЕсли;
				
				// Добавление новых узлов.
				НоваяСтрокаВход = СтекНоменклатурыСтекВходов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаВход, ВыборкаНоменклатуры, "ТипСтрокиСостава, Этап, Тип, Номенклатура, Характеристика, ЕдиницаИзмерения, КоличествоПродукции, Спецификация");
				
				КоэффициентЕдиницыИзмерения			= ?(ТипЗнч(СтруктураСостава.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения"),
				СтруктураСостава.ЕдиницаИзмерения.Коэффициент,
				1);
															
				НоваяСтрокаВход.Количество			= ВыборкаНоменклатуры.Количество * КоэффициентЕдиницыИзмерения;
				НоваяСтрокаВход.Уровень				= НоваяСтрокаСтека.Уровень + 1;
				НоваяСтрокаВход.УчетнаяЦена			= Число(ВыборкаНоменклатуры.УчетнаяЦена);
				НоваяСтрокаВход.Стоимость			= Число(ВыборкаНоменклатуры.Стоимость) * КоэффициентЕдиницыИзмерения;
				
			КонецЦикла; // ВыборкаНоменклатуры
			
		КонецЕсли; 
		
		// Конец ветви или нет?
		Если СтекНоменклатурыСтекВходов.Количество() = 0 Тогда
			
			// Удаляем из стека номенклатуру, которая не имеет продолжения.
			СтекНоменклатуры.Удалить(НоваяСтрокаСтека);
			
			ФлагГотовности = Истина;
			Пока СтекНоменклатуры.Количество() <> 0 И ФлагГотовности Цикл
				
				// Получаем предыдущую строку стека номенклатуры.
				ПредСтрокаСтекНоменклатуры = СтекНоменклатуры.Получить(СтекНоменклатуры.Количество() - 1);
				
				// Удаляем из стека входов.
				ПредСтрокаСтекНоменклатуры.СтекВходов.Удалить(0);
					
				Если ПредСтрокаСтекНоменклатуры.СтекВходов.Количество() = 0 Тогда
					
					// Если стек входов пустой, удаляем строку из стека номенклатуры.
					СтекНоменклатуры.Удалить(ПредСтрокаСтекНоменклатуры);
					
				Иначе // разузловываем следующую номенклатуру из стека входов.
					
					ФлагГотовности = Ложь;
					
					ТекСтрока = ПредСтрокаСтекНоменклатуры.СтекВходов.Получить(0);
					
					ДобавитьУзел(СтруктураСостава, СтекНоменклатуры, СтекНоменклатурыСтекВходов, НоваяСтрокаСтека, ТекСтрока);
					ВыполнитьРазузлование(СтруктураСостава, ТаблицаСостава);
					
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе // добавляем узлы
			
			ТекСтрока = СтекНоменклатурыСтекВходов.Получить(0);
			
			ДобавитьУзел(СтруктураСостава, СтекНоменклатуры, СтекНоменклатурыСтекВходов, НоваяСтрокаСтека, ТекСтрока);
			ВыполнитьРазузлование(СтруктураСостава, ТаблицаСостава);
			
		КонецЕсли;
		
	КонецЦикла; // СтекНоменклатуры
	
КонецПроцедуры // Разузлование() 

#КонецОбласти

#Область Инициализация

ЭтоОтчетУНФ = Истина;

Если НЕ КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Свойство("РазвернутьДо") Тогда
	КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("РазвернутьДо", "*");
КонецЕсли;

МодифицироватьСхемуФО();

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли