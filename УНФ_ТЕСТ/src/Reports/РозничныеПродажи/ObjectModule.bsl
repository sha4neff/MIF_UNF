#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиОтчета.ИспользоватьСравнение = Истина;
	НастройкиОтчета.ИспользоватьПериодичность = Истина;
	НастройкиОтчета.ПоляСебестоимости = "Себестоимость, ВаловаяПрибыль, Рентабельность, Наценка";
	
	НастройкиОтчета.ДополнительныеГруппировкиСтрок.Добавить("КассаККМ");
	НастройкиОтчета.ДополнительныеГруппировкиСтрок.Добавить("Ответственный");
	НастройкиОтчета.ДополнительныеФильтры.Добавить("Ответственный");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("Организация");
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УчетПоНесколькимПодразделениям") Тогда
		НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("Подразделение");
	КонецЕсли;
	
	НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("Ответственный");
	
	Если ПолучитьФункциональнуюОпцию("УчетПоНесколькимСкладам") Тогда
		НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("Склад");
		НастройкиОтчета.ДополнительныеГруппировкиСтрок.Добавить("Склад");
		НастройкиОтчета.ДополнительныеФильтры.Добавить("Склад");
	КонецЕсли;
	
	НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("КатегорияНоменклатуры");
	НастройкиОтчета.Вставить("РежимПериода", "ЗаПериод");
	
	НастройкиВариантов["РозничнаяВыручка"].Рекомендуемый = Истина;
	
	ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов);
	УстановитьТегиВариантов(НастройкиВариантов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ОтчетыУНФ.ДобавитьСимволВалютыКЗаголовкамПолей(СхемаКомпоновкиДанных, "Себестоимость,Сумма,СуммаНДС,ВаловаяПрибыль");
	
	ОтчетыУНФ.ПриКомпоновкеРезультата(КомпоновщикНастроек, СхемаКомпоновкиДанных, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов)
	
	МассивПолейСумм = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("Сумма,Себестоимость,СуммаНДС,ВаловаяПрибыль");
	
	Для каждого ОписаниеВарианта Из НастройкиВариантов Цикл
		
		ВариантыОформления = ОписаниеВарианта.Значение.ВариантыОформления;
		ОтчетыУНФ.ДобавитьВариантыОформленияСумм(ВариантыОформления, МассивПолейСумм, КомпоновщикНастроек.Настройки);
		ОтчетыУНФ.ДобавитьВариантыОформленияКоличества(ВариантыОформления, "Количество", КомпоновщикНастроек.Настройки);
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура УстановитьТегиВариантов(НастройкиВариантов)
	
	НастройкиВариантов["РозничныеПродажиКонтекст"].Теги = НСТР("ru = 'Главное,Продажи,Розница,Номенклатура,Выручка'");
	НастройкиВариантов["РозничныеПродажиПоТочкамКонтекст"].Теги = НСТР("ru = 'Главное,Продажи,Розница,Номенклатура,Выручка,ККМ'");
	НастройкиВариантов["РозничнаяВыручка"].Теги = НСТР("ru = 'Главное,Продажи,Розница,Номенклатура,Выручка,ККМ'");
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ЭтоОтчетУНФ = Истина;

Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПодарочныеСертификаты") Тогда
	ОтчетыУНФ.ОграничитьИспользованиеПоля(СхемаКомпоновкиДанных, "ПолученоСертификатами");
КонецЕсли; 

Если Не ПравоДоступа("Просмотр", Метаданные.РегистрыНакопления.Продажи.Ресурсы.Себестоимость) Тогда
	ОтчетыУНФ.ОграничитьИспользованиеПоля(СхемаКомпоновкиДанных, "ВаловаяПрибыль");
	ОтчетыУНФ.ОграничитьИспользованиеПоля(СхемаКомпоновкиДанных, "Наценка");
	ОтчетыУНФ.ОграничитьИспользованиеПоля(СхемаКомпоновкиДанных, "Рентабельность");
	ОтчетыУНФ.ОграничитьИспользованиеПоля(СхемаКомпоновкиДанных, "Себестоимость");
КонецЕсли;

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли