#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиОтчета.ПоказыватьНастройкиДиаграммыНаФормеОтчета = Ложь;
	НастройкиОтчета.Вставить("ФиксироватьКолонки", Истина);
	
	НастройкиВариантов["Основной"].Рекомендуемый = Истина;
	
	ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов);
	УстановитьТегиВариантов(НастройкиВариантов);
	ДобавитьОписанияСвязанныхПолей(НастройкиВариантов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВидЦен"));
	Если Параметр=Неопределено 
		ИЛИ НЕ Параметр.Использование 
		ИЛИ НЕ ЗначениеЗаполнено(Параметр.Значение) Тогда
		ВыбранноеПоле = ВыбранноеПолеРекурсивно(КомпоновщикНастроек.Настройки.Выбор.Элементы, Новый ПолеКомпоновкиДанных("ЦенаНабора"));
		Если ВыбранноеПоле <> Неопределено Тогда
			ВыбранноеПоле.Использование = Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	ОтчетыУНФ.ПриКомпоновкеРезультата(КомпоновщикНастроек, СхемаКомпоновкиДанных, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов)
	
	МассивПолейКоличества = Новый Массив;
	Для Каждого ДоступноеПоле Из КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл
		Если Не ДоступноеПоле.Ресурс Тогда
			Продолжить;
		КонецЕсли;
		МассивПолейКоличества.Добавить(Строка(ДоступноеПоле.Поле));
	КонецЦикла; 
	
	Для Каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		
		ОтчетыУНФ.ДобавитьВариантыОформленияКоличества(НастройкиТекВарианта.Значение.ВариантыОформления, МассивПолейКоличества);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьТегиВариантов(НастройкиВариантов)
	
	НастройкиВариантов["Основной"].Теги = НСтр("ru = 'Запасы,Закупки,Номенклатура,Товары,Наборы'");
	
КонецПроцедуры

Процедура ДобавитьОписанияСвязанныхПолей(НастройкиВариантов)
	
	Для Каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиТекВарианта.Значение.СвязанныеПоля, "СтруктурнаяЕдиница", "Справочник.СтруктурныеЕдиницы");
		ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиТекВарианта.Значение.СвязанныеПоля, "НоменклатураНабора", "Справочник.Номенклатура");
	КонецЦикла;
	
КонецПроцедуры

Функция ВыбранноеПолеРекурсивно(Элементы, Поле)
	Перем Результат;
	
	Для каждого Элемент Из Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			Результат = ВыбранноеПолеРекурсивно(Элемент.Элементы, Поле);
		ИначеЕсли ТипЗнч(Элемент) = Тип("ВыбранноеПолеКомпоновкиДанных") И Элемент.Поле = Поле Тогда 
			Результат = Элемент;
		Иначе
			Результат = Неопределено;
		КонецЕсли;
		Если Результат <> Неопределено Тогда
			Возврат Результат;
		КонецЕсли; 
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область Инициализация

ЭтоОтчетУНФ = Истина;

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли