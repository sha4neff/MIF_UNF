#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура - Обработчик заполнения настроек отчета и варианта
//
// Параметры:
//  НастройкиОтчета		 - Структура - Настройки отчета, подробнее см. процедуру ОтчетыУНФ.ИнициализироватьНастройкиОтчета 
//  НастройкиВариантов	 - Структура - Настройки варианта отчета, подробнее см. процедуру ОтчетыУНФ.ИнициализироватьНастройкиВарианта
//
Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиОтчета.ИспользоватьСравнение = Истина;
	НастройкиОтчета.ИспользоватьПериодичность = Истина;
	НастройкиОтчета.ПоляСебестоимости = "Себестоимость, ВаловаяПрибыль, Рентабельность, Наценка";
	
	НастройкиОтчета.ДополнительныеГруппировкиСтрок.Добавить("Ответственный");
	НастройкиОтчета.ДополнительныеГруппировкиСтрок.Добавить("Контрагент");
	НастройкиОтчета.ДополнительныеГруппировкиСтрок.Добавить("Регистратор");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("Организация");
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УчетПоНесколькимПодразделениям") Тогда
		НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("Подразделение");
	КонецЕсли;
	
	НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("Ответственный");
	
	Если ПолучитьФункциональнуюОпцию("УчетПоНесколькимСкладам") Тогда
		НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("Склад");
		НастройкиОтчета.ДополнительныеГруппировкиСтрок.Добавить("Склад");
		НастройкиОтчета.ДополнительныеФильтры.Добавить("Склад");
	КонецЕсли;
	
	НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("КатегорияНоменклатуры");
	НастройкиОтчета.Вставить("РежимПериода", "ЗаПериод");
	
	НастройкиВариантов["Основной"].ИмяМакетаОбразца = "ОбразецПродажи";
	
	НастройкиВариантов["Основной"].Рекомендуемый = Истина;
	НастройкиВариантов["ПродажиКонтекст"].Рекомендуемый = Истина;
	НастройкиВариантов["ВаловаяПрибыль"].Рекомендуемый = Истина;
	
	ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов);
	УстановитьТегиВариантов(НастройкиВариантов);
	ДобавитьОписанияСвязанныхПолей(НастройкиВариантов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УстановитьЗаголовокКолонкиВыручка();
	
	ОтчетыУНФ.ДобавитьСимволВалютыКЗаголовкамПолей(СхемаКомпоновкиДанных, "Себестоимость,Сумма,СуммаНДС,ВаловаяПрибыль");
	
	// Наборы
	МодифицироватьСхемуПродажаНаборов();
	// Конец Наборы
	
	ОтчетыУНФ.ПриКомпоновкеРезультата(КомпоновщикНастроек, СхемаКомпоновкиДанных, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов)
	
	МассивПолейСумм = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("Сумма,Себестоимость,СуммаНДС,ВаловаяПрибыль");
	
	Для каждого ОписаниеВарианта Из НастройкиВариантов Цикл
		
		ВариантыОформления = ОписаниеВарианта.Значение.ВариантыОформления;
		ОтчетыУНФ.ДобавитьВариантыОформленияСумм(ВариантыОформления, МассивПолейСумм, КомпоновщикНастроек.Настройки);
		ОтчетыУНФ.ДобавитьВариантыОформленияКоличества(ВариантыОформления, "Количество", КомпоновщикНастроек.Настройки);
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура УстановитьТегиВариантов(НастройкиВариантов)
	
	НастройкиВариантов["Основной"].Теги = НСТР("ru = 'Главное,Продажи,CRM,Номенклатура,Выручка'");
	НастройкиВариантов["ВаловаяПрибыль"].Теги = НСТР("ru = 'Главное,Продажи,CRM,Номенклатура,Выручка,Себестоимость,Прибыль,Рентабельность,Маржа'");
	НастройкиВариантов["ВаловаяПрибыльПоКатегориямНоменклатуры"].Теги = НСТР("ru = 'Продажи,CRM,Категории,Выручка,Себестоимость,Прибыль,Рентабельность,Маржа'");
	НастройкиВариантов["ВаловаяПрибыльПоПокупателям"].Теги = НСТР("ru = 'Продажи,CRM,Контрагенты,Покупатели,Выручка,Себестоимость,Прибыль,Рентабельность,Маржа'");
	НастройкиВариантов["ВаловаяПрибыльПоМенеджерам"].Теги = НСТР("ru = 'Продажи,CRM,Менеджеры,Выручка,Себестоимость,Прибыль,Рентабельность,Маржа'");
	НастройкиВариантов["ДинамикаПродаж"].Теги = НСТР("ru = 'Продажи,CRM'");
	НастройкиВариантов["ДинамикаПродажПоНоменклатуре"].Теги = НСТР("ru = 'Продажи,CRM,Выручка,Номенклатура'");
	НастройкиВариантов["ДинамикаПродажПоКатегориямНоменклатуры"].Теги = НСТР("ru = 'Продажи,Выручка,Категории'");
	НастройкиВариантов["ДинамикаПродажПоПокупателям"].Теги = НСТР("ru = 'Продажи,CRM,Выручка,Контрагенты,Покупатели'");
	НастройкиВариантов["ДинамикаПродажПоМенеджерам"].Теги = НСТР("ru = 'Продажи,CRM,Выручка,Менеджеры'");
	НастройкиВариантов["ПродажиНашимОрганизациям"].Теги = НСТР("ru = 'Продажи,CRM,Передача,Интеркампани,Взаимозависимые'");
	НастройкиВариантов["ПродажиБезВнутрифирменныхПродаж"].Теги = НСТР("ru = 'Продажи,CRM,Передача,Интеркампани,Взаимозависимые'");
	
КонецПроцедуры

Процедура ДобавитьОписанияСвязанныхПолей(НастройкиВариантов)
	
	СтруктураВарианта = НастройкиВариантов["ПродажиКонтекст"];
	ОтчетыУНФ.ДобавитьОписаниеПривязки(СтруктураВарианта.СвязанныеПоля, "Контрагент", "Справочник.Контрагенты",,, Истина);
	ОтчетыУНФ.ДобавитьОписаниеПривязки(СтруктураВарианта.СвязанныеПоля, "Номенклатура", "Справочник.Номенклатура",,, Истина);
	ОтчетыУНФ.ДобавитьОписаниеПривязки(СтруктураВарианта.СвязанныеПоля, "ЗаказПокупателя", "Документ.ЗаказПокупателя",,, Истина);
	СтруктураВарианта = НастройкиВариантов["ВаловаяПрибыль"];
	ОтчетыУНФ.ДобавитьОписаниеПривязки(СтруктураВарианта.СвязанныеПоля, "Документ", "Документ.ЧекККМ",,, Истина);
	ОтчетыУНФ.ДобавитьОписаниеПривязки(СтруктураВарианта.СвязанныеПоля, "Документ", "Документ.ОтчетОРозничныхПродажах",,, Истина);
	
КонецПроцедуры

Процедура УстановитьЗаголовокКолонкиВыручка()
	
	ПараметрТолькоВозвраты = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ТолькоВозвраты"));
	Если Не ПараметрТолькоВозвраты.Значение Тогда
		Возврат;
	КонецЕсли;
	
	ПолеСумма = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Поля.Найти("Сумма");
	ПолеСумма.Заголовок = НСтр("ru = 'Сумма'");
	
КонецПроцедуры

Процедура МодифицироватьСхемуПродажаНаборов()
	
	Группировка = ОтчетыУНФКлиентСервер.НайтиПолеРекурсивно(Новый ПолеКомпоновкиДанных("НоменклатураНабора"), КомпоновщикНастроек.Настройки.Структура);
	Если Группировка=Неопределено Тогда
		КУдалению = Новый Массив;
		Для каждого Поле Из СхемаКомпоновкиДанных.ПоляИтога Цикл
			Если Поле.ПутьКДанным="Количество" Тогда
				Если Поле.Группировки.Количество()>0 Тогда
					Поле.Группировки.Очистить();
				Иначе
					КУдалению.Добавить(Поле);
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла;
		Для каждого Поле Из КУдалению Цикл
			СхемаКомпоновкиДанных.ПоляИтога.Удалить(Поле);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ЭтоОтчетУНФ = Истина;

#КонецОбласти 

#КонецЕсли
