
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОбъектСсылка=ПараметрКоманды;
	АдресХраненияНастройкиЦветов=ПараметрыВыполненияКоманды.Источник.АдресХраненияНастройкиЦветов;	
	ЭтаФормаОбъекта=ПараметрыВыполненияКоманды.Источник;
	ОбъектФормы=ЭтаФормаОбъекта.Объект;
	
	ТекПользователь=КП_ОбщееСервер.ТекущийПользователь();
		
	Если ПустаяСтрока(АдресХраненияНастройкиЦветов) Тогда
		МассивНастройкиЦветов=Новый Массив;		
		АдресХраненияНастройкиЦветов=ПоместитьВоВременноеХранилище(МассивНастройкиЦветов, ЭтаФормаОбъекта.УникальныйИдентификатор);
		ПараметрыВыполненияКоманды.Источник.АдресХраненияНастройкиЦветов=АдресХраненияНастройкиЦветов;
	КонецЕсли;
	
	Если КП_ОбщееСерверПС.ЭтоЭкземплярБизнесПроцесса(ОбъектСсылка) Тогда
		ИмяРеквизита="Наименование";
		ЗаголовокФормыНастройки="Настройка цвета процесса";
		ТекстОбразца=?(ПустаяСтрока(ОбъектФормы[ИмяРеквизита]), "Наименование процесса", СокрЛП(ОбъектФормы[ИмяРеквизита]));
		
	ИначеЕсли КП_ОбщееСерверПС.ЭтоЗадачаБизнесПроцесса(ОбъектСсылка) Тогда		
		ИмяРеквизита="Наименование";			
		ЗаголовокФормыНастройки="Настройка цвета задачи";
		ТекстОбразца=?(ПустаяСтрока(ОбъектФормы[ИмяРеквизита]), "Наименование задачи", СокрЛП(ОбъектФормы[ИмяРеквизита]));
				
	Иначе
		КП_ОбщееКлиент.ПредупреждениеПользователю("Для данного типа настройка оформления не поддерживается");
		Возврат;
	КонецЕсли;

	ПараметрыФормы=Новый Структура;
	ПараметрыФормы.Вставить("ЭтоЗаполнениеИзФормыОбъекта", Истина);
	ПараметрыФормы.Вставить("АдресХраненияНастройкиЦветов", АдресХраненияНастройкиЦветов);
	ПараметрыФормы.Вставить("ТекстОбразцаЗаголовка", ТекстОбразца);
	ПараметрыФормы.Вставить("Заголовок", ЗаголовокФормыНастройки);
	ПараметрыФормы.Вставить("ТекущийПользователь", ТекПользователь);
	
	МассивОбъектов=Новый Массив;
	МассивОбъектов.Добавить(ОбъектСсылка);
	ПараметрыФормы.Вставить("МассивОбъектов", МассивОбъектов);
	
	СтруктураПараметровОбработкиРезультата=Новый Структура("ОформляемыйЭлемент, ЭтаФорма, ТекущийПользователь, АдресХраненияНастройкиЦветов", 
		ЭтаФормаОбъекта.Элементы[ИмяРеквизита], ЭтаФормаОбъекта, ТекПользователь, АдресХраненияНастройкиЦветов);
		
	ОткрытьФорму("ОбщаяФорма.КП_НастройкаОформления", ПараметрыФормы,,,,,
		Новый ОписаниеОповещения("Оповещение_ЦветВыбран", КП_ОформлениеСтрокКлиент, СтруктураПараметровОбработкиРезультата),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
КонецПроцедуры

	