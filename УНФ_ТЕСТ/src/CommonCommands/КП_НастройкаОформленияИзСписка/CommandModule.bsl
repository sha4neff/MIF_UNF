
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	МассивОбъектов=ПараметрКоманды;
	
	Если МассивОбъектов.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФормыВыполненияКоманды=ПараметрыВыполненияКоманды.Источник.ИмяФормы;
	
	ДопПараметры=Новый Структура("МассивОбъектов", МассивОбъектов);
	ДопПараметры.Вставить("ИмяФормы", ИмяФормыВыполненияКоманды);
	
	Попытка
		Если ИмяФормыВыполненияКоманды="Обработка.КП_НавигаторЗадач.Форма.ФормаНавигаторЗадач" Тогда
			ИмяЭлементаСписка="СписокПолученныеЗадачи";
			ДопПараметры.Вставить("ЭлементСписок", ПараметрыВыполненияКоманды.Источник.Элементы[ИмяЭлементаСписка]);
			ДопПараметры.Вставить("ЭлементСписок2", ПараметрыВыполненияКоманды.Источник.Элементы["СписокВыданныеЗадачи"]);
		Иначе
			ИмяЭлементаСписка="Список";
			ДопПараметры.Вставить("ЭлементСписок", ПараметрыВыполненияКоманды.Источник.Элементы[ИмяЭлементаСписка]);
		КонецЕсли;
	Исключение
		КП_ОбщееКлиент.ПредупреждениеПользователю("В форме нет элемента с именем """+ИмяЭлементаСписка+""". Выполнение команды невозможно");
		Возврат;
	КонецПопытки;
	
	Если МассивОбъектов.Количество()>5 Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("Оповещение_ОтветНаВопрос", ЭтотОбъект, ДопПараметры),
			"Будет установлен цвет сразу для "+Формат(МассивОбъектов.Количество(), "ЧДЦ=0; ЧН=; ЧГ=")+" элементов. Продолжить?", КП_ОбщееКлиент.КнопкиВопроса("Да, продолжить"),,, КП_ОбщееКлиент.ЗаголовокДиалога());
		Возврат;
	
	КонецЕсли;
	
	Оповещение_ОтветНаВопрос(КодВозвратаДиалога.Да, ДопПараметры); 
			
КонецПроцедуры

&НаКлиенте
Процедура Оповещение_ОтветНаВопрос(Результат, ДопПараметры) Экспорт
	
	Если Результат=КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	МассивОбъектов=ДопПараметры.МассивОбъектов;
	
	ПерваяСсылка=МассивОбъектов[0];
	
	Если КП_ОбщееСерверПС.ЭтоЭкземплярБизнесПроцесса(ПерваяСсылка) Тогда
		КоличествоОбъектов=МассивОбъектов.Количество();
		ИмяРеквизита="Наименование";
		Если КоличествоОбъектов=1 Тогда
			ЗаголовокФормыНастройки="Настройка цвета процесса";
			НаименованиеПервойСсылки=КП_ОбщееСервер.ПолучитьЗначениеРеквизитаОбъекта(ПерваяСсылка, ИмяРеквизита);
			ТекстОбразца=?(ПустаяСтрока(НаименованиеПервойСсылки), "Наименование процесса", НаименованиеПервойСсылки);
		Иначе
			ЗаголовокФормыНастройки="Настройка цвета процессов ("+Формат(КоличествоОбъектов, "ЧДЦ=0; ЧН=; ЧГ=")+")";
			ТекстОбразца="Наименование процесса";
		КонецЕсли;
		
	ИначеЕсли КП_ОбщееСерверПС.ЭтоЗадачаБизнесПроцесса(ПерваяСсылка) Тогда
		КоличествоОбъектов=МассивОбъектов.Количество();
		ИмяРеквизита="Наименование";
		Если КоличествоОбъектов=1 Тогда			
			НаименованиеПервойСсылки=КП_ОбщееСервер.ПолучитьЗначениеРеквизитаОбъекта(ПерваяСсылка, ИмяРеквизита);			
			ЗаголовокФормыНастройки="Настройка цвета задачи";
			ТекстОбразца=?(ПустаяСтрока(НаименованиеПервойСсылки), "Наименование задачи", НаименованиеПервойСсылки);
		Иначе
			ЗаголовокФормыНастройки="Настройка цвета задач ("+Формат(КоличествоОбъектов, "ЧДЦ=0; ЧН=; ЧГ=")+")";
			ТекстОбразца="Наименование задачи";
		КонецЕсли;
				
	Иначе
		КП_ОбщееКлиент.ПредупреждениеПользователю("Для данного типа настройка оформления не поддерживается");
		Возврат;
		
	КонецЕсли;
	
	ТекПользователь=КП_ОбщееСервер.ТекущийПользователь();

	ПараметрыФормы=Новый Структура;
	ПараметрыФормы.Вставить("ЭтоЗаполнениеИзФормыОбъекта", Ложь);
	ПараметрыФормы.Вставить("ТекстОбразцаЗаголовка", ТекстОбразца);
	ПараметрыФормы.Вставить("Заголовок", ЗаголовокФормыНастройки);
	ПараметрыФормы.Вставить("МассивОбъектов", МассивОбъектов);
	ПараметрыФормы.Вставить("ТекущийПользователь", ТекПользователь);
	ПараметрыФормы.Вставить("ИмяФормы", ДопПараметры.ИмяФормы);
			
	СтруктураПараметровОбработкиРезультата=Новый Структура("МассивОбъектов, ТекущийПользователь", МассивОбъектов, ТекПользователь);
	
	СтруктураПараметровОбработкиРезультата.Вставить("ЭлементСписок", ДопПараметры.ЭлементСписок);
	
	Если ДопПараметры.Свойство("ЭлементСписок2") Тогда
		СтруктураПараметровОбработкиРезультата.Вставить("ЭлементСписок2", ДопПараметры.ЭлементСписок2);
	КонецЕсли;
		
	ОткрытьФорму("ОбщаяФорма.КП_НастройкаОформления", ПараметрыФормы,,,,,
		Новый ОписаниеОповещения("Оповещение_ЦветДляСпискаВыбран", КП_ОформлениеСтрокКлиент, СтруктураПараметровОбработкиРезультата),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры
